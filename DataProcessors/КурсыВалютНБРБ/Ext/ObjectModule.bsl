Перем ИмяФайла;

#Если Клиент Тогда

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Производит загрузку курсов и кратностей валют с сайта НБРБ
//
// Параметры:
//  ИндикаторФормы     - ЭлементыФормы типа Индикатор - для отработки индикатора формы
//  НадписьВалютыФормы - ЭлементыФормы типа Надпись  - Для отработки надписи 
//													   загружаемой валюты
//
Процедура ЗагрузитьКурсыНБРБ() Экспорт
	Перем HTTP;
	
	Если Не ЗначениеЗаполнено(НачДата) ИЛИ Не ЗначениеЗаполнено(КонДата) Тогда
		Предупреждение("Укажите даты", 10, "Предупрежедние");
		Возврат;
	КонецЕсли;
	
	Если Год(НачДата) <> Год(КонДата) Тогда
		Предупреждение("Период не в рамках одного года", 10, "Предупрежедние");
		Возврат;
	КонецЕсли;	
	Если Год(НачДата)<2000 Тогда
		Предупреждение("Курсы валют на сайте представлены начиная с 2000-го года", 10, "Предупрежедние");
		Возврат;
	КонецЕсли;
	Если Началодня(КонДата) > НачалоДня(ТекущаяДата())Тогда
		Предупреждение("Конечная дата должна быть не больше текущей", 10, "Предупрежедние");
		Возврат;
	КонецЕсли; 	
	
	СтруктураПоиска = Новый Структура("ЗагружатьКурс", Истина);
	МассивВыбранныеВалюты =   СписокВалют.НайтиСтроки(СтруктураПоиска);
	Если МассивВыбранныеВалюты.Количество()>0 Тогда
		РегистрКурсыВалют = РегистрыСведений.КурсыВалют;
		ЗаписьКурсовВалют = РегистрКурсыВалют.СоздатьМенеджерЗаписи();
		СерверИсточник = "www.nbrb.by";    
		ОбработкаПолученияФайлов = Обработки.ПолучениеФайловИзИнтернета.Создать();
		Адрес = "statistics/rates/";   
		ВремКаталог = КаталогВременныхФайлов() + "tempKurs";
		СоздатьКаталог(ВремКаталог);
		Попытка
			УдалитьФайлы(ВремКаталог,"*.*");
		Исключение
			Предупреждение("Для загрузки курсов необходимо закрыть файлы  Excel. Закройте файлы Excel и повторите попытку.", 10, "Предупрежедние");
			Возврат;
		КонецПопытки;	
		Стр = "";
		ИмяВходящегоФайла = "" + ВремКаталог + "\" + ИмяФайла;
		СтрокаПараметраПолучения = Адрес + "ratesDaily.asp?yr=" +  Строка(Формат(Год(НачДата), "ЧГ = 0"));
		Если ОбработкаПолученияФайлов.ЗапроситьФайлыССервера(СерверИсточник, СтрокаПараметраПолучения, ИмяВходящегоФайла, HTTP) <> Истина Тогда
			Предупреждение("Не удалось получить интернет-ресурс. Курсы валют не будут загружены",10, "Предупрежедние"); 
			Возврат;
		КонецЕсли;
		ВходящийФайл = Новый Файл(ИмяВходящегоФайла);
		Если НЕ ВходящийФайл.Существует() Тогда
			Предупреждение("Не удалось получить данные. Курсы валют не будут загружены",10, "Предупрежедние"); 
			Возврат;
		КонецЕсли;	
		ФормаИндикации = ПолучитьОбщуюФорму("ХодВыполненияОбработкиДанных");
		ФормаИндикации.НаименованиеОбработкиДанных = "Загрузка курсов валют с сайта Национального Банка Беларуси";
		ФормаИндикации.КомментарийЗначения         = "Загружено:";
		ФормаИндикации.МаксимальноеЗначение         = 100;
		ФормаИндикации.Открыть();
		Попытка 
			КурсыXLS = ПолучитьCOMОбъект(ИмяВходящегоФайла);
		Исключение
			Предупреждение("Возможно открыт файл Excel. Закройте его и повторите попытку.", 10, "Предупрежедние");
			ФормаИндикации.Закрыть();
			Возврат;
		КонецПопытки;
		счСт = 2;
		Страница = КурсыXLS.Sheets(1);
		
		тзВалюта = Новый ТаблицаЗначений;
		тзВалюта.Колонки.Добавить("Обозначение");
		тзВалюта.Колонки.Добавить("Кратность");
		тзВалюта.Колонки.Добавить("НомерЯчейки");
		
		Пока СокрЛП(Страница.Cells(4, счСт).Text) <> "" Цикл
			СтрВалюта = НРег(СокрЛП(Страница.Cells(4, счСт).Text));
			ПозицияПервогоПробела = Найти(СтрВалюта, " ");
			НоваяСтрока = тзВалюта.Добавить();
    		НоваяСтрока.Обозначение = Сред(СтрВалюта, Найти(СтрВалюта, "(") + 1, 3); 
			НоваяСтрока.Кратность =   Сред(СтрВалюта, 1, ПозицияПервогоПробела - 1) ;
			НоваяСтрока.НомерЯчейки =  счСт;
			счСт =счСт  + 1;
		КонецЦикла;
		
		// если баг то если форма индикации открыта то  ее закрыть
		СтруктураПоиска = Новый Структура;	
		НачатьТранзакцию();
		Попытка
			Для сч = 0 по МассивВыбранныеВалюты.ВГраница() Цикл
				ОбозначениеВалюты = НРег(МассивВыбранныеВалюты[сч].Валюта);
				СтруктураПоиска.Вставить("Обозначение", ОбозначениеВалюты);
				
				НайденноеЗначение =  тзВалюта.НайтиСтроки(СтруктураПоиска);
				Если НайденноеЗначение.количество() > 0 Тогда
					КоличествоДней = Число((НачалоДня(КонДата) - НачалоДня(НачДата)))/(60*60*24)-1;
					КоличествоПозиций =?(КоличествоДней = 0, 1, КоличествоДней);
					Позиция = 0;
					счСтр = 5 + Число((НачалоДня(НачДата) - НачалоДня(Дата(Год(НачДата),1,1)))/(60*60*24));//НачалоДня(НачДата) ;//НачалоДня(НачДата)
					ФормаИндикации.КомментарийЗначения   = "Загружено: " + МассивВыбранныеВалюты[сч].Валюта.НаименованиеПолное;
					Пока СокрЛП(Страница.Cells(счСтр, 1).Text) <> ""  Цикл
						ДатаКурса =  НачалоДня(Дата(СокрЛП(Прав(Страница.Cells(счСтр, 1).Text, 4) + Сред(Страница.Cells(счСтр, 1).Text,4,2) +Лев(Страница.Cells(счСтр, 1).Text ,2))));
						Если ДатаКурса >= НачДата И ДатаКурса <= КонДата Тогда
							Курс = Число(СтрЗаменить(СокрЛП(Страница.Cells(счСтр, НайденноеЗначение[0].НомерЯчейки).Text), " ", ""));
							ЗаписьКурсовВалют.Валюта = МассивВыбранныеВалюты[сч].Валюта;
							ЗаписьКурсовВалют.Период = ДатаКурса;
							ЗаписьКурсовВалют.Прочитать();
							ЗаписьКурсовВалют.Валюта    = МассивВыбранныеВалюты[сч].Валюта;
							ЗаписьКурсовВалют.Период    = ДатаКурса;
							ЗаписьКурсовВалют.Курс      = Курс;
							ЗаписьКурсовВалют.Кратность = НайденноеЗначение[0].Кратность;
							ЗаписьКурсовВалют.Записать();
							Позиция = Позиция  +  100/КоличествоПозиций ;  
							ФормаИндикации.Значение = Позиция;
						ИначеЕсли  ДатаКурса > КонДата Тогда
							Прервать;
						Конецесли;	
						счСтр =  счСтр  + 1 ;
					КонецЦикла;	
				Иначе	
					Сообщить("Курс для " +  МассивВыбранныеВалюты[сч].Валюта.НаименованиеПолное + " не найден");
				КонецЕсли;
			КонецЦикла; 
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			Сообщить("Не удалось записать данные");
			Если ФормаИндикации.Открыта() Тогда
			    ФормаИндикации.Закрыть();
			КонецЕсли;
		КонецПопытки;
		Попытка
			КурсыXLS.Application.Quit();
			УдалитьФайлы(ВремКаталог,"*.*");
		Исключение
		КонецПопытки;
		
		Если ФормаИндикации.Открыта() Тогда
			 ФормаИндикации.Закрыть();
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры // ЗагрузитьКурсыНБРБ()


////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

ИмяФайла = "Curses.xls";  

#КонецЕсли