
#Если Клиент Тогда

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура СформироватьОтчет(ДокументРезультат, ЕстьОшибки = Ложь) Экспорт

	//Параметры документа
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.ПолеСлева			 = 0;
	ДокументРезультат.ПолеСправа		 = 0;
	ДокументРезультат.Защита             = Ложь;
	ДокументРезультат.ТолькоПросмотр     = Истина;
    ДокументРезультат.АвтоМасштаб		 = Истина;
	
	РеглВалюта = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// Дата актуальности установим на последний день года. 
	Запрос.УстановитьПараметр("ДатаАктуальности",	Дата(Год, 12, 31, 23, 59, 59));
	Запрос.УстановитьПараметр("Физлицо",			Физлицо);
	Запрос.УстановитьПараметр("Организация",		Организация);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",ОбщегоНазначения.ГоловнаяОрганизация(Организация));

	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество, ФизическиеЛица.Наименование) КАК ФИО,
	|	РаботникиОрганизацииСрезПоследних.Сотрудник.Код КАК ТабельныйНомер
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаАктуальности, ФизЛицо = &ФизЛицо) КАК ФИОФизЛицСрезПоследних
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|				&ДатаАктуальности,
	|				Сотрудник.ФизЛицо = &ФизЛицо
	|					И Организация = &ГоловнаяОрганизация
	|					И Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство)) КАК РаботникиОрганизацииСрезПоследних
	|		ПО (ВЫБОР
	|				КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &ДатаАктуальности
	|						И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА РаботникиОрганизацииСрезПоследних.ОбособленноеПодразделениеЗавершения
	|				ИНАЧЕ РаботникиОрганизацииСрезПоследних.ОбособленноеПодразделение
	|			КОНЕЦ = &Организация)
	|ГДЕ
	|	ФизическиеЛица.Ссылка = &Физлицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	РаботникиОрганизацииСрезПоследних.Период УБЫВ";
	
	ВыборкаРезультата	=	Запрос.Выполнить().Выбрать();
	
	ДокументРезультат.Очистить();
	Макет 		 = ПолучитьМакет("КарточкаЛицевогоСчета");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	Если ВыборкаРезультата.Следующий() Тогда
		
		ОбластьШапка.Параметры.Заполнить(ВыборкаРезультата);
		
		// Организация
		ОбластьШапка.Параметры.Организация	= Организация.Наименование;
	
		
		ОбластьШапка.Параметры.Год	 = Год;
				
	КонецЕсли;	

	ДокументРезультат.Вывести(ОбластьШапка);
	
	СоциальныеВычеты = Новый Массив;
	СоциальныеВычеты.Добавить(Справочники.ВычетыНДФЛ.Код640);
	СоциальныеВычеты.Добавить(Справочники.ВычетыНДФЛ.Код640страх);
	Пособия = Новый Массив;
	Пособия.Добавить(Справочники.ДоходыЕСН.БольничныйИзФСЗН);
	Пособия.Добавить(Справочники.ДоходыЕСН.ПособияИзФСЗН);
	Запрос.УстановитьПараметр("Пособия",Пособия);
	Запрос.УстановитьПараметр("ПоДоговору",Перечисления.СпособыРасчетаОплатыТруда.ПоДоговоруФиксированнойСуммой);
	Запрос.УстановитьПараметр("НеОблагается",Перечисления.НДФЛСтавкиНалогообложенияРезидента.НеОблагается);
	Запрос.УстановитьПараметр("Имущественный",Справочники.ВычетыНДФЛ.Код650);
    Запрос.УстановитьПараметр("Социальный",СоциальныеВычеты);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",ОбщегоНазначения.ГоловнаяОрганизация(Организация));


	
	Запрос.УстановитьПараметр("ДатаАктуальностиС",	Дата(Год, 01, 01));
	Запрос.УстановитьПараметр("ДатаАктуальностиПо",	Дата(Год, 12, 31, 23, 59, 59));

	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НАЧАЛОПЕРИОДА(РегламентированныйПроизводственныйКалендарь.ДатаКалендаря, МЕСЯЦ) КАК Месяц
	|ПОМЕСТИТЬ ВТПериоды
	|ИЗ
	|	РегистрСведений.РегламентированныйПроизводственныйКалендарь КАК РегламентированныйПроизводственныйКалендарь
	|ГДЕ
	|	РегламентированныйПроизводственныйКалендарь.ДатаКалендаря МЕЖДУ &ДатаАктуальностиС И &ДатаАктуальностиПо";
	
	Запрос.Выполнить();

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ОсновныеНачисленияРаботниковОрганизаций.ПериодДействия, МЕСЯЦ) КАК Месяц,
	|	СУММА(ОсновныеНачисленияРаботниковОрганизаций.Результат) КАК ОсновноеНачисление
	|ПОМЕСТИТЬ ВТОсновныеНачисления
	|ИЗ
	|	РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисленияРаботниковОрганизаций
	|ГДЕ
	|	ОсновныеНачисленияРаботниковОрганизаций.ФизЛицо = &ФизЛицо
	|	И ОсновныеНачисленияРаботниковОрганизаций.ПериодДействия МЕЖДУ &ДатаАктуальностиС И &ДатаАктуальностиПо
	|	И ОсновныеНачисленияРаботниковОрганизаций.ВидРасчета.КодДоходаЕСН НЕ В (&Пособия)
	|	И ОсновныеНачисленияРаботниковОрганизаций.ВидРасчета.СпособРасчета <> &ПоДоговору
	|	И ОсновныеНачисленияРаботниковОрганизаций.Организация = &ГоловнаяОрганизация
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ОсновныеНачисленияРаботниковОрганизаций.ПериодДействия, МЕСЯЦ)";
	
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(ДополнительныеНачисленияРаботниковОрганизаций.ПериодРегистрации, МЕСЯЦ) КАК Месяц,
	|	СУММА(ДополнительныеНачисленияРаботниковОрганизаций.Результат) КАК ДополнительноеНачисление
	|ПОМЕСТИТЬ ВТДополнительныеНачисления
	|ИЗ
	|	РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК ДополнительныеНачисленияРаботниковОрганизаций
	|ГДЕ
	|	ДополнительныеНачисленияРаботниковОрганизаций.ФизЛицо = &ФизЛицо
	|	И ДополнительныеНачисленияРаботниковОрганизаций.ПериодРегистрации МЕЖДУ &ДатаАктуальностиС И &ДатаАктуальностиПо
	|	И ДополнительныеНачисленияРаботниковОрганизаций.ВидРасчета.СпособРасчета <> &ПоДоговору
	|	И НЕ ДополнительныеНачисленияРаботниковОрганизаций.ВидРасчета.КодДоходаЕСН В (&Пособия)
	|	И ДополнительныеНачисленияРаботниковОрганизаций.Организация = &ГоловнаяОрганизация
	|	И НЕ ДополнительныеНачисленияРаботниковОрганизаций.ВидРасчета.ЯвляетсяДоходомВНатуральнойФорме
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ДополнительныеНачисленияРаботниковОрганизаций.ПериодРегистрации, МЕСЯЦ)";
	
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(НачисленияРаботниковОрганизаций.Месяц, МЕСЯЦ) КАК Месяц,
	|	СУММА(НачисленияРаботниковОрганизаций.Пособие) КАК Пособие
	|ПОМЕСТИТЬ ВТПособия
	|ИЗ
	|	(ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ОсновныеНачисленияРаботниковОрганизаций.ПериодДействия, МЕСЯЦ) КАК Месяц,
	|		ОсновныеНачисленияРаботниковОрганизаций.Результат КАК Пособие
	|	ИЗ
	|		РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисленияРаботниковОрганизаций
	|	ГДЕ
	|		ОсновныеНачисленияРаботниковОрганизаций.ФизЛицо = &ФизЛицо
	|		И ОсновныеНачисленияРаботниковОрганизаций.ПериодДействия МЕЖДУ &ДатаАктуальностиС И &ДатаАктуальностиПо
	|		И ОсновныеНачисленияРаботниковОрганизаций.ВидРасчета.КодДоходаЕСН В (&Пособия)
	|		И ОсновныеНачисленияРаботниковОрганизаций.Организация = &ГоловнаяОрганизация
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ДополнительныеНачисленияРаботниковОрганизаций.ПериодРегистрации, МЕСЯЦ),
	|		ДополнительныеНачисленияРаботниковОрганизаций.Результат
	|	ИЗ
	|		РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК ДополнительныеНачисленияРаботниковОрганизаций
	|	ГДЕ
	|		ДополнительныеНачисленияРаботниковОрганизаций.ФизЛицо = &ФизЛицо
	|		И ДополнительныеНачисленияРаботниковОрганизаций.ПериодРегистрации МЕЖДУ &ДатаАктуальностиС И &ДатаАктуальностиПо
	|		И ДополнительныеНачисленияРаботниковОрганизаций.ВидРасчета.КодДоходаЕСН В (&Пособия)
	|		И ДополнительныеНачисленияРаботниковОрганизаций.Организация = &ГоловнаяОрганизация) КАК НачисленияРаботниковОрганизаций
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(НачисленияРаботниковОрганизаций.Месяц, МЕСЯЦ)";
	
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(НачисленияРаботниковОрганизаций.Месяц, МЕСЯЦ) КАК Месяц,
	|	СУММА(НачисленияРаботниковОрганизаций.Договор) КАК Договор
	|ПОМЕСТИТЬ ВТДоговора
	|ИЗ
	|	(ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ОсновныеНачисленияРаботниковОрганизаций.ПериодДействия, МЕСЯЦ) КАК Месяц,
	|		ОсновныеНачисленияРаботниковОрганизаций.Результат КАК Договор
	|	ИЗ
	|		РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК ОсновныеНачисленияРаботниковОрганизаций
	|	ГДЕ
	|		ОсновныеНачисленияРаботниковОрганизаций.ФизЛицо = &ФизЛицо
	|		И ОсновныеНачисленияРаботниковОрганизаций.ПериодДействия МЕЖДУ &ДатаАктуальностиС И &ДатаАктуальностиПо
	|		И ОсновныеНачисленияРаботниковОрганизаций.ВидРасчета.КодДоходаЕСН НЕ В (&Пособия)
	|		И ОсновныеНачисленияРаботниковОрганизаций.ВидРасчета.СпособРасчета = &ПоДоговору
	|		И ОсновныеНачисленияРаботниковОрганизаций.Организация = &ГоловнаяОрганизация
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НАЧАЛОПЕРИОДА(ДополнительныеНачисленияРаботниковОрганизаций.ПериодРегистрации, МЕСЯЦ),
	|		ДополнительныеНачисленияРаботниковОрганизаций.Результат
	|	ИЗ
	|		РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций КАК ДополнительныеНачисленияРаботниковОрганизаций
	|	ГДЕ
	|		ДополнительныеНачисленияРаботниковОрганизаций.ФизЛицо = &ФизЛицо
	|		И ДополнительныеНачисленияРаботниковОрганизаций.ПериодРегистрации МЕЖДУ &ДатаАктуальностиС И &ДатаАктуальностиПо
	|		И ДополнительныеНачисленияРаботниковОрганизаций.ВидРасчета.КодДоходаЕСН НЕ В (&Пособия)
	|		И ДополнительныеНачисленияРаботниковОрганизаций.ВидРасчета.СпособРасчета = &ПоДоговору
	|		И ДополнительныеНачисленияРаботниковОрганизаций.Организация = &ГоловнаяОрганизация) КАК НачисленияРаботниковОрганизаций
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(НачисленияРаботниковОрганизаций.Месяц, МЕСЯЦ)";
	
	Запрос.Выполнить();
	
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НДФЛСведенияОДоходах.ПериодРегистрации КАК Месяц,
	|	СУММА(ЕСТЬNULL(НДФЛСведенияОДоходах.СуммаВычета, 0) + ВЫБОР
	|			КОГДА НДФЛСведенияОДоходах.КодДохода.СтавкаНалогообложенияРезидента = &НеОблагается
	|				ТОГДА ЕСТЬNULL(НДФЛСведенияОДоходах.СуммаДохода, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК НеОблагаемаяБаза
	|ПОМЕСТИТЬ ВТНеОблагаемаяБаза
	|ИЗ
	|	РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
	|ГДЕ
	|	НДФЛСведенияОДоходах.ФизЛицо = &ФизЛицо
	|	И НДФЛСведенияОДоходах.ПериодРегистрации МЕЖДУ &ДатаАктуальностиС И &ДатаАктуальностиПо
	|	И НДФЛСведенияОДоходах.Организация = &ГоловнаяОрганизация
	|
	|СГРУППИРОВАТЬ ПО
	|	НДФЛСведенияОДоходах.ПериодРегистрации";
	
	Запрос.Выполнить();
			
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода КАК Месяц,
	|	СУММА(ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПримененныйВычетЛичный, 0) + ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПримененныйВычетНаДетей, 0) + ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПримененныйВычетНаДетейЛьгота, 0) + ЕСТЬNULL(НДФЛРасчетыСБюджетом.ПримененныйДополнительныйВычет, 0)) КАК СтандартныеВычеты,
	|	СУММА(ЕСТЬNULL(НДФЛРасчетыСБюджетом.Налог, 0)) КАК Налог
	|ПОМЕСТИТЬ ВТВычетыИНалог
	|ИЗ
	|	РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
	|ГДЕ
	|	НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода МЕЖДУ &ДатаАктуальностиС И &ДатаАктуальностиПо
	|	И НДФЛРасчетыСБюджетом.ФизЛицо = &ФизЛицо
	|	И НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация
	|
	|СГРУППИРОВАТЬ ПО
	|	НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода";
	
	Запрос.Выполнить();
    	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(НДФЛИмущественныеВычетыФизлиц.Период, МЕСЯЦ) КАК Месяц,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛИмущественныеВычетыФизлиц.КодВычетаИмущественный В (&Социальный)
	|				ТОГДА ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.Размер, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Социальный,
	|	СУММА(ВЫБОР
	|			КОГДА НДФЛИмущественныеВычетыФизлиц.КодВычетаИмущественный В (&Имущественный)
	|				ТОГДА ЕСТЬNULL(НДФЛИмущественныеВычетыФизлиц.Размер, 0)
	|			ИНАЧЕ 0
	|		КОНЕЦ) КАК Имущественный
	|ПОМЕСТИТЬ ВТСоциальныеИИмущественные
	|ИЗ
	|	РегистрНакопления.НДФЛИмущественныеВычетыФизлиц КАК НДФЛИмущественныеВычетыФизлиц
	|ГДЕ
	|	НДФЛИмущественныеВычетыФизлиц.ФизЛицо = &ФизЛицо
	|	И НДФЛИмущественныеВычетыФизлиц.Период МЕЖДУ &ДатаАктуальностиС И &ДатаАктуальностиПо
	|	И НДФЛИмущественныеВычетыФизлиц.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И НДФЛИмущественныеВычетыФизлиц.Организация = &ГоловнаяОрганизация
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(НДФЛИмущественныеВычетыФизлиц.Период, МЕСЯЦ)";
	
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСНИсчисленный.ПериодВзаиморасчетов КАК Месяц,
	|	СУММА(ЕСТЬNULL(ЕСНИсчисленный.ПенсионныйФонд, 0)) КАК ПФ
	|ПОМЕСТИТЬ ВТПенсионныйФонд
	|ИЗ
	|	РегистрНакопления.ЕСНИсчисленный КАК ЕСНИсчисленный
	|ГДЕ
	|	ЕСНИсчисленный.ФизЛицо = &ФизЛицо
	|	И ЕСНИсчисленный.ПериодВзаиморасчетов МЕЖДУ &ДатаАктуальностиС И &ДатаАктуальностиПо
	|	И ЕСНИсчисленный.Организация = &ГоловнаяОрганизация
	|
	|СГРУППИРОВАТЬ ПО
	|	ЕСНИсчисленный.ПериодВзаиморасчетов";
	
	Запрос.Выполнить();

		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	УдержанияРаботниковОрганизаций.ПериодРегистрации КАК Месяц,
	|	СУММА(УдержанияРаботниковОрганизаций.Результат) КАК Удержания
	|ПОМЕСТИТЬ ВТУдержания
	|ИЗ
	|	РегистрРасчета.УдержанияРаботниковОрганизаций КАК УдержанияРаботниковОрганизаций
	|ГДЕ
	|	УдержанияРаботниковОрганизаций.ФизЛицо = &ФизЛицо
	|	И УдержанияРаботниковОрганизаций.ПериодРегистрации МЕЖДУ &ДатаАктуальностиС И &ДатаАктуальностиПо
	|	И УдержанияРаботниковОрганизаций.Организация = &ГоловнаяОрганизация
	|
	|СГРУППИРОВАТЬ ПО
	|	УдержанияРаботниковОрганизаций.ПериодРегистрации"; 
	
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СУММА(ПогашениеЗаймовРаботниками.ОсновнойДолг) КАК Займ,
	|	НАЧАЛОПЕРИОДА(ПогашениеЗаймовРаботниками.Период, МЕСЯЦ) КАК Месяц
	|ПОМЕСТИТЬ ВТЗаймы
	|ИЗ
	|	РегистрНакопления.ПогашениеЗаймовРаботникамиОрганизаций КАК ПогашениеЗаймовРаботниками
	|ГДЕ
	|	ПогашениеЗаймовРаботниками.ФизЛицо = &ФизЛицо
	|	И ПогашениеЗаймовРаботниками.УдержаноИзЗарплаты
	|	И ПогашениеЗаймовРаботниками.Организация = &ГоловнаяОрганизация
	|
	|СГРУППИРОВАТЬ ПО
	|	НАЧАЛОПЕРИОДА(ПогашениеЗаймовРаботниками.Период, МЕСЯЦ)";	
	
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Периоды.Месяц КАК Месяц,
	|	СУММА(ЕСТЬNULL(ОсновныеНачисления.ОсновноеНачисление, 0)) КАК ОсновныеНачисления,
	|	СУММА(ЕСТЬNULL(ДополнительныеНачисления.ДополнительноеНачисление, 0)) КАК ДополнительныеНачисления,
	|	СУММА(ЕСТЬNULL(Пособия.Пособие, 0)) КАК Пособия,
	|	СУММА(ЕСТЬNULL(Договора.Договор, 0)) КАК Договора,
	|	СУММА(ЕСТЬNULL(ТНеОблагаемаяБаза.НеОблагаемаяБаза, 0)) КАК НеОблагаемаяБаза,
	|	СУММА(ЕСТЬNULL(ВычетыИНалог.СтандартныеВычеты, 0)) КАК ВычетыСтандартные,
	|	СУММА(ЕСТЬNULL(СоциальныеИИмущественные.Социальный, 0)) КАК ВычетыСоциальные,
	|	СУММА(ЕСТЬNULL(СоциальныеИИмущественные.Имущественный, 0)) КАК ВычетыИмущественные,
	|	СУММА(ЕСТЬNULL(ВычетыИНалог.Налог, 0)) КАК Налог,
	|	СУММА(ЕСТЬNULL(ТУдержания.Удержания, 0) + ЕСТЬNULL(Займы.Займ, 0)) КАК Удержания,
	|	СУММА(ЕСТЬNULL(ПенсионныйФонд.ПФ, 0)) КАК ПФ,
	|	СУММА(ЕСТЬNULL(ВычетыИНалог.Налог, 0) + ЕСТЬNULL(ТУдержания.Удержания, 0) + ЕСТЬNULL(ПенсионныйФонд.ПФ, 0)+ ЕСТЬNULL(Займы.Займ, 0)) КАК ИтогоУдержаноЗаМесяц,
	|	СУММА(ЕСТЬNULL(ОсновныеНачисления.ОсновноеНачисление, 0) + ЕСТЬNULL(ДополнительныеНачисления.ДополнительноеНачисление, 0) + ЕСТЬNULL(Пособия.Пособие, 0) + ЕСТЬNULL(Договора.Договор, 0)) КАК ИтогоЗаМесяц
	|ИЗ
	|	ВТПериоды КАК Периоды
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТОсновныеНачисления КАК ОсновныеНачисления
	|		ПО Периоды.Месяц = ОсновныеНачисления.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДополнительныеНачисления КАК ДополнительныеНачисления
	|		ПО Периоды.Месяц = ДополнительныеНачисления.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПособия КАК Пособия
	|		ПО Периоды.Месяц = Пособия.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДоговора КАК Договора
	|		ПО Периоды.Месяц = Договора.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНеОблагаемаяБаза КАК ТНеОблагаемаяБаза
	|		ПО Периоды.Месяц = ТНеОблагаемаяБаза.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТВычетыИНалог КАК ВычетыИНалог
	|		ПО Периоды.Месяц = ВычетыИНалог.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСоциальныеИИмущественные КАК СоциальныеИИмущественные
	|		ПО Периоды.Месяц = СоциальныеИИмущественные.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТУдержания КАК ТУдержания
	|		ПО Периоды.Месяц = ТУдержания.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПенсионныйФонд КАК ПенсионныйФонд
	|		ПО Периоды.Месяц = ПенсионныйФонд.Месяц
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТЗаймы КАК Займы
	|		ПО Периоды.Месяц = Займы.Месяц
	|
	|СГРУППИРОВАТЬ ПО
	|	Периоды.Месяц
	|
	|УПОРЯДОЧИТЬ ПО
	|	Месяц";
	
	ВыборкаРезультата = Запрос.Выполнить().Выбрать();
	
	ИтогоОсновныеНачисления  = 0;
	ИтогоДополнительныеНачисления  = 0;
	ИтогоПособия = 0;
	ИтогоДоговора = 0;
	ИтогоЗаГод = 0;
	ИтогоНеОблагаемаяБаза = 0;
	ИтогоВычетыСтандартные = 0;
	ИтогоВычетыСоциальныеИмущественные = 0;
	ИтогоНалоговаяБаза = 0;
	ИтогоНалог = 0;
	ИтогоУдержания = 0;
	ИтогоПФ = 0;
	ИтогоУдержаноЗаГоД = 0;
	ИтогоКВыплате = 0;
	
	Пока ВыборкаРезультата.Следующий() Цикл
		Если ЗначениеЗаполнено(ВыборкаРезультата.ОсновныеНачисления) 
			ИЛИ ЗначениеЗаполнено(ВыборкаРезультата.ДополнительныеНачисления)
		    ИЛИ ЗначениеЗаполнено(ВыборкаРезультата.Пособия)
			ИЛИ ЗначениеЗаполнено(ВыборкаРезультата.Договора) тогда
			
			НалоговаяБаза = ВыборкаРезультата.ИтогоЗаМесяц - ВыборкаРезультата.НеОблагаемаяБаза - ВыборкаРезультата.ВычетыСтандартные - ВыборкаРезультата.ВычетыСоциальные - ВыборкаРезультата.ВычетыИмущественные;
			КВыплате = ВыборкаРезультата.ИтогоЗаМесяц - ВыборкаРезультата.ИтогоУдержаноЗаМесяц;
			
			ОбластьСтрока.Параметры.Заполнить(ВыборкаРезультата);
			ОбластьСтрока.Параметры.ВычетыСоциальныеИмущественные = ВыборкаРезультата.ВычетыСоциальные + ВыборкаРезультата.ВычетыИмущественные;
			ОбластьСтрока.Параметры.НалоговаяБаза = НалоговаяБаза;
			ОбластьСтрока.Параметры.КВыплате = КВыплате;
			ДокументРезультат.Вывести(ОбластьСтрока);
			ИтогоОсновныеНачисления       = ИтогоОсновныеНачисления + ВыборкаРезультата.ОсновныеНачисления;
			ИтогоДополнительныеНачисления = ИтогоДополнительныеНачисления +  ВыборкаРезультата.ДополнительныеНачисления;
			ИтогоПособия  				  = ИтогоПособия + ВыборкаРезультата.Пособия;
			ИтогоДоговора 				  = ИтогоДоговора +  ВыборкаРезультата.Договора;
			ИтогоЗаГод 					  = ИтогоЗаГод + ВыборкаРезультата.ИтогоЗаМесяц;
			ИтогоНеОблагаемаяБаза 		  = ИтогоНеОблагаемаяБаза + ВыборкаРезультата.НеОблагаемаяБаза;
			ИтогоВычетыСтандартные		  = ИтогоВычетыСтандартные + ВыборкаРезультата.ВычетыСтандартные;
			ИтогоНалоговаяБаза 			  = ИтогоНалоговаяБаза + НалоговаяБаза;
			ИтогоНалог 					  = ИтогоНалог + ВыборкаРезультата.Налог;
			ИтогоУдержания                = ИтогоУдержания + ВыборкаРезультата.Удержания;
			ИтогоПФ			              = ИтогоПФ + ВыборкаРезультата.ПФ;
			ИтогоУдержаноЗаГоД 			  = ИтогоУдержаноЗаГоД + ВыборкаРезультата.ИтогоУдержаноЗаМесяц;
			ИтогоКВыплате 				  = ИтогоКВыплате + КВыплате;
			ИтогоВычетыСоциальныеИмущественные = ИтогоВычетыСоциальныеИмущественные + ВыборкаРезультата.ВычетыСоциальные + ВыборкаРезультата.ВычетыИмущественные;
   		КонецЕсли;
	КонецЦикла;
	
	ОбластьПодвал.Параметры.ИтогоОсновныеНачисления 	       = ИтогоОсновныеНачисления;
	ОбластьПодвал.Параметры.ИтогоДополнительныеНачисления      = ИтогоДополнительныеНачисления;
	ОбластьПодвал.Параметры.ИтогоПособия 				       = ИтогоПособия;
	ОбластьПодвал.Параметры.ИтогоДоговора				  	   = ИтогоДоговора;
	ОбластьПодвал.Параметры.ИтогоЗаГод 					  	   = ИтогоЗаГод;
	ОбластьПодвал.Параметры.ИтогоНеОблагаемаяБаза 		  	   = ИтогоНеОблагаемаяБаза;
	ОбластьПодвал.Параметры.ИтогоВычетыСтандартные		 	   = ИтогоВычетыСтандартные;
	ОбластьПодвал.Параметры.ИтогоВычетыСоциальныеИмущественные = ИтогоВычетыСоциальныеИмущественные;
	ОбластьПодвал.Параметры.ИтогоНалоговаяБаза 			       = ИтогоНалоговаяБаза;
	ОбластьПодвал.Параметры.ИтогоНалог 					       = ИтогоНалог;
	ОбластьПодвал.Параметры.ИтогоУдержания                     = ИтогоУдержания;
	ОбластьПодвал.Параметры.ИтогоПФ                    		   = ИтогоПФ;
	ОбластьПодвал.Параметры.ИтогоУдержаноЗаГоД 			       = ИтогоУдержаноЗаГоД;
	ОбластьПодвал.Параметры.ИтогоКВыплате 				       = ИтогоКВыплате;
			
	ДокументРезультат.Вывести(ОбластьПодвал);
КонецПроцедуры	

#КонецЕсли

