#Если Клиент Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем НП Экспорт;  				  // Настройка периода

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ПолучитьРезультатЗапроса()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Счет70",       ПланыСчетов.Налоговый.РасчетыСПерсоналомПоОплатеТруда);
	Запрос.УстановитьПараметр("Счет97",       ПланыСчетов.Налоговый.РасходыНаОплатуТрудаБудущихПериодов);
	Запрос.УстановитьПараметр("Счет07",       ПланыСчетов.Налоговый.ОборудованиеКУстановке);
	Запрос.УстановитьПараметр("Счет08",       ПланыСчетов.Налоговый.ВложенияВоВнеоборотныеАктивы);
	Запрос.УстановитьПараметр("Счет08_05",    ПланыСчетов.Налоговый.ПриобретениеНематериальныхАктивов);
	Запрос.УстановитьПараметр("Счет08_08",    ПланыСчетов.Налоговый.ВыполнениеНИОКР);
	Запрос.УстановитьПараметр("Счет10",       ПланыСчетов.Налоговый.Материалы);
	Запрос.УстановитьПараметр("Счет41",       ПланыСчетов.Налоговый.Товары);

	Запрос.УстановитьПараметр("Счет58_01_1",  ПланыСчетов.Налоговый.Паи);
	Счет58_02 = Новый Массив;
	Счет58_02.Добавить(ПланыСчетов.Налоговый.Акции);
	Счет58_02.Добавить(ПланыСчетов.Налоговый.ДолговыеЦенныеБумаги);
	Запрос.УстановитьПараметр("Счет58_02",  Счет58_02);
	Запрос.УстановитьПараметр("Счет58_05",  ПланыСчетов.Налоговый.ПриобретенныеПрава);

	Запрос.УстановитьПараметр("Счет44_01",  ПланыСчетов.Налоговый.ИздержкиОбращения);
	
	СчетКосвенныхРасходов = Новый Массив;                          
	СчетКосвенныхРасходов.Добавить(ПланыСчетов.Налоговый.РасходыНаПродажу);							 // Счет44
	СчетКосвенныхРасходов.Добавить(ПланыСчетов.Налоговый.КосвенныеРасходыОсновногоПроизводства);	 // Счет20_01_02
	СчетКосвенныхРасходов.Добавить(ПланыСчетов.Налоговый.КосвенныеРасходыВспомогательныхПроизводств);// Счет23_02
	СчетКосвенныхРасходов.Добавить(ПланыСчетов.Налоговый.КосвенныеОбщепроизводственныеРасходы);		 // Счет25_02
	СчетКосвенныхРасходов.Добавить(ПланыСчетов.Налоговый.КосвенныеОбщехозяйственныеРасходы);		 // Счет26_02
	СчетКосвенныхРасходов.Добавить(ПланыСчетов.Налоговый.КосвенныеРасходыПоВыявленномуБраку);		 // Счет28_02
	СчетКосвенныхРасходов.Добавить(ПланыСчетов.Налоговый.КосвенныеРасходыОбслуживающихПроизводств);	 // Счет29_02
	Запрос.УстановитьПараметр("СчетКосвенныхРасходов",СчетКосвенныхРасходов);
	
	СчетРасходов = Новый Массив;
	СчетРасходов.Добавить(ПланыСчетов.Налоговый.РасходыНаПродажу);			// Счет44
	СчетРасходов.Добавить(ПланыСчетов.Налоговый.ОсновноеПроизводство_);		// Счет20
	СчетРасходов.Добавить(ПланыСчетов.Налоговый.ВспомогательныеПроизводства); // Счет23
	СчетРасходов.Добавить(ПланыСчетов.Налоговый.ОбщепроизводственныеРасходы); // Счет25
	СчетРасходов.Добавить(ПланыСчетов.Налоговый.ОбщехозяйственныеРасходы);	// Счет26
	СчетРасходов.Добавить(ПланыСчетов.Налоговый.БракВПроизводстве);			// Счет28
	СчетРасходов.Добавить(ПланыСчетов.Налоговый.ОбслуживающиеПроизводства);	// Счет29
	СчетРасходов.Добавить(ПланыСчетов.Налоговый.ПрочиеРасходы);				// Счет91_02
	Запрос.УстановитьПараметр("СчетРасходов",СчетРасходов);
	
	СчетПрямыхРасходов = Новый Массив;
    СчетПрямыхРасходов.Добавить(ПланыСчетов.Налоговый.ПрямыеРасходыОсновногоПроизводства);		// Счет20_01_01
    СчетПрямыхРасходов.Добавить(ПланыСчетов.Налоговый.ПрямыеРасходыВспомогательныхПроизводств); // Счет23_01
    СчетПрямыхРасходов.Добавить(ПланыСчетов.Налоговый.ПрямыеОбщепроизводственныеРасходы);		// Счет25_01
    СчетПрямыхРасходов.Добавить(ПланыСчетов.Налоговый.ПрямыеРасходыПоВыявленномуБраку);			// Счет28_01
    СчетПрямыхРасходов.Добавить(ПланыСчетов.Налоговый.ПрямыеРасходыОбслуживающихПроизводств);	// Счет29_01
	Запрос.УстановитьПараметр("СчетПрямыхРасходов",СчетПрямыхРасходов);

	Запрос.УстановитьПараметр("Счет91_02_1",  ПланыСчетов.Налоговый.РасходыСвязанныеСРеализациейОсновныхСредств);
	Запрос.УстановитьПараметр("Счет91_02_2",  ПланыСчетов.Налоговый.РасходыСвязанныеСРеализациейНематериальныхАктивов);
	Запрос.УстановитьПараметр("Счет91_02_3",  ПланыСчетов.Налоговый.РасходыСвязанныеСРеализациейПрочегоИмущества);
	Запрос.УстановитьПараметр("Счет91_02_4",  ПланыСчетов.Налоговый.РасходыСвязанныеСРеализациейИмущественныхПрав);
	Запрос.УстановитьПараметр("Счет91_02_5",  ПланыСчетов.Налоговый.РасходыСвязанныеСРеализациейЦенныхБумаг);
	Запрос.УстановитьПараметр("Счет91_02_6",  ПланыСчетов.Налоговый.Внереализационные_Расходы);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Налоговый.Ссылка,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&Счет07)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Счет07,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&Счет08)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Счет08,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&Счет08_05)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Счет08_05,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&Счет08_08)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Счет08_08,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&Счет10)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Счет10,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&Счет41)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Счет41,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&Счет58_01_1)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Счет58_01_1,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&Счет58_02)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Счет58_02,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&Счет58_05)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Счет58_05,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&Счет44_01)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Счет44_01,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&СчетКосвенныхРасходов)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетКосвенныхРасходов,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&СчетРасходов)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетРасходов,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&СчетПрямыхРасходов)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СчетПрямыхРасходов,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&Счет91_02_1)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Счет91_02_1,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&Счет91_02_2)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Счет91_02_2,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&Счет91_02_3)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Счет91_02_3,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&Счет91_02_4)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Счет91_02_4,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&Счет91_02_5)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Счет91_02_5,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&Счет91_02_6)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Счет91_02_6,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&Счет70)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Счет70,
	|	ВЫБОР
	|		КОГДА Налоговый.Ссылка В ИЕРАРХИИ (&Счет97)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Счет97
	|ИЗ
	|	ПланСчетов.Налоговый КАК Налоговый";
	ВыборкаСчетов = Запрос.Выполнить().Выбрать();
	Счета70 = Новый Массив;
	Счета97 = Новый Массив;
	Счета07 = Новый Массив;
	Счета08 = Новый Массив;
	Счета08_05 = Новый Массив;
	Счета08_08 = Новый Массив;
	Счета10 = Новый Массив;
	Счета41 = Новый Массив;
	Счета58_01_1 = Новый Массив;
	Счета58_02 = Новый Массив;
	Счета58_05 = Новый Массив;
	Счета44_01 = Новый Массив;
	СчетаКосвенныхРасходов = Новый Массив;
	СчетаРасходов = Новый Массив;
	СчетаПрямыхРасходов = Новый Массив;
	Счета91_02_1 = Новый Массив;
	Счета91_02_2 = Новый Массив;
	Счета91_02_3 = Новый Массив;
	Счета91_02_4 = Новый Массив;
	Счета91_02_5 = Новый Массив;
	Счета91_02_6 = Новый Массив;
	Пока ВыборкаСчетов.Следующий() Цикл
		Если ВыборкаСчетов.Счет91_02_1 Тогда
			Счета91_02_1.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.Счет91_02_2 Тогда
			Счета91_02_2.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.Счет91_02_3 Тогда
			Счета91_02_3.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.Счет91_02_4 Тогда
			Счета91_02_4.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.Счет91_02_5 Тогда
			Счета91_02_5.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.Счет91_02_6 Тогда
			Счета91_02_6.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.СчетРасходов Тогда
			СчетаРасходов.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.СчетПрямыхРасходов Тогда
			СчетаПрямыхРасходов.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.СчетКосвенныхРасходов Тогда
			СчетаКосвенныхРасходов.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.Счет44_01 Тогда
			Счета44_01.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.Счет58_01_1 Тогда
			Счета58_01_1.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.Счет58_02 Тогда
			Счета58_02.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.Счет58_05 Тогда
			Счета58_05.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.Счет41 Тогда
			Счета41.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.Счет10 Тогда
			Счета10.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.Счет07 Тогда
			Счета07.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.Счет08 Тогда
			Счета08.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.Счет08_05 Тогда
			Счета08_05.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.Счет08_08 Тогда
			Счета08_08.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.Счет70 Тогда
			Счета70.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
		Если ВыборкаСчетов.Счет97 Тогда
			Счета97.Добавить(ВыборкаСчетов.Ссылка)
		КонецЕсли;
	КонецЦикла;
		
	СписокВидов = Новый Массив;
	Для каждого ЭлементСписка из ВидыРасходов Цикл
		Если ЭлементСписка.Пометка Тогда
			СписокВидов.Добавить(ЭлементСписка.Значение);                       
		КонецЕсли;
	КонецЦикла;
	СписокВидов.Добавить("<Вид расходов не указан>");                       
	
	Запрос = Новый Запрос;

	СтатьиРБПНалогиСФОТ = Новый Массив;
	СтатьиРБПНалогиСФОТ.Добавить(Справочники.РасходыБудущихПериодов.РБПНаЕСН);
	//СтатьиРБПНалогиСФОТ.Добавить(Справочники.РасходыБудущихПериодов.РБПНаПФР);
	СтатьиРБПНалогиСФОТ.Добавить(Справочники.РасходыБудущихПериодов.РБПНаФСС);
	Запрос.УстановитьПараметр("СтатьиРБПНалогиСФОТ",СтатьиРБПНалогиСФОТ);
	
	Запрос.УстановитьПараметр("ДатаНач",			НачалоДня(ДатаНач));
	Запрос.УстановитьПараметр("ДатаКон",			КонецДня(ДатаКон));
	Запрос.УстановитьПараметр("Организация",		Организация);
	Запрос.УстановитьПараметр("СчетаРасходов",		СчетаРасходов);
	Запрос.УстановитьПараметр("СчетаПрямыхРасходов",СчетаПрямыхРасходов);
	ОтдельныеКосвенныеРасходы = Новый Массив;
	ОтдельныеКосвенныеРасходы.Добавить(Перечисления.ВидыРасходовНУ.ПредставительскиеРасходы);
	ОтдельныеКосвенныеРасходы.Добавить(Перечисления.ВидыРасходовНУ.РасходыНаРекламуНормируемые);
	ОтдельныеКосвенныеРасходы.Добавить(Перечисления.ВидыРасходовНУ.НеУчитываемыеВЦеляхНалогообложения);
	Запрос.УстановитьПараметр("ОтдельныеКосвенныеРасходы",ОтдельныеКосвенныеРасходы);
	Запрос.УстановитьПараметр("ПредставительскиеРасходы",Перечисления.ВидыРасходовНУ.ПредставительскиеРасходы);
	Запрос.УстановитьПараметр("РасходыНаРекламуНормируемые",Перечисления.ВидыРасходовНУ.РасходыНаРекламуНормируемые);
	Запрос.УстановитьПараметр("НеУчитываемыеВЦеляхНалогообложения",Перечисления.ВидыРасходовНУ.НеУчитываемыеВЦеляхНалогообложения);
	Запрос.УстановитьПараметр("СчетаКосвенныхРасходов",СчетаКосвенныхРасходов);
	Запрос.УстановитьПараметр("Счета44_01",			Счета44_01);
	Запрос.УстановитьПараметр("Счета58_05",			Счета58_05);
	Запрос.УстановитьПараметр("Счета58_02",			Счета58_02);
	Запрос.УстановитьПараметр("Счета58_01_1",		Счета58_01_1);
	Запрос.УстановитьПараметр("Счета41",			Счета41);
	Запрос.УстановитьПараметр("Счета10",			Счета10);
	Запрос.УстановитьПараметр("Счета08_08",			Счета08_08);
	Запрос.УстановитьПараметр("Счета08_05",			Счета08_05);
	Запрос.УстановитьПараметр("Счета08",			Счета08);
	Запрос.УстановитьПараметр("Счета07",			Счета07);
	Запрос.УстановитьПараметр("Счета70",			Счета70);
	Запрос.УстановитьПараметр("Счета91_02_1",		Счета91_02_1);
	Запрос.УстановитьПараметр("Счета91_02_2",		Счета91_02_2);
	Запрос.УстановитьПараметр("Счета91_02_3",		Счета91_02_3);
	Запрос.УстановитьПараметр("Счета91_02_4",		Счета91_02_4);
	Запрос.УстановитьПараметр("Счета91_02_5",		Счета91_02_5);
	Запрос.УстановитьПараметр("Счета91_02_6",		Счета91_02_6);
	Запрос.УстановитьПараметр("Счета97",			Счета97);
	Запрос.УстановитьПараметр("ПустойСчет",			ПланыСчетов.Налоговый.ПустаяСсылка());
	Запрос.УстановитьПараметр("СписокВидовРасходов",СписокВидов);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДанныеУчета.ДатаОперации КАК ДатаОперации,
	|	ДанныеУчета.Расшифровка,
	|	ДанныеУчета.СчетДт,
	|	ДанныеУчета.СубконтоДт1,
	|	ДанныеУчета.СубконтоДт2,
	|	ДанныеУчета.СубконтоДт3,
	|	ДанныеУчета.Работник КАК Работник,
	|	ДанныеУчета.НаименованиеРасхода,
	|	СУММА(ДанныеУчета.Сумма) КАК Сумма,
	|	ДанныеУчета.ВидРасхода,
	|	ДанныеУчета.ОбъектУчета
	|ИЗ
	|	(ВЫБРАТЬ
	|		НалоговыйОборотыДтКт.Ссылка.ПериодРегистрации КАК ДатаОперации,
	|		НалоговыйОборотыДтКт.Ссылка КАК Расшифровка,
	|		НалоговыйОборотыДтКт.СчетДтНУ КАК СчетДт,
	|		НалоговыйОборотыДтКт.СубконтоДтНУ1 КАК СубконтоДт1,
	|		НалоговыйОборотыДтКт.СубконтоДтНУ2 КАК СубконтоДт2,
	|		НалоговыйОборотыДтКт.СубконтоДтНУ3 КАК СубконтоДт3,
	|		ВЫБОР
	|			КОГДА НалоговыйОборотыДтКт.СчетКтНУ В (&Счета70)
	|				ТОГДА НалоговыйОборотыДтКт.СубконтоКтНУ1.Наименование
	|			ИНАЧЕ НалоговыйОборотыДтКт.СубконтоКтНУ2.Наименование
	|		КОНЕЦ КАК Работник,
	|		ВЫБОР
	|			КОГДА НалоговыйОборотыДтКт.СчетКтНУ В (&Счета70)
	|				ТОГДА НалоговыйОборотыДтКт.СубконтоКтНУ2
	|			ИНАЧЕ НалоговыйОборотыДтКт.СубконтоКтНУ3
	|		КОНЕЦ КАК НаименованиеРасхода,
	|		НалоговыйОборотыДтКт.Сумма КАК Сумма,
	|		ВЫБОР
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ = &ПустойСчет
	|				ТОГДА ""Без расходов""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета07)
	|				ТОГДА ""На приобретение оборудования, требующего монтажа""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета08_05)
	|				ТОГДА ""На приобретение НМА""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета08_08)
	|				ТОГДА ""На НИОКР""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета08)
	|				ТОГДА ""На приобретение ОС""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета10)
	|				ТОГДА ""На приобретение материалов""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета41)
	|				ТОГДА ""На приобретение товаров""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета58_01_1)
	|				ТОГДА ""На приобретение паев""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета58_02)
	|				ТОГДА ""На приобретение ценных бумаг""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета58_05)
	|				ТОГДА ""На приобретение прав требования""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета44_01)
	|				ТОГДА ""Издержки обращения""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&СчетаКосвенныхРасходов)
	|				ТОГДА ВЫБОР
	|						КОГДА ВЫБОР
	|								КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ1 ССЫЛКА Справочник.СтатьиЗатрат
	|									ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ1.ВидРасходовНУ
	|								КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ2 ССЫЛКА Справочник.СтатьиЗатрат
	|									ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ2.ВидРасходовНУ
	|								КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ3 ССЫЛКА Справочник.СтатьиЗатрат
	|									ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ3.ВидРасходовНУ
	|								ИНАЧЕ """"
	|							КОНЕЦ = &ПредставительскиеРасходы
	|							ТОГДА ""Представительские расходы""
	|						КОГДА ВЫБОР
	|								КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ1 ССЫЛКА Справочник.СтатьиЗатрат
	|									ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ1.ВидРасходовНУ
	|								КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ2 ССЫЛКА Справочник.СтатьиЗатрат
	|									ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ2.ВидРасходовНУ
	|								КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ3 ССЫЛКА Справочник.СтатьиЗатрат
	|									ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ3.ВидРасходовНУ
	|								ИНАЧЕ """"
	|							КОНЕЦ = &РасходыНаРекламуНормируемые
	|							ТОГДА ""Расходы на рекламу (нормируемые)""
	|						КОГДА ВЫБОР
	|								КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ1 ССЫЛКА Справочник.СтатьиЗатрат
	|									ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ1.ВидРасходовНУ
	|								КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ2 ССЫЛКА Справочник.СтатьиЗатрат
	|									ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ2.ВидРасходовНУ
	|								КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ3 ССЫЛКА Справочник.СтатьиЗатрат
	|									ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ3.ВидРасходовНУ
	|								ИНАЧЕ """"
	|							КОНЕЦ <> &НеУчитываемыеВЦеляхНалогообложения
	|							ТОГДА ""Косвенные расходы""
	|						ИНАЧЕ ""<Вид расходов не указан>""
	|					КОНЕЦ
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&СчетаПрямыхРасходов)
	|				ТОГДА ""Прямые расходы""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета91_02_1)
	|				ТОГДА ""Расходы на реализацию ОС""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета91_02_2)
	|				ТОГДА ""Расходы на реализацию НМА""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета91_02_3)
	|				ТОГДА ""Расходы на реализацию прочего имущества""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета91_02_4)
	|				ТОГДА ""Расходы, связанные с реализацией имущественных прав""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета91_02_5)
	|				ТОГДА ""Расходы, связанные с реализацией ЦБ""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета91_02_6)
	|				ТОГДА ""Внереализационные расходы""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&СчетаРасходов)
	|					И ВЫБОР
	|						КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ1 ССЫЛКА Справочник.СтатьиЗатрат
	|							ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ1.ВидРасходовНУ
	|						КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ2 ССЫЛКА Справочник.СтатьиЗатрат
	|							ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ2.ВидРасходовНУ
	|						КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ3 ССЫЛКА Справочник.СтатьиЗатрат
	|							ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ3.ВидРасходовНУ
	|						ИНАЧЕ """"
	|					КОНЕЦ = &НеУчитываемыеВЦеляхНалогообложения
	|				ТОГДА ""Расходы, не учитываемые в целях налогообложения""
	|			ИНАЧЕ ""Без расходов""
	|		КОНЕЦ КАК ВидРасхода,
	|		ВЫБОР
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ = &ПустойСчет
	|				ТОГДА """"
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета07)
	|				ТОГДА ""("" + НалоговыйОборотыДтКт.СубконтоДтНУ1.Наименование + "")""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета08_05)
	|				ТОГДА ""("" + НалоговыйОборотыДтКт.СубконтоДтНУ1.Наименование + "")""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета08_08)
	|				ТОГДА ""("" + НалоговыйОборотыДтКт.СубконтоДтНУ1.Наименование + "")""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета08)
	|				ТОГДА ""("" + НалоговыйОборотыДтКт.СубконтоДтНУ1.Наименование + "")""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета10)
	|				ТОГДА ""("" + НалоговыйОборотыДтКт.СубконтоДтНУ1.Наименование + "")""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета41)
	|				ТОГДА ""("" + НалоговыйОборотыДтКт.СубконтоДтНУ1.Наименование + "")""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета58_01_1)
	|				ТОГДА ""("" + НалоговыйОборотыДтКт.СубконтоДтНУ1.Наименование + "")""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета58_02)
	|				ТОГДА ""("" + НалоговыйОборотыДтКт.СубконтоДтНУ1.Наименование + ЕСТЬNULL(""; "" + НалоговыйОборотыДтКт.СубконтоДтНУ2.Наименование, """") + "")""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета58_05)
	|				ТОГДА ""("" + НалоговыйОборотыДтКт.СубконтоДтНУ1.Наименование + ЕСТЬNULL(""; "" + НалоговыйОборотыДтКт.СубконтоДтНУ2.Наименование, """") + "")""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета44_01)
	|				ТОГДА """"
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&СчетаКосвенныхРасходов)
	|				ТОГДА ВЫБОР
	|						КОГДА (НЕ ВЫБОР
	|									КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ1 ССЫЛКА Справочник.СтатьиЗатрат
	|										ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ1.ВидРасходовНУ
	|									КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ2 ССЫЛКА Справочник.СтатьиЗатрат
	|										ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ2.ВидРасходовНУ
	|									КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ3 ССЫЛКА Справочник.СтатьиЗатрат
	|										ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ3.ВидРасходовНУ
	|									ИНАЧЕ """"
	|								КОНЕЦ В (&ОтдельныеКосвенныеРасходы))
	|							ТОГДА ВЫБОР
	|									КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ1 ССЫЛКА Справочник.СтатьиЗатрат
	|										ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ1.ВидРасходовНУ
	|									КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ2 ССЫЛКА Справочник.СтатьиЗатрат
	|										ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ2.ВидРасходовНУ
	|									КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ3 ССЫЛКА Справочник.СтатьиЗатрат
	|										ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ3.ВидРасходовНУ
	|									ИНАЧЕ """"
	|								КОНЕЦ
	|						ИНАЧЕ """"
	|					КОНЕЦ
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&СчетаПрямыхРасходов)
	|				ТОГДА ВЫБОР
	|						КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ1 ССЫЛКА Справочник.СтатьиЗатрат
	|							ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ1.ВидРасходовНУ
	|						КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ2 ССЫЛКА Справочник.СтатьиЗатрат
	|							ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ2.ВидРасходовНУ
	|						КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ3 ССЫЛКА Справочник.СтатьиЗатрат
	|							ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ3.ВидРасходовНУ
	|						ИНАЧЕ """"
	|					КОНЕЦ
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета91_02_1)
	|				ТОГДА ""("" + НалоговыйОборотыДтКт.СубконтоДтНУ2.Наименование + "")""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета91_02_2)
	|				ТОГДА ""("" + НалоговыйОборотыДтКт.СубконтоДтНУ2.Наименование + "")""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета91_02_3)
	|				ТОГДА """"
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета91_02_4)
	|				ТОГДА ""("" + НалоговыйОборотыДтКт.СубконтоДтНУ2.Наименование + ЕСТЬNULL(""; "" + НалоговыйОборотыДтКт.СубконтоДтНУ3.Наименование, """") + "")""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета91_02_5)
	|				ТОГДА ""("" + НалоговыйОборотыДтКт.СубконтоДтНУ2.Наименование + "")""
	|			КОГДА НалоговыйОборотыДтКт.СчетДтНУ В (&Счета91_02_6)
	|				ТОГДА ВЫБОР
	|						КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ1 ССЫЛКА Справочник.СтатьиЗатрат
	|							ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ1.ВидРасходовНУ
	|						КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ2 ССЫЛКА Справочник.СтатьиЗатрат
	|							ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ2.ВидРасходовНУ
	|						КОГДА НалоговыйОборотыДтКт.СубконтоДтНУ3 ССЫЛКА Справочник.СтатьиЗатрат
	|							ТОГДА НалоговыйОборотыДтКт.СубконтоДтНУ3.ВидРасходовНУ
	|						ИНАЧЕ """"
	|					КОНЕЦ
	|			ИНАЧЕ """"
	|		КОНЕЦ КАК ОбъектУчета
	|	ИЗ
	|		Документ.ОтражениеЗарплатыВРеглУчете.ОтражениеВУчете КАК НалоговыйОборотыДтКт
	|	ГДЕ
	|		НалоговыйОборотыДтКт.Ссылка.Организация = &Организация
	|		И НалоговыйОборотыДтКт.Ссылка.Проведен
	|		И НалоговыйОборотыДтКт.Ссылка.ПериодРегистрации МЕЖДУ &ДатаНач И &ДатаКон
	|		И ((НЕ НалоговыйОборотыДтКт.СчетДтНУ В (&Счета97))
	|					И НалоговыйОборотыДтКт.СчетКтНУ В (&Счета70)
	|				ИЛИ НалоговыйОборотыДтКт.СчетКтНУ В (&Счета97))
	|		И (НЕ НалоговыйОборотыДтКт.СубконтоКтНУ1 В (&СтатьиРБПНалогиСФОТ))) КАК ДанныеУчета
	|ГДЕ
	|	ДанныеУчета.ВидРасхода В(&СписокВидовРасходов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеУчета.ДатаОперации,
	|	ДанныеУчета.Расшифровка,
	|	ДанныеУчета.СчетДт,
	|	ДанныеУчета.СубконтоДт1,
	|	ДанныеУчета.СубконтоДт2,
	|	ДанныеУчета.СубконтоДт3,
	|	ДанныеУчета.Работник,
	|	ДанныеУчета.НаименованиеРасхода,
	|	ДанныеУчета.ВидРасхода,
	|	ДанныеУчета.ОбъектУчета
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаОперации,
	|	Работник
	|ИТОГИ ПО
	|	ОБЩИЕ";
	
	Возврат Запрос.Выполнить();

КонецФункции //ПолучитьРезультатЗапроса()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ 
// 

// Выполняет запрос и формирует табличный документ-результат отчета
// в соответствии с настройками, заданными значениями реквизитов отчета.
//
// Параметры:
//	ДокументРезультат - табличный документ, формируемый отчетом
//	ПоказыватьЗаголовок - признак видимости строк с заголовком отчета
//	ВысотаЗаголовка - параметр, через который возвращается высота заголовка в строках 
//
Процедура СформироватьОтчет(ДокументРезультат, ПоказыватьЗаголовок, ВысотаЗаголовка, ТолькоЗаголовок = Ложь) Экспорт

	ДокументРезультат.Очистить();
	//Параметры документа
	ДокументРезультат.ПолеСлева			 = 0;
	ДокументРезультат.ПолеСправа		 = 0;
	ДокументРезультат.ОриентацияСтраницы = ОриентацияСтраницы.Ландшафт;
	ДокументРезультат.ИмяПараметровПечати= "ПАРАМЕТРЫ_ПЕЧАТИ_РегистрУчетаРасходовПоОплатеТруда";
	ДокументРезультат.Защита             = Ложь;
	ДокументРезультат.ТолькоПросмотр     = Истина;

	Макет = ПолучитьМакет("Отчет");

	ОбластьЗаголовок  = Макет.ПолучитьОбласть("Заголовок");
	ОбластьПодвал        = Макет.ПолучитьОбласть("Подвал");
	ОбластьШапкаТаблицы  = Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрока        = Макет.ПолучитьОбласть("Строка");
	ОбластьИтог          = Макет.ПолучитьОбласть("Итог");

	ОбластьЗаголовок.Параметры.НачалоПериода       = Формат(ДатаНач, "ДФ=dd.MM.yyyy");
	ОбластьЗаголовок.Параметры.КонецПериода        = Формат(ДатаКон, "ДФ=dd.MM.yyyy");
	ОбластьЗаголовок.Параметры.НазваниеОрганизации = Организация.НаименованиеПолное;
	ОбластьЗаголовок.Параметры.ИННОрганизации      = "" + Организация.ИНН + " / " + Организация.КПП;
	ДокументРезультат.Вывести(ОбластьЗаголовок);

	// Параметр для показа заголовка
	ВысотаЗаголовка = ДокументРезультат.ВысотаТаблицы;

	// Когда нужен только заголовок:
	Если ТолькоЗаголовок Тогда
		Возврат;
	КонецЕсли;

	Если ЗначениеЗаполнено(ВысотаЗаголовка) Тогда
		ДокументРезультат.Область("R1:R" + ВысотаЗаголовка).Видимость = ПоказыватьЗаголовок;
	КонецЕсли;

	СтруктураЛиц = РегламентированнаяОтчетность.ОтветственныеЛицаОрганизаций(Организация, ДатаКон);
	ОбластьПодвал.Параметры.ОтветственныйЗаРегистры = СтруктураЛиц.ОтветственныйЗаРегистры;
	
	ДокументРезультат.Вывести(ОбластьШапкаТаблицы);

	Выборка = ПолучитьРезультатЗапроса().Выбрать();
	
	// подсчитываем количество строк документа - для корректного разбиения на страницы
	ВсегоСтрокДокумента = Выборка.Количество();
	// массив с областью строки
    ВыводимыеОбласти = Новый Массив();
	ВыводимыеОбласти.Добавить(ОбластьСтрока);
	
	ВыведеноСтрок = 0;
	Пока Выборка.Следующий() Цикл
		Запись = Выборка.ТипЗаписи();
		Если Запись = ТипЗаписиЗапроса.ОбщийИтог Тогда
			ОбластьИтог.Параметры.Заполнить(Выборка);
		ИначеЕсли Запись = ТипЗаписиЗапроса.ДетальнаяЗапись Тогда

			ОбластьСтрока.Параметры.Заполнить(Выборка);
			// разбиение на страницы
			ВыведеноСтрок = ВыведеноСтрок + 1;

			// Проверим, уместится ли строка на странице или надо открывать новую страницу
			ВывестиПодвалЛиста = Не ДокументРезультат.ПроверитьВывод(ВыводимыеОбласти);
			Если Не ВывестиПодвалЛиста и ВыведеноСтрок = ВсегоСтрокДокумента Тогда
				ВыводимыеОбласти.Добавить(ОбластьИтог);
				ВыводимыеОбласти.Добавить(ОбластьПодвал);
				ВывестиПодвалЛиста = Не ДокументРезультат.ПроверитьВывод(ВыводимыеОбласти);
			КонецЕсли;
			Если ВывестиПодвалЛиста Тогда
				ДокументРезультат.ВывестиГоризонтальныйРазделительСтраниц();
				ДокументРезультат.Вывести(ОбластьШапкаТаблицы);
			КонецЕсли;
			
			ДокументРезультат.Вывести(ОбластьСтрока);
		КонецЕсли;
	КонецЦикла;

	// если не было ни одного работника - выводим пустой бланк
    ВыводимыеОбласти = Новый Массив();
	ВыводимыеОбласти.Добавить(ОбластьСтрока);
	ВыводимыеОбласти.Добавить(ОбластьИтог);
	ВыводимыеОбласти.Добавить(ОбластьПодвал);
	Для Сч = 1 По ОбластьСтрока.Параметры.Количество() Цикл
		ОбластьСтрока.Параметры.Установить(Сч - 1,""); 
	КонецЦикла;
	ОбластьСтрока.Параметры.Работник = " " + Символы.ПС + " ";
	Пока ДокументРезультат.ПроверитьВывод(ВыводимыеОбласти) Цикл
		ДокументРезультат.Вывести(ОбластьСтрока);
	КонецЦикла;
	
	ДокументРезультат.Вывести(ОбластьИтог);
	ДокументРезультат.Вывести(ОбластьПодвал);
	
КонецПроцедуры // СформироватьОтчет

Процедура ЗаполнитьСписокВидовРасходов() Экспорт
	
	// Заполним список видов расходов если он пуст.
	ВидыРасходов.Очистить();
	
	// Внеоборотные активы
	ВидыРасходов.Добавить("На приобретение ОС");
	ВидыРасходов.Добавить("На приобретение оборудования, требующего монтажа");
	ВидыРасходов.Добавить("На НИОКР");
	ВидыРасходов.Добавить("На освоение природных ресурсов");
	ВидыРасходов.Добавить("На приобретение НМА");
	
	// ТМЦ
	ВидыРасходов.Добавить("На приобретение материалов");
	ВидыРасходов.Добавить("На приобретение товаров");
	ВидыРасходов.Добавить("На приобретение паев");
	ВидыРасходов.Добавить("На приобретение ценных бумаг");
	ВидыРасходов.Добавить("На приобретение прав требования");
	
	//Нормируемые
	ВидыРасходов.Добавить("Представительские расходы");
	ВидыРасходов.Добавить("Расходы на рекламу (нормируемые)");
	
	// Текущие расходы
	ВидыРасходов.Добавить("Косвенные расходы");
	ВидыРасходов.Добавить("Прямые расходы");
	ВидыРасходов.Добавить("Расходы на реализацию ОС");
	ВидыРасходов.Добавить("Расходы на реализацию НМА");
	ВидыРасходов.Добавить("Расходы на реализацию прочего имущества");
	ВидыРасходов.Добавить("Внереализационные расходы");
	
	// РБП 
	ВидыРасходов.Добавить("На долгосрочное страхование жизни");
	ВидыРасходов.Добавить("Страхование на оплату медицинских расходов");
	ВидыРасходов.Добавить("Страхование на случай смерти и утраты трудоспособности");
	ВидыРасходов.Добавить("Расходы, связанные с реализацией имущественных прав");
	ВидыРасходов.Добавить("Расходы, связанные с реализацией ЦБ");
	ВидыРасходов.Добавить("Прочие РБП");
	
	ВидыРасходов.Добавить("Расходы, не учитываемые в целях налогообложения");
	ВидыРасходов.Добавить("Без расходов");
	ВидыРасходов.Добавить("Издержки обращения");
	
	
	ВидыРасходов.ЗаполнитьПометки(Истина);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ
// 

НП = Новый НастройкаПериода;

#КонецЕсли