////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Перем КоллекцияПечатныхФорм, ПараметрыВывода;
	
	Если Параметры.Свойство("ИмяМенеджераПечати") Тогда
		
		ИмяМенеджераПечати = Параметры.ИмяМенеджераПечати;
		ИменаМакетов       = Параметры.ИменаМакетов;
		ПараметрКоманды    = Параметры.ПараметрКоманды;
		ПараметрыПечати    = Параметры.ПараметрыПечати;
		
		УправлениеПечатью.СформироватьПечатныеФормы(ИмяМенеджераПечати, ИменаМакетов, ПараметрКоманды, ПараметрыПечати,
			КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
		
		ПечатныйДокумент		= КоллекцияПечатныхФорм[0].ТабличныйДокумент;
		КоличествоЭкземпляров	= КоллекцияПечатныхФорм[0].Экземпляров;
		Заголовок				= КоллекцияПечатныхФорм[0].СинонимМакета;
		
	Иначе
	
		ПечатныйДокумент			= Параметры.ПечатныйДокумент;
		Заголовок					= Параметры.Заголовок;
		Защита						= Параметры.Защита;
		ПараметрыРасшифровки		= Параметры.ПараметрыРасшифровки;
		ПараметрыПечатногоДокумента	= Параметры.ПараметрыПечатногоДокумента;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПечатныйДокумент.ОтображатьЗаголовки	= Ложь;
	ПечатныйДокумент.ОтображатьСетку		= Ложь;
	ПечатныйДокумент.Защита					= Защита;
	ПечатныйДокумент.ТолькоПросмотр			= Истина;
	
	Если ПараметрыПечатногоДокумента <> Неопределено Тогда
		ЗаполнитьЗначенияСвойств(ПечатныйДокумент, ПараметрыПечатногоДокумента);
	КонецЕсли;
	
	КоличествоЭкземпляров	= ПечатныйДокумент.КоличествоЭкземпляров;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

&НаКлиенте
Процедура Печать(Команда)
	
	ПечатныйДокумент.КоличествоЭкземпляров = КоличествоЭкземпляров;
	ПечатныйДокумент.Напечатать();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПоПочте(Команда)
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		УправлениеОтчетами.ОтправитьДокументПоЭлектроннойПочте(ПечатныйДокумент, Заголовок);
		
	#Иначе
		Предупреждение("Отправка по электронной почте недоступна в режиме управляемого приложения.");
		
	#КонецЕсли
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ ПолеТабличногоДокумента

&НаКлиенте
Процедура ПолеТабличногоДокументаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	Если ПараметрыРасшифровки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Расшифровка) = Тип("Строка") Тогда
		СтандартнаяОбработка = Ложь;
		Оповестить(Расшифровка, ПараметрыРасшифровки);
	КонецЕсли;
	
КонецПроцедуры
