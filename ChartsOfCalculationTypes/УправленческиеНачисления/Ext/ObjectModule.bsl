Процедура УстановитьПризнакЗапрашиватьДляТарифныхСтавок()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПоказателиСхемМотивации.Ссылка
	|ИЗ
	|	Справочник.ПоказателиСхемМотивации КАК ПоказателиСхемМотивации
	|ГДЕ
	|	(ПоказателиСхемМотивации.Ссылка В (ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ТарифнаяСтавкаМесячная), ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ТарифнаяСтавкаДневная), ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ТарифнаяСтавкаЧасовая))
	|			ИЛИ ПоказателиСхемМотивации.ТарифнаяСтавка)
	|	И ПоказателиСхемМотивации.Ссылка В(&ПоказателиФормулы)";
	
	Запрос.УстановитьПараметр("ПоказателиФормулы", Показатели.ВыгрузитьКолонку("Показатель"));
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаПоказателя = Показатели.Найти(Выборка.Ссылка, "Показатель");
		СтрокаПоказателя.ЗапрашиватьПриКадровыхПеремещениях = ЗачетОтработанногоВремени;
	КонецЦикла;
	
КонецПроцедуры // УстановитьПризнакЗапрашиватьДляТарифныхСтавок

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ


Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	НачисляетсяВЦеломЗаМесяц = ПроведениеРасчетов.УстановитьФлагНачисляетсяВЦеломЗаМесяц(Истина, Показатели);

	УправленческиеНачисленияПереопределяемый.ПередЗаписьюДополнительно(ЭтотОбъект);
	
	УстановитьПризнакЗапрашиватьДляТарифныхСтавок();	
	
КонецПроцедуры // ПередЗаписью()
