
&НаКлиенте
Процедура ПодборДолжностей(Команда)
	СтруктураПараметры = Новый Структура;
	СтруктураПараметры.Вставить("ЗакрыватьПриВыборе", Ложь);
	ОткрытьФорму("Справочник.ДолжностиОрганизаций.ФормаВыбора", СтруктураПараметры, Элементы.КонтролируемыеДолжности);
КонецПроцедуры

&НаКлиенте
Процедура КонтролируемыеДолжностиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.ДолжностиОрганизаций") Тогда
		СтандартнаяОбработка = Ложь;
		Если Объект.КонтролируемыеДолжности.НайтиСтроки(Новый Структура("Должность", ВыбранноеЗначение)).Количество() = 0 Тогда
			Объект.КонтролируемыеДолжности.Добавить().Должность = ВыбранноеЗначение;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Обработчик подсистемы "ВерсионированиеОбъектов"
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	
	ВалютаУпрУчета  = Константы.ВалютаУправленческогоУчета.Получить();
	Если ЗначениеЗаполнено(ВалютаУпрУчета) Тогда
		Элементы.СуммаРасходов.Заголовок = "Сумма расходов организации (" + ВалютаУпрУчета.Наименование +")";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СрокДействияПриИзменении(Элемент)
	Если Объект.СрокДействия <= 0 Тогда
		Объект.СрокДействия = 1;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КонтролируемыеДолжностиДолжностьОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.ДолжностиОрганизаций") Тогда
		СтандартнаяОбработка = Ложь;
		Если Объект.КонтролируемыеДолжности.НайтиСтроки(Новый Структура("Должность", ВыбранноеЗначение)).Количество() = 0 Тогда
			Элементы.КонтролируемыеДолжности.ТекущиеДанные.Должность = ВыбранноеЗначение;
		Иначе
			Предупреждение("Должность " + ВыбранноеЗначение + " уже присутствует в списке.");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПериодичностьОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПериодичностьОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)
	Если СокрЛП(Текст) = "" Тогда
		ДанныеВыбора = Новый СписокЗначений;
		ДанныеВыбора.Добавить(Объект.Периодичность);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ИзмененВидМедосмотра", Объект.Ссылка, Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура КонтролируемыеДолжностиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока и Копирование Тогда
		Элементы.КонтролируемыеДолжности.ТекущиеДанные.Должность = "";
	КонецЕсли;
КонецПроцедуры
