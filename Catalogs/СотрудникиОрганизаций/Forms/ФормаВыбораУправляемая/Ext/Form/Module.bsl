///////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура УстановитьВидимостьКолонок()
	
	Если Параметры.Свойство("ВидОрганизационнойСтруктурыПредприятия") Тогда
		Элементы.ГруппаВКомпании.Видимость = Параметры.ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности;
		Элементы.ГруппаВОрганизации.Видимость = Параметры.ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц;
		Элементы.Организация.Видимость = Параметры.ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц;
		Элементы.ВидЗанятости.Видимость = Параметры.ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоСтруктуреЮридическихЛиц;
	КонецЕсли;
	
	Если Параметры.Свойство("ТекущееСостояние") Тогда
		Элементы.ГруппаСостояние.Видимость = Параметры.ТекущееСостояние;
	Иначе
		Элементы.ГруппаСостояние.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтбор()
	
	Если Параметры.Свойство("Организация") Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Организация", Параметры.Организация, , , ЗначениеЗаполнено(Параметры.Организация));
	Иначе
		Если Параметры.Отбор.Свойство("Организация") Тогда
			Если НЕ ЗначениеЗаполнено(Параметры.Отбор.Организация) Тогда
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Организация", , , , Ложь);
				Параметры.Отбор.Удалить("Организация");
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("ПодразделениеОрганизации") Тогда
		Если НЕ ЗначениеЗаполнено(Параметры.Отбор.ПодразделениеОрганизации) Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ПодразделениеОрганизации", , , , ЗначениеЗаполнено(Параметры.Отбор.ПодразделениеОрганизации));
			Параметры.Отбор.Удалить("ПодразделениеОрганизации");
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("ТекущееПодразделениеКомпании") Тогда
		Если НЕ ЗначениеЗаполнено(Параметры.Отбор.ТекущееПодразделениеКомпании) Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ТекущееПодразделениеКомпании", , , , ЗначениеЗаполнено(Параметры.Отбор.ТекущееПодразделениеКомпании));
			Параметры.Отбор.Удалить("ТекущееПодразделениеКомпании");
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("ТекущееОбособленноеПодразделение") Тогда
		Если НЕ ЗначениеЗаполнено(Параметры.Отбор.ТекущееОбособленноеПодразделение) Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ТекущееОбособленноеПодразделение", , , , Ложь);
			Параметры.Отбор.Удалить("ТекущееОбособленноеПодразделение");
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("ТолькоПринятые") Тогда
		Если Параметры.Свойство("ВидОрганизационнойСтруктурыПредприятия") Тогда
			Если Параметры.ВидОрганизационнойСтруктурыПредприятия = Перечисления.ВидыОрганизационнойСтруктурыПредприятия.ПоЦентрамОтветственности Тогда
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ДатаПриемаНаРаботуВКомпанию", , ВидСравненияКомпоновкиДанных.Заполнено, , Параметры.ТолькоПринятые);
			Иначе
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "ДатаПриемаНаРаботу", , ВидСравненияКомпоновкиДанных.Заполнено, , Параметры.ТолькоПринятые);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Список.Параметры.УстановитьЗначениеПараметра("ДатаАктуальности", ОбщегоНазначенияЗК.ПолучитьРабочуюДату());
	Если Параметры.Свойство("ВидОрганизационнойСтруктурыПредприятия") Тогда
		Список.Параметры.УстановитьЗначениеПараметра("ВидОрганизационнойСтруктурыПредприятия", Параметры.ВидОрганизационнойСтруктурыПредприятия);
	Иначе
		Список.Параметры.УстановитьЗначениеПараметра("ВидОрганизационнойСтруктурыПредприятия", Неопределено);
	КонецЕсли;
	
	УстановитьОтбор();
	УстановитьВидимостьКолонок();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Для каждого ЭлементОтбора Из Список.Отбор.Элементы Цикл
	
		Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПодразделениеОрганизации") Или ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ТекущееПодразделениеКомпании") Тогда
			ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии;
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры
