////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем мДлинаСуток;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Если Клиент Тогда

// Процедура выводит на экран печатную форму 
	//
	// Параметры: 
	//  Нет
	//
	// Возвращаемое значение:
	//  Нет.
	//
Функция Печать(ТабДокумент = Неопределено, ДатаАктуальности = Неопределено) Экспорт

	СтруктураДанных = Новый Структура; 
	СтруктураДанныхОрганизации = Новый Структура; 
		
	//Если ТабДокумент = Неопределено Тогда
		Подробно = Истина;
	//Иначе
	//	Подробно = Ложь
	//КонецЕсли;
		
	Запрос = Новый Запрос;

	// Установим параметры запроса.
	Запрос.УстановитьПараметр("ФизЛицо" ,	Ссылка);
	Если ДатаАктуальности = Неопределено Тогда
		Запрос.УстановитьПараметр("ДатаАктуальности" ,	КонецДня(ОбщегоНазначения.ПолучитьРабочуюДату()));
	Иначе
		Запрос.УстановитьПараметр("ДатаАктуальности" ,	КонецДня(ДатаАктуальности));
	КонецЕсли;
	Запрос.УстановитьПараметр("Изображение" ,Перечисления.ВидыДополнительнойИнформацииОбъектов.Изображение);
	Запрос.УстановитьПараметр("ПустаяСтрока" ,"");
	Запрос.УстановитьПараметр("ПустойОтвет" ,Справочники.ВариантыОтветовОпросов.ПустаяСсылка());
	
	Если Подробно Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ФизическиеЛица.Наименование,
		|	ФизическиеЛица.ДатаРождения,
		|	ФизическиеЛица.МестоРождения,
		|	ФизическиеЛица.ИНН,
		|	ФизическиеЛица.Код,
		|	ФизическиеЛица.КодИМНС,
		|	ФизическиеЛица.Пол,
		|	ФизическиеЛица.СтраховойНомерПФР,
		|	ФизическиеЛица.ОсновноеИзображение.Хранилище КАК Данные,
		|	ФизическиеЛица.ОсновноеИзображение.Наименование КАК ОписаниеИзображения,
		|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументВид.Представление КАК ДокументВид,
		|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументДатаВыдачи,
		|	ВЫРАЗИТЬ(ПаспортныеДанныеФизЛицСрезПоследних.ДокументКемВыдан КАК СТРОКА(300)) КАК ДокументКемВыдан,
		|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументКодПодразделения,
		|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументНомер,
		|	ПаспортныеДанныеФизЛицСрезПоследних.ДокументСерия,
		|	ФИОФизЛицСрезПоследних.Фамилия,
		|	ФИОФизЛицСрезПоследних.Имя,
		|	ФИОФизЛицСрезПоследних.Отчество
		|ИЗ
		|	Справочник.ФизическиеЛица КАК ФизическиеЛица
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(, ФизЛицо = &ФизЛицо) КАК ФИОФизЛицСрезПоследних
		|		ПО ФИОФизЛицСрезПоследних.ФизЛицо = ФизическиеЛица.Ссылка
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПаспортныеДанныеФизЛиц.СрезПоследних(, ФизЛицо = &ФизЛицо) КАК ПаспортныеДанныеФизЛицСрезПоследних
		|		ПО ФизическиеЛица.Ссылка = ПаспортныеДанныеФизЛицСрезПоследних.ФизЛицо
		|
		|ГДЕ
		|	ФизическиеЛица.Ссылка = &ФизЛицо";
		
		Результат = Запрос.Выполнить(); 
		ВыборкаДляПроверок = Результат.Выбрать();
		ВыборкаДляПроверок.Следующий();
		СтруктураДанных.Вставить("ФизическиеЛица", Результат.Выбрать());
		Если ВыборкаДляПроверок.ДокументВид <> Null Тогда
			СтруктураДанных.Вставить("ПаспортныеДанныеФизЛиц", Результат.Выбрать());
		КонецЕсли;
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КонтактнаяИнформация.Тип КАК Тип,
		|	ВЫБОР КОГДА КонтактнаяИнформация.Вид ССЫЛКА Справочник.ВидыКонтактнойИнформации ТОГДА
		|		КонтактнаяИнформация.Вид.Представление
		|	ИНАЧЕ
		|		КонтактнаяИнформация.Вид
		|	КОНЕЦ КАК ВидКИ,
		|	КонтактнаяИнформация.Представление КАК ПредставлениеКИ
		|ИЗ
		|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Объект = &ФизЛицо
		|УПОРЯДОЧИТЬ ПО
		|	Тип
		|АВТОУПОРЯДОЧИВАНИЕ";
		
		СтруктураДанных.Вставить("КонтактнаяИнформация", Запрос.Выполнить().Выбрать());
		
		// разделы информации о физлице из табличных частей справочника
		Для каждого ТЧ Из Метаданные.Справочники.ФизическиеЛица.ТабличныеЧасти Цикл
			ИмяРегистра = "ФизическиеЛица_" + СтрЗаменить(ТЧ.Имя,"_","");
			Если ТЧ.Имя = "СоставСемьи" Тогда
				Запрос.Текст = 
				"ВЫБРАТЬ
				|	ФизическиеЛица_СоставСемьи.СтепеньРодства.Представление КАК СтепеньРодства,
				|	ФизическиеЛица_СоставСемьи.Имя КАК Имя,
				|	ФизическиеЛица_СоставСемьи.ГодРождения КАК ГодРождения,
				|	СемейноеПоложениеФизЛицСрезПоследних.СемейноеПоложение.Представление КАК СемейноеПоложение
				|ИЗ
				|	Справочник.ФизическиеЛица.СоставСемьи КАК ФизическиеЛица_СоставСемьи
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СемейноеПоложениеФизЛиц.СрезПоследних(, ФизЛицо = &ФизЛицо) КАК СемейноеПоложениеФизЛицСрезПоследних
				|		ПО ФизическиеЛица_СоставСемьи.Ссылка = СемейноеПоложениеФизЛицСрезПоследних.ФизЛицо
				|ГДЕ
				|	ФизическиеЛица_СоставСемьи.Ссылка = &ФизЛицо
				|
				|ОБЪЕДИНИТЬ ВСЕ
				|
				|ВЫБРАТЬ
				|	NULL,
				|	NULL,
				|	ЕСТЬNULL(КОЛИЧЕСТВО(ФизическиеЛица_СоставСемьи.ГодРождения), 0),
				|	СемейноеПоложениеФизЛицСрезПоследних.СемейноеПоложение.Представление
				|ИЗ
				|	РегистрСведений.СемейноеПоложениеФизЛиц.СрезПоследних(, ФизЛицо = &ФизЛицо) КАК СемейноеПоложениеФизЛицСрезПоследних
				|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица.СоставСемьи КАК ФизическиеЛица_СоставСемьи
				|		ПО ФизическиеЛица_СоставСемьи.Ссылка = СемейноеПоложениеФизЛицСрезПоследних.ФизЛицо
				|
				|СГРУППИРОВАТЬ ПО
				|	СемейноеПоложениеФизЛицСрезПоследних.СемейноеПоложение
				|
				|ИМЕЮЩИЕ
				|	ЕСТЬNULL(КОЛИЧЕСТВО(ФизическиеЛица_СоставСемьи.ГодРождения), 0) = 0";
				
			Иначе
				
				ТекстПолей = "";
				Для каждого Реквизит Из ТЧ.Реквизиты Цикл
					ЕстьПолеПредставление = Ложь;
					// Определим принадлежность к ссылочным типам (т.е. возможность использования поля Представление)
					Для Каждого ЭлементТипа Из Реквизит.Тип.Типы() Цикл
						// Нет у примитивных
						Если ЭлементТипа = Тип("Число")
							ИЛИ ЭлементТипа = Тип("Строка")
							ИЛИ ЭлементТипа = Тип("Дата")
							ИЛИ ЭлементТипа = Тип("Булево") Тогда
							
							ЕстьПолеПредставление = Ложь;
							Прервать;
						Иначе
							
							// Нет у перечисления
							ПустоеЗначениеТипа = Новый(ЭлементТипа);
							
							Если Метаданные.Перечисления.Найти(ПустоеЗначениеТипа.Метаданные().Имя) <> Неопределено Тогда
								ЕстьПолеПредставление = Ложь;
								Прервать;
							Иначе
								ЕстьПолеПредставление = Истина;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
					ТекстПолей = ТекстПолей + "," + Символы.ПС + Символы.Таб + ИмяРегистра + "." + Реквизит.Имя + ?(ЕстьПолеПредставление,".Представление","") + " КАК " + Реквизит.Имя
				КонецЦикла;
				
				Запрос.Текст = "ВЫБРАТЬ" + Сред(ТекстПолей,2) 
				+ ?(ТЧ.Имя = "СоставСемьи",",
				|	СемейноеПоложениеФизЛицСрезПоследних.СемейноеПоложение.Представление КАК СемейноеПоложение" , "") + Символы.ПС
				+ "ИЗ" + Символы.ПС + Символы.Таб + "Справочник." + СтрЗаменить(ИмяРегистра,"_",".") + " КАК " + ИмяРегистра 
				+ ?(ТЧ.Имя = "СоставСемьи","
				|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СемейноеПоложениеФизЛиц.СрезПоследних(, ФизЛицо = &ФизЛицо) КАК СемейноеПоложениеФизЛицСрезПоследних
				|		ПО "+ ИмяРегистра +".Ссылка = СемейноеПоложениеФизЛицСрезПоследних.ФизЛицо","") + Символы.ПС 
				+ "ГДЕ" + Символы.ПС + Символы.Таб + ИмяРегистра + ".Ссылка = &ФизЛицо";
				
			КонецЕсли;
			
			СтруктураДанных.Вставить(ИмяРегистра, Запрос.Выполнить().Выбрать());
		КонецЦикла;
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗначенияСвойствОбъектов.Значение,
		|	ЗначенияСвойствОбъектов.Свойство
		|ИЗ
		|	РегистрСведений.ЗначенияСвойствОбъектов КАК ЗначенияСвойствОбъектов
		|
		|ГДЕ
		|	ЗначенияСвойствОбъектов.Объект = &ФизЛицо";
		
		СтруктураДанных.Вставить("ДополнительныеДанные", Запрос.Выполнить().Выбрать());
    КонецЕсли;
	Если НастройкаПравДоступа.ДоступнаРольМенеджераПоНабору() Тогда
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	РезультатыАнкетирования.Ссылка.ТиповаяАнкета.Представление КАК Анкета,
		|	РезультатыАнкетирования.Вопрос.ПолнаяФормулировка КАК Вопрос,
		|	РезультатыАнкетирования.ТиповойОтвет КАК Ответ,
		|	РезультатыАнкетирования.Ссылка.Представление КАК РегистраторПредставление,
		|	ВариантыОтветовОпросов.ОценкаОтвета
		|ИЗ
		|	Документ.Опрос.Вопросы КАК РезультатыАнкетирования
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВариантыОтветовОпросов КАК ВариантыОтветовОпросов
		|		ПО РезультатыАнкетирования.ТиповойОтвет = ВариантыОтветовОпросов.Ссылка
		|
		|ГДЕ
		|	РезультатыАнкетирования.Ссылка.ОпрашиваемоеЛицо = &ФизЛицо
		|
		|УПОРЯДОЧИТЬ ПО
		|	РезультатыАнкетирования.Ссылка.Дата";
		
		СтруктураДанных.Вставить("Опросы", Запрос.Выполнить().Выбрать());
		
	КонецЕсли;

	Если НастройкаПравДоступа.ДоступнаРольКадровикаУпр() Тогда
		
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	РаботникиСрезПоследних.ЗанимаемыхСтавок,
		|	РаботникиСрезПоследних.Должность.Представление КАК Должность,
		|	РаботникиСрезПоследних.Подразделение.Представление КАК Подразделение,
		|	ВЫБОР
		|		КОГДА РаботникиСрезПоследних.ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
		|			ТОГДА ""Не работает (уволен)""
		|		ИНАЧЕ ""Работает""
		|	КОНЕЦ КАК Состояние
		|ИЗ
		|	РегистрСведений.Работники.СрезПоследних(&ДатаАктуальности, ФизЛицо = &ФизЛицо) КАК РаботникиСрезПоследних";
		
		Запрос.УстановитьПараметр("Уволен",Перечисления.ПричиныИзмененияСостояния.Увольнение);
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		Если Выборка.Состояние <> Null Тогда
			СтруктураДанных.Вставить("Работники", РезультатЗапроса.Выбрать());
		КонецЕсли;
			
		Если НЕ РезультатЗапроса.Пустой() Тогда
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПриемНаРаботуРаботники.Ссылка КАК Документ,
			|	ПриемНаРаботуРаботники.Ссылка.Представление,
			|	ПриемНаРаботуРаботники.Ссылка.Дата КАК Дата,
			|	ПриемНаРаботуРаботники.ДатаПриема КАК ДатаС,
			|	ПриемНаРаботуРаботники.ДатаУвольнения КАК ДатаПо,
			|	ПриемНаРаботуРаботники.Подразделение КАК Подразделение,
			|	ПриемНаРаботуРаботники.Должность,
			|	ПриемНаРаботуРаботники.ЗанимаемыхСтавок,
			|	ПриемНаРаботуРаботники.ИспытательныйСрок КАК Примечание
			|ИЗ
			|	Документ.ПриемНаРаботу.Работники КАК ПриемНаРаботуРаботники
			|
			|ГДЕ
			|	ПриемНаРаботуРаботники.Сотрудник.ФизЛицо = &ФизЛицо И
			|	ПриемНаРаботуРаботники.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	КадровоеПеремещениеРаботники.Ссылка,
			|	КадровоеПеремещениеРаботники.Ссылка.Представление,
			|	КадровоеПеремещениеРаботники.Ссылка.Дата,
			|	КадровоеПеремещениеРаботники.ДатаНачала,
			|	КадровоеПеремещениеРаботники.ДатаОкончания,
			|	КадровоеПеремещениеРаботники.НовоеПодразделение,
			|	КадровоеПеремещениеРаботники.НоваяДолжность,
			|	КадровоеПеремещениеРаботники.ЗанимаемыхСтавок,
			|	NULL
			|ИЗ
			|	Документ.КадровоеПеремещение.Работники КАК КадровоеПеремещениеРаботники
			|
			|ГДЕ
			|	КадровоеПеремещениеРаботники.Сотрудник.ФизЛицо = &ФизЛицо И
			|	КадровоеПеремещениеРаботники.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	УвольнениеРаботники.Ссылка,
			|	УвольнениеРаботники.Ссылка.Представление,
			|	УвольнениеРаботники.Ссылка.Дата,
			|	УвольнениеРаботники.ДатаУвольнения,
			|	NULL,
			|	NULL,
			|	NULL,
			|	NULL,
			|	УвольнениеРаботники.ПричинаУвольнения
			|ИЗ
			|	Документ.Увольнение.Работники КАК УвольнениеРаботники
			|
			|ГДЕ
			|	УвольнениеРаботники.Сотрудник.ФизЛицо = &ФизЛицо И
			|	УвольнениеРаботники.Ссылка.Проведен
			|
			|УПОРЯДОЧИТЬ ПО
			|	Дата";
			СтруктураДанных.Вставить("ПеремещенияВКомпании", Запрос.Выполнить().Выбрать());
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ОтсутствиеНаРаботеРаботники.ДатаНачала КАК ДатаС,
			|	ОтсутствиеНаРаботеРаботники.ДатаОкончания КАК ДатаПо,
			|	ОтсутствиеНаРаботеРаботники.Ссылка.Дата КАК Дата,
			|	ОтсутствиеНаРаботеРаботники.Ссылка.Представление,
			|	ОтсутствиеНаРаботеРаботники.ПричинаОтсутствия,
			|	ВЫБОР
			|		КОГДА ОтсутствиеНаРаботеРаботники.ПричинаОтсутствия = ЗНАЧЕНИЕ(Перечисление.ПричиныОтсутствияНаРаботе.Командировка)
			|			ТОГДА ""Командировка""
			|		КОГДА ОтсутствиеНаРаботеРаботники.ПричинаОтсутствия = ЗНАЧЕНИЕ(Перечисление.ПричиныОтсутствияНаРаботе.Заболевание)
			|			ТОГДА ""Болезнь""
			|		ИНАЧЕ ""Отпуск""
			|	КОНЕЦ КАК ВидОтсутствия
			|ИЗ
			|	Документ.ОтсутствиеНаРаботе.Работники КАК ОтсутствиеНаРаботеРаботники
			|ГДЕ
			|	ОтсутствиеНаРаботеРаботники.Сотрудник.Физлицо = &ФизЛицо
			|	И ОтсутствиеНаРаботеРаботники.Ссылка.Проведен
			|
			|УПОРЯДОЧИТЬ ПО
			|	ВидОтсутствия,
			|	Дата";
			СтруктураДанных.Вставить("Отклонения", Запрос.Выполнить().Выбрать());
			
		КонецЕсли; 
		
	КонецЕсли;

	//vvv
	//Если НастройкаПравДоступа.ДоступнаРольКадровикаРегл() Тогда
	Если (НастройкаПравДоступа.ДоступнаРольКадровикаРегл()) или (НастройкаПравДоступа.ДоступнаРольРасчетчикаРегл())Тогда
	//	
	
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВЫБОР
		|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &ДатаАктуальности
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА РаботникиОрганизацииСрезПоследних.ЗанимаемыхСтавокЗавершения
		|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.ЗанимаемыхСтавок
		|	КОНЕЦ КАК ЗанимаемыхСтавок,
		|	ВЫБОР
		|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &ДатаАктуальности
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА РаботникиОрганизацииСрезПоследних.ДолжностьЗавершения.Представление
		|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.Должность.Представление
		|	КОНЕЦ КАК Должность,
		|	ВЫБОР
		|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &ДатаАктуальности
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизацииЗавершения.Представление
		|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации.Представление
		|	КОНЕЦ КАК Подразделение,
		|	РаботникиОрганизацииСрезПоследних.Организация.Представление КАК Организация,
		|	РаботникиОрганизацииСрезПоследних.Организация КАК ОрганизацияСсылка,
		|	ВЫБОР
		|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &ДатаАктуальности
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА ВЫБОР
		|					КОГДА РаботникиОрганизацииСрезПоследних.ПричинаИзмененияСостоянияЗавершения = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
		|						ТОГДА ""Не работает (уволен)""
		|					ИНАЧЕ ""Работает""
		|				КОНЕЦ
		|		ИНАЧЕ ВЫБОР
		|				КОГДА РаботникиОрганизацииСрезПоследних.ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
		|					ТОГДА ""Не работает (уволен)""
		|				ИНАЧЕ ""Работает""
		|			КОНЕЦ
		|	КОНЕЦ КАК Состояние,
		|	РаботникиОрганизацииСрезПоследних.Сотрудник.Код КАК ТабельныйНомер
		|ИЗ
		|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(&ДатаАктуальности, Сотрудник.ФизЛицо = &ФизЛицо) КАК РаботникиОрганизацииСрезПоследних
		|
		|УПОРЯДОЧИТЬ ПО
		|	Организация,
		|	РаботникиОрганизацииСрезПоследних.Период УБЫВ";
		
		РезультатПоОрганизациям = Запрос.Выполнить(); 
		
		Если Не РезультатПоОрганизациям.Пустой() Тогда
			
			СтруктураДанных.Вставить("Организация", РезультатПоОрганизациям.Выбрать());
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ПриемНаРаботуВОрганизациюРаботники.Ссылка КАК Документ,
			|	ПриемНаРаботуВОрганизациюРаботники.Ссылка.Представление,
			|	ПриемНаРаботуВОрганизациюРаботники.ПодразделениеОрганизации.Владелец КАК Организация,
			|	ПриемНаРаботуВОрганизациюРаботники.Ссылка.Дата КАК Дата,
			|	ПриемНаРаботуВОрганизациюРаботники.ДатаПриема КАК ДатаС,
			|	ВЫБОР
			|		КОГДА НЕ ПриемНаРаботуВОрганизациюРаботники.НапомнитьПоЗавершении
			|			ТОГДА ПриемНаРаботуВОрганизациюРаботники.ДатаУвольнения
			|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
			|	КОНЕЦ КАК ДатаПо,
			|	ПриемНаРаботуВОрганизациюРаботники.ПодразделениеОрганизации.Представление КАК Подразделение,
			|	ПриемНаРаботуВОрганизациюРаботники.Должность.Представление КАК Должность,
			|	ПриемНаРаботуВОрганизациюРаботники.ЗанимаемыхСтавок,
			|	ПриемНаРаботуВОрганизациюРаботники.ИспытательныйСрок КАК Примечание
			|ИЗ
			|	Документ.ПриемНаРаботуВОрганизацию.РаботникиОрганизации КАК ПриемНаРаботуВОрганизациюРаботники
			|ГДЕ
			|	ПриемНаРаботуВОрганизациюРаботники.Сотрудник.ФизЛицо = &ФизЛицо
			|	И ПриемНаРаботуВОрганизациюРаботники.Ссылка.Проведен
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	КадровоеПеремещениеОрганизацииРаботники.Ссылка,
			|	КадровоеПеремещениеОрганизацииРаботники.Ссылка.Представление,
			|	КадровоеПеремещениеОрганизацииРаботники.ПодразделениеОрганизации.Владелец,
			|	КадровоеПеремещениеОрганизацииРаботники.Ссылка.Дата,
			|	КадровоеПеремещениеОрганизацииРаботники.ДатаНачала,
			|	ВЫБОР
			|		КОГДА НЕ КадровоеПеремещениеОрганизацииРаботники.НапомнитьПоЗавершении
			|			ТОГДА КадровоеПеремещениеОрганизацииРаботники.ДатаОкончания
			|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
			|	КОНЕЦ,
			|	КадровоеПеремещениеОрганизацииРаботники.ПодразделениеОрганизации.Представление,
			|	КадровоеПеремещениеОрганизацииРаботники.Должность.Представление,
			|	КадровоеПеремещениеОрганизацииРаботники.ЗанимаемыхСтавок,
			|	NULL
			|ИЗ
			|	Документ.КадровоеПеремещениеОрганизаций.РаботникиОрганизации КАК КадровоеПеремещениеОрганизацииРаботники
			|ГДЕ
			|	КадровоеПеремещениеОрганизацииРаботники.Сотрудник.ФизЛицо = &ФизЛицо
			|	И КадровоеПеремещениеОрганизацииРаботники.Ссылка.Проведен 
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	УвольнениеИзОрганизацииРаботники.Ссылка,
			|	УвольнениеИзОрганизацииРаботники.Ссылка.Представление,
			|	УвольнениеИзОрганизацииРаботники.Ссылка.Организация,
			|	УвольнениеИзОрганизацииРаботники.Ссылка.Дата,
			|	УвольнениеИзОрганизацииРаботники.ДатаУвольнения,
			|	NULL,
			|	NULL,
			|	NULL,
			|	NULL,
			|	УвольнениеИзОрганизацииРаботники.СтатьяТКРФ.Наименование
			|ИЗ
			|	Документ.УвольнениеИзОрганизаций.РаботникиОрганизации КАК УвольнениеИзОрганизацииРаботники
			|ГДЕ
			|	УвольнениеИзОрганизацииРаботники.Сотрудник.Физлицо = &ФизЛицо
			|	И УвольнениеИзОрганизацииРаботники.Ссылка.Проведен 
			|
			|УПОРЯДОЧИТЬ ПО
			|	Дата
			|ИТОГИ ПО
			|	Организация";
			
			СтруктураДанныхОрганизации.Вставить("ПеремещенияВОрганизации", Запрос.Выполнить());
			
		КонецЕсли;
		
	КонецЕсли; 
	Если Подробно Тогда	
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ХранилищеДополнительнойИнформации.Наименование КАК ОписаниеДанных,
		|	ХранилищеДополнительнойИнформации.Хранилище КАК Данные
		|ИЗ
		|	Справочник.ХранилищеДополнительнойИнформации КАК ХранилищеДополнительнойИнформации
		|
		|ГДЕ
		|	((ВЫРАЗИТЬ(ХранилищеДополнительнойИнформации.Объект КАК Справочник.ФизическиеЛица)) = &ФизЛицо) И
		|	ХранилищеДополнительнойИнформации.ВидДанных = &Изображение";
		
		ВыборкаИзображений = Запрос.Выполнить().Выбрать();
    КонецЕсли;
    
    ВыводитьНаПечать = Ложь;
	Если ТабДокумент = Неопределено Тогда
		ТабДокумент = Новый ТабличныйДокумент;
        ВыводитьНаПечать = Истина;
    КонецЕсли;
    
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ФизическиеЛица_ДанныеПоФизлицу";
	Макет = ПолучитьМакет("ДанныеПоФизлицу");
	Если Подробно Тогда
		ВыборкаОсновноеФото = Результат.Выбрать();
		Если ВыборкаОсновноеФото.Следующий() Тогда
			ДанныеДляФото = ВыборкаОсновноеФото.Данные;
			Если ДанныеДляФото <> Null Тогда
				ФотоНаПропуск = ДанныеДляФото.Получить();
				Если ФотоНаПропуск <> Неопределено Тогда
					ОбластьМакета = Макет.ПолучитьОбласть("ОсновноеФото_Заголовок");
					ТабДокумент.Вывести(ОбластьМакета);
					ТабДокумент.Область(ТабДокумент.ВысотаТаблицы,,ТабДокумент.ВысотаТаблицы,).ЦветФона = ЦветаСтиля.ФонГруппировкиВерхнегоУровня;
					ТабДокумент.НачатьГруппуСтрок("", Не Подробно);
					ОбластьМакета = Макет.ПолучитьОбласть("ОсновноеФото");
					ОбластьМакета.Рисунки.ФотоНаПропуск.Картинка = ФотоНаПропуск;
					ОбластьМакета.Рисунки.ФотоНаПропуск.Линия = Новый Линия(ТипЛинииРисункаТабличногоДокумента.НетЛинии,);
					ОбластьМакета.Параметры.ОписаниеДанных = ВыборкаОсновноеФото.ОписаниеИзображения;
					ТабДокумент.Вывести(ОбластьМакета);
					ТабДокумент.ЗакончитьГруппуСтрок();
				КонецЕсли;				
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Для Каждого СекцияДанных Из СтруктураДанных Цикл

		ИмяСекции = СекцияДанных.Ключ;
		Если Не Подробно Тогда
			УсловиеВыводаРаскрытойГруппы = Истина
		Иначе
			УсловиеВыводаРаскрытойГруппы = ИмяСекции = "ФизическиеЛица";
		КонецЕсли;
		Выборка = СекцияДанных.Значение;
		Если ИмяСекции = "Отклонения" Тогда

			Пока Выборка.СледующийПоЗначениюПоля("ВидОтсутствия") Цикл
				ИмяСекции = СокрЛП(Выборка.ВидОтсутствия);
				Отбивка = ?(Макет.Области.Найти(ИмяСекции + "_Отбивка") <> Неопределено,Макет.ПолучитьОбласть(ИмяСекции + "_Отбивка"),Макет.ПолучитьОбласть("Отбивка"));
				Если Макет.Области.Найти(ИмяСекции + "_Заголовок") <> Неопределено Тогда
					ОбластьМакета = Макет.ПолучитьОбласть(ИмяСекции + "_Заголовок_ПерваяСтрока");
					ТабДокумент.Вывести(ОбластьМакета);
					ТабДокумент.Область(ТабДокумент.ВысотаТаблицы,,ТабДокумент.ВысотаТаблицы,).ЦветФона = ЦветаСтиля.ФонГруппировкиВерхнегоУровня;
					ТабДокумент.НачатьГруппуСтрок("", Не Подробно);
					ТабДокумент.Вывести(Отбивка);
					ТабДокумент.Область(ТабДокумент.ВысотаТаблицы, 2, ТабДокумент.ВысотаТаблицы, 10).ЦветФона = ЦветаСтиля.ФонГруппировкиПромежуточногоУровня;
					ОбластьМакета = Макет.ПолучитьОбласть(ИмяСекции + "_Заголовок_Остальное");
					ТабДокумент.Вывести(ОбластьМакета);
					ТабДокумент.Область(ТабДокумент.ВысотаТаблицы - ОбластьМакета.ВысотаТаблицы + 1, 2, ТабДокумент.ВысотаТаблицы, 10).ЦветФона = ЦветаСтиля.ФонГруппировкиПромежуточногоУровня;
					ОбластьМакета = Макет.ПолучитьОбласть(ИмяСекции + "_Данные");
					Пока Выборка.Следующий() Цикл
						ОбластьМакета.Параметры.Заполнить(Выборка);
						ТабДокумент.Вывести(ОбластьМакета);
					КонецЦикла;
					ТабДокумент.Вывести(Отбивка);
					ТабДокумент.ЗакончитьГруппуСтрок();
				КонецЕсли;
			КонецЦикла;

		ИначеЕсли ИмяСекции = "КонтактнаяИнформация" и НЕ Выборка.Количество() = 0 Тогда

			СекцияЗаголовка = Макет.ПолучитьОбласть("КонтактнаяИнформация_Заголовок");
			ТабДокумент.Вывести(СекцияЗаголовка);
			ТабДокумент.Область(ТабДокумент.ВысотаТаблицы,,ТабДокумент.ВысотаТаблицы,).ЦветФона = ЦветаСтиля.ФонГруппировкиВерхнегоУровня;
			ТабДокумент.НачатьГруппуСтрок("",Не Подробно);
			Отбивка = Макет.ПолучитьОбласть("Отбивка");
			
			СекцияТипаКИ = Макет.ПолучитьОбласть("КонтактнаяИнформация_Тип");
			Пока Выборка.СледующийПоЗначениюПоля("Тип") цикл

				ТабДокумент.Вывести(Отбивка);
				СекцияТипаКИ.Параметры.ТипКИ = Строка(Выборка.Тип) + ":";
				ТабДокумент.Вывести(СекцияТипаКИ);

				СекцияКИ = Макет.ПолучитьОбласть("КонтактнаяИнформация_Данные");
				Пока Выборка.СледующийПоЗначениюПоля("ВидКИ") цикл
					СекцияКИ.Параметры.Заполнить(Выборка);
					ТабДокумент.Вывести(СекцияКИ);
				КонецЦикла;

			КонецЦикла; 

			ТабДокумент.Вывести(Отбивка);
			ТабДокумент.ЗакончитьГруппуСтрок();
			
		ИначеЕсли ИмяСекции = "Организация"  Тогда

			Пока Выборка.СледующийПоЗначениюПоля("Организация") Цикл

				Отбивка = Макет.ПолучитьОбласть("Отбивка");
				ОбластьМакета = Макет.ПолучитьОбласть(ИмяСекции + "_Заголовок");
				ОбластьМакета.Параметры.Заполнить(Выборка);
				ТабДокумент.Вывести(ОбластьМакета);
				ТабДокумент.Область(ТабДокумент.ВысотаТаблицы,,ТабДокумент.ВысотаТаблицы,).ЦветФона = ЦветаСтиля.ФонГруппировкиВерхнегоУровня;
				ТабДокумент.НачатьГруппуСтрок("", УсловиеВыводаРаскрытойГруппы);
				ТабДокумент.Вывести(Отбивка);
				ОбластьМакета = Макет.ПолучитьОбласть(ИмяСекции + "_Данные");
				ОбластьМакета.Параметры.Заполнить(Выборка);
				ТабДокумент.Вывести(ОбластьМакета);
				ТабДокумент.Вывести(Отбивка);

				Для Каждого СекцияДанныхОрганизации Из СтруктураДанныхОрганизации Цикл
					ИмяСекцииОрганизации = СекцияДанныхОрганизации.Ключ;

					Если ИмяСекцииОрганизации = "Отклонения" Тогда

						ВыборкаОрганизации = СекцияДанныхОрганизации.Значение;
						Пока ВыборкаОрганизации.СледующийПоЗначениюПоля("ВидОтсутствия") Цикл
							ИмяСекцииОрганизации = СокрЛП(ВыборкаОрганизации.ВидОтсутствия);
							Отбивка = ?(Макет.Области.Найти(ИмяСекцииОрганизации + "_Отбивка") <> Неопределено,Макет.ПолучитьОбласть(ИмяСекцииОрганизации + "_Отбивка"),Макет.ПолучитьОбласть("Отбивка"));
							Если Макет.Области.Найти(ИмяСекцииОрганизации + "_Заголовок") <> Неопределено Тогда
								ОбластьМакета = Макет.ПолучитьОбласть(ИмяСекцииОрганизации + "_Заголовок_ПерваяСтрока");
								ТабДокумент.Вывести(ОбластьМакета);
								ТабДокумент.Область(ТабДокумент.ВысотаТаблицы,,ТабДокумент.ВысотаТаблицы,).ЦветФона = ЦветаСтиля.ФонГруппировкиВерхнегоУровня;
								ТабДокумент.НачатьГруппуСтрок("", Не Подробно);
								ТабДокумент.Вывести(Отбивка);
								ТабДокумент.Область(ТабДокумент.ВысотаТаблицы, 2, ТабДокумент.ВысотаТаблицы, 10).ЦветФона = ЦветаСтиля.ФонГруппировкиПромежуточногоУровня;
								ОбластьМакета = Макет.ПолучитьОбласть(ИмяСекцииОрганизации + "_Заголовок_Остальное");
								ТабДокумент.Вывести(ОбластьМакета);
								ТабДокумент.Область(ТабДокумент.ВысотаТаблицы - ОбластьМакета.ВысотаТаблицы + 1, 2, ТабДокумент.ВысотаТаблицы, 10).ЦветФона = ЦветаСтиля.ФонГруппировкиПромежуточногоУровня;
								ОбластьМакета = Макет.ПолучитьОбласть(ИмяСекцииОрганизации + "_Данные");
								Пока ВыборкаОрганизации.Следующий() Цикл
									ОбластьМакета.Параметры.Заполнить(ВыборкаОрганизации);
									ТабДокумент.Вывести(ОбластьМакета);
								КонецЦикла;
								ТабДокумент.Вывести(Отбивка);
								ТабДокумент.ЗакончитьГруппуСтрок();
							КонецЕсли;
						КонецЦикла;
						
					ИначеЕсли Макет.Области.Найти(ИмяСекцииОрганизации + "_Заголовок") <> Неопределено Тогда

                        // Спозиционируемся в выборке "ПеремещенияВОрганизации" на нужной организации
						ВыборкаПеремещенийПоВсемОрганизациям = СекцияДанныхОрганизации.Значение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
						ВыборкаПеремещенийПоВсемОрганизациям.Сбросить();
						СтруктураПоиска = Новый Структура("Организация");
						СтруктураПоиска.Организация = Выборка.ОрганизацияСсылка;
						Если ВыборкаПеремещенийПоВсемОрганизациям.НайтиСледующий(СтруктураПоиска) Тогда

							// Выберем кадровые перемещения в рамках организации
							ВыборкаОрганизации = ВыборкаПеремещенийПоВсемОрганизациям.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
							
							Отбивка = ?(Макет.Области.Найти(ИмяСекцииОрганизации + "_Отбивка") <> Неопределено,Макет.ПолучитьОбласть(ИмяСекцииОрганизации + "_Отбивка"),Макет.ПолучитьОбласть("Отбивка"));
							ОбластьМакета = Макет.ПолучитьОбласть(ИмяСекцииОрганизации + "_Заголовок");
							Если ОбластьМакета.Области.Количество() = 1 Тогда
								ТабДокумент.Вывести(ОбластьМакета);
								ТабДокумент.Область(ТабДокумент.ВысотаТаблицы,,ТабДокумент.ВысотаТаблицы,).ЦветФона = ЦветаСтиля.ФонГруппировкиВерхнегоУровня;
								ТабДокумент.НачатьГруппуСтрок("", УсловиеВыводаРаскрытойГруппы);
								ТабДокумент.Вывести(Отбивка);
							Иначе
								ОбластьМакета = Макет.ПолучитьОбласть(ИмяСекцииОрганизации + "_Заголовок_ПерваяСтрока");
								ТабДокумент.Вывести(ОбластьМакета);
								ТабДокумент.Область(ТабДокумент.ВысотаТаблицы,,ТабДокумент.ВысотаТаблицы,).ЦветФона = ЦветаСтиля.ФонГруппировкиВерхнегоУровня;
								ТабДокумент.НачатьГруппуСтрок("", УсловиеВыводаРаскрытойГруппы);
								ТабДокумент.Вывести(Отбивка);
								ТабДокумент.Область(ТабДокумент.ВысотаТаблицы, 2, ТабДокумент.ВысотаТаблицы, 10).ЦветФона = ЦветаСтиля.ФонГруппировкиПромежуточногоУровня;
								ОбластьМакета = Макет.ПолучитьОбласть(ИмяСекцииОрганизации + "_Заголовок_Остальное");
								ТабДокумент.Вывести(ОбластьМакета);
								ТабДокумент.Область(ТабДокумент.ВысотаТаблицы - ОбластьМакета.ВысотаТаблицы + 1, 2, ТабДокумент.ВысотаТаблицы, 10).ЦветФона = ЦветаСтиля.ФонГруппировкиПромежуточногоУровня;
							КонецЕсли;
							ОбластьМакета = Макет.ПолучитьОбласть(ИмяСекцииОрганизации + "_Данные");
							Пока ВыборкаОрганизации.Следующий() Цикл
								ОбластьМакета.Параметры.Заполнить(ВыборкаОрганизации);
								ТабДокумент.Вывести(ОбластьМакета);
							КонецЦикла;
							ТабДокумент.Вывести(Отбивка);
							ТабДокумент.ЗакончитьГруппуСтрок();
						
						КонецЕсли; 

					КонецЕсли;
					
				КонецЦикла;
				
				ТабДокумент.ЗакончитьГруппуСтрок();

			КонецЦикла;

		ИначеЕсли Макет.Области.Найти(ИмяСекции + "_Заголовок") <> Неопределено и Выборка.Следующий() Тогда

			Отбивка = ?(Макет.Области.Найти(ИмяСекции + "_Отбивка") <> Неопределено,Макет.ПолучитьОбласть(ИмяСекции + "_Отбивка"),Макет.ПолучитьОбласть("Отбивка"));
			ОбластьМакета = Макет.ПолучитьОбласть(ИмяСекции + "_Заголовок");
			Если ОбластьМакета.Области.Количество() = 1 Тогда
				ТабДокумент.Вывести(ОбластьМакета);
				ТабДокумент.Область(ТабДокумент.ВысотаТаблицы,,ТабДокумент.ВысотаТаблицы,).ЦветФона = ЦветаСтиля.ФонГруппировкиВерхнегоУровня;
				ТабДокумент.НачатьГруппуСтрок("", УсловиеВыводаРаскрытойГруппы);
				ТабДокумент.Вывести(Отбивка);
			Иначе
				ОбластьМакета = Макет.ПолучитьОбласть(ИмяСекции + "_Заголовок_ПерваяСтрока");
				ТабДокумент.Вывести(ОбластьМакета);
				ТабДокумент.Область(ТабДокумент.ВысотаТаблицы,,ТабДокумент.ВысотаТаблицы,).ЦветФона = ЦветаСтиля.ФонГруппировкиВерхнегоУровня;
				ТабДокумент.НачатьГруппуСтрок("", УсловиеВыводаРаскрытойГруппы);
				ТабДокумент.Вывести(Отбивка);
				ОбластьМакета = Макет.ПолучитьОбласть(ИмяСекции + "_Заголовок_Остальное");
				Если ОбластьМакета.Параметры.Количество() = 0 Тогда // раскрашиваем заголовок без выведенных данных
					ТабДокумент.Область(ТабДокумент.ВысотаТаблицы, 2, ТабДокумент.ВысотаТаблицы, 10).ЦветФона = ЦветаСтиля.ФонГруппировкиПромежуточногоУровня;
				Иначе	
					ОбластьМакета.Параметры.Заполнить(Выборка);
				КонецЕсли;
				ТабДокумент.Вывести(ОбластьМакета);
				Если ОбластьМакета.Параметры.Количество() = 0 Тогда // раскрашиваем заголовок без выведенных данных
					ТабДокумент.Область(ТабДокумент.ВысотаТаблицы - ОбластьМакета.ВысотаТаблицы + 1, 2, ТабДокумент.ВысотаТаблицы, 10).ЦветФона = ЦветаСтиля.ФонГруппировкиПромежуточногоУровня;
				КонецЕсли;
			КонецЕсли;
			ОбластьМакета = Макет.ПолучитьОбласть(ИмяСекции + "_Данные");
			ОбластьМакета.Параметры.Заполнить(Выборка);
			Если ИмяСекции = "ФизическиеЛица" Тогда
				ОбластьМакета.Параметры.МестоРожденияРаботника = РегламентированнаяОтчетность.ПредставлениеМестаРождения(Выборка.МестоРождения);
				Если НЕ ЗначениеЗаполнено(Выборка.Фамилия) Тогда
					Фамилия = " "; Имя = " "; Отчество = " ";
					ПроцедурыУправленияПерсоналом.ФамилияИнициалыФизЛица(ЭтотОбъект, Фамилия, Имя, Отчество);
					ОбластьМакета.Параметры.Фамилия = Фамилия;
					ОбластьМакета.Параметры.Имя = Имя;
					ОбластьМакета.Параметры.Отчество = Отчество;
				КонецЕсли;
			КонецЕсли;
			ТабДокумент.Вывести(ОбластьМакета);
			Пока Выборка.Следующий() Цикл
				ОбластьМакета.Параметры.Заполнить(Выборка);
				ТабДокумент.Вывести(ОбластьМакета);
			КонецЦикла;
			ТабДокумент.Вывести(Отбивка);
			ТабДокумент.ЗакончитьГруппуСтрок();

		КонецЕсли;	
	КонецЦикла;

	Если Подробно Тогда	
		Если ВыборкаИзображений.Следующий() Тогда
			ОбластьМакета = Макет.ПолучитьОбласть("Фотогалерея_Заголовок");
			ТабДокумент.Вывести(ОбластьМакета);
			ТабДокумент.Область(ТабДокумент.ВысотаТаблицы,,ТабДокумент.ВысотаТаблицы,).ЦветФона = ЦветаСтиля.ФонГруппировкиВерхнегоУровня;
			ТабДокумент.НачатьГруппуСтрок("", Не Подробно);
			ОбластьМакета = Макет.ПолучитьОбласть("Фотогалерея");
			ОбластьМакета.Параметры.ОписаниеДанных = ВыборкаИзображений.ОписаниеДанных;
			Фото = ВыборкаИзображений.Данные.Получить();
			Если Фото <> Неопределено Тогда
				ОбластьМакета.Рисунки.Фото.Картинка = Фото;
				ОбластьМакета.Рисунки.Фото.Линия = Новый Линия(ТипЛинииРисункаТабличногоДокумента.НетЛинии,);
			КонецЕсли;
			ТабДокумент.Вывести(ОбластьМакета);
			Пока ВыборкаИзображений.Следующий() Цикл
				ОбластьМакета.Параметры.ОписаниеДанных = ВыборкаИзображений.ОписаниеДанных;
				Фото = ВыборкаИзображений.Данные.Получить();
				ОбластьМакета.Рисунки.Фото.Картинка = ?(Фото <> Неопределено,Фото,Новый Картинка());
				ОбластьМакета.Рисунки.Фото.Линия = Новый Линия(ТипЛинииРисункаТабличногоДокумента.НетЛинии,);
				ТабДокумент.Вывести(ОбластьМакета);
			КонецЦикла;
			ТабДокумент.ЗакончитьГруппуСтрок();
		КонецЕсли;
    КонецЕсли;
    Если ВыводитьНаПечать Тогда
	    Возврат РаботаСДиалогами.НапечататьДокумент(ТабДокумент,,, "Данные по: " + Наименование);
    КонецЕсли;

КонецФункции // Печать

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

//Раскладывает срок в днях на годы, месяцы, дни
Процедура РазложитьСрокВДнях(СрокВДнях,Лет, Месяцев, Дней) 

	ДнейВГоду 		= 12 * 30;
    Лет 			= Цел(СрокВДнях / ДнейВГоду);
	ОстатокДней 	= СрокВДнях - Лет * ДнейВГоду;
	Месяцев			= Цел(ОстатокДней / 30);
	Дней			= ОстатокДней % 30;
КонецПроцедуры 

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ формы СЗВ-К

// По т.ч. Трудовая деятельность
//
// Параметры
//  ЗаписиОСтаже - набор записей для заполнения
//
Процедура ЗаполнитьСЗВКПоТрудовойДеятельности(ЗаписиОСтаже) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ФизЛицо",Ссылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ФизическиеЛицаТрудоваяДеятельность.Организация,
	|	ФизическиеЛицаТрудоваяДеятельность.Должность,
	|	ФизическиеЛицаТрудоваяДеятельность.ДатаНачала КАК НачалоРаботы,
	|	ФизическиеЛицаТрудоваяДеятельность.ДатаОкончания КАК ОкончаниеРаботы,
	|	ФизическиеЛицаТрудоваяДеятельность.НомерСтроки,
	|	ФизическиеЛицаТрудоваяДеятельность.Ссылка
	|ИЗ
	|	Справочник.ФизическиеЛица.ТрудоваяДеятельность КАК ФизическиеЛицаТрудоваяДеятельность
	|ГДЕ
	|	ФизическиеЛицаТрудоваяДеятельность.Ссылка = &ФизЛицо
	|	И ФизическиеЛицаТрудоваяДеятельность.ДатаНачала < ДАТАВРЕМЯ(2002, 1, 1)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НачалоРаботы";

	Выборка = Запрос.Выполнить().Выбрать();

    Сч = 0;
	Пока Выборка.Следующий() цикл
		НоваяСтрока = ЗаписиОСтаже.Добавить();
        Сч = Сч + 1;
		НоваяСтрока.НомерПериодаТрудовойДеятельности = Сч;
		НоваяСтрока.НомерОсновнойЗаписи		= Сч;
		НоваяСтрока.ФизЛицо					= Ссылка;
		НоваяСтрока.Организация				= Выборка.Организация;
		НоваяСтрока.ВидДеятельности 		= Справочники.ВидыОбщественноПолезнойДеятельности.РАБОТА;
		НоваяСтрока.Должность 				= Выборка.Должность;
		НоваяСтрока.ДатаНачалаПериода 	 	= Выборка.НачалоРаботы;
		НоваяСтрока.ДатаОкончанияПериода 	= Мин(Выборка.ОкончаниеРаботы,'20011231');
	КонецЦикла;	
	

КонецПроцедуры // ЗаполнитьСЗВКПоТрудовойДеятельности()

//Выполняет автоматическое заполнение итоговых данных о стаже
Процедура ВыполнитьРасчетИтоговСтажа(ЗаписиОСтаже,СуммированныйСтаж) Экспорт

	ТаблицаЗаписейОСтаже = ЗаписиОСтаже.Выгрузить();

    //РАСЧЕТ ОБЩЕГО ТРУДОВОГО СТАЖА С УЧЕТОМ ПЕРЕСЕЧЕНИЯ ПЕРИОДОВ

	//Выполним расширение записей с кодом исчисляемого стажа СЕЗОН
	Для Каждого Запись из ТаблицаЗаписейОСтаже цикл
		//Пропустим льготные записи
		Если Запись.НомерДополнительнойЗаписи <> 0 тогда
			Продолжить;
		КонецЕсли;	 
		Если Запись.ОснованиеИсчисляемогоСтажа	=	Справочники.ОснованияИсчисляемогоТрудовогоСтажа.СЕЗОН	тогда	
			//Расширение периода до календарного года
			Запись.ДатаНачалаПериода = НачалоГода(Запись.ДатаНачалаПериода);
			Запись.ДатаОкончанияПериода = КонецГода(Запись.ДатаОкончанияПериода);
		КонецЕсли;
	КонецЦикла;	

	//Выполним календарное поглащение в 2 прохода:
	// 1 проход - поглащение записями ЛЕПРО всех остальных записей
	// 2 проход - поглащение записями СЕЗОН всех оставшихся записей
	Для П = 1 по 2 цикл

		Если П = 1 тогда
			ПриоритетноеОснованиеИсчисляемогоСтажа = Справочники.ОснованияИсчисляемогоТрудовогоСтажа.ЛЕПРО;
		Иначе	
			ПриоритетноеОснованиеИсчисляемогоСтажа = Справочники.ОснованияИсчисляемогоТрудовогоСтажа.СЕЗОН;
		КонецЕсли;	 

		ВсегоЗаписей = ТаблицаЗаписейОСтаже.Количество(); 
		Для Н =0 по ВсегоЗаписей-1 цикл
			Запись = ТаблицаЗаписейОСтаже[Н];

			Если Запись.ОснованиеИсчисляемогоСтажа = ПриоритетноеОснованиеИсчисляемогоСтажа тогда

				Для К =0 по ВсегоЗаписей-1 цикл
					Запись2 = ТаблицаЗаписейОСтаже[К];

					Если Запись = Запись2 тогда//Запись не может поглатить саму себя
						Продолжить;
					КонецЕсли;	 
					
					Если (Запись2.ДатаНачалаПериода > Запись.ДатаОкончанияПериода) или
						(Запись2.ДатаОкончанияПериода < Запись.ДатаНачалаПериода)тогда
						//Периоды не пересекаются
						Продолжить;
					Иначе	
						Если (Запись2.ДатаНачалаПериода >= Запись.ДатаНачалаПериода) тогда
							Если (Запись2.ДатаОкончанияПериода <= Запись.ДатаОкончанияПериода) тогда
								//Полное поглощение периода - удаляем запись2 с помощью задания отрицательного интервала
								Запись2.ДатаНачалаПериода = Дата('00010102');
								Запись2.ДатаОкончанияПериода = Дата('00010101');
							Иначе//Запись 2 начинается позднее и окончивается позднее записи 1 - обрежем голову
								Запись2.ДатаНачалаПериода = Запись.ДатаОкончанияПериода + мДлинаСуток;
							КонецЕсли;	
						Иначе
							Если (Запись2.ДатаОкончанияПериода <= Запись.ДатаОкончанияПериода) тогда
								//Запись2 начиначется раньше и окончивается раньше Записи1 - обрежем хвост
								Запись2.ДатаОкончанияПериода = Запись.ДатаНачалаПериода - мДлинаСуток;
							Иначе//Запись 2 начинается раньше и окончивается позднее записи 1 - разбиение интервала на 2 части
								//породим вторую часть копирование исходной записи
								НоваяСтрока = ТаблицаЗаписейОСтаже.Добавить();
								Для НомерКолонки=0 по ТаблицаЗаписейОСтаже.Колонки.Количество()-1 цикл
									ИмяКолонки = ТаблицаЗаписейОСтаже.Колонки[НомерКолонки].Имя;
									НоваяСтрока[ИмяКолонки] = Запись2[ИмяКолонки];
								КонецЦикла;	
								//обрежем хвост у первой части
								Запись2.ДатаОкончанияПериода = Запись.ДатаНачалаПериода - мДлинаСуток;
								//обрежем голову у второй части
								НоваяСтрока.ДатаНачалаПериода = Запись.ДатаОкончанияПериода + мДлинаСуток;
							КонецЕсли;	
						КонецЕсли;	 
					КонецЕсли;	 
				КонецЦикла;	

			КонецЕсли;	
		КонецЦикла;	
		
	КонецЦикла;	
	

	//Расчет общего стажа по оставшимся записям
	ОбщийСтажЛет				=	0;
	ОбщийСтажМесяцев			=	0;
	ОбщийСтажДней				=	0;

	Для Каждого Запись из ТаблицаЗаписейОСтаже цикл

		//Пропустим поглощенные и дополнительные записи
		Если  Запись.ДатаОкончанияПериода = Дата('00010101') тогда
			Продолжить;
		КонецЕсли;	
		
		ЛетПоПериоду = 0;
		МесяцевПоПериоду = 0;
		ДнейПоПериоду = 0;
		ПроцедурыПерсонифицированогоУчета.ПолучитьСтажЗаПериод(Запись.ДатаОкончанияПериода,	Запись.ДатаНачалаПериода, ЛетПоПериоду,	МесяцевПоПериоду, ДнейПоПериоду);

		//Для видов деятельности, отличных от РАБОТА, день на увольнение не предоставляется
		Если Запись.ВидДеятельности <> Справочники.ВидыОбщественноПолезнойДеятельности.РАБОТА тогда
			ДнейПоПериоду = ДнейПоПериоду - 1;
		КонецЕсли;	 

		Если Запись.ОснованиеИсчисляемогоСтажа	=	Справочники.ОснованияИсчисляемогоТрудовогоСтажа.ЛЕПРО	тогда
			//Стаж удваивается
			ЛетПоПериоду = ЛетПоПериоду * 2;
			МесяцевПоПериоду = МесяцевПоПериоду * 2;
			ДнейПоПериоду = ДнейПоПериоду * 2;
		ИначеЕсли Запись.ОснованиеИсчисляемогоСтажа	=	Справочники.ОснованияИсчисляемогоТрудовогоСтажа.УИК104	тогда	
			//Стаж берется из ФОВ
			ЛетПоПериоду = Запись.НулевойПараметрИсчисляемогоСтажа;
			МесяцевПоПериоду = Запись.ПервыйПараметрИсчисляемогоСтажа;
			ДнейПоПериоду = Запись.ВторойПараметрИсчисляемогоСтажа;
		КонецЕсли;	
		
		ОбщийСтажЛет		=	ОбщийСтажЛет		+	ЛетПоПериоду;
		ОбщийСтажМесяцев	=	ОбщийСтажМесяцев	+	МесяцевПоПериоду;
		ОбщийСтажДней		=	ОбщийСтажДней		+	ДнейПоПериоду;
	КонецЦикла;	
	

    //РАСЧЕТ СТАЖА ТЕРР,ОСОБ,ВЫСЛ

	ТаблицаЗаписейОСтаже = ЗаписиОСтаже.Выгрузить();

	ТаблицаТЕРР = Новый ТаблицаЗначений;
	ТаблицаТЕРР.Колонки.Добавить("КодСтажа");
	ТаблицаТЕРР.Колонки.Добавить("Лет");
	ТаблицаТЕРР.Колонки.Добавить("Месяцев");
	ТаблицаТЕРР.Колонки.Добавить("Дней");

	ТаблицаОСОБ = ТаблицаТЕРР.Скопировать();
	ТаблицаВЫСЛ = ТаблицаТЕРР.Скопировать();

	ТекущаяОсновнаяЗапись = Неопределено;

	Для Каждого Запись из ТаблицаЗаписейОСтаже цикл

		Если Запись.НомерДополнительнойЗаписи = 0 тогда
			ТекущаяОсновнаяЗапись = Запись;
			ЭтоОсновнаяЗапись = ИСТИНА;
			//Счетчики терр стажа по коду МКС и РКС в рамках основной записи
			НакопленныйСтажМКС = 0;
			НакопленныйСтажРКС = 0;
		Иначе
			ЭтоОсновнаяЗапись = ЛОЖЬ;
		КонецЕсли;	 
		
		ЛетПоПериоду = 0;
		МесяцевПоПериоду = 0;
		ДнейПоПериоду = 0;
		ПроцедурыПерсонифицированогоУчета.ПолучитьСтажЗаПериод(ТекущаяОсновнаяЗапись.ДатаОкончанияПериода,	ТекущаяОсновнаяЗапись.ДатаНачалаПериода, ЛетПоПериоду,	МесяцевПоПериоду, ДнейПоПериоду);

		ФОВ_Лет = Запись.НулевойПараметрИсчисляемогоСтажа;		
		ФОВ_Месяцев = Запись.ПервыйПараметрИсчисляемогоСтажа;
		ФОВ_Дней = Запись.ВторойПараметрИсчисляемогоСтажа;

		//Признак указания фактически-отработанного времени
		ФОВУказан = (ФОВ_Лет + ФОВ_Месяцев + ФОВ_Дней)  <> 0;
        УказаныОсобыеУсловияТруда = ЗначениеЗаполнено(Запись.ОсобыеУсловияТруда);
		
		//Расчет стажа по территориальным условиям
		Если ЗначениеЗаполнено(Запись.ТерриториальныеУсловия) тогда
			НоваяСтрока = ТаблицаТЕРР.Добавить();
			НоваяСтрока.КодСтажа = Запись.ТерриториальныеУсловия;

			//При неуказании ФОВ, для ВОДОЛАЗОВ, при указании условий труда стаж берем по периоду
			Если (НЕ ФОВУказан)
				ИЛИ (Запись.ОснованиеИсчисляемогоСтажа = Справочники.ОснованияИсчисляемогоТрудовогоСтажа.ВОДОЛАЗ)
				ИЛИ (УказаныОсобыеУсловияТруда) тогда
				СтажДляЗаписи = ЛетПоПериоду*12*30 + МесяцевПоПериоду*30 + ДнейПоПериоду;
			Иначе	
				СтажДляЗаписи = ФОВ_Лет*12*30 + ФОВ_Месяцев*30 + ФОВ_Дней;
			КонецЕсли;	  

			//Для кодов МКС и РКС надо выполнить контроль стажа на непревышение всего периода основной записи
			СтажПоПериодуОсновнойЗаписи = ЛетПоПериоду*12*30 + МесяцевПоПериоду*30 + ДнейПоПериоду;
			Если Запись.ТерриториальныеУсловия = Справочники.ТерриториальныеУсловия.МКС тогда
				СтажДляЗаписи = Мин(СтажПоПериодуОсновнойЗаписи - НакопленныйСтажМКС, СтажДляЗаписи);
				НакопленныйСтажМКС = НакопленныйСтажМКС + СтажДляЗаписи;
			ИначеЕсли Запись.ТерриториальныеУсловия = Справочники.ТерриториальныеУсловия.РКС тогда
				СтажДляЗаписи = Мин(СтажПоПериодуОсновнойЗаписи - НакопленныйСтажРКС, СтажДляЗаписи);
				НакопленныйСтажРКС = НакопленныйСтажРКС + СтажДляЗаписи;
			КонецЕсли;	 

			РазложитьСрокВДнях(СтажДляЗаписи, НоваяСтрока.Лет, НоваяСтрока.Месяцев, НоваяСтрока.Дней);
		КонецЕсли;	

		//Расчет стажа за особые условия труда
		Если УказаныОсобыеУсловияТруда тогда
			НоваяСтрока = ТаблицаОСОБ.Добавить();
			НоваяСтрока.КодСтажа = Запись.ОсобыеУсловияТруда;
			НоваяСтрока.Лет = ФОВ_Лет; 
			НоваяСтрока.Месяцев = ФОВ_Месяцев;
			НоваяСтрока.Дней = ФОВ_Дней;

			//При неуказании ФОВ и ВОДОЛАЗОВ стаж берем по периоду
			Если (НЕ ФОВУказан)или(Запись.ОснованиеИсчисляемогоСтажа = Справочники.ОснованияИсчисляемогоТрудовогоСтажа.ВОДОЛАЗ) тогда
				НоваяСтрока.Лет = ЛетПоПериоду; 
				НоваяСтрока.Месяцев = МесяцевПоПериоду;
				НоваяСтрока.Дней = ДнейПоПериоду;
			Иначе	
				НоваяСтрока.Лет = ФОВ_Лет; 
				НоваяСтрока.Месяцев = ФОВ_Месяцев;
				НоваяСтрока.Дней = ФОВ_Дней;
			КонецЕсли;	  
		КонецЕсли;	

		//Расчет стажа за выслугу лет
		Если ЗначениеЗаполнено(Запись.ОснованиеВыслугиЛет) тогда
			НоваяСтрока = ТаблицаВЫСЛ.Добавить();
			НоваяСтрока.КодСтажа = Запись.ОснованиеВыслугиЛет;

			ВЫСЛпоПериоду = Истина;

			//Если (Запись.ОснованиеВыслугиЛет = Справочники.ОснованияВыслугиЛет.УВД)
			//	или(Запись.ОснованиеВыслугиЛет = Справочники.ОснованияВыслугиЛет.ИТС) тогда
			//	Если Запись.ПервыйПараметрВыслугиЛет + Запись.ВторойПараметрВыслугиЛет <> 0 тогда
			//		НоваяСтрока.Лет = 0; 
			//		НоваяСтрока.Месяцев = Запись.ПервыйПараметрВыслугиЛет;
			//		НоваяСтрока.Дней = Запись.ВторойПараметрВыслугиЛет;
			//		ВЫСЛпоПериоду = Ложь;
			//	КонецЕсли;	
			//КонецЕсли;	

			Если ВЫСЛпоПериоду тогда	
				НоваяСтрока.Лет = ЛетПоПериоду; 
				НоваяСтрока.Месяцев = МесяцевПоПериоду;
				НоваяСтрока.Дней = ДнейПоПериоду;
			КонецЕсли;	 
		КонецЕсли;	

	КонецЦикла;	

	//Заносим в таблицу итогового стажа данные об общем стаже

	ПроцедурыПерсонифицированогоУчета.ПолучитьПриведенныйСтаж(ОбщийСтажЛет,ОбщийСтажМесяцев,ОбщийСтажДней);
	Если ОбщийСтажЛет + ОбщийСтажМесяцев + ОбщийСтажДней <> 0 тогда
		СтрокаИтоговоСтажа = СуммированныйСтаж.Добавить();
		СтрокаИтоговоСтажа.ФизЛицо = Ссылка;
		СтрокаИтоговоСтажа.НомерЗаписи = 1;
		СтрокаИтоговоСтажа.ВидСтажа = Перечисления.ВидыТрудовогоСтажа.ОбщийТрудовойСтаж;
		СтрокаИтоговоСтажа.Лет = ОбщийСтажЛет;
		СтрокаИтоговоСтажа.Месяцев = ОбщийСтажМесяцев;
		СтрокаИтоговоСтажа.Дней = ОбщийСтажДней;
	КонецЕсли;	 

	ТаблицаТЕРР.Свернуть("КодСтажа","Лет,Месяцев,Дней");
	ТаблицаОСОБ.Свернуть("КодСтажа","Лет,Месяцев,Дней");
	ТаблицаВЫСЛ.Свернуть("КодСтажа","Лет,Месяцев,Дней");

	//Заносим в итоговую таблицу данные о территориальных условиях
    Сч = 1;
	Для Каждого Строка из ТаблицаТЕРР цикл
		ПроцедурыПерсонифицированогоУчета.ПолучитьПриведенныйСтаж(Строка.Лет,Строка.Месяцев,Строка.Дней);
		Если Строка.Лет + Строка.Месяцев + Строка.Дней <> 0 тогда
			СтрокаИтоговоСтажа = СуммированныйСтаж.Добавить();
	        Сч = Сч + 1;
			СтрокаИтоговоСтажа.НомерЗаписи = Сч;
			СтрокаИтоговоСтажа.ФизЛицо = Ссылка;
			СтрокаИтоговоСтажа.ВидСтажа = Перечисления.ВидыТрудовогоСтажа.ТерриториальныеУсловия;
			СтрокаИтоговоСтажа.КодСтажа = Строка.КодСтажа;
			СтрокаИтоговоСтажа.Лет = Строка.Лет;
			СтрокаИтоговоСтажа.Месяцев = Строка.Месяцев;
			СтрокаИтоговоСтажа.Дней = Строка.Дней;
		КонецЕсли;	 
	КонецЦикла;	

	//Заносим в итоговую таблицу данные об особых условиях труда
	Для Каждого Строка из ТаблицаОСОБ цикл
		ПроцедурыПерсонифицированогоУчета.ПолучитьПриведенныйСтаж(Строка.Лет,Строка.Месяцев,Строка.Дней);
		Если Строка.Лет + Строка.Месяцев + Строка.Дней <> 0 тогда
			СтрокаИтоговоСтажа = СуммированныйСтаж.Добавить();
	        Сч = Сч + 1;
			СтрокаИтоговоСтажа.НомерЗаписи = Сч;
			СтрокаИтоговоСтажа.ФизЛицо = Ссылка;
			СтрокаИтоговоСтажа.ВидСтажа = Перечисления.ВидыТрудовогоСтажа.ОсобыеУсловияТруда;
			СтрокаИтоговоСтажа.КодСтажа = Строка.КодСтажа;
			СтрокаИтоговоСтажа.Лет = Строка.Лет;
			СтрокаИтоговоСтажа.Месяцев = Строка.Месяцев;
			СтрокаИтоговоСтажа.Дней = Строка.Дней;
		КонецЕсли;	 
	КонецЦикла;	

	//Заносим в итоговую таблицу данные о выслуге лет
	Для Каждого Строка из ТаблицаВЫСЛ цикл
		ПроцедурыПерсонифицированогоУчета.ПолучитьПриведенныйСтаж(Строка.Лет,Строка.Месяцев,Строка.Дней);
		Если Строка.Лет + Строка.Месяцев + Строка.Дней <> 0 тогда
			СтрокаИтоговоСтажа = СуммированныйСтаж.Добавить();
	        Сч = Сч + 1;
			СтрокаИтоговоСтажа.НомерЗаписи = Сч;
			СтрокаИтоговоСтажа.ФизЛицо = Ссылка;
			СтрокаИтоговоСтажа.ВидСтажа = Перечисления.ВидыТрудовогоСтажа.ВыслугаЛет;
			СтрокаИтоговоСтажа.КодСтажа = Строка.КодСтажа;
			СтрокаИтоговоСтажа.Лет = Строка.Лет;
			СтрокаИтоговоСтажа.Месяцев = Строка.Месяцев;
			СтрокаИтоговоСтажа.Дней = Строка.Дней;
		КонецЕсли;	 
	КонецЦикла;	
КонецПроцедуры 

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриКопировании(ОбъектКопирования)
	
	Если Не ЭтоГруппа Тогда
		ОсновноеИзображение = Неопределено;
		ИНН = "";
		КодИМНС = "";
		СтраховойНомерПФР = "";
		МестоРождения = ""; МестоРожденияКодПоОКАТО = "";
		СоставСемьи.Очистить();
		Образование.Очистить();
		ТрудоваяДеятельность.Очистить();
		ЗнаниеЯзыков.Очистить();
		Профессии.Очистить();
		Стажи.Очистить();
	КонецЕсли;
	
КонецПроцедуры

//vvv
Процедура ПриУстановкеНовогоКода(СтандартнаяОбработка, Префикс)	
	
КонецПроцедуры
//vvv
Процедура ПередЗаписью(Отказ)
	
	ОбработкаКомментариев=глЗначениеПеременной("глОбработкаСообщений");
	ОбработкаКомментариев.УдалитьСообщения();
	
	Если ПараметрыСеанса.ИспользоватьОграниченияПравДоступаНаУровнеЗаписей Тогда
		Если Не ЗначениеЗаполнено(ГруппаДоступаФизическогоЛица) ТОгда
			ОбработкаКомментариев.ДобавитьСообщение("Не выбрана группа доступа физического лица!",Перечисления.ВидыСообщений.Ошибка);
			Отказ=Истина;
			ОбработкаКомментариев.ПоказатьСообщения();								
			Возврат;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мДлинаСуток = 86400;
