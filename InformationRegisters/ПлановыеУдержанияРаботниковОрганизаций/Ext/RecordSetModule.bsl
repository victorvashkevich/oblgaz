
// Запись перерасчетов
// Процедура предназначена для записи наборов записей рег-ов ЗаполнениеПлановыхНачислений для тех документов, 
// которые затронуты данным набором записей регистра
// Если набор записей ПлановыеУдержанийРаботниковОрганизаций записывается с датами, после которых проводились 
// начисления зарплаты (по тем же физлицам, по которым записываем удержания), то нужно переначислить 
// зарплату (т.е. перезаполнить соответствующие документы Начисление зарплаты)
// 
// Параметры:
//	нет
// Возвращаемое значение:
//	нет
//
Процедура ЗаписьПерерасчетовВРегистрЗаполнениеПлановыхУдержаний()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	Расчеты.Регистратор КАК ОбъектЗаполнения,
	|	Расчеты.Сотрудник,
	|	Расчеты.ПериодРегистрации,
	|	Расчеты.ОбособленноеПодразделение,
	|	Расчеты.Организация
	|ИЗ
	|	РегистрСведений.ПлановыеУдержанияРаботниковОрганизаций КАК Удержания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК Расчеты
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЗаполнениеПлановыхНачислений КАК Перерасчеты
	|			ПО Расчеты.Регистратор = Перерасчеты.ОбъектЗаполнения
	|				И Расчеты.Сотрудник = Перерасчеты.Сотрудник
	|		ПО Удержания.ФизЛицо = Расчеты.ФизЛицо
	|			И Удержания.Организация = Расчеты.Организация
	|			И Удержания.Период <= Расчеты.ПериодДействияКонец
	|ГДЕ
	|	Удержания.Регистратор = &Регистратор
	|	И Перерасчеты.ОбъектЗаполнения ЕСТЬ NULL 
	|	И Расчеты.Регистратор ССЫЛКА Документ.НачислениеЗарплатыРаботникамОрганизаций";
	
	Выборка = Запрос.Выполнить().Выбрать();
	ПроведениеРасчетов.ДописатьПерерасчетыВЗаполнениеПлановыхНачислений(Выборка);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Замещение Тогда
		// запишем перерасчеты по тем записям, которые сейчас будут замещены
		ЗаписьПерерасчетовВРегистрЗаполнениеПлановыхУдержаний();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// запишем перерасчеты по новым записям
	ЗаписьПерерасчетовВРегистрЗаполнениеПлановыхУдержаний();
КонецПроцедуры