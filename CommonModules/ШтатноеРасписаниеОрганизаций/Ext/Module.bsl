//Функция рассчитывает оклад / ЧТС
//на текущую дату
Функция РассчитатьТариф(Организация, ДатаВступленияВСилу, ТекущиеДанные) Экспорт
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.Должность) Тогда
		Возврат 0;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.ТарифнаяСтавкаПервогоРазряда) Тогда
		Возврат 0;
	КонецЕсли;
	
	Если Не ТекущиеДанные.Должность.СтрокаЕТС.КратныеКоэффициенты Тогда
		Если Не ЗначениеЗаполнено(ТекущиеДанные.РазрядЕТС) Тогда
			Возврат 0;
		КонецЕсли;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ТекущиеДанные.КоэффициентЕТС) Тогда
		Возврат "";
	КонецЕсли;
	
	ПараметрыНормыЧасов = РегистрыСведений.РазмерыПараметровДляРасчета.ПолучитьПоследнее(ДатаВступленияВСилу,
		Новый Структура("ВидПараметра", Перечисления.ВидыПараметровДляРасчетаЗарплаты.СреднемесячнаяНормаЧасов));
		
	СреднееКоличествоЧасов = ПараметрыНормыЧасов.Размер;
	
	РазмерСтавки1Разряда = 0; 
	Тариф = 0;
	РазмерСтавки1Разряда = ПолучитьРазмерТарифнойСтавки(Организация, ДатаВступленияВСилу, ТекущиеДанные.ТарифнаяСтавкаПервогоРазряда);
	
	Если РазмерСтавки1Разряда <> 0 Тогда
		
		ПовышенныйКоэффициентЕТС = ТекущиеДанные.ПовышенныйКоэффициентЕТС;
		КоэффициентыПоЕТС = ТекущиеДанные.Должность.КоэффициентыПоЕТС;
		
		Для Каждого СтрокаКоэффициентыПоЕТС Из КоэффициентыПоЕТС Цикл
			ПовышенныйКоэффициентЕТС = ПовышенныйКоэффициентЕТС * СтрокаКоэффициентыПоЕТС.Размер;
		КонецЦикла;                       
		
		Если ТекущиеДанные.Должность.ВидТарифнойСтавки = Перечисления.ВидыТарифныхСтавок.Месячная Тогда
			
			Тариф = Окр(РазмерСтавки1Разряда * ТекущиеДанные.КоэффициентЕТС * ПовышенныйКоэффициентЕТС);
			
		Иначе
			
			Если СреднееКоличествоЧасов = 0 Тогда
				Возврат 0;
			КонецЕсли;
			
			Тариф = Окр((РазмерСтавки1Разряда * ТекущиеДанные.КоэффициентЕТС * ПовышенныйКоэффициентЕТС) / СреднееКоличествоЧасов , 0);
			
		КонецЕсли;
		
	Иначе 
		
		Возврат 0;
		
	КонецЕсли;
	
	Возврат Тариф;
КонецФункции

//Функция возвращает размер ТС 1-го разряда организации
//на текущую дату
Функция ПолучитьРазмерТарифнойСтавки(Организация, ДатаВступленияВСилу, ТарифнаяСтавка) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Организация", 	Организация);
	Запрос.УстановитьПараметр("Период", 		ДатаВступленияВСилу);
	Запрос.УстановитьПараметр("ТарифнаяСтавка", ТарифнаяСтавка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТарифныеСтавкиПервогоРазрядаОрганизацийСрезПоследних.Размер
	|ИЗ
	|	РегистрСведений.ТарифныеСтавкиПервогоРазрядаОрганизаций.СрезПоследних(
	|			&Период,
	|			Организация = &Организация
	|				И ТарифнаяСтавка = &ТарифнаяСтавка) КАК ТарифныеСтавкиПервогоРазрядаОрганизацийСрезПоследних";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() тогда
		Возврат Выборка.Размер;
	КонецЕсли;
	
КонецФункции
