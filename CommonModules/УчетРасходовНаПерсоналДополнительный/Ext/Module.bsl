
////////////////////////////////////////////////////////////////////////////////
// Процедуры, функции для работы формы подразделения

#Если ТолстыйКлиентОбычноеПриложение Тогда

Процедура ДобавитьЭлементыУправленияНастройкамиУчетаРасходов(Форма, ДополнительныеДействия) Экспорт
	
	ЭлементыФормы	= Форма.ЭлементыФормы;
	Панель			= ЭлементыФормы.Панель;
	Страницы 		= Панель.Страницы;
	
	ШиринаПанели = Панель.Ширина;
	ВысотаПанели = Панель.Высота;
	
	//добавление страницы
	НоваяСтраница = Страницы.Добавить("УчетРасходов", "Расходы на персонал");
	
	//для добавления элементов на соответствующую страницу
	Панель.ТекущаяСтраница = НоваяСтраница;
	
	// добавление надписей с картинками
	// КартинкаИнфо
	КартинкаИнфо = ЭлементыФормы.Добавить(Тип("ПолеКартинки"), "_УчетРасходов_Картинка_Инфо", Истина, Панель);
	КартинкаИнфо.Лево	= 5;
	КартинкаИнфо.Верх	= 20;
	КартинкаИнфо.Ширина	= 20;
	КартинкаИнфо.Высота	= 28;
	КартинкаИнфо.Картинка = БиблиотекаКартинок.СообщениеИнформация;
	
	// Информационная надпись
	НадписьИнфо = ЭлементыФормы.Добавить(Тип("Надпись"), "_УчетРасходов_Надпись_Инфо", Истина, Панель);
	НадписьИнфо.Лево	= 30;
	НадписьИнфо.Верх	= 20;
	НадписьИнфо.Ширина	= ШиринаПанели - 10 - 20 - 5; //30
	НадписьИнфо.Высота	= 28;
	НадписьИнфо.Заголовок = "Настройку учета расходов на персонал подразделения следует задавать в том случае, если она отличается от настройки для компании в целом.";
	НадписьИнфо.ВертикальноеПоложение = ВертикальноеПоложение.Верх;
	НадписьИнфо.УстановитьПривязку(ГраницаЭлементаУправления.Право, Панель, ГраницаЭлементаУправления.Право);
	
	// текст текущей настройки учета расходов
	НадписьНастройкаУчетаРасходов = ЭлементыФормы.Добавить(Тип("Надпись"), "НадписьНастройкаУчетаРасходов", Истина, Панель);
	НадписьНастройкаУчетаРасходов.Лево	= 30;
	НадписьНастройкаУчетаРасходов.Верх	= 60;
	НадписьНастройкаУчетаРасходов.Ширина	= ШиринаПанели - 10 - 20 - 5; //30
	НадписьНастройкаУчетаРасходов.Высота	= 130;
	НадписьНастройкаУчетаРасходов.ВертикальноеПоложение = ВертикальноеПоложение.Верх;
	НадписьНастройкаУчетаРасходов.УстановитьПривязку(ГраницаЭлементаУправления.Право, Панель, ГраницаЭлементаУправления.Право);
	НадписьНастройкаУчетаРасходов.ЦветТекста = ЦветаСтиля.ТекстИнформационнойНадписи;
	
	// гиперссылка для перехода к настройке учета расходов для подразделения
	НадписьНастройкаУчетаРасходовИзменить = ЭлементыФормы.Добавить(Тип("Надпись"), "НадписьНастройкаУчетаРасходовИзменить", Истина, Панель);
	НадписьНастройкаУчетаРасходовИзменить.Лево	= 30;
	НадписьНастройкаУчетаРасходовИзменить.Верх	= 200;
	НадписьНастройкаУчетаРасходовИзменить.Ширина	= ШиринаПанели - 10 - 20 - 5; //30
	НадписьНастройкаУчетаРасходовИзменить.Высота	= 20;
	НадписьНастройкаУчетаРасходовИзменить.ВертикальноеПоложение = ВертикальноеПоложение.Верх;
	НадписьНастройкаУчетаРасходовИзменить.УстановитьПривязку(ГраницаЭлементаУправления.Право, Панель, ГраницаЭлементаУправления.Право);
	НадписьНастройкаУчетаРасходовИзменить.Заголовок = "Задать настройку учета расходов на персонал подразделения";
	НадписьНастройкаУчетаРасходовИзменить.ЦветТекста = ЦветаСтиля.ЦветГиперссылки;
	НадписьНастройкаУчетаРасходовИзменить.Гиперссылка = Истина;
	НадписьНастройкаУчетаРасходовИзменить.УстановитьДействие("Нажатие", ДополнительныеДействия);
	
	//вернем исходную текущую страницу
	Панель.ТекущаяСтраница = Панель.Страницы[0];
	
КонецПроцедуры

Процедура ПрочитатьНастройкуУчетаРасходовПодразделения(Форма) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(УчетЗаработкаРаботниковПодразделения.СтатьяРасходовЗарплата, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА УчетЗаработкаРаботниковПодразделения.СтатьяРасходовЗарплата
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(УчетЗаработкаРаботниковКомпании.СтатьяРасходовЗарплата, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|					ТОГДА УчетЗаработкаРаботниковКомпании.СтатьяРасходовЗарплата
	|				ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатьяРасходовЗарплата,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(УчетЗаработкаРаботниковПодразделения.СтатьяРасходовЗарплата, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА "" (действует для подразделения)""
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(УчетЗаработкаРаботниковКомпании.СтатьяРасходовЗарплата, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|					ТОГДА "" (действует для компании)""
	|				ИНАЧЕ "" (нет действующего значения)""
	|			КОНЕЦ
	|	КОНЕЦ КАК ИсточникСтатьиРасходовЗарплата,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(УчетЗаработкаРаботниковПодразделения.СтатьяРасходовНДФЛ, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА УчетЗаработкаРаботниковПодразделения.СтатьяРасходовНДФЛ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(УчетЗаработкаРаботниковКомпании.СтатьяРасходовНДФЛ, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|					ТОГДА УчетЗаработкаРаботниковКомпании.СтатьяРасходовНДФЛ
	|				ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатьяРасходовНДФЛ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(УчетЗаработкаРаботниковПодразделения.СтатьяРасходовНДФЛ, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА "" (действует для подразделения)""
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(УчетЗаработкаРаботниковКомпании.СтатьяРасходовНДФЛ, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|					ТОГДА "" (действует для компании)""
	|				ИНАЧЕ "" (нет действующего значения)""
	|			КОНЕЦ
	|	КОНЕЦ КАК ИсточникСтатьиРасходовНДФЛ,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(УчетЗаработкаРаботниковПодразделения.СтатьяРасходовСтраховыеВзносы, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА УчетЗаработкаРаботниковПодразделения.СтатьяРасходовСтраховыеВзносы
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(УчетЗаработкаРаботниковКомпании.СтатьяРасходовСтраховыеВзносы, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|					ТОГДА УчетЗаработкаРаботниковКомпании.СтатьяРасходовСтраховыеВзносы
	|				ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатьяРасходовСтраховыеВзносы,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(УчетЗаработкаРаботниковПодразделения.СтатьяРасходовСтраховыеВзносы, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА "" (действует для подразделения)""
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(УчетЗаработкаРаботниковКомпании.СтатьяРасходовСтраховыеВзносы, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|					ТОГДА "" (действует для компании)""
	|				ИНАЧЕ "" (нет действующего значения)""
	|			КОНЕЦ
	|	КОНЕЦ КАК ИсточникСтатьиРасходовСтраховыеВзносы,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(УчетЗаработкаРаботниковПодразделения.СтатьяРасходовЛьготы, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА УчетЗаработкаРаботниковПодразделения.СтатьяРасходовЛьготы
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(УчетЗаработкаРаботниковКомпании.СтатьяРасходовЛьготы, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|					ТОГДА УчетЗаработкаРаботниковКомпании.СтатьяРасходовЛьготы
	|				ИНАЧЕ ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			КОНЕЦ
	|	КОНЕЦ КАК СтатьяРасходовЛьготы,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(УчетЗаработкаРаботниковПодразделения.СтатьяРасходовЛьготы, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|			ТОГДА "" (действует для подразделения)""
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ЕСТЬNULL(УчетЗаработкаРаботниковКомпании.СтатьяРасходовЛьготы, ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)) <> ЗНАЧЕНИЕ(ПланВидовХарактеристик.СтатьиРасходов.ПустаяСсылка)
	|					ТОГДА "" (действует для компании)""
	|				ИНАЧЕ "" (нет действующего значения)""
	|			КОНЕЦ
	|	КОНЕЦ КАК ИсточникСтатьиРасходовЛьготы,
	|	УчетЗаработкаРаботниковПодразделения.НаправлениеДеятельности,
	|	ЕСТЬNULL(УчетЗаработкаРаботниковПодразделения.Коэффициент, 0) КАК Коэффициент
	|ИЗ
	|	Справочник.Подразделения КАК Подразделения
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетЗаработкаРаботниковПодразделения КАК УчетЗаработкаРаботниковПодразделения
	|		ПО (УчетЗаработкаРаботниковПодразделения.Подразделение = Подразделения.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетЗаработкаРаботниковКомпании КАК УчетЗаработкаРаботниковКомпании
	|		ПО (ИСТИНА)
	|ГДЕ
	|	Подразделения.Ссылка = &Ссылка";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Ссылка", Форма.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	ВидыСтатейРасходов = Новый Структура;
	ВидыСтатейРасходов.Вставить("Зарплата", "Расходы на зарплату: ");
	ВидыСтатейРасходов.Вставить("НДФЛ", "НДФЛ: ");
	ВидыСтатейРасходов.Вставить("СтраховыеВзносы", "Страховые взносы: ");
	ВидыСтатейРасходов.Вставить("Льготы", "Льготы: ");
	
	// заголовок надписи
	ТекстНастройкиУчетаРасходов = "Расходы на персонал подразделения распределяются ";
	Выборка.Следующий();
	Если Выборка.Количество() > 1 Тогда
		ТекстНастройкиУчетаРасходов = ТекстНастройкиУчетаРасходов + "по нескольким направлениям деятельности: ";
		ТекстНастройкиУчетаРасходов = ТекстНастройкиУчетаРасходов + Символы.ПС
		+ " по направлению " + Выборка.НаправлениеДеятельности + ":"; 
	Иначе
		ТекстНастройкиУчетаРасходов = ТекстНастройкиУчетаРасходов + "по статьям:";
	КонецЕсли;
	
	// список статей расходов
	Для Каждого ВидСтатьи Из ВидыСтатейРасходов Цикл
		ТекстНастройкиУчетаРасходов = ТекстНастройкиУчетаРасходов + Символы.ПС
		+ Символы.Таб 
		+ ВидСтатьи.Значение 
		+ Выборка["СтатьяРасходов" + ВидСтатьи.Ключ] + Выборка["ИсточникСтатьиРасходов" + ВидСтатьи.Ключ];
	КонецЦикла;
	
	// завершающий текст
	Если Выборка.Количество() > 1 Тогда
		ТекстНастройкиУчетаРасходов = ТекстНастройкиУчетаРасходов + "." + Символы.ПС 
		+ "Статьи расходов по другим направлениям подробнее см. в настройке учета расходов для подразделения.
		|Расходы распределяются по направлениям деятельности пропорционально " 
		+ ?(Выборка.Коэффициент = 0, "остальным расходам.", "коэффицентам.");
		Форма.ПравилоРаспределенияРасходовНаПерсонал = ?(Выборка.Коэффициент = 0, 
		Перечисления.ПравилаРаспределенияПоНаправлениямДеятельности.ПропорциональноРасходам, 
		Перечисления.ПравилаРаспределенияПоНаправлениямДеятельности.ПропорциональноКоэффициентам);
	Иначе
		ТекстНастройкиУчетаРасходов = ТекстНастройкиУчетаРасходов + "." + Символы.ПС 
		+ "Расходы " 
		+ ?(ЗначениеЗаполнено(Выборка.НаправлениеДеятельности), 
		" относятся на направление деятельности " + Выборка.НаправлениеДеятельности, 
		"распределяются по всем направлениям деятельности пропорционально остальным расходам.");
		Форма.ПравилоРаспределенияРасходовНаПерсонал = Перечисления.ПравилаРаспределенияПоНаправлениямДеятельности.ПропорциональноРасходам;
	КонецЕсли;
	
	Форма.ЭлементыФормы.НадписьНастройкаУчетаРасходов.Заголовок = ТекстНастройкиУчетаРасходов;
	
КонецПроцедуры

Процедура ОткрытьФормуНастройкиУчетаРасходовПодразделения(Форма, Элемент) Экспорт
	
	Отказ = Ложь;

	Если Форма.ЭтоНовый() Тогда
		Вопрос = "Перед заданием бухучета зарплаты необходимо записать подразделение. Записать?";
		Ответ  = Вопрос(Вопрос, РежимДиалогаВопрос.ОКОтмена);
		Если Ответ = КодВозвратаДиалога.ОК Тогда
			Попытка
				Отказ = Не Форма.ЗаписатьВФорме();
			Исключение
				ОбщегоНазначенияЗК.СообщитьОбОшибке("Не удалось записать подразделение");
				Отказ = Истина;
			КонецПопытки;
		Иначе
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;

	Если Не Отказ Тогда
		Ссылка = Форма.Ссылка;
		ФормаРегистра = РегистрыСведений.УчетЗаработкаРаботниковПодразделения.ПолучитьФормуСписка("ФормаСпискаОтборПоПодразделению", Форма, Элемент);
		ФормаРегистра.ПравилоРаспределения = Форма.ПравилоРаспределенияРасходовНаПерсонал;
		ФормаРегистра.РегистрСведенийСписок.Отбор.Подразделение.Установить(Ссылка);
		ФормаРегистра.ЭлементыФормы.РегистрСведенийСписок.НачальноеОтображениеСписка = НачальноеОтображениеСписка.Конец;
		ФормаРегистра.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Настройка учета расходов на персонал подразделения %1",
									СокрЛП(Форма.Наименование));
		ФормаРегистра.Открыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// Процедуры, функции для работы регистров учета расходов

Функция ОчереднойНомерНаправленияДеятельности(Подразделение) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(УчетЗаработкаРаботниковПодразделения.НомерНаправленияДеятельности), 0) КАК МаксимальныйНомер
	|ИЗ
	|	РегистрСведений.УчетЗаработкаРаботниковПодразделения КАК УчетЗаработкаРаботниковПодразделения
	|ГДЕ
	|	УчетЗаработкаРаботниковПодразделения.Подразделение = &Подразделение";
	
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка.МаксимальныйНомер + 1;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Процедуры, функции 

Процедура ПроверитьЗаполнитьКурсВалютыРасходовНаПерсонал(Курс, Период, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Константы.ВалютаУправленческогоУчета = ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеЗаданаВалюта,
	|	ВЫБОР
	|		КОГДА КурсВалютыУпрУчета.Валюта ЕСТЬ NULL 
	|				И Константы.ВалютаУправленческогоУчета <> Константы.ВалютаРегламентированногоУчета
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК НеЗаданКурс,
	|	Константы.ВалютаУправленческогоУчета КАК Валюта,
	|	ВЫБОР
	|		КОГДА Константы.ВалютаУправленческогоУчета = Константы.ВалютаРегламентированногоУчета
	|			ТОГДА 1
	|		ИНАЧЕ КурсВалютыУпрУчета.Курс / КурсВалютыУпрУчета.Кратность
	|	КОНЕЦ КАК Курс
	|ИЗ
	|	Константы КАК Константы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК КурсВалютыУпрУчета
	|		ПО (КурсВалютыУпрУчета.Валюта = Константы.ВалютаУправленческогоУчета)
	|			И (КурсВалютыУпрУчета.Период = &Период)";
	
	Запрос.УстановитьПараметр("Период", Период);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если Выборка.НеЗаданаВалюта Тогда
		ОбщегоНазначенияЗК.СообщитьОбОшибке("Валюта учета расходов на персонал не задана", Отказ);
	Иначе
		Если Выборка.НеЗаданКурс Тогда
			ОбщегоНазначенияЗК.СообщитьОбОшибке(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("Курс валюты учета расходов на персонал (%1) не задан на текущий период",
						Выборка.Валюта), Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ Отказ Тогда
		Курс = Выборка.Курс;
	КонецЕсли;
	
КонецПроцедуры

// Функция строит текст запроса, создающий временные таблицы данных 
//  по расходам на персонал всех категорий. 
//  Расходы на персонал собираются по данных учета для физических лиц 
//  временной таблицы Физлица за 1 месяц
//
Функция ТекстЗапросаСуммыРасходовНаПерсонал(ОграничиватьПоФизлицу = Ложь) Экспорт

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ФизическиеЛица.Ссылка КАК Физлицо
	|ПОМЕСТИТЬ Физлица
	|ИЗ
	|	Справочник.ФизическиеЛица КАК ФизическиеЛица";
	Если ОграничиватьПоФизлицу Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|ГДЕ
		|	ФизическиеЛица.Ссылка = &Физлицо";
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + "
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизическиеЛица.Ссылка
	|;
	|";
	
	ТекстЗапроса = ТекстЗапроса + 
	"ВЫБРАТЬ
	|	СведенияОФизлицеДляУпрУчетаСрезПоследних.Физлицо КАК Физлицо,
	|	СведенияОФизлицеДляУпрУчетаСрезПоследних.Подразделение КАК Подразделение
	|ПОМЕСТИТЬ СведенияОФизлицах
	|ИЗ
	|	РегистрСведений.СведенияОФизлицеДляУпрУчета.СрезПоследних(
	|			&ПериодРегистрации,
	|			Физлицо В
	|				(ВЫБРАТЬ
	|					Физлица.Физлицо
	|				ИЗ
	|					Физлица)) КАК СведенияОФизлицеДляУпрУчетаСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА Константы.МетодПланированияУчетаРасходовНаПерсонал = ЗНАЧЕНИЕ(Перечисление.МетодыПланированияУчетаРасходовНаПерсонал.МетодНачислений)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК МетодНачислений,
	|	ВЫБОР
	|		КОГДА Константы.МетодПланированияУчетаРасходовНаПерсонал = ЗНАЧЕНИЕ(Перечисление.МетодыПланированияУчетаРасходовНаПерсонал.КассовыйМетод)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК КассовыйМетод
	|ПОМЕСТИТЬ МетодПланированияУчетаРасходовНаПерсонал
	|ИЗ
	|	Константы КАК Константы
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	УправленческиеНачисления.ФизЛицо,
	|	СУММА(УправленческиеНачисления.Результат) КАК Результат,
	|	УправленческиеНачисления.Подразделение КАК Подразделение
	|ПОМЕСТИТЬ Начисления
	|ИЗ
	|	РегистрРасчета.УправленческиеНачисления КАК УправленческиеНачисления
	|ГДЕ
	|	УправленческиеНачисления.ФизЛицо В
	|			(ВЫБРАТЬ
	|				Физлица.Физлицо
	|			ИЗ
	|				Физлица)
	|	И НЕ УправленческиеНачисления.ПоВременнойСхемеМотивации
	|	И УправленческиеНачисления.Результат <> 0
	|	И УправленческиеНачисления.ПериодРегистрации = &ПериодРегистрации
	|
	|СГРУППИРОВАТЬ ПО
	|	УправленческиеНачисления.ФизЛицо,
	|	УправленческиеНачисления.Подразделение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДФЛРасчетыСБюджетомОбороты.ФизЛицо,
	|	СУММА(НДФЛРасчетыСБюджетомОбороты.СуммаРасход / &КурсВалютыРасходовНаПерсонал) КАК СуммаНалога
	|ПОМЕСТИТЬ НДФЛУплаченный
	|ИЗ
	|	РегистрНакопления.РасчетыНалоговыхАгентовСБюджетомПоНДФЛ.Обороты(
	|			&ПериодРегистрации,
	|			КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ),
	|			Месяц,
	|			Физлицо В
	|				(ВЫБРАТЬ
	|					Физлица.Физлицо
	|				ИЗ
	|					Физлица)) КАК НДФЛРасчетыСБюджетомОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	НДФЛРасчетыСБюджетомОбороты.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДФЛНачисленный.ФизЛицо,
	|	СУММА(НДФЛНачисленный.Налог / &КурсВалютыРасходовНаПерсонал) КАК СуммаНалога
	|ПОМЕСТИТЬ НДФЛНачисленный
	|ИЗ
	|	РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛНачисленный
	|ГДЕ
	|	НДФЛНачисленный.МесяцНалоговогоПериода = &ПериодРегистрации
	|	И НДФЛНачисленный.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	И НДФЛНачисленный.ФизЛицо В
	|			(ВЫБРАТЬ
	|				Физлица.Физлицо
	|			ИЗ
	|				Физлица)
	|
	|СГРУППИРОВАТЬ ПО
	|	НДФЛНачисленный.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НДФЛ.ФизЛицо,
	|	НДФЛ.СуммаНалога
	|ПОМЕСТИТЬ НДФЛ
	|ИЗ
	|	НДФЛНачисленный КАК НДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МетодПланированияУчетаРасходовНаПерсонал КАК МетодПланированияУчетаРасходовНаПерсонал
	|		ПО (МетодПланированияУчетаРасходовНаПерсонал.МетодНачислений)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	НДФЛ.ФизЛицо,
	|	НДФЛ.СуммаНалога
	|ИЗ
	|	НДФЛУплаченный КАК НДФЛ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МетодПланированияУчетаРасходовНаПерсонал КАК МетодПланированияУчетаРасходовНаПерсонал
	|		ПО (МетодПланированияУчетаРасходовНаПерсонал.КассовыйМетод)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоставляющиеРасходовНаЗарплату.Физлицо КАК Физлицо,
	|	СУММА(СоставляющиеРасходовНаЗарплату.СальдоНачальное) КАК СальдоНачальное,
	|	СУММА(СоставляющиеРасходовНаЗарплату.СальдоКонечное) КАК СальдоКонечное,
	|	СУММА(СоставляющиеРасходовНаЗарплату.Начислено) КАК Начислено,
	|	СУММА(СоставляющиеРасходовНаЗарплату.Штрафы) КАК Штрафы,
	|	СУММА(СоставляющиеРасходовНаЗарплату.НДФЛ) КАК НДФЛ
	|ПОМЕСТИТЬ СоставляющиеРасходовНаЗарплату
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВзаиморасчетыСРаботникамиОстаткиИОбороты.Физлицо КАК Физлицо,
	|		ВзаиморасчетыСРаботникамиОстаткиИОбороты.СуммаУпрНачальныйОстаток КАК СальдоНачальное,
	|		ВзаиморасчетыСРаботникамиОстаткиИОбороты.СуммаУпрКонечныйОстаток КАК СальдоКонечное,
	|		0 КАК Начислено,
	|		0 КАК Штрафы,
	|		0 КАК НДФЛ
	|	ИЗ
	|		РегистрНакопления.ВзаиморасчетыСРаботниками.ОстаткиИОбороты(
	|				&ПериодРегистрации,
	|				КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ),
	|				Месяц,
	|				,
	|				Физлицо В
	|					(ВЫБРАТЬ
	|						Физлица.Физлицо
	|					ИЗ
	|						Физлица)) КАК ВзаиморасчетыСРаботникамиОстаткиИОбороты
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Начисления.ФизЛицо,
	|		0,
	|		0,
	|		Начисления.Результат,
	|		0,
	|		0
	|	ИЗ
	|		Начисления КАК Начисления
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		УправленческиеУдержания.ФизЛицо,
	|		0,
	|		0,
	|		0,
	|		ЕСТЬNULL(УправленческиеУдержания.Результат, 0),
	|		0
	|	ИЗ
	|		РегистрРасчета.УправленческиеУдержания КАК УправленческиеУдержания
	|	ГДЕ
	|		УправленческиеУдержания.ФизЛицо В
	|				(ВЫБРАТЬ
	|					Физлица.Физлицо
	|				ИЗ
	|					Физлица)
	|		И УправленческиеУдержания.ПериодРегистрации = &ПериодРегистрации
	|		И УправленческиеУдержания.Результат <> 0
	|		И НЕ УправленческиеУдержания.ПоВременнойСхемеМотивации
	|		И НЕ УправленческиеУдержания.ВидРасчета.ЯвляетсяУдержаниемВПользуТретихЛиц
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		НДФЛ.ФизЛицо,
	|		0,
	|		0,
	|		0,
	|		0,
	|		НДФЛ.СуммаНалога
	|	ИЗ
	|		НДФЛ КАК НДФЛ) КАК СоставляющиеРасходовНаЗарплату
	|
	|СГРУППИРОВАТЬ ПО
	|	СоставляющиеРасходовНаЗарплату.Физлицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходыНаЗарплату.Физлицо,
	|	РасходыНаЗарплату.СальдоНачальное,
	|	РасходыНаЗарплату.СальдоКонечное,
	|	РасходыНаЗарплату.Начислено,
	|	РасходыНаЗарплату.Штрафы,
	|	РасходыНаЗарплату.НДФЛ,
	|	РасходыНаЗарплату.СальдоНачальное + РасходыНаЗарплату.Начислено - ВЫБОР
	|		КОГДА ЕСТЬNULL(УчетЗаработкаРаботников.УчетНачисленийПоОрганизации, ЛОЖЬ)
	|			ТОГДА РасходыНаЗарплату.НДФЛ
	|		ИНАЧЕ 0
	|	КОНЕЦ - РасходыНаЗарплату.Штрафы - РасходыНаЗарплату.СальдоКонечное КАК Расходы
	|ПОМЕСТИТЬ РасходыНаЗарплатуКассовымМетодом
	|ИЗ
	|	СоставляющиеРасходовНаЗарплату КАК РасходыНаЗарплату
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетЗаработкаРаботников.СрезПоследних(КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ), ) КАК УчетЗаработкаРаботников
	|		ПО РасходыНаЗарплату.Физлицо = УчетЗаработкаРаботников.Физлицо
	|ГДЕ
	|	РасходыНаЗарплату.СальдоНачальное + РасходыНаЗарплату.Начислено - ВЫБОР
	|			КОГДА ЕСТЬNULL(УчетЗаработкаРаботников.УчетНачисленийПоОрганизации, ЛОЖЬ)
	|				ТОГДА РасходыНаЗарплату.НДФЛ
	|			ИНАЧЕ 0
	|		КОНЕЦ - РасходыНаЗарплату.Штрафы - РасходыНаЗарплату.СальдоКонечное <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходыНаЗарплату.Физлицо,
	|	РасходыНаЗарплату.Начислено,
	|	РасходыНаЗарплату.Штрафы,
	|	РасходыНаЗарплату.НДФЛ,
	|	РасходыНаЗарплату.Начислено - РасходыНаЗарплату.Штрафы КАК Расходы
	|ПОМЕСТИТЬ РасходыНаЗарплатуМетодомНачислений
	|ИЗ
	|	СоставляющиеРасходовНаЗарплату КАК РасходыНаЗарплату
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетЗаработкаРаботников.СрезПоследних(КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ), ) КАК УчетЗаработкаРаботников
	|		ПО РасходыНаЗарплату.Физлицо = УчетЗаработкаРаботников.Физлицо
	|ГДЕ
	|	РасходыНаЗарплату.Начислено - РасходыНаЗарплату.Штрафы <> 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходыНаЗарплату.Физлицо,
	|	РасходыНаЗарплату.Расходы
	|ПОМЕСТИТЬ РасходыНаЗарплату
	|ИЗ
	|	РасходыНаЗарплатуМетодомНачислений КАК РасходыНаЗарплату
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МетодПланированияУчетаРасходовНаПерсонал КАК МетодПланированияУчетаРасходовНаПерсонал
	|		ПО (МетодПланированияУчетаРасходовНаПерсонал.МетодНачислений)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасходыНаЗарплату.Физлицо,
	|	РасходыНаЗарплату.Расходы
	|ИЗ
	|	РасходыНаЗарплатуКассовымМетодом КАК РасходыНаЗарплату
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МетодПланированияУчетаРасходовНаПерсонал КАК МетодПланированияУчетаРасходовНаПерсонал
	|		ПО (МетодПланированияУчетаРасходовНаПерсонал.КассовыйМетод)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПособияСоциальномуСтрахованию.Сотрудник.Физлицо КАК Физлицо,
	|	СУММА(ПособияСоциальномуСтрахованию.СуммаВсего) КАК СуммаВсего
	|ПОМЕСТИТЬ ПособияПоСоциальномуСтрахованию
	|ИЗ
	|	РегистрНакопления.ПособияСоциальномуСтрахованию КАК ПособияСоциальномуСтрахованию
	|ГДЕ
	|	ПособияСоциальномуСтрахованию.Период >= &ПериодРегистрации
	|	И ПособияСоциальномуСтрахованию.Период <= КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ)
	|	И ПособияСоциальномуСтрахованию.ВидПособияСоциальногоСтрахования <> ЗНАЧЕНИЕ(Перечисление.ВидыПособийСоциальногоСтрахования.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПособияСоциальномуСтрахованию.Сотрудник.Физлицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтраховыеВзносыИсчисленные.ФизЛицо,
	|	СУММА(СтраховыеВзносыИсчисленные.СуммаВзносов) КАК СуммаВзносов
	|ПОМЕСТИТЬ СтраховыеВзносыНачисленные
	|ИЗ
	|	(ВЫБРАТЬ
	|		СтраховыеВзносыИсчисленныеОбороты.ФизЛицо КАК ФизЛицо,
	|		(СтраховыеВзносыИсчисленныеОбороты.ПФРСтраховаяОборот + СтраховыеВзносыИсчисленныеОбороты.ПФРСтраховаяЕНВДОборот + СтраховыеВзносыИсчисленныеОбороты.ПФРНакопительнаяОборот + СтраховыеВзносыИсчисленныеОбороты.ПФРНакопительнаяЕНВДОборот + СтраховыеВзносыИсчисленныеОбороты.ФССОборот + СтраховыеВзносыИсчисленныеОбороты.ФССЕНВДОборот + СтраховыеВзносыИсчисленныеОбороты.ФФОМСОборот + СтраховыеВзносыИсчисленныеОбороты.ФФОМСЕНВДОборот + СтраховыеВзносыИсчисленныеОбороты.ТФОМСОборот + СтраховыеВзносыИсчисленныеОбороты.ТФОМСЕНВДОборот + СтраховыеВзносыИсчисленныеОбороты.ФССНесчастныеСлучаиОборот + СтраховыеВзносыИсчисленныеОбороты.ПФРПоДополнительномуТарифуОборот + СтраховыеВзносыИсчисленныеОбороты.ПФРНаДоплатуКПенсииШахтерамОборот + СтраховыеВзносыИсчисленныеОбороты.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахОборот + СтраховыеВзносыИсчисленныеОбороты.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахОборот + СтраховыеВзносыИсчисленныеОбороты.ПФРПоСуммарномуТарифуОборот + СтраховыеВзносыИсчисленныеОбороты.ПФРПоСуммарномуТарифуЕНВДОборот + СтраховыеВзносыИсчисленныеОбороты.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахОпасныйОборот + СтраховыеВзносыИсчисленныеОбороты.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахВредный1Оборот + СтраховыеВзносыИсчисленныеОбороты.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахВредный2Оборот + СтраховыеВзносыИсчисленныеОбороты.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахВредный3Оборот + СтраховыеВзносыИсчисленныеОбороты.ПФРЗаЗанятыхНаПодземныхИВредныхРаботахВредный4Оборот + СтраховыеВзносыИсчисленныеОбороты.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахОпасныйОборот + СтраховыеВзносыИсчисленныеОбороты.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахВредный1Оборот + СтраховыеВзносыИсчисленныеОбороты.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахВредный2Оборот + СтраховыеВзносыИсчисленныеОбороты.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахВредный3Оборот + СтраховыеВзносыИсчисленныеОбороты.ПФРЗаЗанятыхНаТяжелыхИПрочихРаботахВредный4Оборот) / &КурсВалютыРасходовНаПерсонал КАК СуммаВзносов
	|	ИЗ
	|		РегистрНакопления.СтраховыеВзносыИсчисленные.Обороты(&ПериодРегистрации, КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ), Месяц, ) КАК СтраховыеВзносыИсчисленныеОбороты
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ПособияПоУходуЗаРебенкомДоПолутораЛет.ФизЛицо,
	|		-ПособияПоУходуЗаРебенкомДоПолутораЛет.СуммаВсегоОборот / &КурсВалютыРасходовНаПерсонал
	|	ИЗ
	|		РегистрНакопления.ПособияПоУходуЗаРебенкомДоПолутораЛет.Обороты(&ПериодРегистрации, КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ), Месяц, ) КАК ПособияПоУходуЗаРебенкомДоПолутораЛет
	|	
	|	ОБЪЕДИНИТЬ
	|	
	|	ВЫБРАТЬ
	|		ПособияПоСоциальномуСтрахованию.Физлицо,
	|		-ПособияПоСоциальномуСтрахованию.СуммаВсего / &КурсВалютыРасходовНаПерсонал
	|	ИЗ
	|		ПособияПоСоциальномуСтрахованию КАК ПособияПоСоциальномуСтрахованию) КАК СтраховыеВзносыИсчисленные
	|
	|СГРУППИРОВАТЬ ПО
	|	СтраховыеВзносыИсчисленные.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(СтраховыеВзносыНачисленные.СуммаВзносов) КАК СуммаВзносов
	|ПОМЕСТИТЬ СтраховыеВзносыНачисленныеВсего
	|ИЗ
	|	СтраховыеВзносыНачисленные КАК СтраховыеВзносыНачисленные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтраховыеВзносыНачисленные.ФизЛицо,
	|	ВЫБОР
	|		КОГДА СтраховыеВзносыНачисленныеВсего.СуммаВзносов = 0
	|			ТОГДА 0
	|		ИНАЧЕ СтраховыеВзносыНачисленные.СуммаВзносов / СтраховыеВзносыНачисленныеВсего.СуммаВзносов
	|	КОНЕЦ КАК КоэффициентРаспределенияУплаченныхВзносов
	|ПОМЕСТИТЬ СтраховыеВзносыРаспределение
	|ИЗ
	|	СтраховыеВзносыНачисленные КАК СтраховыеВзносыНачисленные,
	|	СтраховыеВзносыНачисленныеВсего КАК СтраховыеВзносыНачисленныеВсего
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА((РасчетыПоСтраховымВзносам.ПФРСтраховая + РасчетыПоСтраховымВзносам.ПФРНакопительная + РасчетыПоСтраховымВзносам.ФСС + РасчетыПоСтраховымВзносам.ФФОМС + РасчетыПоСтраховымВзносам.ТФОМС + РасчетыПоСтраховымВзносам.ФССНесчастныеСлучаи + РасчетыПоСтраховымВзносам.ПФРПоДополнительномуТарифу + РасчетыПоСтраховымВзносам.ПФРНаДоплатуКПенсииШахтерам) / &КурсВалютыРасходовНаПерсонал) КАК СуммаВзносов
	|ПОМЕСТИТЬ СтраховыеВзносыУплаченныеВсего
	|ИЗ
	|	РегистрНакопления.РасчетыПоСтраховымВзносам КАК РасчетыПоСтраховымВзносам
	|ГДЕ
	|	РасчетыПоСтраховымВзносам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И РасчетыПоСтраховымВзносам.ВидПлатежа <> ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВГосБюджет.РасходыПоСтрахованию)
	|	И РасчетыПоСтраховымВзносам.МесяцРасчетногоПериода = &ПериодРегистрации
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтраховыеВзносыРаспределение.ФизЛицо,
	|	СтраховыеВзносыРаспределение.КоэффициентРаспределенияУплаченныхВзносов * СтраховыеВзносыУплаченныеВсего.СуммаВзносов КАК СуммаВзносов
	|ПОМЕСТИТЬ СтраховыеВзносыУплаченные
	|ИЗ
	|	СтраховыеВзносыРаспределение КАК СтраховыеВзносыРаспределение,
	|	СтраховыеВзносыУплаченныеВсего КАК СтраховыеВзносыУплаченныеВсего
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СтраховыеВзносы.ФизЛицо,
	|	СтраховыеВзносы.СуммаВзносов
	|ПОМЕСТИТЬ СтраховыеВзносы
	|ИЗ
	|	СтраховыеВзносыНачисленные КАК СтраховыеВзносы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МетодПланированияУчетаРасходовНаПерсонал КАК МетодПланированияУчетаРасходовНаПерсонал
	|		ПО (МетодПланированияУчетаРасходовНаПерсонал.МетодНачислений)
	|ГДЕ
	|	СтраховыеВзносы.ФизЛицо В
	|			(ВЫБРАТЬ
	|				Физлица.Физлицо
	|			ИЗ
	|				Физлица)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СтраховыеВзносы.ФизЛицо,
	|	СтраховыеВзносы.СуммаВзносов
	|ИЗ
	|	СтраховыеВзносыУплаченные КАК СтраховыеВзносы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МетодПланированияУчетаРасходовНаПерсонал КАК МетодПланированияУчетаРасходовНаПерсонал
	|		ПО (МетодПланированияУчетаРасходовНаПерсонал.КассовыйМетод)
	|ГДЕ
	|	СтраховыеВзносы.ФизЛицо В
	|			(ВЫБРАТЬ
	|				Физлица.Физлицо
	|			ИЗ
	|				Физлица)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПредоставленныеЛьготыОбороты.Физлицо,
	|	ПредоставленныеЛьготыОбороты.СуммаРасходовОборот КАК СуммаРасходов
	|ПОМЕСТИТЬ ПредоставленныеЛьготы
	|ИЗ
	|	РегистрНакопления.ПредоставленныеЛьготы.Обороты(
	|			&ПериодРегистрации,
	|			КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ),
	|			Месяц,
	|			Физлицо В
	|				(ВЫБРАТЬ
	|					Физлица.Физлицо
	|				ИЗ
	|					Физлица)) КАК ПредоставленныеЛьготыОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходыНаСотрудниковОбороты.ФизЛицо,
	|	РасходыНаСотрудниковОбороты.КатегорияРасходов,
	|	РасходыНаСотрудниковОбороты.СуммаРасходовОборот КАК СуммаРасходов
	|ПОМЕСТИТЬ ПрочиеРасходы
	|ИЗ
	|	РегистрНакопления.РасходыНаСотрудниковОрганизации.Обороты(
	|			&ПериодРегистрации,
	|			КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ),
	|			Месяц,
	|			Физлицо В
	|				(ВЫБРАТЬ
	|					Физлица.Физлицо
	|				ИЗ
	|					Физлица)) КАК РасходыНаСотрудниковОбороты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Физлица.Физлицо,
	|	ЕСТЬNULL(Начисления.Подразделение, СведенияОФизлицах.Подразделение) КАК Подразделение
	|ПОМЕСТИТЬ ФизлицаПодразделения
	|ИЗ
	|	Физлица КАК Физлица
	|		ЛЕВОЕ СОЕДИНЕНИЕ Начисления КАК Начисления
	|		ПО Физлица.Физлицо = Начисления.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ СведенияОФизлицах КАК СведенияОФизлицах
	|		ПО Физлица.Физлицо = СведенияОФизлицах.Физлицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ФизЛицо,
	|	СУММА(Начисления.Результат) КАК Расходы
	|ПОМЕСТИТЬ НачисленияПоФизлицам
	|ИЗ
	|	Начисления КАК Начисления
	|
	|СГРУППИРОВАТЬ ПО
	|	Начисления.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.ФизЛицо,
	|	Начисления.Подразделение,
	|	ВЫБОР
	|		КОГДА НачисленияПоФизлицам.Расходы = 0
	|			ТОГДА 0
	|		ИНАЧЕ Начисления.Результат / НачисленияПоФизлицам.Расходы
	|	КОНЕЦ КАК ДоляРасходовПоПодразделению
	|ПОМЕСТИТЬ НачисленияПоПодразделениям
	|ИЗ
	|	Начисления КАК Начисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НачисленияПоФизлицам КАК НачисленияПоФизлицам
	|		ПО Начисления.ФизЛицо = НачисленияПоФизлицам.ФизЛицо";
	
	Возврат ТекстЗапроса;
	
КонецФункции // ТекстЗапросаСуммыРасходовНаПерсонал

////////////////////////////////////////////////////////////////////////////////
// Процедуры, функции для поддержки прежнего механизма учета затрат

#Если ТолстыйКлиентОбычноеПриложение Тогда

Процедура УдалитьКолонкуСпособОтраженияВУпрУчете(ТаблицаФормыСписокНачислений) Экспорт
	
	КолонкаСпособОтраженияВУпрУчете = ТаблицаФормыСписокНачислений.Колонки.Найти("СпособОтраженияВУпрУчете");
	Если КолонкаСпособОтраженияВУпрУчете <> Неопределено Тогда
		ТаблицаФормыСписокНачислений.Колонки.Удалить(КолонкаСпособОтраженияВУпрУчете);
	КонецЕсли;
	
	
КонецПроцедуры

#КонецЕсли

////////////////////////////////////////////////////////////////////////////////
// Процедуры, функции управления полями ввода аналитики (для обычных форм)

#Если ТолстыйКлиентОбычноеПриложение Тогда

// Процедура выполняет создание отдельных полей ввода 
// для редактирования отдельных типов аналитики расходов 
//
Процедура СоздатьПоляВводаАналитикиРасходов(Форма, ИмяТаблицыФормы, ВставитьПередЭлементом, ДействиеПриИзмененииАналитики) Экспорт
	
	РеквизитыАналитики = УчетРасходовНаПерсонал.РеквизитыВводаАналитикиРасходов(Форма.Ссылка, ИмяТаблицыФормы);
	
	Форма.ПоляВводаАналитики = УчетРасходовНаПерсонал.СоответствиеТиповАналитикиРасходов(РеквизитыАналитики);
	
	// добавление полей формы
	КоллекцияКолонок = Форма.ЭлементыФормы[ИмяТаблицыФормы].Колонки;
	Для Каждого Реквизит Из РеквизитыАналитики Цикл
		Если ВставитьПередЭлементом <> Неопределено Тогда
			КолонкаАналитики = КоллекцияКолонок.Вставить(КоллекцияКолонок.Индекс(ВставитьПередЭлементом), Реквизит.Заголовок);
			КолонкаАналитики.Имя = Реквизит.Имя;
		Иначе
			КолонкаАналитики = КоллекцияКолонок.Добавить(Реквизит.Имя, Реквизит.Заголовок);
		КонецЕсли;
		КолонкаАналитики.ОтображатьВПодвале = Ложь;
		КолонкаАналитики.УстановитьЭлементУправления(Тип("ПолеВвода"));
		// все, кроме первой, колонки - на следующей строке
		Если РеквизитыАналитики.Найти(Реквизит) > 0 Тогда
			КолонкаАналитики.Положение = ПоложениеКолонки.НаСледующейСтроке;
		КонецЕсли;
		ПолеАналитики = КолонкаАналитики.ЭлементУправления;
		ПолеАналитики.УстановитьДействие("ПриИзменении", ДействиеПриИзмененииАналитики);
		ПолеАналитики.ТипЗначения = Реквизит.ОписаниеТипов;
	КонецЦикла;
	
КонецПроцедуры // СоздатьПоляВводаАналитикиРасходов

Процедура ОформитьПоляВводаАналитикиРасходов(ДанныеСтроки, Ячейки, ПоляВводаАналитики) Экспорт
	
	Если ЗначениеЗаполнено(ДанныеСтроки.АналитикаРасходов) Тогда
		// определяем имя поля ввода аналитики в зависимости от типа значения
		ИмяПоляВвода = ПоляВводаАналитики.Получить(ТипЗнч(ДанныеСтроки.АналитикаРасходов));
		// заполняем "декорацию" поля
		Если ИмяПоляВвода <> Неопределено И Ячейки[ИмяПоляВвода].Значение <> ДанныеСтроки.АналитикаРасходов Тогда
			Ячейки[ИмяПоляВвода].УстановитьТекст(ДанныеСтроки.АналитикаРасходов);
		КонецЕсли;
	    // все остальные ячейки делаем недоступными для редактирования
		Для Каждого ПолеВводаАналитики Из ПоляВводаАналитики Цикл
			ИмяДругогоПоля = ПолеВводаАналитики.Значение;
			Если ИмяДругогоПоля <> ИмяПоляВвода Тогда
				Ячейки[ИмяДругогоПоля].ТолькоПросмотр = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры


// Процедура подставляет значение аналитики расходов в качестве начального значения 
// во временное поле ввода
//
Процедура ПередНачаломИзмененияСтрокиТаблицы(Форма, Элемент, Отказ) Экспорт
	
	ИмяКолонки = Элемент.ТекущаяКолонка.Имя;
	Для Каждого ПолеВводаАналитики Из Форма.ПоляВводаАналитики Цикл
		Если ИмяКолонки = ПолеВводаАналитики.Значение Тогда
			Элемент.ТекущаяКолонка.ЭлементУправления.Значение = Элемент.ТекущиеДанные.АналитикаРасходов;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Процедура выполняет установку в строке документа значения аналитики расходов,
// полученное из временного поля ввода
//
Процедура ПриИзмененииАналитикиРасходов(Элемент, ТекущиеДанные) Экспорт
	
	ТекущиеДанные.АналитикаРасходов = Элемент.Значение;
	
КонецПроцедуры // ПриИзмененииАналитикиРасходов


#КонецЕсли

