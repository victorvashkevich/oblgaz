
Функция ЗаголовкиФлажка(ТипЭлемента)
	
	Синонимы = Новый Соответствие;
	Синонимы.Вставить(Тип("СправочникСсылка.Подразделения"), 			"Подразделение расформировано (не отображается в списках)");
	Синонимы.Вставить(Тип("СправочникСсылка.ПодразделенияОрганизаций"),	"Подразделение расформировано (не отображается в списках)");
	Синонимы.Вставить(Тип("СправочникСсылка.ДолжностиОрганизаций"),		"Должность упразднена (не отображается в списках)");
	//vvv
	Синонимы.Вставить(Тип("СправочникСсылка.ШтатныеЕдиницыОрганизаций"),		"Штатная единица выведена (не отображается в списках)");
	Синонимы.Вставить(Тип("СправочникСсылка.ШтатныеЕдиницыОрганизацийДляРасценок"),		"Штатная единица выведена (не отображается в списках)");
	Синонимы.Вставить(Тип("ПланВидовРасчетаСсылка.ОсновныеНачисленияОрганизаций"),		"Вид расчета не актуален (или является служебным)");
	Синонимы.Вставить(Тип("ПланВидовРасчетаСсылка.ДополнительныеНачисленияОрганизаций"),"Вид расчета не актуален");
	Синонимы.Вставить(Тип("ПланВидовРасчетаСсылка.УдержанияОрганизаций"),"Вид расчета не актуален");
	Синонимы.Вставить(Тип("СправочникСсылка.Автомобили"),"Автомобиль не актуален");
	Синонимы.Вставить(Тип("СправочникСсылка.ТабеляУчетаОрганизаций"),"Табель не актуален");
	Синонимы.Вставить(Тип("СправочникСсылка.Организации"),"Организация ликвидирована (не отображается в списках)");
	//
	
	Возврат Синонимы[ТипЭлемента];
	
КонецФункции
       
Функция ЗаголовкиКомандыОтборПоАктуальности(ТипСписка)
	
	Синонимы = Новый Соответствие;
	Синонимы.Вставить(Тип("СправочникСписок.Подразделения"), 			"Показывать расформированные подразделения");
	Синонимы.Вставить(Тип("СправочникСписок.ПодразделенияОрганизаций"),	"Показывать расформированные подразделения");
	Синонимы.Вставить(Тип("СправочникСписок.ДолжностиОрганизаций"),		"Показывать упраздненные должности");
	//vvv
	Синонимы.Вставить(Тип("СправочникСписок.ШтатныеЕдиницыОрганизаций"),		"Показывать выведенные штатные единицы");
	Синонимы.Вставить(Тип("СправочникСписок.ШтатныеЕдиницыОрганизацийДляРасценок"),		"Показывать выведенные штатные единицы");
	Синонимы.Вставить(Тип("ПланВидовРасчетаСписок.ОсновныеНачисленияОрганизаций"),		"Показывать неактуальные (служебные) виды расчетов");
	Синонимы.Вставить(Тип("ПланВидовРасчетаСписок.ДополнительныеНачисленияОрганизаций"),"Показывать неактуальные виды расчетов");
	Синонимы.Вставить(Тип("ПланВидовРасчетаСписок.УдержанияОрганизаций"),"Показывать неактуальные виды расчетов");
	Синонимы.Вставить(Тип("СправочникСписок.Автомобили"),	"Показывать неактуальные автомобили");
	Синонимы.Вставить(Тип("СправочникСписок.ТабеляУчетаОрганизаций"),"Показывать неактуальные табеля");
	Синонимы.Вставить(Тип("СправочникСписок.Организации"),"Показывать ликвидированные организации");
	//
	
	Возврат Синонимы[ТипСписка];
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ ЭЛЕМЕНТА 

Процедура ДобавитьЭлементыУправленияАктуальностью(Форма, ДополнительныеДействия)
	
	ЭлементыФормы = Форма.ЭлементыФормы;
	
	Если ЭлементыФормы.Найти("НеАктуальность") <> Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ФлажокНеАктуальность = ЭлементыФормы.Добавить(Тип("Флажок"), "НеАктуальность", Истина, ЭлементыФормы.ПанельАктуальность);
	
	ФлажокНеАктуальность.Лево	= 0;
	ФлажокНеАктуальность.Верх	= 0;
	ФлажокНеАктуальность.Ширина	= ЭлементыФормы.ПанельАктуальность.Ширина;
	ФлажокНеАктуальность.Высота	= 15;
	
	ФлажокНеАктуальность.Данные = "НеАктуальность";
	ФлажокНеАктуальность.ИзменяетДанные = Истина;
	
	ФлажокНеАктуальность.Заголовок = ЗаголовкиФлажка(ТипЗнч(Форма.Ссылка));
	ФлажокНеАктуальность.УстановитьПривязку(ГраницаЭлементаУправления.Право, ЭлементыФормы.ПанельАктуальность, ГраницаЭлементаУправления.Право);
	
	ФлажокНеАктуальность.УстановитьДействие("ПриИзменении", ДополнительныеДействия);
	
КонецПроцедуры

Процедура ФормаЭлементаПередОткрытиемДополнительно(Форма, ДополнительныеДействия) Экспорт
	
	ДобавитьЭлементыУправленияАктуальностью(Форма, ДополнительныеДействия);
	
КонецПроцедуры

Процедура ФормаЭлементаПриОткрытииДополнительно(Форма) Экспорт
	
	// инициализируем реквизит Актуальность
	Если Форма.Ссылка.Пустая() Тогда
		Форма.ЭтотОбъект.Актуальность = Истина;
		Форма.ЭлементыФормы.ПанельАктуальность.Свертка = РежимСверткиЭлементаУправления.Верх;
	КонецЕсли;
	
	Форма.НеАктуальность = Не Форма.ЭтотОбъект.Актуальность;
	
КонецПроцедуры

Процедура ФормаЭлементаПослеЗаписиДополнительно(Форма) Экспорт
	
	ПанельАктуальность = Форма.ЭлементыФормы.ПанельАктуальность;
	Если ПанельАктуальность.Свертка <> РежимСверткиЭлементаУправления.Нет Тогда
		ПанельАктуальность.Свертка = РежимСверткиЭлементаУправления.Нет;
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьДополнительныеДействияФормыЭлемента(Элемент, Форма)  Экспорт
	
	// инициализируем реквизит Актуальность
	Если Элемент.Имя = "НеАктуальность" Тогда
		Форма.ЭтотОбъект.Актуальность = Не Форма.НеАктуальность;
	КонецЕсли;
	
КонецПроцедуры

Процедура ФормаЭлементаПередЗаписьюДополнительно(Форма, СправочникОбъект, Отказ) Экспорт
	
	ИзменениеАктуальностиЭлементовСервер.УстановитьАктуальностьПодчиненныхЭлементов(СправочникОбъект.Актуальность, СправочникОбъект.Ссылка, Отказ);
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ СПИСКА

Функция ОсновнойСписокФормы(Список)
	
	Возврат ?(ТипЗнч(Список) = Тип("Массив"), Список[0], Список);
	
КонецФункции

Процедура УстановитьОтборПоАктуальности(Список, Актуальность, Кнопка)
	
	Если ТипЗнч(Список) = Тип("Массив") Тогда
		Для Каждого ЭлементМассива Из Список Цикл
			ЭлементМассива.Отбор.Актуальность.Использование	= Актуальность;
			ЭлементМассива.Отбор.Актуальность.Значение		= Истина;
		КонецЦикла;
	Иначе
		Список.Отбор.Актуальность.Использование	= Актуальность;
		Список.Отбор.Актуальность.Значение		= Истина;
	КонецЕсли;
	
	Кнопка.Пометка = НЕ Актуальность;

КонецПроцедуры

Процедура ДобавитьКнопкуОтбораПоАктуальности(КоллекцияКнопок, ДополнительныеДействия, Список)
	
	КоллекцияКнопок.Добавить("Разделитель" + Строка(Новый УникальныйИдентификатор), ТипКнопкиКоманднойПанели.Разделитель);
	
	ТекстКнопки = ЗаголовкиКомандыОтборПоАктуальности(ТипЗнч(ОсновнойСписокФормы(Список)));
	
	КнопкаОтбора = КоллекцияКнопок.Добавить("ПоказыватьНеАктуальные", ТипКнопкиКоманднойПанели.Действие, ТекстКнопки, ДополнительныеДействия);
	КнопкаОтбора.Подсказка = ТекстКнопки;
	
КонецПроцедуры

Процедура ДобавитьКолонкуАктуальностьВСписок(Список)
	
	Если ТипЗнч(Список) = Тип("Массив") Тогда
		Для Каждого ЭлементМассива Из Список Цикл
			ЭлементМассива.Колонки.Добавить("Актуальность");
		КонецЦикла;
	Иначе
		Список.Колонки.Добавить("Актуальность");
	КонецЕсли;
	
КонецПроцедуры

Процедура ФормаСпискаПередОткрытиемДополнительно(Форма, ДополнительныеДействия, Список) Экспорт
	
	ДобавитьКнопкуОтбораПоАктуальности(Форма.ЭлементыФормы.ДействияФормы.Кнопки.Действия.Кнопки, ДополнительныеДействия, Список);
	
	ДобавитьКолонкуАктуальностьВСписок(Список);
	
КонецПроцедуры

Процедура ФормаСпискаПриОткрытииДополнительно(Форма, Список) Экспорт
	
	СохраненноеЗначение	= ВосстановитьЗначение(Строка(ТипЗнч(ОсновнойСписокФормы(Список))));
	Актуальность = ?(СохраненноеЗначение = Неопределено, Истина, СохраненноеЗначение);
	
	УстановитьОтборПоАктуальности(Список, Актуальность, Форма.ЭлементыФормы.ДействияФормы.Кнопки.Действия.Кнопки.ПоказыватьНеАктуальные);
	
КонецПроцедуры

Процедура ФормаСпискаПриЗакрытииДополнительно(Форма, Список) Экспорт
	
	ОсновнойСписок = ОсновнойСписокФормы(Список);
	СохранитьЗначение(Строка(ТипЗнч(ОсновнойСписок)), ОсновнойСписок.Отбор.Актуальность.Использование);
	
КонецПроцедуры

Процедура ВыполнитьДополнительныеДействияФормыСписка(Элемент, Форма, Список) Экспорт
	
	Если Элемент.Имя = "ПоказыватьНеАктуальные" Тогда
	
		ОсновнойСписок = ОсновнойСписокФормы(Список);
		УстановитьОтборПоАктуальности(Список, НЕ ОсновнойСписок.Отбор.Актуальность.Использование, Форма.ЭлементыФормы.ДействияФормы.Кнопки.Действия.Кнопки.ПоказыватьНеАктуальные);
		
	КонецЕсли;
		
КонецПроцедуры

Процедура ФормаСпискаОформлениеСпискаДополнительно(Форма, ПолеСписка, ОформленияСтрок, ПервыеПодразделения = Неопределено) Экспорт
	
	Для Каждого ОформлениеСтроки Из ОформленияСтрок Цикл
		Если ОформлениеСтроки.ДанныеСтроки <> Неопределено Тогда
			Если НЕ ОформлениеСтроки.ДанныеСтроки.Актуальность Тогда
				Если ПервыеПодразделения <> Неопределено И ПервыеПодразделения.Найти(ОформлениеСтроки.ДанныеСтроки.Ссылка) <> Неопределено Тогда
					// в строке, в которой выводится название организации в списке подразделений организации
					// окрашиваем все, кроме поля Организация
					Для Каждого Ячейка Из ОформлениеСтроки.Ячейки Цикл
						Если Ячейка.Имя <> "Организация" Тогда
							Ячейка.ЦветТекста = ЦветаСтиля.ЦветРамки;
						КонецЕсли;
					КонецЦикла;
				Иначе
					ОформлениеСтроки.ЦветТекста = ЦветаСтиля.ЦветРамки;	
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры