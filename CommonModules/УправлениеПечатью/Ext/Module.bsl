
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ, ФОРМИРУЮЩИЕ РЕЗУЛЬТАТЫ ДЛЯ КОМАНД ПЕЧАТИ

// Сформировать печатные формы
Процедура СформироватьПечатныеФормы(ИмяМенеджераПечати, ИменаМакетов, МассивОбъектов, ПараметрыПечати,
	КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Получим менеджер печати
	МенеджерПечати = ПолучитьМенеджерОбъекта(ИмяМенеджераПечати);
	
	// Подготовим коллекцию для формируемых печатных форм
	КоллекцияПечатныхФорм = ПодготовитьКоллекциюПечатныхФорм(ИменаМакетов);
	
	// Подготовим структуру параметров вывода
	ПараметрыВывода = ПодготовитьСтруктуруПараметровВывода();
	
	ОбъектыПечати = Новый СписокЗначений;
	
	// Сформируем печатные формы
	МенеджерПечати.Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
	
	// Проверим, все ли макеты были сформированы
	Для Каждого Стр Из КоллекцияПечатныхФорм Цикл
		Если Стр.ТабличныйДокумент = Неопределено Тогда
			ТекстСообщенияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
										НСтр("ru = 'В обработчике печати не был сформирован табличный документ для: %1'"),
										Стр.ИмяМакета);
			ВызватьИсключение(ТекстСообщенияОбОшибке);
		КонецЕсли;
		
		Стр.ТабличныйДокумент.КоличествоЭкземпляров = Стр.Экземпляров;
	КонецЦикла;
	
КонецПроцедуры

// Сформировать печатные формы для непосредственного вывода на принтер
Процедура СформироватьПечатныеФормыДляБыстройПечати(
		ИмяМенеджераПечати, ИменаМакетов, МассивОбъектов, ПараметрыПечати,
		ТабличныеДокументы, ОбъектыПечати, ПараметрыВывода, Отказ) Экспорт
	
	Если НЕ ПравоДоступа("Вывод", Метаданные) Тогда
		Отказ = Истина;
		Возврат;
	Иначе
		Отказ = Ложь;
	КонецЕсли;
	
	КоллекцияПечатныхФорм = Неопределено;
	ОбъектыПечати = Новый СписокЗначений;
	
	СформироватьПечатныеФормы(ИмяМенеджераПечати, ИменаМакетов, МассивОбъектов, ПараметрыПечати,
		КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода);
		
	ТабличныеДокументы = Новый СписокЗначений;
	
	Для Каждого Стр Из КоллекцияПечатныхФорм Цикл
		Если (ТипЗнч(Стр.ТабличныйДокумент) = Тип("ТабличныйДокумент")) И (Стр.ТабличныйДокумент.ВысотаТаблицы <> 0) Тогда
			ТабличныеДокументы.Добавить(Стр.ТабличныйДокумент, Стр.СинонимМакета);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Сформировать печатные формы для непосредственного вывода на принтер
// в серверном режиме в обычном приложении
Процедура СформироватьПечатныеФормыДляБыстройПечатиОбычноеПриложение(
				ИмяМенеджераПечати, ИменаМакетов, МассивОбъектов, ПараметрыПечати,
				Адрес, ОбъектыПечати, ПараметрыВывода, Отказ) Экспорт
	
	Перем ОбъектыПечатиСЗ, ТабличныеДокументы;
	
	СформироватьПечатныеФормыДляБыстройПечати(
			ИмяМенеджераПечати, ИменаМакетов, МассивОбъектов, ПараметрыПечати,
			ТабличныеДокументы, ОбъектыПечатиСЗ, ПараметрыВывода, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОбъектыПечати = Новый Соответствие;
	
	Для Каждого ОбъектПечати Из ОбъектыПечатиСЗ Цикл
		ОбъектыПечати.Вставить(ОбъектПечати.Представление, ОбъектПечати.Значение);
	КонецЦикла;
	
	Адрес = ПоместитьВоВременноеХранилище(ТабличныеДокументы);
	
КонецПроцедуры
////////////////////////////////////////////////////////////////////////////////
// ФУНКЦИИ И ПРОЦЕДУРЫ, ИСПОЛЬЗУЕМЫЕ МОДУЛЯМИ МЕНЕДЖЕРОВ ОБЪЕКТОВ ПРИ ФОРМИРОВАНИИ ТАБЛИЧНЫХ ДОКУМЕНТОВ

// Проверить, нужно ли печатать макет
Функция НужноПечататьМакет(КоллекцияПечатныхФорм, ИмяМакета) Экспорт
	
	Возврат КоллекцияПечатныхФорм.Найти(ВРег(ИмяМакета), "ИмяВРЕГ") <> Неопределено;
	
КонецФункции

// Вывести табличный документ в коллекцию печатных форм
Процедура ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, ИмяМакета, СинонимМакета, ТабличныйДокумент, Картинка = Неопределено, ПолныйПутьКМакету = "") Экспорт
	
	Стр = КоллекцияПечатныхФорм.Найти(ВРег(ИмяМакета), "ИмяВРЕГ");
	
	Если Стр <> Неопределено Тогда
		Стр.ТабличныйДокумент = ТабличныйДокумент;
		Стр.СинонимМакета = СинонимМакета;
		Стр.Картинка = Картинка;
		Стр.ПолныйПутьКМакету = ПолныйПутьКМакету;
	КонецЕсли;
	
КонецПроцедуры

// Задать область печати объекта в табличном документе.
// Применяется для связывания области в табличном документе, с объектом печати (ссылка).
// Необходимо вызывать при формировании очередной области печатной формы в табличном
// документе.
// Параметры:
//  ТабличныйДокумент - табличный документ - табличный документ печатной формы
//  НомерСтрокиНачало - число - позиция начала очередной области в документе
//  ОбъектыПечати - СписокЗначений - список объектов печати
//  Ссылка - ссылка на объект ИБ - объект печати
//
Процедура ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Ссылка) Экспорт
	
	Элемент = ОбъектыПечати.НайтиПоЗначению(Ссылка);
	Если Элемент = Неопределено Тогда
		ИмяОбласти = "Документ_" + Формат(ОбъектыПечати.Количество() + 1, "ЧН=; ЧГ=");
		ОбъектыПечати.Добавить(Ссылка, ИмяОбласти);
	Иначе
		ИмяОбласти = Элемент.Представление;
	КонецЕсли;
	
	НомерСтрокиОкончание = ТабличныйДокумент.ВысотаТаблицы;
	ТабличныйДокумент.Область(НомерСтрокиНачало, , НомерСтрокиОкончание, ).Имя = ИмяОбласти;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ НЕЭКСПОРТНЫЕ ФУНКЦИИ

// По имени объекта получить его менеджер
//
Функция ПолучитьМенеджерОбъекта(ИмяМенеджера)
	
	Поз = Найти(ИмяМенеджера, ".");
	ИмяГруппы  = Лев(ИмяМенеджера,  Поз-1);
	ИмяОбъекта = Сред(ИмяМенеджера, Поз+1);
	
	Если ВРег(ИмяГруппы) = "СПРАВОЧНИК" Тогда
		Возврат Справочники[ИмяОбъекта];
		
	ИначеЕсли ВРег(ИмяГруппы) = "ДОКУМЕНТ" Тогда
		Возврат Документы[ИмяОбъекта];
		
	ИначеЕсли ВРег(ИмяГруппы) = "ЖУРНАЛДОКУМЕНТОВ" Тогда
		Возврат ЖурналыДокументов[ИмяОбъекта];
		
	ИначеЕсли ВРег(ИмяГруппы) = "ОБРАБОТКА" Тогда
		Возврат Обработки[ИмяОбъекта];
		
	ИначеЕсли ВРег(ИмяГруппы) = "ОТЧЕТ" Тогда
		Возврат Отчеты[ИмяОбъекта];
		
	ИначеЕсли ВРег(ИмяГруппы) = "РЕГИСТРСВЕДЕНИЙ" Тогда
		Возврат РегистрыСведений[ИмяОбъекта];
		
	ИначеЕсли ВРег(ИмяГруппы) = "БИЗНЕСПРОЦЕСС" Тогда
		Возврат БизнесПроцессы[ИмяОбъекта];
		
	ИначеЕсли ВРег(ИмяГруппы) = "ЗАДАЧА" Тогда
		Возврат Задачи[ИмяОбъекта];
		
	Иначе
		
		ТекстСообщенияОбОшибке = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
									НСтр("ru = 'Не найден менеджер для %1'"), ИмяМенеджера);
		
		ВызватьИсключение(ТекстСообщенияОбОшибке);
		
	КонецЕсли;
	
КонецФункции

// Подготовить коллекцию печатных форм - таблицу значений используемые при формировании печатных форм
//
Функция ПодготовитьКоллекциюПечатныхФорм(ИменаМакетов) Экспорт
	
	Макеты = Новый ТаблицаЗначений;
	Макеты.Колонки.Добавить("ИмяМакета");
	Макеты.Колонки.Добавить("ИмяВРЕГ");
	Макеты.Колонки.Добавить("СинонимМакета");
	Макеты.Колонки.Добавить("ТабличныйДокумент");
	Макеты.Колонки.Добавить("Экземпляров");
	Макеты.Колонки.Добавить("Картинка");
	Макеты.Колонки.Добавить("ПолныйПутьКМакету");
	
	СтрИмен = СтрЗаменить(ИменаМакетов, ",", Символы.ПС);
	Для Сч = 1 По СтрЧислоСтрок(СтрИмен) Цикл
		Имя = СтрПолучитьСтроку(СтрИмен, Сч);
		Стр = Макеты.Найти(Имя, "ИмяМакета");
		Если Стр = Неопределено Тогда
			Стр = Макеты.Добавить();
			Стр.ИмяМакета = Имя;
			Стр.ИмяВРЕГ   = ВРег(Имя);
			Стр.Экземпляров = 1;
		Иначе
			Стр.Экземпляров = Стр.Экземпляров + 1;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Макеты;
	
КонецФункции

// Подготовить структуру параметров вывода для менеджера объекта формирующего печатные формы
//
Функция ПодготовитьСтруктуруПараметровВывода() Экспорт
	
	ПараметрыВывода = Новый Структура;
	ПараметрыВывода.Вставить("ДоступнаПечатьПоКомплектно",		Ложь);
	ПараметрыВывода.Вставить("ПолучательЭлектронногоПисьма",	Неопределено);
	ПараметрыВывода.Вставить("ОтправительЭлектронногоПисьма",	Неопределено);
	
	Возврат ПараметрыВывода;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// СЕКЦИЯ ФУНКЦИОНАЛЬНОСТИ ДЛЯ РАБОТЫ С ШАБЛОНАМИ ОФИСНЫХ ДОКУМЕНТОВ

// Добавляет к параметру НаборОбластей новую запись об области
// Параметры
// НаборОбластей - массив - набор областей (массив структур)
// ИмяОбласти - строка - имя добавляемой области
// ТипОбласти - строка - тип области:
//			ВерхнийКолонтитул
//			НижнийКолонтитул
//			Общая
//			СтрокаТаблицы
//			Список
//
Процедура ДобавитьОписаниеОбласти(НаборОбластей, знач ИмяОбласти, знач ТипОбласти) Экспорт
	
	НоваяОбласть = Новый Структура;
	
	НоваяОбласть.Вставить("ИмяОбласти", ИмяОбласти);
	НоваяОбласть.Вставить("ТипОбласти", ТипОбласти);
	
	НаборОбластей.Вставить(ИмяОбласти, НоваяОбласть);
	
КонецПроцедуры

// Интерфейс для вызова из клиентских модулей печати форм по макетам офисных документов.
// Получает за один вызов всю необходимую информацию: данные объектов по макетам, двоичные
// данные макетов, описание областей макетов.
// Параметры:
// ИмяМенеджераПечати - строка - имя для обращения к менеджеру объекта, например "Документ.<Имя документа>"
// ИменаМакетов       - строка - имена макетов, по которым будут формироваться печатные формы
// СоставДокументов   - массив ссылок - ссылки на объекты информационной базы (должны быть одного типа)
//
Функция ПолучитьМакетыИДанныеОбъектов(знач ИмяМенеджераПечати,
									  знач ИменаМакетов,
									  знач СоставДокументов) Экспорт
	
	МассивИменМакетов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрЗаменить(ИменаМакетов, " ", ""), ",");
	
	МенеджерОбъекта = ОбщегоНазначенияБСП.МенеджерОбъектаПоПолномуИмени(ИмяМенеджераПечати);
	МакетыИДанные = МенеджерОбъекта.ПолучитьДанныеПечати(СоставДокументов, МассивИменМакетов);
	МакетыИДанные.Вставить("ЛокальныйКаталогФайловПечати", ПолучитьЛокальныйКаталогФайловПечати());
	
	Возврат МакетыИДанные;
	
КонецФункции

// Возвращает макет по полному пути к макету.
// Параметры:
//  ПолныйПутьКМакету - Строка - формат полного пути:
//								"Документ.<ИмяДокумента>.<ИмяМакета>"
//								"Обработка.<ИмяОбработки>.<ИмяМакета>"
//								"ОбщийМакет.<ИмяМакета>"
// Возвращаемое значение:
//	для макета типа MXL - табличный документ
//	для макетов DOC и ODT - двоичные данные
//
Функция ПолучитьМакет(ПолныйПутьКМакету) Экспорт
	
	ЧастиПути = СтрЗаменить(ПолныйПутьКМакету, ".", Символы.ПС);
	
	Если СтрЧислоСтрок(ЧастиПути) = 3 Тогда
		ПутьКМетаданным = СтрПолучитьСтроку(ЧастиПути, 1) + "." + СтрПолучитьСтроку(ЧастиПути, 2);
		ПутьКОбъектуМетаданных = СтрПолучитьСтроку(ЧастиПути, 3);
	ИначеЕсли СтрЧислоСтрок(ЧастиПути) = 2 Тогда
		ПутьКМетаданным = СтрПолучитьСтроку(ЧастиПути, 1);
		ПутьКОбъектуМетаданных = СтрПолучитьСтроку(ЧастиПути, 2);
	Иначе
		ВызватьИсключение НСтр("ru = 'Некорректные параметры функции.'");
	КонецЕсли;
	
	Если СтрЧислоСтрок(ЧастиПути) = 3 Тогда
		Результат = ОбщегоНазначенияБСП.МенеджерОбъектаПоПолномуИмени(ПутьКМетаданным).ПолучитьМакет(ПутьКОбъектуМетаданных);
	Иначе
		Результат = ПолучитьОбщийМакет(ПутьКОбъектуМетаданных);
	КонецЕсли;
	
	Если Результат = Неопределено Тогда
		ВызватьИсключение "Некорректные данные пользовательского макета";
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

Функция ПолучитьТабличныйДокументПоДвоичнымДанным(ДвоичныеДанные) Экспорт
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла();
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Прочитать(ИмяВременногоФайла);
	УдалитьФайлы(ИмяВременногоФайла);
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПолучитьЛокальныйКаталогФайловПечати() Экспорт
	
	Значение = ХранилищеОбщихНастроек.Загрузить("ЛокальныйКаталогФайловПечати");
	Возврат ?(Значение = Неопределено, "", Значение);
	
КонецФункции

Процедура СохранитьЛокальныйКаталогФайловПечати(Каталог) Экспорт
	
	ХранилищеОбщихНастроек.Сохранить("ЛокальныйКаталогФайловПечати", , Каталог);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Печать объектов по данным СКД

Функция ПечатьПриПомощиСКД(СхемаКомпоновкиДанных, ИмяПоляСКДОсновногоЭлемента, Ссылка = Неопределено, ОбъектыПечати, НазваниеОтчета = Неопределено, Результат = Неопределено) Экспорт
	
	Если Результат = Неопределено тогда
		Результат = Новый ТабличныйДокумент;
	КонецЕсли;
	
	Результат.ТолькоПросмотр  = Истина;
	Результат.ОтображатьСетку = Ложь;
	Результат.ОтображатьЗаголовки = Ложь;
	
	// Получить настройки схемы компоновки данных
	Настройки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	Если Ссылка <> Неопределено Тогда
		ТиповыеОтчеты.ДобавитьОтбор(Настройки.Отбор, ИмяПоляСКДОсновногоЭлемента, Ссылка, ?(ТипЗнч(Ссылка) = Тип("Массив"), ВидСравненияКомпоновкиДанных.ВСписке, Неопределено));
	КонецЕсли;
	
	// очистим результирующий табличный документ
	Результат.Очистить();
	
	// получим макет компоновки данных
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки   = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, );
	
	// создадим и инициализируем процессор компоновки данных
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , );
	
	// выведем отчет в табличный документ "Результат"
	ВывестиРезультатВТабличныйДокумент(ПроцессорКомпоновки, Результат);
	
	ОбластьЯчеек = Результат.НайтиТекст("_РазделительСтраницы_");
	Если ОбластьЯчеек <> Неопределено Тогда
		ОбластьРедактирования = Результат.Область(ОбластьЯчеек.Верх,,ОбластьЯчеек.Верх);
		Результат.УдалитьОбласть(ОбластьРедактирования, ТипСмещенияТабличногоДокумента.ПоВертикали);
	КонецЕсли;
	ОбластьЯчеек = Результат.НайтиТекст("_РазделительСтраницы_");
	Пока ОбластьЯчеек <> Неопределено Цикл
		ОбластьЯчеек.Текст = "";
		ОбластьРедактирования = Результат.Область(ОбластьЯчеек.Верх+1,,ОбластьЯчеек.Верх+1);
		ОбластьРедактирования.НачалоСтраницы = Истина;
		ОбластьРедактирования = Результат.Область(ОбластьЯчеек.Верх,,ОбластьЯчеек.Верх);
		Результат.УдалитьОбласть(ОбластьРедактирования, ТипСмещенияТабличногоДокумента.ПоВертикали);
		ОбластьЯчеек = Результат.НайтиТекст("_РазделительСтраницы_");
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ВывестиРезультатВТабличныйДокумент(ПроцессорКомпоновкиДанных, ТабличныйДокумент)
	
	// Создадим и инициализируем процессор вывода результата
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ТабличныйДокумент);

	// Обозначим начало вывода
	ПроцессорВывода.НачатьВывод();
	
	// Основной цикл вывода отчета
	Пока Истина Цикл

		// Получим следующий элемент результата компоновки
		ЭлементРезультата = ПроцессорКомпоновкиДанных.Следующий();
		
		Если ЭлементРезультата = Неопределено Тогда
			
			// Следующий элемент не получен - заканчиваем цикл вывода
			Прервать;
			
		Иначе
			
			// Элемент получен - выведем его при помощи процессора вывода
			ПроцессорВывода.ВывестиЭлемент(ЭлементРезультата);
			
		КонецЕсли;
		
	КонецЦикла;
	
	ПроцессорВывода.ЗакончитьВывод();
	
КонецПроцедуры
