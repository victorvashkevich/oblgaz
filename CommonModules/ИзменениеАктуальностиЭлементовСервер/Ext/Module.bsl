
// Распространяет отметку актуальности на все подчиненные элементы
// Выполняется в привилегированном режиме
//
Процедура УстановитьАктуальностьПодчиненныхЭлементов(Актуальность, СправочникСсылка, Отказ) Экспорт
	
	Если Не ЗначениеЗаполнено(СправочникСсылка) Тогда 
		// У незаписанного элемента не может быть подчиненных элементов
		Возврат;
	КонецЕсли;
	
	МетаданныеОбъекта = Метаданные.НайтиПоТипу(ТипЗнч(СправочникСсылка));
	Если НЕ МетаданныеОбъекта.Иерархический Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = ПодчиненныеЭлементы(СправочникСсылка, МетаданныеОбъекта.Имя).Выбрать();
	Пока Выборка.Следующий() Цикл
		ПодчиненныйЭлементОбъект = Выборка.Ссылка.ПолучитьОбъект();
		ПодчиненныйЭлементОбъект.Актуальность = Актуальность;
		Попытка
			ПодчиненныйЭлементОбъект.Записать();
		Исключение
			Отказ = Истина;
			Прервать;
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры

Функция ПодчиненныеЭлементы(Ссылка, ИмяМетаданных)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ 
	|	Ссылка
	|ИЗ
	|	Справочник." + ИмяМетаданных + "
	|ГДЕ
	|	Ссылка В ИЕРАРХИИ(&Ссылка)
	|	И Ссылка <> &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Запрос.Выполнить();
	
КонецФункции
