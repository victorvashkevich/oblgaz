
// Функция выполняет запрос для получения погашаемых при увольнении 
//  обязательств по ученическим договорам
//
Функция СформироватьЗапросПоОбязательствам(ДокументУвольнениеСсылка) Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	УвольнениеРаботники.ДатаУвольнения КАК Период,
	|	УвольнениеРаботники.Сотрудник,
	|	УвольнениеРаботники.Физлицо КАК Физлицо,
	|	ОбязательстваРаботниковПоУченическимДоговорам.Организация КАК Организация,
	|	ОбязательстваРаботниковПоУченическимДоговорам.Договор,
	|	ОбязательстваРаботниковПоУченическимДоговорам.Мероприятие КАК Мероприятие,
	|	ОбязательстваРаботниковПоУченическимДоговорам.ДатаНачала КАК ДатаНачала,
	|	ОбязательстваРаботниковПоУченическимДоговорам.ДатаОкончания КАК ДатаОкончания,
	|	МИНИМУМ(УченическийДоговорПериодыОбязательств.ДатаОкончанияОбязательства) КАК ДатаОкончанияОбязательства,
	|	УченическийДоговорПериодыОбязательств.Валюта
	|ПОМЕСТИТЬ Обязательства
	|ИЗ
	|	Документ.УвольнениеИзОрганизаций.РаботникиОрганизации КАК УвольнениеРаботники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			МАКСИМУМ(ОбязательстваРаботниковПоУченическимДоговорам.Период) КАК Период,
	|			УвольнениеРаботники.Физлицо КАК Физлицо,
	|			ОбязательстваРаботниковПоУченическимДоговорам.Договор КАК Договор,
	|			КОЛИЧЕСТВО(СотрудникиОрганизаций.Ссылка) КАК Количество
	|		ИЗ
	|			Документ.УвольнениеИзОрганизаций.РаботникиОрганизации КАК УвольнениеРаботники
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОбязательстваРаботниковПоУченическимДоговорам КАК ОбязательстваРаботниковПоУченическимДоговорам
	|				ПО УвольнениеРаботники.Физлицо = ОбязательстваРаботниковПоУченическимДоговорам.ФизЛицо
	|					И УвольнениеРаботники.ДатаУвольнения >= ОбязательстваРаботниковПоУченическимДоговорам.Период
	|				ЛЕВОЕ СОЕДИНЕНИЕ Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	|				ПО УвольнениеРаботники.Физлицо = СотрудникиОрганизаций.Физлицо
	|					И (СотрудникиОрганизаций.ВидЗанятости В (ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ОсновноеМестоРаботы), ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.Совместительство)))
	|					И (ВЫБОР
	|						КОГДА УвольнениеРаботники.Сотрудник.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ОсновноеМестоРаботы)
	|							ТОГДА УвольнениеРаботники.Сотрудник.ВидЗанятости = СотрудникиОрганизаций.ВидЗанятости
	|						ИНАЧЕ ИСТИНА
	|					КОНЕЦ)
	|					И (СотрудникиОрганизаций.ДатаУвольнения >= УвольнениеРаботники.ДатаУвольнения
	|						ИЛИ СотрудникиОрганизаций.ДатаУвольнения = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0))
	|		ГДЕ
	|			ОбязательстваРаботниковПоУченическимДоговорам.СпособПогашения = ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияОбязательствПоУченическомуДоговору.ПриУвольнении)
	|			И (НЕ ОбязательстваРаботниковПоУченическимДоговорам.Регистратор = &Регистратор)
	|			И УвольнениеРаботники.Ссылка = &Регистратор
	|		
	|		СГРУППИРОВАТЬ ПО
	|			УвольнениеРаботники.Физлицо,
	|			ОбязательстваРаботниковПоУченическимДоговорам.Договор) КАК Сотрудники
	|		ПО УвольнениеРаботники.Физлицо = Сотрудники.Физлицо
	|			И (Сотрудники.Количество = 1
	|				ИЛИ &ВестиОбучениеРазвитиеПоЦентрамОтветственности)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОбязательстваРаботниковПоУченическимДоговорам КАК ОбязательстваРаботниковПоУченическимДоговорам
	|		ПО УвольнениеРаботники.Физлицо = ОбязательстваРаботниковПоУченическимДоговорам.ФизЛицо
	|			И (Сотрудники.Период = ОбязательстваРаботниковПоУченическимДоговорам.Период)
	|			И (Сотрудники.Договор = ОбязательстваРаботниковПоУченическимДоговорам.Договор)
	|			И ((НЕ ОбязательстваРаботниковПоУченическимДоговорам.Погашено))
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УченическийДоговор.ПериодыОбязательств КАК УченическийДоговорПериодыОбязательств
	|		ПО (Сотрудники.Договор = УченическийДоговорПериодыОбязательств.Ссылка)
	|			И УвольнениеРаботники.Физлицо = УченическийДоговорПериодыОбязательств.ФизЛицо
	|			И (УченическийДоговорПериодыОбязательств.ДатаОкончанияОбязательства >= УвольнениеРаботники.ДатаУвольнения)
	|ГДЕ
	|	УвольнениеРаботники.Ссылка = &Регистратор
	|	И (НЕ УвольнениеРаботники.Сторно)
	|
	|СГРУППИРОВАТЬ ПО
	|	УвольнениеРаботники.ДатаУвольнения,
	|	УвольнениеРаботники.Сотрудник,
	|	УвольнениеРаботники.Физлицо,
	|	ОбязательстваРаботниковПоУченическимДоговорам.Организация,
	|	ОбязательстваРаботниковПоУченическимДоговорам.Договор,
	|	ОбязательстваРаботниковПоУченическимДоговорам.Мероприятие,
	|	ОбязательстваРаботниковПоУченическимДоговорам.ДатаНачала,
	|	ОбязательстваРаботниковПоУченическимДоговорам.ДатаОкончания,
	|	УченическийДоговорПериодыОбязательств.Валюта
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОбязательстваРаботниковПоУченическимДоговорам.Договор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалют.Валюта,
	|	КурсыВалют.Курс,
	|	КурсыВалют.Кратность,
	|	ДатыВалюты.ДатаВалюты,
	|	ДатыВалюты.ДатаОкончанияОбязательства
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	(ВЫБРАТЬ
	|		Валюты.Ссылка КАК Валюта,
	|		Обязательства.ДатаОкончанияОбязательства КАК ДатаОкончанияОбязательства,
	|		МАКСИМУМ(КурсыВалют.Период) КАК ДатаВалюты
	|	ИЗ
	|		Обязательства КАК Обязательства
	|			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Валюты КАК Валюты
	|			ПО (ИСТИНА)
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|			ПО Обязательства.ДатаОкончанияОбязательства >= КурсыВалют.Период
	|				И (Валюты.Ссылка = КурсыВалют.Валюта)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		Валюты.Ссылка,
	|		Обязательства.ДатаОкончанияОбязательства) КАК ДатыВалюты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют КАК КурсыВалют
	|		ПО ДатыВалюты.Валюта = КурсыВалют.Валюта
	|			И ДатыВалюты.ДатаВалюты = КурсыВалют.Период
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КурсыВалют.Валюта,
	|	ДатыВалюты.ДатаОкончанияОбязательства
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Обязательства.Период КАК Период,
	|	ВЫБОР
	|		КОГДА &ВестиОбучениеРазвитиеПоЦентрамОтветственности
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|		ИНАЧЕ Обязательства.Организация
	|	КОНЕЦ КАК Организация,
	|	Обязательства.Сотрудник КАК Сотрудник,
	|	Обязательства.Физлицо КАК Физлицо,
	|	Обязательства.Договор КАК Договор,
	|	ИСТИНА КАК Погашено,
	|	Обязательства.Мероприятие КАК Мероприятие,
	|	Обязательства.ДатаНачала КАК ДатаНачала,
	|	Обязательства.ДатаОкончания КАК ДатаОкончания,
	|	ЗНАЧЕНИЕ(Перечисление.СпособыПогашенияОбязательствПоУченическомуДоговору.ПриУвольнении) КАК СпособПогашения,
	|	ЕСТЬNULL(УченическийДоговорПериодыОбязательств.Сумма * (КурсыВалют.Курс / КурсыВалют.Кратность) / (КурсВалютыУпрУчета.Курс / КурсВалютыУпрУчета.Кратность), 0) КАК Сумма,
	|	УченическийДоговорПериодыОбязательств.ДатаОкончанияОбязательства КАК ДатаОкончанияОбязательства,
	|	ЗНАЧЕНИЕ(Перечисление.КатегорииРасходовНаПерсонал.Обучение) КАК КатегорияРасходов
	|ИЗ
	|	Обязательства КАК Обязательства
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.УченическийДоговор.ПериодыОбязательств КАК УченическийДоговорПериодыОбязательств
	|		ПО Обязательства.Договор = УченическийДоговорПериодыОбязательств.Ссылка
	|			И Обязательства.Физлицо = УченическийДоговорПериодыОбязательств.ФизЛицо
	|			И Обязательства.ДатаОкончанияОбязательства = УченическийДоговорПериодыОбязательств.ДатаОкончанияОбязательства
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсыВалют
	|		ПО Обязательства.Валюта = КурсыВалют.Валюта
	|			И Обязательства.ДатаОкончанияОбязательства = КурсыВалют.ДатаОкончанияОбязательства
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалютыУпрУчета
	|		ПО (Константы.ВалютаУправленческогоУчета = КурсВалютыУпрУчета.Валюта)
	|			И Обязательства.ДатаОкончанияОбязательства = КурсВалютыУпрУчета.ДатаОкончанияОбязательства";
	
	Если ПолучитьФункциональнуюОпцию("ВестиОбучениеРазвитиеПоЦентрамОтветственности") Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "Документ.УвольнениеИзОрганизаций.РаботникиОрганизации", "Документ.Увольнение.Работники");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И (НЕ УвольнениеРаботники.Сторно)", "");
	КонецЕсли; 
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Регистратор", ДокументУвольнениеСсылка);
	Запрос.УстановитьПараметр("ВестиОбучениеРазвитиеПоЦентрамОтветственности", ПолучитьФункциональнуюОпцию("ВестиОбучениеРазвитиеПоЦентрамОтветственности"));
	
	УстановитьПривилегированныйРежим(Истина);

	Возврат Запрос.Выполнить();
	
КонецФункции // СформироватьЗапросПоОбязательствам
