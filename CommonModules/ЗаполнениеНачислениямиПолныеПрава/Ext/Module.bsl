
Функция ПросмотрНачисленийКадровикамиДоступенДляВсехСотрудников(СотрудникиДокумента, ПользовательСсылка) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НастройкиПравДоступаПользователей.ОбъектДоступа,
	|	НастройкиПравДоступаПользователей.ОбластьДанных
	|ПОМЕСТИТЬ ДоступКФизлицам
	|ИЗ
	|	РегистрСведений.НастройкиПравДоступаПользователей КАК НастройкиПравДоступаПользователей
	|ГДЕ
	|	(НастройкиПравДоступаПользователей.Пользователь = &Пользователь
	|			ИЛИ НастройкиПравДоступаПользователей.Пользователь В
	|				(ВЫБРАТЬ
	|					ГруппыПользователей.Ссылка
	|				ИЗ
	|					Справочник.ГруппыПользователей КАК ГруппыПользователей ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыПользователей.ПользователиГруппы КАК СоставГруппы
	|						ПО
	|							ГруппыПользователей.Ссылка = СоставГруппы.Ссылка ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.НазначениеВидовОбъектовДоступа КАК НазначениеВидовОбъектовДоступа
	|						ПО
	|							ГруппыПользователей.Ссылка = НазначениеВидовОбъектовДоступа.ГруппаПользователей
	|								И НазначениеВидовОбъектовДоступа.ВидОбъектаДоступа = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовДоступа.ФизическиеЛица)
	|				ГДЕ
	|					СоставГруппы.Пользователь = &Пользователь))
	|	И НастройкиПравДоступаПользователей.ВидОбъектаДоступа = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовДоступа.ФизическиеЛица)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	1 КАК Скрывать
	|ИЗ
	|	Справочник.СотрудникиОрганизаций КАК Сотрудники
	|ГДЕ
	|	Сотрудники.Ссылка В(&СотрудникиДокумента)
	|	И ИСТИНА В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				ИСТИНА
	|			ИЗ
	|				РегистрСведений.НазначениеВидовОбъектовДоступа КАК НазначениеВидов ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыПользователей.ПользователиГруппы КАК ПользователиГруппы
	|					ПО
	|						ПользователиГруппы.Ссылка = НазначениеВидов.ГруппаПользователей
	|							И НазначениеВидов.ВидОбъектаДоступа = ЗНАЧЕНИЕ(Перечисление.ВидыОбъектовДоступа.ФизическиеЛица)
	|							И ПользователиГруппы.Пользователь = &Пользователь)
	|	И (Сотрудники.Физлицо.ГруппаДоступаФизическогоЛица В
	|				(ВЫБРАТЬ
	|					ДоступКФизлицам.ОбъектДоступа
	|				ИЗ
	|					ДоступКФизлицам
	|				ГДЕ
	|					НЕ ДоступКФизлицам.ОбъектДоступа В
	|							(ВЫБРАТЬ
	|								ДоступКФизлицам.ОбъектДоступа
	|							ИЗ
	|								ДоступКФизлицам
	|							ГДЕ
	|								ДоступКФизлицам.ОбластьДанных = ЗНАЧЕНИЕ(Перечисление.ОбластиДанныхОбъектовДоступа.ФизическиеЛицаНачисленияКадровикам)))
	|			ИЛИ Сотрудники.Физлицо.ГруппаДоступаФизическогоЛица = ЗНАЧЕНИЕ(Справочник.ГруппыДоступаФизическихЛиц.ПустаяСсылка))");
	
	Запрос.УстановитьПараметр("СотрудникиДокумента", СотрудникиДокумента);
	Запрос.УстановитьПараметр("Пользователь", ПользовательСсылка);
	
	// Запрос не пустой, если сведения о начислениях нужно скрывать, 
	// пустой - если скрывать не нужно
	
	Возврат Запрос.Выполнить().Пустой();
	
КонецФункции // ПросмотрНачисленийКадровикамиДоступенДляВсехСотрудников
