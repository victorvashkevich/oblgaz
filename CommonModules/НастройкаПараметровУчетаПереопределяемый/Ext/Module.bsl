
#Если Клиент Тогда

// В процедуре производится оформление колонок табличного поля истории значений
Процедура ОформитьДополнительныеКолонки(ФормаОбработки) Экспорт

	Если ФормаОбработки.ИмяТаблицыИсторииЗначений = "УчетнаяПолитикаНалоговыйУчет" Тогда
		
		ЕстьКолонкаНУ = НЕ ФормаОбработки.ЭлементыФормы.ТабличноеПоле.Колонки.Найти("ВариантУчетаРасходовПоНалогамСФОТВНалоговомУчете") = Неопределено;
		
		ФормаОбработки.ЭлементыФормы.ТабличноеПоле.Колонки.УСН.ТекстШапки = "Система налогообложения";
		ФормаОбработки.ЭлементыФормы.ТабличноеПоле.Колонки.УСН.ЭлементУправления.Формат = "БЛ=Общая; БИ=Упрощенная";
		ФормаОбработки.ЭлементыФормы.ТабличноеПоле.Колонки.УСН.Формат = "БЛ=Общая; БИ=Упрощенная";
		
		Если ЕстьКолонкаНУ Тогда
			ФормаОбработки.ЭлементыФормы.ТабличноеПоле.Колонки.Сдвинуть(ФормаОбработки.ЭлементыФормы.ТабличноеПоле.Колонки.ВариантУчетаРасходовПоНалогамСФОТВНалоговомУчете, -2);
			ФормаОбработки.ЭлементыФормы.ТабличноеПоле.Колонки.ВариантУчетаРасходовПоНалогамСФОТВНалоговомУчете.Положение = ПоложениеКолонки.НаСледующейСтроке;
			ФормаОбработки.ЭлементыФормы.ТабличноеПоле.Колонки.ВариантУчетаРасходовПоНалогамСФОТВНалоговомУчете.ЭлементУправления.КнопкаОчистки = Ложь;
			ФормаОбработки.ЭлементыФормы.ТабличноеПоле.Колонки.ВариантУчетаРасходовПоНалогамСФОТВНалоговомУчете.АвтоОтметкаНезаполненного = Истина;
			ФормаОбработки.ЭлементыФормы.ТабличноеПоле.Колонки.ВариантУчетаРасходовПоНалогамСФОТВНалоговомУчете.ТекстШапки = "Учет расходов по налогам с ФОТ";
			ФормаОбработки.ЭлементыФормы.ТабличноеПоле.Колонки.ВариантУчетаРасходовПоНалогамСФОТВНалоговомУчете.ЭлементУправления.ШиринаСпискаВыбора = 40;
		КонецЕсли;
		
		ФормаОбработки.ЭлементыФормы.ТабличноеПоле.Колонки.ОбъектНалогообложенияУСН.ЭлементУправления.КнопкаОчистки = Ложь;
		ФормаОбработки.ЭлементыФормы.ТабличноеПоле.Колонки.ОбъектНалогообложенияУСН.АвтоОтметкаНезаполненного = Истина;
		ФормаОбработки.ЭлементыФормы.ТабличноеПоле.Колонки.ОбъектНалогообложенияУСН.ТекстШапки = "Объект налогообложения УСН";
		
	КонецЕсли;

КонецПроцедуры

// В процедуре производится оформление ячеек табличного поля истории значений
Процедура ОформитьЯчейкиДополнительныхКолонк(ФормаОбработки, ОформлениеСтроки) Экспорт

	Если ФормаОбработки.ИмяТаблицыИсторииЗначений = "УчетнаяПолитикаНалоговыйУчет" Тогда
		
		ЕстьКолонкаНУ = НЕ ФормаОбработки.ЭлементыФормы.ТабличноеПоле.Колонки.Найти("ВариантУчетаРасходовПоНалогамСФОТВНалоговомУчете") = Неопределено;
		
		ОформлениеСтроки.Ячейки.ОбъектНалогообложенияУСН.Видимость = ОформлениеСтроки.Ячейки.УСН.Значение;
		ОформлениеСтроки.Ячейки.ОбъектНалогообложенияУСН.ТолькоПросмотр = Не ОформлениеСтроки.Ячейки.УСН.Значение И НЕ ЕстьКолонкаНУ;
		Если ЕстьКолонкаНУ Тогда
			ОформлениеСтроки.Ячейки.ВариантУчетаРасходовПоНалогамСФОТВНалоговомУчете.Видимость = Не ОформлениеСтроки.Ячейки.УСН.Значение;
		КонецЕсли;
		ОформлениеСтроки.Ячейки.УплачиватьДобровольныеВзносыВФСС.ТолькоПросмотр = Не ОформлениеСтроки.Ячейки.ОрганизацияЯвляетсяПлательщикомЕНВД.Значение и Не ОформлениеСтроки.Ячейки.УСН.Значение;
	
	КонецЕсли;

КонецПроцедуры

// Функция проверяет заполнение обязательных реквилитов в строках ТаблицаИсторииЗначений
Функция ТаблицаИсторииЗаполненаБезОшибок(ФормаОбработки, ТаблицаИсторииЗначений) Экспорт

	 //проверим заполнение данных
	Для каждого СтрокаТЗ Из ТаблицаИсторииЗначений Цикл
		Для каждого КолонкаТП Из ФормаОбработки.мКолонкиДляПроверки Цикл
			
			ДопУсловие = Истина;
			Если ФормаОбработки.ИмяТаблицыИсторииЗначений = "УчетнаяПолитикаНалоговыйУчет" Тогда
				ДопУсловие = ?(КолонкаТП = "ОбъектНалогообложенияУСН" и НЕ СтрокаТЗ.УСН, Ложь, ?(КолонкаТП = "ВариантУчетаРасходовПоНалогамСФОТВНалоговомУчете" и СтрокаТЗ.УСН, Ложь, Истина));
			КонецЕсли;	
				
			Если ДопУсловие и НЕ ЗначениеЗаполнено(СтрокаТЗ[КолонкаТП]) Тогда
				ОбщегоНазначения.ВывестиИнформациюОбОшибке("Не заполнены обязательные поля в таблице!");
				Возврат Ложь;
			КонецЕсли;	
			
		КонецЦикла;
	КонецЦикла;

	Возврат Истина;

КонецФункции // ТаблицаИсторииЗаполненаБезОшибок()

// Функция фомирует спискок доступных видов Тарифов страховых взносов
//
Функция ПолучитьСписокДоступныхТарифов(ФормаОбработки, ТекущиеДанные = Неопределено) Экспорт

	Если Не ТекущиеДанные = Неопределено Тогда
		УСН = ТекущиеДанные.УСН;
	Иначе
		УСН = ФормаОбработки.УСН;
	КонецЕсли;	
	
	СписокДляВыбора = Новый СписокЗначений;
	Если Не УСН Тогда
		СписокДляВыбора.Добавить(Перечисления.ТарифыСтраховыхВзносов.ОбщийНалоговыйРежим);
		СписокДляВыбора.Добавить(Перечисления.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов);
		СписокДляВыбора.Добавить(Перечисления.ТарифыСтраховыхВзносов.СельХозПроизводители);
		СписокДляВыбора.Добавить(Перечисления.ТарифыСтраховыхВзносов.РезидентТехникоВнедренческойЗоны);
	Иначе
		СписокДляВыбора.Добавить(Перечисления.ТарифыСтраховыхВзносов.УпрощенныйНалоговыйРежим);
		СписокДляВыбора.Добавить(Перечисления.ТарифыСтраховыхВзносов.ОрганизацияИнвалидов);
		СписокДляВыбора.Добавить(Перечисления.ТарифыСтраховыхВзносов.ЕСХН);
	КонецЕсли;
	Возврат СписокДляВыбора;

КонецФункции // ПолучитьСписокДоступныхТарифов()

#КонецЕсли
