
Процедура ЗаполнитьГрейдыВТабличнойЧасти(ПолеТабличнойЧасти, ОформленияСтрок, ИмяКолонкиДолжность, ИмяКолонкиГрейд) Экспорт
	
	Если ПолеТабличнойЧасти.Колонки.Найти(ИмяКолонкиГрейд) = Неопределено 
		Или Не ПолеТабличнойЧасти.Колонки[ИмяКолонкиГрейд].Видимость Тогда
		Возврат;
	КонецЕсли;
	
	МассивДолжностей = Новый Массив;
	Для Каждого СтрокаТабличнойЧасти Из ОформленияСтрок Цикл
		МассивДолжностей.Добавить(СтрокаТабличнойЧасти.ДанныеСтроки[ИмяКолонкиДолжность]);
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДолжностиОрганизаций.Ссылка КАК Должность,
	|	ДолжностиОрганизаций.Грейд.Код КАК Грейд
	|ИЗ
	|	Справочник.ДолжностиОрганизаций КАК ДолжностиОрганизаций
	|ГДЕ
	|	ДолжностиОрганизаций.Ссылка В(&МассивДолжностей)";
	
	Запрос.УстановитьПараметр("МассивДолжностей", МассивДолжностей);
	
	Грейды = Новый Соответствие;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Грейды[Выборка.Должность] = Выборка.Грейд;
	КонецЦикла; 
	
	Для Каждого СтрокаТабличнойЧасти Из ОформленияСтрок Цикл
		СтрокаТабличнойЧасти.Ячейки[ИмяКолонкиГрейд].УстановитьТекст(Грейды[СтрокаТабличнойЧасти.ДанныеСтроки[ИмяКолонкиДолжность]]);
	КонецЦикла;
	
КонецПроцедуры