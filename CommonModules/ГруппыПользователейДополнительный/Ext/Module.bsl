
#Если ТолстыйКлиентОбычноеПриложение Тогда
	
Процедура ФормаЭлементаПередОткрытиемДополнительно(Форма, ДополнительныеДействия) Экспорт
	
	Если ПравоДоступа("Редактирование", Метаданные.РегистрыСведений.НастройкиГруппПользователей) Тогда
		ДобавитьЭлементыУправленияНастройками(Форма, ДополнительныеДействия);
	Иначе
		Форма.ЭлементыФормы.ПанельНастройкиГруппы.Свертка = РежимСверткиЭлементаУправления.Низ;
	КонецЕсли;
	
	// ЭлементыФормы.ПанельНастройкиГруппы.Свертка = РежимСверткиЭлементаУправления.Низ;
	
КонецПроцедуры
	
Процедура ФормаЭлементаПриОткрытииДополнительно(Форма) Экспорт
	
	Форма.ДополнительныеЗначения = Новый Соответствие;
	
	ПрочитатьНастройкиГруппыПользователей(Форма);
	
	ФормаЭлементаУстановитьСвойстваЭлементов(Форма);
	
КонецПроцедуры

Процедура ФормаЭлементаПередЗаписьюДополнительно(Форма, Отказ) Экспорт
	
	ПроверитьНастройкиГруппыПользователей(Форма, Отказ);
	
КонецПроцедуры

Процедура ФормаЭлементаПриЗаписиДополнительно(Форма, Отказ) Экспорт
	
	ЗаписатьНастройкиГруппыПользователей(Форма, Отказ);
	
КонецПроцедуры

Процедура ФормаЭлементаВыполнитьДополнительныеДействия(Элемент, Форма) Экспорт
	
	ИмяЭлемента = Элемент.Имя;
	
	Если ИмяЭлемента = "ОграничиватьВремяСеанса" Тогда
		
		Форма.ДополнительныеЗначения.Вставить("ОграничиватьВремяСеанса", Элемент.Значение);
		
	ИначеЕсли ИмяЭлемента = "ВариантыОграничения" 
		Или ИмяЭлемента = "МаксимальноеВремяСеанса" Тогда
		
		Форма.ДополнительныеЗначения.Вставить("МаксимальноеВремяСеанса", Элемент.Значение);

	КонецЕсли;
	
	ФормаЭлементаУстановитьСвойстваЭлементов(Форма);
	
КонецПроцедуры

Процедура ФормаЭлементаУстановитьСвойстваЭлементов(Форма)
	
	ЭлементыФормы = Форма.ЭлементыФормы;
	
	ОграничиватьВремяСеанса = Форма.ДополнительныеЗначения["ОграничиватьВремяСеанса"];
	
	ЭлементыФормы.ВариантыОграничения.Доступность			= ОграничиватьВремяСеанса;
	ЭлементыФормы.НадписьВариантыОграничения.Доступность	= ОграничиватьВремяСеанса;
	ЭлементыФормы.МаксимальноеВремяСеанса.Доступность		= ОграничиватьВремяСеанса;
	ЭлементыФормы.НадписьМаксимальноеВремяСеанса.Доступность= ОграничиватьВремяСеанса;
	
	ЭлементыФормы.МаксимальноеВремяСеанса.АвтоОтметкаНезаполненного = ОграничиватьВремяСеанса;
	ЭлементыФормы.МаксимальноеВремяСеанса.ОтметкаНезаполненного = ОграничиватьВремяСеанса И ЭлементыФормы.МаксимальноеВремяСеанса.Значение = 0;
	
	ВыбранВариантТочноеВремя = ЭлементыФормы.ВариантыОграничения.Значение = 0;
	ЭлементыФормы.МаксимальноеВремяСеанса.Видимость			= ВыбранВариантТочноеВремя;
	ЭлементыФормы.НадписьМаксимальноеВремяСеанса.Видимость	= ВыбранВариантТочноеВремя;
	
КонецПроцедуры

Процедура ДобавитьЭлементыУправленияНастройками(Форма, ДополнительныеДействия)
	
	ЭлементыФормы = Форма.ЭлементыФормы;
	Панель = ЭлементыФормы.ПанельНастройкиГруппы;
	
	ШиринаПанели = Панель.Ширина;
	ВысотаПанели = Панель.Высота;
	
	// флажок "Ограничивать по времени"
	ФлажокОграничивать = ЭлементыФормы.Добавить(Тип("Флажок"), "ОграничиватьВремяСеанса", Истина, Панель);
	ФлажокОграничивать.Лево		= 0;
	ФлажокОграничивать.Верх		= 20;
	ФлажокОграничивать.Ширина	= 285;
	ФлажокОграничивать.Высота	= 19;
	ФлажокОграничивать.Заголовок = "Ограничивать время сеанса пользователей группы";
	ФлажокОграничивать.ИзменяетДанные = Истина;
	ФлажокОграничивать.УстановитьДействие("ПриИзменении", ДополнительныеДействия);
	
	// заголовок поля выбора "Варианты ограничения"
	НадписьВариантыОграничения = ЭлементыФормы.Добавить(Тип("Надпись"), "НадписьВариантыОграничения", Истина, Панель);
	НадписьВариантыОграничения.Лево		= 0;
	НадписьВариантыОграничения.Верх		= 41;
	НадписьВариантыОграничения.Ширина	= 208;
	НадписьВариантыОграничения.Высота	= 19;
	НадписьВариантыОграничения.Заголовок = "Автоматически завершать сеанс через:";
	
	// поле выбора "Варианты ограничения"
	ПолеВариантыОграничения = ЭлементыФормы.Добавить(Тип("ПолеВвода"), "ВариантыОграничения", Истина, Панель);
	ПолеВариантыОграничения.Лево	= 213;
	ПолеВариантыОграничения.Верх	= 41;
	ПолеВариантыОграничения.Ширина	= 128;
	ПолеВариантыОграничения.Высота	= 19;
	ПолеВариантыОграничения.ТипЗначения = Новый ОписаниеТипов();
	ПолеВариантыОграничения.РежимВыбораИзСписка = Истина; 	
	ПолеВариантыОграничения.СписокВыбора = СписокВариантовОграниченияВремениСеанса();
	ПолеВариантыОграничения.РедактированиеТекста = Ложь;
	ПолеВариантыОграничения.ИзменяетДанные = Истина;
	ПолеВариантыОграничения.УстановитьДействие("ПриИзменении", ДополнительныеДействия);
	
	// поле ввода точного значения "Максимальное время сеанса"
	ПолеВремяТочно = ЭлементыФормы.Добавить(Тип("ПолеВвода"), "МаксимальноеВремяСеанса", Истина, Панель);
	ПолеВремяТочно.Лево		= 352;
	ПолеВремяТочно.Верх		= 41;
	ПолеВремяТочно.Ширина	= 60;
	ПолеВремяТочно.Высота	= 19;
	ПолеВремяТочно.ТипЗначения = Новый ОписаниеТипов("Число", Новый КвалификаторыЧисла(5, 0, ДопустимыйЗнак.Неотрицательный));
	ПолеВремяТочно.КнопкаРегулирования = Истина;
	ПолеВремяТочно.АвтоОтметкаНезаполненного = Истина;
	ПолеВремяТочно.ИзменяетДанные = Истина;
	ПолеВремяТочно.УстановитьДействие("ПриИзменении", ДополнительныеДействия);
	
	// подпись поля ввода точного значения "Максимальное время сеанса"
	НадписьВремяТочно = ЭлементыФормы.Добавить(Тип("Надпись"), "НадписьМаксимальноеВремяСеанса", Истина, Панель);
	НадписьВремяТочно.Лево		= 420;
	НадписьВремяТочно.Верх		= 41;
	НадписьВремяТочно.Ширина	= 30;
	НадписьВремяТочно.Высота	= 19;
	НадписьВремяТочно.Заголовок	= "мин.";
	
КонецПроцедуры

Функция СписокВариантовОграниченияВремениСеанса()
	
	СписокВариантов = Новый СписокЗначений;
	СписокВариантов.Добавить(0, "указанное время...");
	СписокВариантов.Добавить(1, "1 минуту");
	СписокВариантов.Добавить(2, "2 минуты");
	СписокВариантов.Добавить(5, "5 минут");
	СписокВариантов.Добавить(10, "10 минут");
	СписокВариантов.Добавить(15, "15 минут");
	СписокВариантов.Добавить(30, "30 минут");
	СписокВариантов.Добавить(60, "1 час");
	СписокВариантов.Добавить(120, "2 часа");
	СписокВариантов.Добавить(240, "4 часа");
	СписокВариантов.Добавить(480, "8 часов");
	
	Возврат СписокВариантов;
	
КонецФункции

Процедура ПрочитатьНастройкиГруппыПользователей(Форма)
	
	// значения по умолчанию
	ОграничиватьВремяСеанса	= Ложь;
	МаксимальноеВремяСеанса	= 5;
	
	Если ПравоДоступа("Редактирование", Метаданные.РегистрыСведений.НастройкиГруппПользователей) 
		И Не Форма.Ссылка.Пустая() Тогда
	
		НаборЗаписей = РегистрыСведений.НастройкиГруппПользователей.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор["ГруппаПользователей"].Установить(Форма.Ссылка);
		НаборЗаписей.Прочитать();
	
		Если НаборЗаписей.Количество() > 0 Тогда
			ОграничиватьВремяСеанса = НаборЗаписей[0].ОграничиватьВремяСеанса;
			МаксимальноеВремяСеанса = НаборЗаписей[0].МаксимальноеВремяСеанса;
		КонецЕсли;
	
		Форма.ДополнительныеЗначения.Вставить("НастройкиГруппПользователей", НаборЗаписей);
		
	КонецЕсли;
	
	Форма.ДополнительныеЗначения.Вставить("ОграничиватьВремяСеанса", ОграничиватьВремяСеанса);
	Форма.ДополнительныеЗначения.Вставить("МаксимальноеВремяСеанса", МаксимальноеВремяСеанса);
	
	// заполним элементы формы
	ЭлементыФормы = Форма.ЭлементыФормы;
	
	// флажок
	ЭлементыФормы.ОграничиватьВремяСеанса.Значение = ОграничиватьВремяСеанса;
	
	// список вариантов
	ЭлементСпискаВариантОграничения = ЭлементыФормы.ВариантыОграничения.СписокВыбора.НайтиПоЗначению(МаксимальноеВремяСеанса);
	ЭлементыФормы.ВариантыОграничения.Значение = ?(ЭлементСпискаВариантОграничения = Неопределено, 0, ЭлементСпискаВариантОграничения.Значение);
	
	ЭлементыФормы.МаксимальноеВремяСеанса.Значение = МаксимальноеВремяСеанса;
	
КонецПроцедуры

Процедура ПроверитьНастройкиГруппыПользователей(Форма, Отказ)
	
	ОграничиватьВремяСеанса = Форма.ДополнительныеЗначения["ОграничиватьВремяСеанса"];
	МаксимальноеВремяСеанса = Форма.ДополнительныеЗначения["МаксимальноеВремяСеанса"];
	
	Если ОграничиватьВремяСеанса И Не ЗначениеЗаполнено(МаксимальноеВремяСеанса) Тогда
		ОбщегоНазначенияЗК.СообщитьОбОшибке(НСтр("ru = 'Не заполнено максимально разрешенное время сеанса.'"), Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаписатьНастройкиГруппыПользователей(Форма, Отказ)
	
	Если Не ПравоДоступа("Редактирование", Метаданные.РегистрыСведений.НастройкиГруппПользователей) Тогда
		Возврат;
	КонецЕсли;

	ИсходныйНаборЗаписей	= Форма.ДополнительныеЗначения["НастройкиГруппПользователей"];
	ОграничиватьВремяСеанса	= Форма.ДополнительныеЗначения["ОграничиватьВремяСеанса"];
	МаксимальноеВремяСеанса	= Форма.ДополнительныеЗначения["МаксимальноеВремяСеанса"];
	
	Если ИсходныйНаборЗаписей = Неопределено Тогда
		НастройкиМодифицированы = ОграничиватьВремяСеанса;
	Иначе
		Если ИсходныйНаборЗаписей.Количество() = 0 Тогда
			НастройкиМодифицированы = ОграничиватьВремяСеанса;
		Иначе
			НастройкиМодифицированы = ИсходныйНаборЗаписей[0].ОграничиватьВремяСеанса <> ОграничиватьВремяСеанса 
								Или ИсходныйНаборЗаписей[0].МаксимальноеВремяСеанса <> МаксимальноеВремяСеанса;
		КонецЕсли;
	КонецЕсли;
	
	Если НастройкиМодифицированы Тогда
		
		НаборЗаписей = РегистрыСведений.НастройкиГруппПользователей.СоздатьНаборЗаписей();
		
		СтрокаНабора = НаборЗаписей.Добавить();
		СтрокаНабора.ГруппаПользователей		= Форма.Ссылка;
		СтрокаНабора.ОграничиватьВремяСеанса	= ОграничиватьВремяСеанса;
		СтрокаНабора.МаксимальноеВремяСеанса	= МаксимальноеВремяСеанса;
		
		НаборЗаписей.Отбор["ГруппаПользователей"].Установить(Форма.Ссылка);
		НаборЗаписей.Записать(Истина);
		
		Форма.ДополнительныеЗначения.Вставить("НастройкиГруппПользователей", НаборЗаписей);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли