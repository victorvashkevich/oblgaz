////////////////////////////////////////////////////////////////////////////////
// Процедуры печати документа.

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт	
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ДопущениеКРаботе") Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ДопущениеКРаботе", "Приказ о допущении к работе", ПриказОДопущении(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
КонецПроцедуры	


Функция ПриказОДопущении(ДокументСсылка, ОбъектыПечати)
	
	ДокументРезультат = Новый ТабличныйДокумент;

	Макет = Документы.ДопускКРаботам.ПолучитьМакет("ДопущениеКРаботе");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);
	Запрос.УстановитьПараметр("ДатаСреза", ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница",ДокументСсылка.Организация);
		
	Запрос.Текст = ФормированиеПечатныхФормЗК.ПолучитьТекстЗапросаПоОтветственнымЛицам(
		"ДатаСреза",
		"ОтветственноеЛицо = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.Руководитель)
		|И СтруктурнаяЕдиница = &СтруктурнаяЕдиница");
	Запрос.Выполнить();
				
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДопускКРаботам.Организация.НаименованиеПолное КАК НаименованиеОрганизации,
	|	ДопускКРаботам.Номер КАК КодДокумента,
	|	ДопускКРаботам.Дата,
	|	ДопускКРаботам.Номер,
	|	ЕСТЬNULL(ФИОФизЛиц.Фамилия, """") КАК Фамилия,
	|	ЕСТЬNULL(ФИОФизЛиц.Имя, """") КАК Имя,
	|	ЕСТЬNULL(ФИОФизЛиц.Отчество, """") КАК Отчество,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|				И РаботникиОрганизаций.ПериодЗавершения < &ДатаСреза
	|			ТОГДА ЕСТЬNULL(РаботникиОрганизаций.ДолжностьЗавершения.Наименование, """")
	|		ИНАЧЕ ЕСТЬNULL(РаботникиОрганизаций.Должность.Наименование, """")
	|	КОНЕЦ КАК ДолжностьСотрудника,
	|	ЕСТЬNULL(КонтактнаяИнформация.Поле4, """") КАК Поле3,
	|	ЕСТЬNULL(КонтактнаяИнформация.Поле2, """") КАК Поле1,
	|	ОтветственныеЛицаОрганизаций.Должность КАК ДолжностьРуководителя,
	|	ОтветственныеЛицаОрганизаций.НаименованиеОтветственногоЛица КАК ФИОРуководителя,
	|	ДопускКРаботам.Ссылка КАК ДокументСсылка,
	|	РаботникиДокумента.ФизЛицо.Пол КАК Пол,
	|	РаботникиДокумента.Сотрудник
	|ИЗ
	|	Документ.ДопускКРаботам КАК ДопускКРаботам
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|		ПО ДопускКРаботам.Организация = КонтактнаяИнформация.Объект
	|			И (КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|			И (КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ФактАдресОрганизации))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ДопускКРаботам.Работники КАК РаботникиДокумента
	|		ПО ДопускКРаботам.Ссылка = РаботникиДокумента.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаСреза, ) КАК ФИОФизЛиц
	|		ПО (ФИОФизЛиц.ФизЛицо = РаботникиДокумента.Сотрудник.Физлицо)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(&ДатаСреза, ) КАК РаботникиОрганизаций
	|		ПО (РаботникиОрганизаций.Организация = ДопускКРаботам.Организация)
	|			И (РаботникиОрганизаций.Сотрудник = РаботникиДокумента.Сотрудник)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОбОтветственномЛице КАК ОтветственныеЛицаОрганизаций
	|		ПО ДопускКРаботам.Организация = ОтветственныеЛицаОрганизаций.СтруктурнаяЕдиница
	|ГДЕ
	|	ДопускКРаботам.Ссылка = &Документ
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДопускКРаботам.Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.СледующийПоЗначениюПоля("ДокументСсылка") Цикл
		ОбластьШапка.Параметры.НаименованиеОрганизации = Выборка.НаименованиеОрганизации;
		ОбластьШапка.Параметры.ДатаДокумента = Формат(Выборка.Дата, "ДЛФ=ДД");
		Город = ?(ЗначениеЗаполнено(Выборка.Поле3), Выборка.Поле3, Выборка.Поле1);
		Если Прав(Город, 2) = " г" Тогда
			Город = "г. " + Лев(Город, СтрДлина(Город)-2);
		КонецЕсли;
		ОбластьШапка.Параметры.Город = Город;
		ОбластьШапка.Параметры.НомерПриказа = Выборка.Номер;
		ДокументРезультат.Вывести(ОбластьШапка);
		НомерСтроки = 0;
		Пока Выборка.Следующий() Цикл
			Если ЗначениеЗаполнено(Выборка.Сотрудник) Тогда
				НомерСтроки = НомерСтроки + 1;
				Фамилия = Выборка.Фамилия;
				УниверсальныеМеханизмы.Просклонять(глЗначениеПеременной("глКомпонентаСклоненияФИО"), Фамилия, 4, Выборка.Пол, Фамилия);
				Имя = Выборка.Имя;
				Отчество = Выборка.Отчество;
				ФИОСокращенно = ?(НЕ ПустаяСтрока(Фамилия), 
				Фамилия + ?(НЕ ПустаяСтрока(Имя)," " + Лев(Имя,1) + "." + ?(НЕ ПустаяСтрока(Отчество),Лев(Отчество,1)+".", ""), ""),
				"");
				ОбластьСтрока.Параметры.ФИОДолжностьСотрудника = ФИОСокращенно + ?(НЕ ПустаяСтрока(Выборка.ДолжностьСотрудника),  " (" + Выборка.ДолжностьСотрудника + ")", "");
				
				
				ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
				ДокументРезультат.Вывести(ОбластьСтрока);
			КонецЕсли;
		КонецЦикла;
		
		ОбластьПодвал.Параметры.Основания = "Допуск к работам " + Выборка.КодДокумента + " от " + Формат(Выборка.Дата, "ДЛФ=ДД");
		ОбластьПодвал.Параметры.ДолжностьРук = Выборка.ДолжностьРуководителя;
		ОбластьПодвал.Параметры.ФИОДиректора = Выборка.ФИОРуководителя;
		ДокументРезультат.Вывести(ОбластьПодвал);	
		
	КонецЦикла;
	
	Возврат ДокументРезультат;	
	
КонецФункции // ПриказОДопущении
