////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//

// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// предназначена для заполнения табличной части 
// Параметры:
//	Основание - передается объект-основание
// Возвращаемое значение: нет
//
Процедура ЗаполнитьПоОснованию(Основание) Экспорт
	
	ИсполнительныйЛист = Основание;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(МАКСИМУМ(ИзменениеУсловийИсполнительногоЛиста.ДатаИзменения), ДАТАВРЕМЯ(1, 1, 1)) КАК ДатаИзменения
	|ИЗ
	|	Документ.ИзменениеУсловийИсполнительногоЛиста КАК ИзменениеУсловийИсполнительногоЛиста
	|ГДЕ
	|	ИзменениеУсловийИсполнительногоЛиста.ИсполнительныйЛист = &ДокументОснование";
	Запрос.УстановитьПараметр("ДокументОснование",Основание);
	Выборка = Запрос.Выполнить().Выбрать();
	ДатаПоследнегоИзменения = '00010101';
	Если Выборка.Следующий() Тогда
		ДатаПоследнегоИзменения = Выборка.ДатаИзменения
	КонецЕсли;
	
	СтрокаРеквизитов = "";
	Если Физлицо.Пустая() Тогда
		СтрокаРеквизитов = СтрокаРеквизитов + "Физлицо,"; 
	КонецЕсли;
	Если Организация.Пустая() Тогда
		СтрокаРеквизитов = СтрокаРеквизитов + "Организация,"; 
	КонецЕсли;
	Если Действие.Пустая() Тогда 
		ДатаИзменения = Макс(ДатаПоследнегоИзменения + 86400, ОбщегоНазначенияЗК.ПолучитьРабочуюДату());
		Действие = Перечисления.ВидыДействияСНачислением.Изменить;
	ИначеЕсли ДатаИзменения = '00010101' Тогда
		ДатаИзменения = Макс(ДатаПоследнегоИзменения + 86400, Дата)
	КонецЕсли;
	
	Если Действие = Перечисления.ВидыДействияСНачислением.Изменить Тогда
		ЗаполняемыеРеквизиты = "ВидИсполнительногоДокумента,ДатаОкончания,  СпособРасчетаИЛ,Размер, Предел, УчитыватьБольничныеЛисты, ПрожиточныйМинимум, ПроцентПочтовогоСбора, ПочтовыйСбор, ВидРасчета, Тариф, УдерживатьКомисиионыйСбор, ВидСбора, ПроцентСбора,Получатель";
	Иначе
		СтруктураПустыхДанных = Новый Структура("ДатаОкончания, Размер, Предел, УчитыватьБольничныеЛисты, ПрожиточныйМинимум, ПроцентПочтовогоСбора, ПочтовыйСбор, ВидРасчета, Тариф, УдерживатьКомисиионыйСбор, ВидСбора, ПроцентСбора");
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураПустыхДанных);
		ЗаполняемыеРеквизиты = "СпособРасчетаИЛ,Получатель,ВидИсполнительногоДокумента";
	КонецЕсли;
	СтрокаРеквизитов = СтрокаРеквизитов + ЗаполняемыеРеквизиты;
	
	Если Не ПустаяСтрока(СтрокаРеквизитов) Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ОбщегоНазначенияЗК.ПолучитьЗначенияРеквизитов(Основание, СтрокаРеквизитов));
	КонецЕсли;
	
КонецПроцедуры  // ЗаполнитьПоОснованию

#Если Клиент Тогда

// Формирует печатную форму по документу
//
// Параметры
//  нет
//
// Возвращаемое значение:
//   сформированный табличный документ
//
Функция ПечатьКарточки()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Адрес", Перечисления.ТипыКонтактнойИнформации.Адрес);
	Запрос.УстановитьПараметр("ФактическийАдрес", Справочники.ВидыКонтактнойИнформации.ФактАдресКонтрагента);
	Запрос.УстановитьПараметр("ФактАдресФизЛица", Справочники.ВидыКонтактнойИнформации.ФактАдресФизЛица);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЕСТЬNULL(ФИОФизлица.Фамилия + "" "" + ФИОФизлица.Имя + "" "" + ФИОФизлица.Отчество, ИсполнительныйЛист.Физлицо.Наименование) КАК ФИОРаботника,
	|	ИсполнительныйЛист.Дата,
	|	ИсполнительныйЛист.Номер,
	|	ИсполнительныйЛист.Размер,
	|	ИсполнительныйЛист.Предел,
	|	ВЫБОР
	|		КОГДА ИсполнительныйЛист.СпособРасчетаИЛ = 2 
	|			ТОГДА  "" руб.""
	|		ИНАЧЕ "" %""
	|	КОНЕЦ КАК ПорядокРасчета,
	|	ИсполнительныйЛист.ВидИсполнительногоДокумента,
	|	ИсполнительныйЛист.РеквизитыИсполнительногоДокумента,
	|	ВЫРАЗИТЬ(ИсполнительныйЛист.Получатель.НаименованиеПолное КАК СТРОКА(300)) КАК Получатель,
	|	ЕСТЬNULL(ФактАдресФизЛица.Представление, ""<адрес не указан>"") КАК ФактАдресФизЛица,
	|	ЕСТЬNULL(ФактическийАдресПолучателя.Представление, ""<адрес не указан>"") КАК ФактическийАдресПолучателя
	|ИЗ
	|	Документ.ИсполнительныйЛист КАК ИсполнительныйЛист
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних КАК ФИОФизлица
	|		ПО ИсполнительныйЛист.Физлицо = ФИОФизлица.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК ФактическийАдресПолучателя
	|		ПО (ФактическийАдресПолучателя.Тип = &Адрес)
	|			И (ФактическийАдресПолучателя.Вид = &ФактическийАдрес)
	|			И ИсполнительныйЛист.Получатель = ФактическийАдресПолучателя.Объект
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК ФактАдресФизЛица
	|		ПО ИсполнительныйЛист.Физлицо = ФактАдресФизЛица.Объект
	|			И (ФактАдресФизЛица.Тип = &Адрес)
	|			И (ФактАдресФизЛица.Вид = &ФактАдресФизЛица)
	|ГДЕ
	|	ИсполнительныйЛист.Ссылка = &Ссылка";
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ИсполнительныйЛист_КарточкаУчета";
	
	Макет = ПолучитьМакет("Карточка");
	СекцияКарточка = Макет.ПолучитьОбласть("Карточка");
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
    СекцияКарточка.Параметры.Заполнить(Выборка);
	СекцияКарточка.Параметры.Дата = Формат(СекцияКарточка.Параметры.Дата,"ДФ='ММММ гггг ""г.""'");
	СекцияКарточка.Параметры.Получатель = СокрЛП(СекцияКарточка.Параметры.Получатель);
	ТабДокумент.Вывести(СекцияКарточка);
	
	Возврат	ТабДокумент
	
КонецФункции // ПечатьКарточки()
	
////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходмое количество копий.
//
//  Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//  НазваниеМакета - строка, название макета.
//
Функция Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт
	
	// Получить экземпляр документа на печать
	Если ИмяМакета = "Карточка" Тогда
		Возврат РаботаСДиалогами.НапечататьДокумент(ПечатьКарточки(), КоличествоЭкземпляров, НаПринтер, РаботаСДиалогами.СформироватьЗаголовокДокумента(ЭтотОбъект,Метаданные().Синоним) + " (карточка учета)");
	КонецЕсли;
	
КонецФункции // Печать

#КонецЕсли

// Возвращает доступные варианты печати документа
//
// Вовращаемое значение:
//  Струткура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Возврат Новый Структура("Карточка", "Карточка учета");
	
КонецФункции // ПолучитьСтруктуруПечатныхФорм()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует запрос по шапке документа
//
// Параметры: 
//  Режим - режим проведения
//
// Возвращаемое значение:
//  Результат запроса
//
Функция СформироватьЗапросПоШапке(Режим)

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка" , Ссылка);
	Запрос.УстановитьПараметр("парамПустаяОрганизация", Справочники.Организации.ПустаяСсылка());

	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ИзменениеУсловийИсполнительногоЛиста.Дата,
	|	ИзменениеУсловийИсполнительногоЛиста.Организация,
	|	ВЫБОР
	|		КОГДА ИзменениеУсловийИсполнительногоЛиста.Организация.ГоловнаяОрганизация = &парамПустаяОрганизация
	|			ТОГДА ИзменениеУсловийИсполнительногоЛиста.Организация
	|		ИНАЧЕ ИзменениеУсловийИсполнительногоЛиста.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	ИзменениеУсловийИсполнительногоЛиста.Физлицо,
	|	ИзменениеУсловийИсполнительногоЛиста.ДатаИзменения КАК ДатаДействия,
	|	ИзменениеУсловийИсполнительногоЛиста.ДатаОкончания,
	|	ИзменениеУсловийИсполнительногоЛиста.Размер,
	//vvv
	|	ИзменениеУсловийИсполнительногоЛиста.ПроцентБПМ,
	//
	|	ИзменениеУсловийИсполнительногоЛиста.Предел,
	|	ИзменениеУсловийИсполнительногоЛиста.УчитыватьБольничныеЛисты,
	|	ИзменениеУсловийИсполнительногоЛиста.СпособРасчетаИЛ,
	|	ИзменениеУсловийИсполнительногоЛиста.Получатель,
	|	ИзменениеУсловийИсполнительногоЛиста.Ссылка,
	|	ИзменениеУсловийИсполнительногоЛиста.ПочтовыйСбор,
	|	ИзменениеУсловийИсполнительногоЛиста.ВидРасчета,
	|	ИзменениеУсловийИсполнительногоЛиста.ПроцентПочтовогоСбора,
	|	ИзменениеУсловийИсполнительногоЛиста.УдерживатьКомисиионыйСбор,
	|	ИзменениеУсловийИсполнительногоЛиста.ВидСбора,
	|	ИзменениеУсловийИсполнительногоЛиста.ПроцентСбора,
	|	ИзменениеУсловийИсполнительногоЛиста.ПрожиточныйМинимум,
	|	ИзменениеУсловийИсполнительногоЛиста.Действие,
	|	ИзменениеУсловийИсполнительногоЛиста.ИсполнительныйЛист,
	|	ВЫБОР
	|		КОГДА ИзменениеУсловийИсполнительногоЛиста.ПочтовыйСбор
	|			ТОГДА ИзменениеУсловийИсполнительногоЛиста.Тариф
	|	КОНЕЦ КАК Тариф,
	|	ИзменениеУсловийИсполнительногоЛиста.Получатель КАК Получатель1
	|ИЗ
	|	Документ.ИзменениеУсловийИсполнительногоЛиста КАК ИзменениеУсловийИсполнительногоЛиста
	|ГДЕ
	|	ИзменениеУсловийИсполнительногоЛиста.Ссылка = &ДокументСсылка";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//  ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//  Отказ 					- флаг отказа в проведении.
//	Заголовок				- Заголовок для сообщений об ошибках проведения
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок)

	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана организация!", Отказ, Заголовок);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Физлицо) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указан работник!", Отказ, Заголовок);
	КонецЕсли;
	Если УдерживатьКомисиионыйСбор Тогда
		Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ВидСбора) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указан вид сбора!", Отказ, Заголовок);
		КонецЕсли;	
		Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ПроцентСбора) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Не указан процент сбора!", Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;

	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// По строке выборки результата запроса по документу формируем движения по регистрам
//
// Параметры: 
//	ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента)

	
	ВводимыеУдержания = Новый Массив;
	Если ВыборкаПоШапкеДокумента.Действие <> Перечисления.ВидыДействияСНачислением.Прекратить Тогда
		ВводимыеУдержания.Добавить(ВыборкаПоШапкеДокумента.ВидРасчета);
		Если ВыборкаПоШапкеДокумента.УдерживатьКомисиионыйСбор Тогда
			ВводимыеУдержания.Добавить(ВыборкаПоШапкеДокумента.ВидСбора);
		КонецЕсли;
		Если ВыборкаПоШапкеДокумента.ПочтовыйСбор Тогда
			ВводимыеУдержания.Добавить(ПланыВидовРасчета.УдержанияОрганизаций.ПочтовыйСборПоИЛ);
		КонецЕсли;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПлановыеУдержанияРаботниковОрганизаций.Организация,
	|	ПлановыеУдержанияРаботниковОрганизаций.ФизЛицо,
	|	ПлановыеУдержанияРаботниковОрганизаций.ВидРасчета,
	|	ПлановыеУдержанияРаботниковОрганизаций.ДокументОснование,
	|	ПлановыеУдержанияРаботниковОрганизаций.Получатель,
	|	ПлановыеУдержанияРаботниковОрганизаций.ПорядокИсчисленияИздержек,
	|	ПлановыеУдержанияРаботниковОрганизаций.ПрожиточныйМинимум
	|ИЗ
	|	РегистрСведений.ПлановыеУдержанияРаботниковОрганизаций КАК ПлановыеУдержанияРаботниковОрганизаций
	|ГДЕ
	|	ПлановыеУдержанияРаботниковОрганизаций.Регистратор = &Регистратор
	|	И (НЕ ПлановыеУдержанияРаботниковОрганизаций.ВидРасчета В (&ВводимыеУдержания))";
	Запрос.УстановитьПараметр("ВводимыеУдержания", ВводимыеУдержания);
	Запрос.УстановитьПараметр("Регистратор", ВыборкаПоШапкеДокумента.ИсполнительныйЛист);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Движение = Движения.ПлановыеУдержанияРаботниковОрганизаций.Добавить();
		ЗаполнитьЗначенияСвойств(Движение,Выборка);
		Движение.Период	= ВыборкаПоШапкеДокумента.ДатаДействия;
		Движение.Действие = Перечисления.ВидыДействияСНачислением.Прекратить;
	КонецЦикла;
	
	Если ВыборкаПоШапкеДокумента.Действие <> Перечисления.ВидыДействияСНачислением.Прекратить Тогда
		Движение = Движения.ПлановыеУдержанияРаботниковОрганизаций.Добавить();
		// свойства
		Движение.Период					= ВыборкаПоШапкеДокумента.ДатаДействия;
	
		// измерения
		Движение.Организация			= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
		Движение.ФизЛицо				= ВыборкаПоШапкеДокумента.ФизЛицо;
		Движение.ВидРасчета				= ВыборкаПоШапкеДокумента.ВидРасчета;
		Движение.ДокументОснование		= ВыборкаПоШапкеДокумента.ИсполнительныйЛист;
	
		// ресурсы
		Движение.Действие				= Перечисления.ВидыДействияСНачислением.Изменить;
		//vvv
		Движение.Показатель2					= ВыборкаПоШапкеДокумента.ПроцентБПМ;
		//
		Движение.Показатель1					= ВыборкаПоШапкеДокумента.Размер;
		
		
		Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ДатаОкончания) Тогда
			Движение.ПериодЗавершения	= ВыборкаПоШапкеДокумента.ДатаОкончания + 86400;
			Движение.ДействиеЗавершения	= Перечисления.ВидыДействияСНачислением.Прекратить;
		КонецЕсли;
		
		Если ВыборкаПоШапкеДокумента.ВидРасчета.СпособРасчета = Перечисления.СпособыРасчетаОплатыТруда.ИсполнительныйЛистВПрожиточныхМинимумах Тогда
			Движение.ПрожиточныйМинимум = ВыборкаПоШапкеДокумента.ПрожиточныйМинимум;
		КонецЕсли;
		Движение.Получатель				= ВыборкаПоШапкеДокумента.Получатель;
	
		Если  ВыборкаПоШапкеДокумента.ПочтовыйСбор Тогда
			
			
			Движение.ПорядокИсчисленияИздержек =  ВыборкаПоШапкеДокумента.Тариф;
	
			Движение = Движения.ПлановыеУдержанияРаботниковОрганизаций.Добавить();
			// свойства
			Движение.Период					= ВыборкаПоШапкеДокумента.ДатаДействия;
	
			// измерения
			Движение.Организация			= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
       		Движение.ФизЛицо				= ВыборкаПоШапкеДокумента.ФизЛицо;
			Движение.ВидРасчета				= ПланыВидовРасчета.УдержанияОрганизаций.ПочтовыйСборПоИЛ;
			Движение.ДокументОснование		= ВыборкаПоШапкеДокумента.ИсполнительныйЛист;
	
			// ресурсы
			Движение.Действие				= Перечисления.ВидыДействияСНачислением.Изменить;
			Движение.Показатель1			= ВыборкаПоШапкеДокумента.ПроцентПочтовогоСбора;
		
				Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ДатаОкончания) Тогда
				Движение.ПериодЗавершения	= ВыборкаПоШапкеДокумента.ДатаОкончания + 86400;
				Движение.ДействиеЗавершения	= Перечисления.ВидыДействияСНачислением.Прекратить;
				// не заполняются
				//СпособОтраженияВБухучетеЗавершения
				//РазмерЗавершения
				//ВалютаЗавершения
			КонецЕсли;
			
			Движение.ПорядокИсчисленияИздержек =  ВыборкаПоШапкеДокумента.Тариф;
			Движение.Получатель				= ВыборкаПоШапкеДокумента.Получатель;
	
		КонецЕсли;
		
		Если  ВыборкаПоШапкеДокумента.УдерживатьКомисиионыйСбор Тогда
			
			Движение.ПорядокИсчисленияИздержек =  ВыборкаПоШапкеДокумента.ПроцентСбора;
	
			
			Движение = Движения.ПлановыеУдержанияРаботниковОрганизаций.Добавить();
			// свойства
			Движение.Период					= ВыборкаПоШапкеДокумента.ДатаДействия;
	
			// измерения
			Движение.Организация			= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
	
			Движение.ФизЛицо				= ВыборкаПоШапкеДокумента.ФизЛицо;
			Движение.ВидРасчета				= ВыборкаПоШапкеДокумента.ВидСбора;
			
			Движение.ДокументОснование		= ВыборкаПоШапкеДокумента.ИсполнительныйЛист;
	
			// ресурсы
			Движение.Действие				= Перечисления.ВидыДействияСНачислением.Изменить;
			Движение.Показатель1					= ВыборкаПоШапкеДокумента.ПроцентСбора;
			
			Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ДатаОкончания) Тогда
				Движение.ПериодЗавершения	= ВыборкаПоШапкеДокумента.ДатаОкончания + 86400;
				Движение.ДействиеЗавершения	= Перечисления.ВидыДействияСНачислением.Прекратить;
			КонецЕсли;
			Движение.ПорядокИсчисленияИздержек =  ВыборкаПоШапкеДокумента.ПроцентСбора;
        	Движение.Получатель				= ВыборкаПоШапкеДокумента.Получатель;
	
		КонецЕсли;
	КонецЕсли;
	
	
КонецПроцедуры // ДобавитьСтрокуВДвиженияПоРегистрамСведений

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	
	РезультатЗапросаПоШапке = СформироватьЗапросПоШапке(Режим);

	// Получим реквизиты шапки из запроса
	ВыборкаПоШапкеДокумента = РезультатЗапросаПоШапке.Выбрать();

	Если ВыборкаПоШапкеДокумента.Следующий() Тогда

		//Надо позвать проверку заполнения реквизитов шапки
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);

		// Движения стоит добавлять, если в проведении еще не отказано (отказ =ложь)
		Если НЕ Отказ Тогда

			Если НЕ Отказ Тогда
				// Заполним записи в наборах записей регистров
				ДобавитьСтрокуВДвиженияПоРегистрамСведений(ВыборкаПоШапкеДокумента);
			КонецЕсли;
			
		КонецЕсли; 

	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ИсполнительныйЛист") Тогда
		ЗаполнитьПоОснованию(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры
