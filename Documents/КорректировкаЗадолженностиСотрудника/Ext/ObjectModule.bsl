
Функция СформироватьЗапросПоШапке()
	
	Запрос=Новый Запрос;
	Запрос.Текст=
	
	"ВЫБРАТЬ
	|	КорректировкаЗадолженностиСотрудника.Организация,
	|	КорректировкаЗадолженностиСотрудника.ПериодВзаиморасчетов,
	|	КорректировкаЗадолженностиСотрудника.Дата КАК Период
	|ИЗ
	|	Документ.КорректировкаЗадолженностиСотрудника КАК КорректировкаЗадолженностиСотрудника
	|ГДЕ
	|	КорректировкаЗадолженностиСотрудника.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Функция СформироватьЗапросПоРаботникиОрганизаций()
	
	Запрос=Новый Запрос;
	Запрос.Текст=
	
	"ВЫБРАТЬ
	|	КорректировкаЗадолженностиСотрудникаРаботникиОрганизаций.ФизЛицо,
	|	КорректировкаЗадолженностиСотрудникаРаботникиОрганизаций.СуммаВзаиморасчетов
	|ИЗ
	|	Документ.КорректировкаЗадолженностиСотрудника.РаботникиОрганизаций КАК КорректировкаЗадолженностиСотрудникаРаботникиОрганизаций
	|ГДЕ
	|	КорректировкаЗадолженностиСотрудникаРаботникиОрганизаций.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	Возврат Запрос.Выполнить().Выбрать();

КонецФункции

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ВыборкаПоШапке=СформироватьЗапросПоШапке();
	ВыборкаПоРаботникам=СформироватьЗапросПоРаботникиОрганизаций();
	
	Движения.ВзаиморасчетыСРаботникамиОрганизаций.Записывать = Истина;
	Движения.ВзаиморасчетыСРаботникамиОрганизаций.Очистить();
	
	Движения.ЗарплатаЗаМесяцОрганизаций.Записывать = Истина;
	Движения.ЗарплатаЗаМесяцОрганизаций.Очистить();

	
	Если ВыборкаПоШапке.Следующий() Тогда
		
		Пока ВыборкаПоРаботникам.Следующий() Цикл			
			
			Движение = Движения.ВзаиморасчетыСРаботникамиОрганизаций.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = ВыборкаПоШапке.Период;
			Движение.Физлицо = ВыборкаПоРаботникам.ФизЛицо;
			Движение.Организация = ВыборкаПоШапке.Организация;
			Движение.ПериодВзаиморасчетов = ВыборкаПоШапке.ПериодВзаиморасчетов;
			Движение.СуммаВзаиморасчетов = ВыборкаПоРаботникам.СуммаВзаиморасчетов;	
			Движение.ХарактерВыплаты = Перечисления.ХарактерВыплатыЗарплаты.Зарплата;	
			
			Движение = Движения.ЗарплатаЗаМесяцОрганизаций.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = ВыборкаПоШапке.Период;
			Движение.Физлицо = ВыборкаПоРаботникам.ФизЛицо;
			Движение.Организация = ВыборкаПоШапке.Организация;
			Движение.СуммаВзаиморасчетов = ВыборкаПоРаботникам.СуммаВзаиморасчетов;	
			
		КонецЦикла;
		
	КонецЕсли;
	
	
КонецПроцедуры
