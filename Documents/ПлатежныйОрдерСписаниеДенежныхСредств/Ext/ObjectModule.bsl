////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

// Заполнение табличной части перечисления ЗП данными клиент-банка
//
// Параметры
//  ТаблицаПлатежей  – ТаблицаЗначений – Таблица перечислений денежных средств физлицам
//  Основание  – Документ основание
//
Процедура ЗаполнитьПеречислениеЗППоОбменуСБанком(ТаблицаПлатежей, Основание) Экспорт
	
	ДокументОснование = Основание;

	ПеречислениеЗаработнойПлаты.Загрузить(ТаблицаПлатежей);
	
КонецПроцедуры // ЗаполнитьПеречислениеЗППоОбменуСБанком()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДОКУМЕНТА

Процедура ПроверитьЗаполнениеДокумента(СтруктураШапкиДокумента,Отказ, Заголовок)

	СтруктураПолей = Новый Структура("Организация");

	ОбщегоНазначения.ПроверитьЗаполнениеШапкиДокумента(ЭтотОбъект,СтруктураПолей, Отказ, Заголовок);

КонецПроцедуры

// Формирует движения по регистрам
//  Отказ                     - флаг отказа в проведении,
//  Заголовок                 - строка, заголовок сообщения об ошибке проведения.
//  Режим 					  - режим проведения документа
//
Процедура ДвиженияПоРегистрам(РежимПроведения, Отказ, Заголовок, СтруктураШапкиДокумента)
	
	ДвиженияПоРегистрамРегл(РежимПроведения, Отказ, Заголовок, СтруктураШапкиДокумента);

КонецПроцедуры // ДвиженияПоРегистрам()

// Формирует запрос по табличной части документа
//
// Параметры:
//  нет
//
// Возвращаемое значение:
//  Результат запроса.
//
Функция СформироватьЗапросПоДокПлатежныйОрдерСписание()
	
	// по регистру ВзаиморасчетыСРаботникамиОрганизаций
	Если ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "УчетЗадолженностиПоМесяцам") Тогда
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ссылка.Дата КАК Период,
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ссылка.Организация,
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Физлицо,
		|	&СпособВыплаты КАК СпособВыплаты,
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ведомость.ПериодРегистрации КАК ПериодВзаиморасчетов,
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ведомость.ХарактерВыплаты КАК ХарактерВыплаты,
		|	СУММА(ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.СуммаПлатежа) КАК СуммаВзаиморасчетов,
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ведомость
		|ИЗ
		|	Документ.ПлатежныйОрдерСписаниеДенежныхСредств.ПеречислениеЗаработнойПлаты КАК ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты
		|ГДЕ
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ссылка.Дата,
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ссылка.Организация,
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Физлицо,
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ведомость.ПериодРегистрации,
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ведомость.ХарактерВыплаты,
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ведомость
		|
		|ИМЕЮЩИЕ
		|	СУММА(ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.СуммаПлатежа) <> 0";
	Иначе 	
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ссылка.Дата КАК Период,
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ссылка.Организация,
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Физлицо,
		|	&СпособВыплаты КАК СпособВыплаты,
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ведомость.ХарактерВыплаты КАК ХарактерВыплаты,
		|	СУММА(ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.СуммаПлатежа) КАК СуммаВзаиморасчетов,
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ведомость
		|ИЗ
		|	Документ.ПлатежныйОрдерСписаниеДенежныхСредств.ПеречислениеЗаработнойПлаты КАК ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты
		|ГДЕ
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ссылка.Дата,
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ссылка.Организация,
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Физлицо,
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ведомость.ХарактерВыплаты,
		|	ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.Ведомость
		|
		|ИМЕЮЩИЕ
		|	СУММА(ПлатежныйОрдерСписаниеДенежныхСредствПеречислениеЗаработнойПлаты.СуммаПлатежа) <> 0";
	КонецЕсли;
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Ссылка",			Ссылка);
	Запрос.УстановитьПараметр("СпособВыплаты",	Перечисления.СпособыВыплатыЗарплаты.ЧерезБанк);
	
	Возврат Запрос.Выполнить();
	
КонецФункции // СформироватьЗапросПоДокПлатежныйОрдерСписание

Процедура ДвиженияПоРегистрамРегл(РежимПроведения, Отказ, Заголовок, СтруктураШапкиДокумента)

	Выборка = СформироватьЗапросПоДокПлатежныйОрдерСписание().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрокаДвижения = Движения.ВзаиморасчетыСРаботникамиОрганизаций.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаДвижения,Выборка);
		СтрокаДвижения.ВидДвижения = ВидДвиженияНакопления.Расход;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	// По регистру расчетов с бюджетом по НДФЛ

	ГоловнаяОрганизация = ОбщегоНазначения.ГоловнаяОрганизация(Организация);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА ПлДок.Ссылка.Дата > КОНЕЦПЕРИОДА(МАКСИМУМ(ПлДок.Ведомость.ПериодРегистрации), МЕСЯЦ)
	|			ТОГДА ПлДок.Ссылка.Дата
	|		ИНАЧЕ КОНЕЦПЕРИОДА(МАКСИМУМ(ПлДок.Ведомость.ПериодРегистрации), МЕСЯЦ)
	|	КОНЕЦ КАК ДатаНДФЛ
	|ИЗ
	|	Документ.ПлатежныйОрдерСписаниеДенежныхСредств.ПеречислениеЗаработнойПлаты КАК ПлДок
	|ГДЕ
	|	ПлДок.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ПлДок.Ссылка.Дата";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	ДатаНДФЛ = Выборка.ДатаНДФЛ;
			
	// Выборка долгов по НДФЛ в разрезе месяцев налогового периода по обособленному подразделению
	Запрос.Текст=
	"ВЫБРАТЬ
	|	СуммыВыплатИСальдоПоМесяцам.ФизЛицо КАК ФизЛицо,
	|	ЕСТЬNULL(СуммыВыплатИСальдоПоМесяцам.ВыплаченнаяСумма, 0) КАК ВыплаченнаяСумма,
	|	НДФЛРасчетыСБюджетомПоМесяцам.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|	НДФЛРасчетыСБюджетомПоМесяцам.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|	ЕСТЬNULL(НДФЛРасчетыСБюджетомПоМесяцам.СальдоПоНалогу, 0) КАК СальдоПоНалогуЗаМесяц,
	|	ЕСТЬNULL(НДФЛРасчетыСБюджетомПоГодам.СальдоПоНалогу, 0) КАК СальдоПоНалогуЗаНалоговыйПериод,
	|	ЕСТЬNULL(НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО.КодПоОКАТО, """") КАК КодПоОКАТО,
	|	ЕСТЬNULL(НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО.КПП, """") КАК КПП,
	|	ЕСТЬNULL(НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО.ПодразделениеОрганизации, ЗНАЧЕНИЕ(Справочник.ПодразделенияОрганизаций.ПустаяСсылка)) КАК ПодразделениеОрганизации,
	|	НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО.Период КАК ДатаРегистрацииКодаОКАТО,
	|	ЕСТЬNULL(НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО.СальдоПоНалогу, 0) КАК СальдоПоОКАТО,
	|	ЕСТЬNULL(НДФЛРасчетыСБюджетомПоМесяцамПоОКАТОвсего.СальдоПоНалогу, 0) КАК ВсегоЗаМесяцПоОКАТО
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПеречислениеЗП.Физлицо КАК ФизЛицо,
	|		СУММА(ПеречислениеЗП.СуммаПлатежа) КАК ВыплаченнаяСумма
	|	ИЗ
	|		Документ.ПлатежныйОрдерСписаниеДенежныхСредств.ПеречислениеЗаработнойПлаты КАК ПеречислениеЗП
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ПеречислениеЗП.Физлицо) КАК СуммыВыплатИСальдоПоМесяцам
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			НДФЛРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
	|			НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ) КАК МесяцНалоговогоПериода,
	|			НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|			СУММА(НДФЛРасчетыСБюджетом.СальдоПоНалогу) КАК СальдоПоНалогу
	|		ИЗ
	|			(ВЫБРАТЬ
	|				НДФЛРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
	|				НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода КАК МесяцНалоговогоПериода,
	|				НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|				ВЫБОР
	|					КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|						ТОГДА НДФЛРасчетыСБюджетом.Налог
	|					ИНАЧЕ -НДФЛРасчетыСБюджетом.Налог
	|				КОНЕЦ КАК СальдоПоНалогу
	|			ИЗ
	|				РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
	|			ГДЕ
	|				НДФЛРасчетыСБюджетом.Регистратор <> &ДокументСсылка
	|				И НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация
	|				И НДФЛРасчетыСБюджетом.ОбособленноеПодразделение = &ОбособленноеПодразделение
	|				И НДФЛРасчетыСБюджетом.Период <= &ДатаНДФЛ
	|			
	|			ОБЪЕДИНИТЬ ВСЕ
	|			
	|			ВЫБРАТЬ
	|				НДФЛКЗачету.ФизЛицо,
	|				НАЧАЛОПЕРИОДА(НДФЛКЗачету.Период, МЕСЯЦ),
	|				&Ставка13,
	|				ВЫБОР
	|					КОГДА НДФЛКЗачету.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|						ТОГДА НДФЛКЗачету.СуммаНДФЛКЗачету
	|					ИНАЧЕ -НДФЛКЗачету.СуммаНДФЛКЗачету
	|				КОНЕЦ
	|			ИЗ
	|				РегистрНакопления.НДФЛКЗачету КАК НДФЛКЗачету
	|			ГДЕ
	|				НДФЛКЗачету.Регистратор <> &ДокументСсылка
	|				И НДФЛКЗачету.Организация = &ГоловнаяОрганизация
	|				И НДФЛКЗачету.Период <= &ДатаНДФЛ) КАК НДФЛРасчетыСБюджетом
	|		
	|		СГРУППИРОВАТЬ ПО
	|			НДФЛРасчетыСБюджетом.ФизЛицо,
	|			НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента,
	|			НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ)) КАК НДФЛРасчетыСБюджетомПоМесяцам
	|			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				НДФЛРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
	|				НДФЛРасчетыСБюджетом.НалоговыйПериод КАК НалоговыйПериод,
	|				НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|				СУММА(НДФЛРасчетыСБюджетом.СальдоПоНалогу) КАК СальдоПоНалогу
	|			ИЗ
	|				(ВЫБРАТЬ
	|					НДФЛРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
	|					НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, ГОД) КАК НалоговыйПериод,
	|					НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|					ВЫБОР
	|						КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|							ТОГДА НДФЛРасчетыСБюджетом.Налог
	|						ИНАЧЕ -НДФЛРасчетыСБюджетом.Налог
	|					КОНЕЦ КАК СальдоПоНалогу
	|				ИЗ
	|					РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
	|				ГДЕ
	|					НДФЛРасчетыСБюджетом.Регистратор <> &ДокументСсылка
	|					И НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация
	|					И НДФЛРасчетыСБюджетом.ОбособленноеПодразделение = &ОбособленноеПодразделение
	|					И НДФЛРасчетыСБюджетом.Период <= &ДатаНДФЛ
	|				
	|				ОБЪЕДИНИТЬ ВСЕ
	|				
	|				ВЫБРАТЬ
	|					НДФЛКЗачету.ФизЛицо,
	|					НАЧАЛОПЕРИОДА(НДФЛКЗачету.Период, ГОД),
	|					&Ставка13,
	|					ВЫБОР
	|						КОГДА НДФЛКЗачету.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|							ТОГДА НДФЛКЗачету.СуммаНДФЛКЗачету
	|						ИНАЧЕ -НДФЛКЗачету.СуммаНДФЛКЗачету
	|					КОНЕЦ
	|				ИЗ
	|					РегистрНакопления.НДФЛКЗачету КАК НДФЛКЗачету
	|				ГДЕ
	|					НДФЛКЗачету.Регистратор <> &ДокументСсылка
	|					И НДФЛКЗачету.Организация = &ГоловнаяОрганизация
	|					И НДФЛКЗачету.Период <= &ДатаНДФЛ) КАК НДФЛРасчетыСБюджетом
	|			
	|			СГРУППИРОВАТЬ ПО
	|				НДФЛРасчетыСБюджетом.ФизЛицо,
	|				НДФЛРасчетыСБюджетом.НалоговыйПериод,
	|				НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента) КАК НДФЛРасчетыСБюджетомПоГодам
	|			ПО НДФЛРасчетыСБюджетомПоМесяцам.ФизЛицо = НДФЛРасчетыСБюджетомПоГодам.ФизЛицо
	|				И НДФЛРасчетыСБюджетомПоМесяцам.СтавкаНалогообложенияРезидента = НДФЛРасчетыСБюджетомПоГодам.СтавкаНалогообложенияРезидента
	|				И (НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетомПоМесяцам.МесяцНалоговогоПериода, ГОД) = НДФЛРасчетыСБюджетомПоГодам.НалоговыйПериод)
	|			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				НДФЛРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
	|				НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ) КАК МесяцНалоговогоПериода,
	|				НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|				СУММА(ВЫБОР
	|						КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|							ТОГДА НДФЛРасчетыСБюджетом.Налог
	|						ИНАЧЕ -НДФЛРасчетыСБюджетом.Налог
	|					КОНЕЦ) КАК СальдоПоНалогу,
	|				НДФЛРасчетыСБюджетом.КодПоОКАТО КАК КодПоОКАТО,
	|				НДФЛРасчетыСБюджетом.КПП КАК КПП,
	|				МАКСИМУМ(НДФЛРасчетыСБюджетом.Период) КАК Период,
	|				НДФЛРасчетыСБюджетом.ПодразделениеОрганизации КАК ПодразделениеОрганизации
	|			ИЗ
	|				РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
	|			ГДЕ
	|				НДФЛРасчетыСБюджетом.Регистратор <> &ДокументСсылка
	|				И НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация
	|				И НДФЛРасчетыСБюджетом.ОбособленноеПодразделение = &ОбособленноеПодразделение
	|				И НДФЛРасчетыСБюджетом.Период <= &ДатаНДФЛ
	|			
	|			СГРУППИРОВАТЬ ПО
	|				НДФЛРасчетыСБюджетом.ФизЛицо,
	|				НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ),
	|				НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента,
	|				НДФЛРасчетыСБюджетом.КодПоОКАТО,
	|				НДФЛРасчетыСБюджетом.КПП,
	|				НДФЛРасчетыСБюджетом.ПодразделениеОрганизации) КАК НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО
	|			ПО НДФЛРасчетыСБюджетомПоМесяцам.ФизЛицо = НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО.ФизЛицо
	|				И НДФЛРасчетыСБюджетомПоМесяцам.СтавкаНалогообложенияРезидента = НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО.СтавкаНалогообложенияРезидента
	|				И НДФЛРасчетыСБюджетомПоМесяцам.МесяцНалоговогоПериода = НДФЛРасчетыСБюджетомПоМесяцамПоОКАТО.МесяцНалоговогоПериода
	|			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				НДФЛРасчетыСБюджетом.ФизЛицо КАК ФизЛицо,
	|				НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ) КАК МесяцНалоговогоПериода,
	|				НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента КАК СтавкаНалогообложенияРезидента,
	|				СУММА(ВЫБОР
	|						КОГДА НДФЛРасчетыСБюджетом.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|							ТОГДА НДФЛРасчетыСБюджетом.Налог
	|						ИНАЧЕ -НДФЛРасчетыСБюджетом.Налог
	|					КОНЕЦ) КАК СальдоПоНалогу
	|			ИЗ
	|				РегистрНакопления.НДФЛРасчетыСБюджетом КАК НДФЛРасчетыСБюджетом
	|			ГДЕ
	|				НДФЛРасчетыСБюджетом.Регистратор <> &ДокументСсылка
	|				И НДФЛРасчетыСБюджетом.Организация = &ГоловнаяОрганизация
	|				И НДФЛРасчетыСБюджетом.ОбособленноеПодразделение = &ОбособленноеПодразделение
	|				И НДФЛРасчетыСБюджетом.Период <= &ДатаНДФЛ
	|			
	|			СГРУППИРОВАТЬ ПО
	|				НДФЛРасчетыСБюджетом.ФизЛицо,
	|				НАЧАЛОПЕРИОДА(НДФЛРасчетыСБюджетом.МесяцНалоговогоПериода, МЕСЯЦ),
	|				НДФЛРасчетыСБюджетом.СтавкаНалогообложенияРезидента) КАК НДФЛРасчетыСБюджетомПоМесяцамПоОКАТОвсего
	|			ПО НДФЛРасчетыСБюджетомПоМесяцам.ФизЛицо = НДФЛРасчетыСБюджетомПоМесяцамПоОКАТОвсего.ФизЛицо
	|				И НДФЛРасчетыСБюджетомПоМесяцам.СтавкаНалогообложенияРезидента = НДФЛРасчетыСБюджетомПоМесяцамПоОКАТОвсего.СтавкаНалогообложенияРезидента
	|				И НДФЛРасчетыСБюджетомПоМесяцам.МесяцНалоговогоПериода = НДФЛРасчетыСБюджетомПоМесяцамПоОКАТОвсего.МесяцНалоговогоПериода
	|		ПО СуммыВыплатИСальдоПоМесяцам.ФизЛицо = НДФЛРасчетыСБюджетомПоМесяцам.ФизЛицо
	|ГДЕ
	|	ЕСТЬNULL(НДФЛРасчетыСБюджетомПоМесяцам.СальдоПоНалогу, 0) > 0
	|	И ЕСТЬNULL(НДФЛРасчетыСБюджетомПоГодам.СальдоПоНалогу, 0) > 0
	|	И ЕСТЬNULL(СуммыВыплатИСальдоПоМесяцам.ВыплаченнаяСумма, 0) > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ФизЛицо,
	|	СтавкаНалогообложенияРезидента,
	|	МесяцНалоговогоПериода,
	|	ДатаРегистрацииКодаОКАТО";

	Запрос.УстановитьПараметр("ДокументСсылка",				Ссылка);
	Запрос.УстановитьПараметр("Дата",						Дата);
	Запрос.УстановитьПараметр("ДатаНДФЛ",					ДатаНДФЛ);
	Запрос.УстановитьПараметр("ОбособленноеПодразделение",	Организация);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",		ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("Ставка13",					Перечисления.НДФЛСтавкиНалогообложенияРезидента.Ставка13);

	ВременнаяТаблица = Новый ТаблицаЗначений;
	ВременнаяТаблица.Колонки.Добавить("КодПоОКАТО");
	ВременнаяТаблица.Колонки.Добавить("КПП");
	ВременнаяТаблица.Колонки.Добавить("ПодразделениеОрганизации");
	ВременнаяТаблица.Колонки.Добавить("СальдоПоОКАТО");
	
	Выборка= Запрос.Выполнить().Выбрать();

	Пока Выборка.СледующийПоЗначениюПоля("ФизЛицо") Цикл
		
		СуммаКВыплате = Выборка.ВыплаченнаяСумма;
		
		Пока Выборка.СледующийПоЗначениюПоля("СтавкаНалогообложенияРезидента") Цикл
			
			ОсталосьРаспределить = Мин(СуммаКВыплате,Выборка.СальдоПоНалогуЗаНалоговыйПериод);
			СуммаКВыплате = Макс(0,СуммаКВыплате - Выборка.СальдоПоНалогуЗаНалоговыйПериод);
			
			Пока Выборка.СледующийПоЗначениюПоля("МесяцНалоговогоПериода") И ОсталосьРаспределить > 0 Цикл
				
				// расчет суммы удерживаемого налога
				ПогашаемаяСумма = Мин(Выборка.СальдоПоНалогуЗаМесяц, ОсталосьРаспределить);
				ОсталосьРаспределить = ОсталосьРаспределить - ПогашаемаяСумма;
				
				Если Выборка.ВсегоЗаМесяцПоОКАТО = Выборка.СальдоПоОКАТО Или Выборка.ВсегоЗаМесяцПоОКАТО = 0 Тогда
					
					// запись удержания
					Движение = Движения.НДФЛРасчетыСБюджетом.Добавить();
					
					ЗаполнитьЗначенияСвойств(Движение,Выборка); // ФизЛицо, СтавкаНалогообложенияРезидента, МесяцНалоговогоПериода 
																// КодПоОКАТО, КПП, ПодразделениеОрганизации
					
					//свойства
					Движение.Период      = Дата;
					Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
					//измерения
					Движение.Организация = ГоловнаяОрганизация;
					// ресурсы
					Движение.Налог       = ПогашаемаяСумма;
					// реквизиты
					Движение.ВидСтроки	 = Перечисления.НДФЛРасчетыСБюджетомВидСтроки.Удержание;
					Движение.ОбособленноеПодразделение = Организация;
					
				Иначе
					
					ФизЛицо = Выборка.ФизЛицо;
					СтавкаНалогообложенияРезидента = Выборка.СтавкаНалогообложенияРезидента;
					МесяцНалоговогоПериода = Выборка.МесяцНалоговогоПериода;
					
					ВременнаяТаблица.Очистить();
					Пока Выборка.Следующий() Цикл
						ЗаполнитьЗначенияСвойств(ВременнаяТаблица.Добавить(),Выборка);
					КонецЦикла;
					
					Результат = ОбщегоНазначения.РаспределитьПропорционально(ПогашаемаяСумма,ВременнаяТаблица.ВыгрузитьКолонку("СальдоПоОКАТО"));
					
					Для каждого СтрокаОКАТО Из ВременнаяТаблица Цикл
						
						// запись удержания
						Движение = Движения.НДФЛРасчетыСБюджетом.Добавить();
						//свойства
						Движение.Период      = Дата;
						Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
						//измерения
						Движение.Физлицо     = ФизЛицо;
						Движение.Организация = ГоловнаяОрганизация;
						Движение.СтавкаНалогообложенияРезидента = СтавкаНалогообложенияРезидента;
						Движение.МесяцНалоговогоПериода = МесяцНалоговогоПериода;
						// ресурсы
						Движение.Налог       = Результат[ВременнаяТаблица.Индекс(СтрокаОКАТО)];
						// реквизиты
						Движение.ОбособленноеПодразделение = Организация;
						Движение.ВидСтроки = Перечисления.НДФЛРасчетыСБюджетомВидСтроки.Удержание;
						Движение.КодПоОКАТО	 = СтрокаОКАТО.КодПоОКАТО; 
						Движение.КПП		 = СтрокаОКАТО.КПП; 
						Движение.ПодразделениеОрганизации = СтрокаОКАТО.ПодразделениеОрганизации; 
						
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
			
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(ПеречислениеЗаработнойПлаты,, "Физлицо");
	
КонецПроцедуры // ПередЗаписью

Процедура ОбработкаПроведения(Отказ, Режим)

	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);

	// Сформируем структуру реквизитов шапки документа
	СтруктураШапкиДокумента = ОбщегоНазначения.СформироватьСтруктуруШапкиДокумента(ЭтотОбъект);

	ПроверитьЗаполнениеДокумента(СтруктураШапкиДокумента,Отказ, Заголовок);

	// Движения по документу
	Если НЕ Отказ Тогда
		ДвиженияПоРегистрам(Режим, Отказ, Заголовок, СтруктураШапкиДокумента);
	КонецЕсли;

КонецПроцедуры

