&НаКлиенте
Функция ТекущиеОтборыДинамическогоСписка(ДинамическийСписок)
	
	ТекущиеОтборы = Новый Массив;
	
	Для каждого ЭлементОтбора Из ДинамическийСписок.Отбор.Элементы Цикл
		ДобавляемыйОтбор = Новый Структура;
		ДобавляемыйОтбор.Вставить("ВидСравнения",	ЭлементОтбора.ВидСравнения);
		ДобавляемыйОтбор.Вставить("Использование",	ЭлементОтбора.Использование);
		ДобавляемыйОтбор.Вставить("ЛевоеЗначение",	ЭлементОтбора.ЛевоеЗначение);
		ДобавляемыйОтбор.Вставить("ПравоеЗначение",	ЭлементОтбора.ПравоеЗначение);
		ДобавляемыйОтбор.Вставить("Применение",		ЭлементОтбора.Применение);
		ТекущиеОтборы.Добавить(ДобавляемыйОтбор);
	КонецЦикла;
	
	Возврат ТекущиеОтборы;
	
КонецФункции // ОтборыДинамическогоСписка

&НаКлиенте
Процедура УстановитьОтбор()
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(Список.Отбор, "Организация", ОрганизацияОтбор, , , Не ОрганизацияОтбор.Пустая());
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьОтбор();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОрганизацияОтбор = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ЗаписьПользователя" И Параметр = глЗначениеПеременной("глТекущийПользователь") Тогда
		ОрганизацияОтбор = УправлениеПользователями.ПолучитьЗначениеПоУмолчанию(глЗначениеПеременной("глТекущийПользователь"), "ОсновнаяОрганизация");
		УстановитьОтбор();
	КонецЕсли;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ОрганизацияОтборПриИзменении(Элемент)
	
	УстановитьОтбор();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПечатьЖурнала(Команда)
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.ИнструктажПоОхранеТруда", "ЖурналРегистрацииИнструктажаПоОхранеТруда", ТекущиеОтборыДинамическогоСписка(Список), ЭтаФорма);

КонецПроцедуры



