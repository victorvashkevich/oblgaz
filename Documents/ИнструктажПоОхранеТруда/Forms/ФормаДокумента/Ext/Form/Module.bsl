
////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Формирует структуру параметров для формы подбора
//
// Возвращаемое значение:
// Структура
//
&НаКлиенте 
Функция ПолучитьПараметрыВызоваФормыПодбораСотрудника(ИмяТаблицы, ИмяПоля)
	
	Сотрудники = Новый Соответствие;
	Для Каждого Строка Из Объект[ИмяТаблицы] Цикл
		Сотрудники.Вставить(Строка[ИмяПоля]);
	КонецЦикла;	
	
	Возврат СоздатьПараметрыВызоваФормыПодбораСотрудника(Объект.Организация, Сотрудники)
	
КонецФункции // ПолучитьПараметрыВызоваФормыПодбораСотрудника()

// Формирует структуру параметров для формы подбора
//
// Возвращаемое значение:
// Структура
//
&НаСервереБезКонтекста 
Функция СоздатьПараметрыВызоваФормыПодбораСотрудника(Организация, Сотрудники)
	
	ПараметрыВызова = Новый Структура;
	
	ПараметрыВызова.Вставить("Организация", Организация); 
	ПараметрыВызова.Вставить("АдресВыбранныхСотрудниковВХранилище", ПоместитьВоВременноеХранилище(Сотрудники)); 
	
	Возврат ПараметрыВызова
	
КонецФункции // СоздатьПараметрыВызоваФормыПодбораСотрудника()

&НаСервере
Функция ПолучитьНовыеСотрудники(Сотрудники, ТЧ , имяПоля = "Сотрудник", ПоискПоФизЛицу = Ложь)
	
	НовыеСотрудники = Новый Массив();
	
	Для Каждого Сотрудник Из Сотрудники Цикл
		Если ПоискПоФизЛицу Тогда
			
			Если ТипЗнч(Сотрудник) = ТИП("СправочникСсылка.СотрудникиОрганизаций") И ТЧ.НайтиСтроки(Новый Структура(ИмяПоля, Сотрудник.ФизЛицо)).Количество() = 0 Тогда
				НовыеСотрудники.Добавить(Сотрудник);
			КонецЕсли;	
		Иначе
			Если ТЧ.НайтиСтроки(Новый Структура(ИмяПоля, Сотрудник)).Количество() = 0 Тогда
				НовыеСотрудники.Добавить(Сотрудник);
			КонецЕсли;	
		КонецЕсли;	
	КонецЦикла;	

	Возврат НовыеСотрудники;
	
КонецФункции

// Осуществляет добавление работников и их показателей после завершения подбора
//
&НаСервере
Процедура ЗавершитьПодборСотрудниковДляРаботники(АдресВременногоХранилища)
	
	МассивСотрудников = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	НовыеСотрудники = ПолучитьНовыеСотрудники(МассивСотрудников, Объект.Работники);
	
	Если НовыеСотрудники.Количество() > 0 Тогда
		Документ = РеквизитФормыВЗначение("Объект");
		Для Каждого Сотрудник Из НовыеСотрудники Цикл
			НоваяСтрока = Документ.Работники.Добавить();
			НоваяСтрока.Сотрудник = Сотрудник;
			НоваяСтрока.ФизЛицо = Сотрудник.ФизЛицо;
		КонецЦикла;
		ЗначениеВРеквизитФормы(Документ, "Объект");
	КонецЕсли;
	
КонецПроцедуры // ЗавершитьПодборСотрудниковДляПострадавшие()

&НаСервере
Процедура ЗаполнитьПоВидуИнструктажаНаСервере()
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	ДокументОбъект.ЗаполнитьПоВидуИнструктажа();
	ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	
КонецПроцедуры // ЗаполнитьПоВидуИнструктажаНаСервере

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДокументОбъект = РеквизитФормыВЗначение("Объект");
	Если ДокументОбъект.ЭтоНовый() Тогда
		ЗаполнениеДокументовПереопределяемый.ЗаполнитьШапкуДокумента(ДокументОбъект, глЗначениеПеременной("глТекущийПользователь"));
		Если Параметры.ЗначенияЗаполнения.Свойство("Организация") Тогда
			Если ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Организация) Тогда
				ДокументОбъект.Организация = Параметры.ЗначенияЗаполнения.Организация;
			КонецЕсли;
		КонецЕсли;
		ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
	КонецЕсли;
	
	// Обработчик подсистемы "ВерсионированиеОбъектов"
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзмененыИнструктажиПоОхранеТруда", ВыгрузитьКолонкуРаботникиНаСервере());
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ КОМАНД ФОРМЫ

&НаКлиенте
Процедура КомандаЗаполнитьПоВидуИнструктажа(Команда)
	
	Если Объект.Работники.Количество() > 0 Тогда
		Если Вопрос("Табличная часть будет очищена. Продолжить?",  РежимДиалогаВопрос.ОКОтмена,, КодВозвратаДиалога.ОК) = КодВозвратаДиалога.ОК Тогда
			Объект.Работники.Очистить();
		Иначе
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьПоВидуИнструктажаНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура РаботникиПодбор(Команда)
	
	Результат = ОткрытьФормуМодально("Справочник.СотрудникиОрганизаций.Форма.ФормаПодбора", ПолучитьПараметрыВызоваФормыПодбораСотрудника("Работники" , "Сотрудник"), ЭтаФорма);
	
	Если Результат <> Неопределено Тогда
		ЗавершитьПодборСотрудниковДляРаботники(Результат);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура РаботникиСотрудникПриИзменении(Элемент)
	
	Элементы.Работники.ТекущиеДанные.ФизЛицо = Неопределено;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Функция ВыгрузитьКолонкуРаботникиНаСервере()
	
	Возврат ОбщегоНазначения.ВыгрузитьКолонку(Объект.Работники, "Сотрудник");
	
КонецФункции
