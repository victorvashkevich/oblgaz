////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Хранит соответствие доходов и положенных по ним вычетов
Перем мСоответствиеДоходовИВычетов;

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Если Клиент Тогда

// Заполняет показатели
//
Функция ЗаполнитьПоказатели(ТекущийСотрудник) Экспорт 
	
	Если ТекущийСотрудник = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;

	ТЗНачисления					= ОсновныеНачисления.Выгрузить(,"Сотрудник,ВидРасчета");
	ТЗДополнительныеНачисления		= ДополнительныеНачисления.Выгрузить(,"Сотрудник,ВидРасчета");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Начисления",ТЗНачисления);
	Запрос.УстановитьПараметр("ДополнительныеНачисления",ТЗДополнительныеНачисления);
	Запрос.УстановитьПараметр("ПарамДата",НачалоМесяца(ПериодРегистрации));
	
	// получим временную таблицу с сотрудниками и видами расчетов
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Начисления.ВидРасчета,
	|	Начисления.Сотрудник
	|ПОМЕСТИТЬ ВТНачисления
	|ИЗ
	|	&Начисления КАК Начисления";
	
	Запрос.Выполнить();
	
	ВТНачисления = "ВТНачисления";
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДополнительныеНачисления.ВидРасчета,
	|	ДополнительныеНачисления.Сотрудник
	|ПОМЕСТИТЬ ВТДополнительныеНачисления
	|ИЗ
	|	&ДополнительныеНачисления КАК ДополнительныеНачисления";
	
	Запрос.Выполнить();
	
	ВТДополнительныеНачисления = "ВТДополнительныеНачисления";
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВложенныйЗапрос.Показатель.ВидПоказателя КАК ВидПоказателя,
	|	ВложенныйЗапрос.Показатель,
	|	ВложенныйЗапрос.Сотрудник
	|ИЗ (
    |
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Показатели.Показатель.ВидПоказателя КАК ВидПоказателя,
	|		Показатели.Показатель,
	|		Начисления.Сотрудник
	|	ИЗ
	|		ВТНачисления КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ОсновныеНачисленияОрганизаций.Показатели КАК Показатели
	|			ПО Показатели.Ссылка = Начисления.ВидРасчета
	|			И Показатели.Показатель.ВозможностьИзменения <> ЗНАЧЕНИЕ(Перечисление.ИзменениеПоказателейСхемМотивации.НеИзменяется)
	|			И Показатели.Показатель.ВозможностьИзменения <> ЗНАЧЕНИЕ(Перечисление.ИзменениеПоказателейСхемМотивации.ПустаяСсылка)
	|			И Показатели.Показатель.ТипПоказателя <> ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейСхемМотивации.ОценочнаяШкалаЧисловая)
	|			И Показатели.Показатель.ТипПоказателя <> ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейСхемМотивации.ОценочнаяШкалаПроцентная)
	|			И Показатели.Показатель.ТипПоказателя <> ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейСхемМотивации.ТарифныйРазряд)
	|			И Не Показатели.Показатель.Предопределенный
	|	ГДЕ
	|		НЕ (Показатели.Показатель ЕСТЬ NULL)
	|		И НЕ (Начисления.Сотрудник ЕСТЬ NULL)
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ РАЗЛИЧНЫЕ
	|		Показатели.Показатель.ВидПоказателя КАК ВидПоказателя,
	|		Показатели.Показатель,
	|		ДополнительныеНачисления.Сотрудник
	|	ИЗ
	|		ВТДополнительныеНачисления КАК ДополнительныеНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ДополнительныеНачисленияОрганизаций.Показатели КАК Показатели
	|			ПО Показатели.Ссылка = ДополнительныеНачисления.ВидРасчета
	|			И Показатели.Показатель.ВозможностьИзменения <> ЗНАЧЕНИЕ(Перечисление.ИзменениеПоказателейСхемМотивации.НеИзменяется)
	|			И Показатели.Показатель.ВозможностьИзменения <> ЗНАЧЕНИЕ(Перечисление.ИзменениеПоказателейСхемМотивации.ПустаяСсылка)
	|			И Показатели.Показатель.ТипПоказателя <> ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейСхемМотивации.ОценочнаяШкалаЧисловая)
	|			И Показатели.Показатель.ТипПоказателя <> ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейСхемМотивации.ОценочнаяШкалаПроцентная)
	|			И Показатели.Показатель.ТипПоказателя <> ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейСхемМотивации.ТарифныйРазряд)
	|			И Не Показатели.Показатель.Предопределенный) КАК ВложенныйЗапрос
	|ГДЕ
	|	НЕ (ВложенныйЗапрос.Показатель ЕСТЬ NULL)
	|	И НЕ (ВложенныйЗапрос.Сотрудник ЕСТЬ NULL)";
	
	
	ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	ТаблицаЗапроса.Свернуть("ВидПоказателя,Показатель,Сотрудник");
	Показатели = ТаблицаЗапроса.ВыгрузитьКолонку("Показатель");
	Сотрудники = ТаблицаЗапроса.ВыгрузитьКолонку("Сотрудник");
	
	Если ЗначениеЗаполнено(ТаблицаЗапроса) Тогда
		
		ФормаВводаПоказателей = РегистрыСведений.ЗначенияПоказателейСхемМотивации.ПолучитьФорму("ФормаВводаЗначенийПоказателей");
		ФормаВводаПоказателей.Организация		= Справочники.Организации.ПустаяСсылка();
		ФормаВводаПоказателей.ПериодДействия	= НачалоМесяца(ПериодРегистрации);
		
		ФормаВводаПоказателей.Организация = Организация;
		ФормаВводаПоказателей.ФормаАвтозаполнение(ТаблицаЗапроса, Сотрудники, Показатели, , Ложь);
		ФормаВводаПоказателей.мСотрудникДляОткрытия = ТекущийСотрудник;
		ФормаВводаПоказателей.Открыть();

		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;

КонецФункции //ЗаполнитьПоказатели

#КонецЕсли

Процедура Автозаполнение(ВременнаяТаблица = Неопределено, Реквизиты = Неопределено, ОсновноеНачисление = Истина) Экспорт
	
	Если ВременнаяТаблица = НеОпределено Тогда
		Возврат
	КонецЕсли;
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ВременнаяТаблица", ВременнаяТаблица);
	Запрос.УстановитьПараметр("ПарамВидРасчета",Реквизиты.ВидРасчета);
	Запрос.УстановитьПараметр("ПарамПериодРегистрации",ПериодРегистрации);
	Запрос.УстановитьПараметр("ПарамСотрудники",ВременнаяТаблица.ВыгрузитьКолонку("Сотрудник"));
	Запрос.УстановитьПараметр("ПарамПустойСторудник",Справочники.СотрудникиОрганизаций.ПустаяСсылка());
  	Запрос.УстановитьПараметр("ПарамОрганизация",Организация);
	Запрос.УстановитьПараметр("ПарамДатаНачала",Реквизиты.ДатаНачала);
	Запрос.УстановитьПараметр("ПарамДатаОкончания",Реквизиты.ДатаОкончания);
	Если Не ОсновноеНачисление Тогда
		Запрос.УстановитьПараметр("ПарамКодВычета",Реквизиты.КодВычета);
	КонецЕсли;
	Запрос.УстановитьПараметр("парамВалютаРегламентУчета" , Константы.ВалютаРегламентированногоУчета.Получить());
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВременнаяТаблица.Сотрудник КАК Сотрудник,
	|	ВременнаяТаблица.ПодразделениеОрганизации КАК Подразделение
	|ПОМЕСТИТЬ ВТСотрудники
	|ИЗ
	|	&ВременнаяТаблица КАК ВременнаяТаблица
	|ИНДЕКСИРОВАТЬ ПО Сотрудник";

	Запрос.Выполнить();
    	
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВременнаяТаблица.Сотрудник КАК Сотрудник,
	|	ВременнаяТаблица.Подразделение КАК Подразделение,
	|	НачисленияОрганизацийПоказатели.Показатель,
	|	НачисленияОрганизацийПоказатели.НомерСтроки
	|ПОМЕСТИТЬ ВТПоказатели
	|ИЗ
	|	ПланВидовРасчета." + ?(ОсновноеНачисление,"ОсновныеНачисленияОрганизаций.Показатели", "ДополнительныеНачисленияОрганизаций.Показатели") + " КАК НачисленияОрганизацийПоказатели
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСотрудники КАК ВременнаяТаблица
	|		ПО Истина
	|ГДЕ
	|	НачисленияОрганизацийПоказатели.Ссылка = &ПарамВидРасчета";
	
	
	Запрос.Выполнить();
	
	// таблица ВТЗначенияПоказателей
	// 
	// Поля:
	//   Сотрудник
	//   Показатель - введенный пользователем показатель
	//   Значение - значение показателя
	//

	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВЫБОР КОГДА ЗначенияПоказателейСхемМотивации.Сотрудник = &ПарамПустойСторудник ТОГДА Работники.Сотрудник ИНАЧЕ ЕСТЬNULL(ЗначенияПоказателейСхемМотивации.Сотрудник,Работники.Сотрудник) КОНЕЦ КАК Сотрудник,
	|	Работники.ПодразделениеОрганизации КАК Подразделение,
	|	ЗначенияПоказателейСхемМотивации.Показатель КАК Показатель,
	|	ВЫБОР
	|		КОГДА ЗначенияПоказателейСхемМотивации.Показатель.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейСхемМотивации.Денежный)
	|			ТОГДА ВЫБОР
	|					КОГДА ЗначенияПоказателейСхемМотивации.Валюта В (&парамВалютаРегламентУчета, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))
	|						ТОГДА
	|							ЗначенияПоказателейСхемМотивации.Значение
	|					ИНАЧЕ ЗначенияПоказателейСхемМотивации.Значение * ЕСТЬNULL(Валюты.Курс / Валюты.Кратность, 0)
	|				КОНЕЦ
	|	ИНАЧЕ 
	|			ЗначенияПоказателейСхемМотивации.Значение
	|	КОНЕЦ 
	|			КАК Значение
	|ПОМЕСТИТЬ ВТЗначенияПоказателей
	|ИЗ
	|		(ВЫБРАТЬ
	|			Ежемесячно.Организация,
	|			Ежемесячно.Подразделение КАК Подразделение,
	|			Ежемесячно.Показатель КАК Показатель,
	|			Ежемесячно.Валюта КАК Валюта,
	|			Ежемесячно.Значение КАК Значение,
	|			Ежемесячно.Сотрудник КАК Сотрудник
	|		ИЗ
	|			РегистрСведений.ЗначенияПоказателейСхемМотивации КАК Ежемесячно
	|		ГДЕ
	|			Ежемесячно.Значение ЕСТЬ НЕ NULL 
	|			И Ежемесячно.Организация = &парамОрганизация
	|			И Ежемесячно.ПериодДействия >= &ПарамДатаНачала
	|			И Ежемесячно.ПериодДействия <= &ПарамДатаОкончания
	|		
	|		ОБЪЕДИНИТЬ
	|		
	|		ВЫБРАТЬ
	|			ПериодическиеЗначенияПоказателейСхемМотивацииСрезПоследних.Организация,
	|			ПериодическиеЗначенияПоказателейСхемМотивацииСрезПоследних.Подразделение,
	|			ПериодическиеЗначенияПоказателейСхемМотивацииСрезПоследних.Показатель,
	|			ПериодическиеЗначенияПоказателейСхемМотивацииСрезПоследних.Валюта,
	|			ПериодическиеЗначенияПоказателейСхемМотивацииСрезПоследних.Значение,
	|			NULL
	|		ИЗ
	|			РегистрСведений.ПериодическиеЗначенияПоказателейСхемМотивации.СрезПоследних(&ПарамДатаОкончания, Организация = &парамОрганизация) КАК ПериодическиеЗначенияПоказателейСхемМотивацииСрезПоследних) КАК ЗначенияПоказателейСхемМотивации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|				&ПарамДатаОкончания,
	|				Организация = &парамОрганизация
	|					И Сотрудник В (&ПарамСотрудники)) КАК Работники
	|		ПО (ВЫБОР
	|				КОГДА ЗначенияПоказателейСхемМотивации.Показатель.ВидПоказателя = ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейСхемМотивации.Индивидуальный)
	|						ИЛИ ЗначенияПоказателейСхемМотивации.Показатель.ВидПоказателя ЕСТЬ NULL 
	|					ТОГДА Работники.Сотрудник = ЗначенияПоказателейСхемМотивации.Сотрудник
	|				КОГДА ЗначенияПоказателейСхемМотивации.Показатель.ВидПоказателя = ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейСхемМотивации.ПоПодразделению)
	|					ТОГДА Работники.ПодразделениеОрганизации = ЗначенияПоказателейСхемМотивации.Подразделение
    |   			ИНАЧЕ ИСТИНА
	|			КОНЕЦ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК Валюты
	|		ПО (Валюты.Период = &ПарамДатаОкончания)
	|			И ЗначенияПоказателейСхемМотивации.Валюта = Валюты.Валюта
	|ГДЕ
	|	ЗначенияПоказателейСхемМотивации.Показатель В(ВЫБРАТЬ Показатель Из ВТПоказатели)
	|	И ЗначенияПоказателейСхемМотивации.Организация = &парамОрганизация
	|	И ЗначенияПоказателейСхемМотивации.Значение ЕСТЬ НЕ NULL 
	|	И ВЫБОР
	|			КОГДА ЗначенияПоказателейСхемМотивации.Показатель.ВидПоказателя = ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейСхемМотивации.Индивидуальный)
	|					ИЛИ ЗначенияПоказателейСхемМотивации.Показатель.ВидПоказателя ЕСТЬ NULL 
	|				ТОГДА ЗначенияПоказателейСхемМотивации.Сотрудник В (&ПарамСотрудники)
	|			КОГДА ЗначенияПоказателейСхемМотивации.Показатель.ВидПоказателя = ЗНАЧЕНИЕ(Перечисление.ВидыПоказателейСхемМотивации.ПоПодразделению)
	|				ТОГДА Работники.ПодразделениеОрганизации = ЗначенияПоказателейСхемМотивации.Подразделение
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Показатель";
	Запрос.Выполнить();
	
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОсновныеНачисленияОрганизацийПоказатели.Показатель,
	|	ОсновныеНачисленияОрганизацийПоказатели.Ссылка.ЗачетОтработанногоВремени,
	|	ОсновныеНачисленияОрганизацийПоказатели.Ссылка,
	|	ОсновныеНачисленияОрганизацийПоказатели.НомерСтроки,
	|	ОсновныеНачисленияОрганизацийПоказатели.Ссылка.СпособРасчета
	|ПОМЕСТИТЬ ВТПоказателиИзПВР
	|ИЗ
	|	ПланВидовРасчета.ОсновныеНачисленияОрганизаций.Показатели КАК ОсновныеНачисленияОрганизацийПоказатели
	|ГДЕ
	|	ОсновныеНачисленияОрганизацийПоказатели.Показатель В (ВЫБРАТЬ Показатель Из ВТПоказатели)
	|				И (ОсновныеНачисленияОрганизацийПоказатели.Показатель.ВозможностьИзменения = ЗНАЧЕНИЕ(Перечисление.ИзменениеПоказателейСхемМотивации.ИзменяетсяПриРасчете)
	|					ИЛИ ОсновныеНачисленияОрганизацийПоказатели.Показатель.ВозможностьИзменения = ЗНАЧЕНИЕ(Перечисление.ИзменениеПоказателейСхемМотивации.НеИзменяется))
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	NULL,
	|	ИСТИНА,
	|	ОсновныеНачисленияОрганизаций.Ссылка,
	|	NULL,
	|	ОсновныеНачисленияОрганизаций.СпособРасчета
	|ИЗ
	|	ПланВидовРасчета.ОсновныеНачисленияОрганизаций КАК ОсновныеНачисленияОрганизаций
	|ГДЕ
	|	ОсновныеНачисленияОрганизаций.Ссылка.ЗачетОтработанногоВремени 
	|	ИЛИ ОсновныеНачисленияОрганизаций.СпособРасчета В (ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоМесячнойТарифнойСтавкеПоДням), ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоМесячнойТарифнойСтавкеПоЧасам))
	|	ИЛИ ОсновныеНачисленияОрганизаций.СпособРасчета В (ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоЧасовойТарифнойСтавке), ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.СдельныйЗаработок))
	|	
	|";
	
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ РАЗРЕШЕННЫЕ
	|	Работники.Сотрудник КАК Сотрудник,
	|	Работники.ПодразделениеОрганизации КАК Подразделение,
	|	Начисления.ВидРасчета.ЗачетОтработанногоВремени КАК ЗачетОтработанногоВремени,
	|	Начисления.ВидРасчета.СпособРасчета КАК СпособРасчета,
	|	Показатели1.Показатель КАК ППоказатель1,
	|	Показатели2.Показатель КАК ППоказатель2,
	|	Показатели3.Показатель КАК ППоказатель3,
	|	Показатели4.Показатель КАК ППоказатель4,
	|	Показатели5.Показатель КАК ППоказатель5,
	|	Показатели6.Показатель КАК ППоказатель6,
	|	ВЫБОР
	|		КОГДА Показатели1.Показатель.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейСхемМотивации.Денежный)
	|			ТОГДА ВЫБОР
	|					КОГДА Начисления.Валюта1 В (&парамВалютаРегламентУчета, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))
	|						ТОГДА Начисления.Показатель1
	|					ИНАЧЕ Начисления.Показатель1 * ЕСТЬNULL(Валюты1.Курс / Валюты1.Кратность, 0)
	|				КОНЕЦ
	|		КОГДА Показатели1.Показатель.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейСхемМотивации.ТарифныйРазряд)
	|			ТОГДА ТарифныеРазряды1.Размер
	|		ИНАЧЕ Начисления.Показатель1
	|	КОНЕЦ КАК Показатель1,
	|	ВЫБОР КОГДА Показатели2.Показатель ЕСТЬ НЕ NULL ТОГДА
	|		ВЫБОР
	|			КОГДА Показатели2.Показатель.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейСхемМотивации.Денежный)
	|				ТОГДА ВЫБОР
	|						КОГДА Начисления.Валюта2 В (&парамВалютаРегламентУчета, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))
	|							ТОГДА Начисления.Показатель2
	|						ИНАЧЕ Начисления.Показатель2 * ЕСТЬNULL(Валюты2.Курс / Валюты2.Кратность, 0)
	|					КОНЕЦ
	|			КОГДА Показатели2.Показатель.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейСхемМотивации.ТарифныйРазряд)
	|				ТОГДА ТарифныеРазряды2.Размер
	|			ИНАЧЕ Начисления.Показатель2
	|		КОНЕЦ
	|	КОНЕЦ КАК Показатель2,
	|	ВЫБОР КОГДА Показатели3.Показатель ЕСТЬ НЕ NULL ТОГДА
	|		ВЫБОР
	|			КОГДА Показатели3.Показатель.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейСхемМотивации.Денежный)
	|				ТОГДА ВЫБОР
	|						КОГДА Начисления.Валюта3 В (&парамВалютаРегламентУчета, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))
	|							ТОГДА Начисления.Показатель3
	|						ИНАЧЕ Начисления.Показатель3 * ЕСТЬNULL(Валюты2.Курс / Валюты2.Кратность, 0)
	|					КОНЕЦ
	|			КОГДА Показатели3.Показатель.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейСхемМотивации.ТарифныйРазряд)
	|				ТОГДА ТарифныеРазряды3.Размер
	|			ИНАЧЕ Начисления.Показатель3
	|		КОНЕЦ
	|	КОНЕЦ КАК Показатель3,
	|	ВЫБОР КОГДА Показатели4.Показатель ЕСТЬ НЕ NULL ТОГДА
	|		ВЫБОР
	|			КОГДА Показатели4.Показатель.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейСхемМотивации.Денежный)
	|				ТОГДА ВЫБОР
	|						КОГДА Начисления.Валюта4 В (&парамВалютаРегламентУчета, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))
	|							ТОГДА Начисления.Показатель4
	|						ИНАЧЕ Начисления.Показатель4 * ЕСТЬNULL(Валюты4.Курс / Валюты4.Кратность, 0)
	|					КОНЕЦ
	|			КОГДА Показатели4.Показатель.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейСхемМотивации.ТарифныйРазряд)
	|				ТОГДА ТарифныеРазряды4.Размер
	|			ИНАЧЕ Начисления.Показатель4
	|		КОНЕЦ
	|	КОНЕЦ КАК Показатель4,
	|	ВЫБОР КОГДА Показатели5.Показатель ЕСТЬ НЕ NULL ТОГДА
	|		ВЫБОР
	|			КОГДА Показатели5.Показатель.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейСхемМотивации.Денежный)
	|				ТОГДА ВЫБОР
	|						КОГДА Начисления.Валюта5 В (&парамВалютаРегламентУчета, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))
	|							ТОГДА Начисления.Показатель5
	|						ИНАЧЕ Начисления.Показатель5 * ЕСТЬNULL(Валюты5.Курс / Валюты5.Кратность, 0)
	|					КОНЕЦ
	|			КОГДА Показатели5.Показатель.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейСхемМотивации.ТарифныйРазряд)
	|				ТОГДА ТарифныеРазряды5.Размер
	|			ИНАЧЕ Начисления.Показатель5
	|		КОНЕЦ
	|	КОНЕЦ КАК Показатель5,
	|	ВЫБОР КОГДА Показатели6.Показатель ЕСТЬ НЕ NULL ТОГДА
	|		ВЫБОР
	|			КОГДА Показатели6.Показатель.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейСхемМотивации.Денежный)
	|				ТОГДА ВЫБОР
	|						КОГДА Начисления.Валюта6 В (&парамВалютаРегламентУчета, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка))
	|							ТОГДА Начисления.Показатель6
	|						ИНАЧЕ Начисления.Показатель6 * ЕСТЬNULL(Валюты6.Курс / Валюты6.Кратность, 0)
	|					КОНЕЦ
	|			КОГДА Показатели6.Показатель.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейСхемМотивации.ТарифныйРазряд)
	|				ТОГДА ТарифныеРазряды6.Размер
	|			ИНАЧЕ Начисления.Показатель6
	|		КОНЕЦ
	|	КОНЕЦ КАК Показатель6
	|ПОМЕСТИТЬ ВТПлановыеНачисления
	|ИЗ 
	|	РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(
	|				&ПарамПериодРегистрации,
	|				Организация = &ПарамОрганизация
	|					И Сотрудник В (&ПарамСотрудники) И (ВидРасчета В (ВЫБРАТЬ Ссылка Из ВТПоказателиИзПВР))) КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|				&ПарамДатаОкончания,
	|				Организация = &парамОрганизация
	|					И Сотрудник В (&ПарамСотрудники)) КАК Работники
	|		ПО (Работники.Сотрудник = Начисления.Сотрудник)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК Валюты1
	|		ПО (Валюты1.Период = &ПарамДатаОкончания)
	|			И Начисления.Валюта1 = Валюты1.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК Валюты2
	|		ПО (Валюты2.Период = &ПарамДатаОкончания)
	|			И Начисления.Валюта2 = Валюты2.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК Валюты3
	|		ПО (Валюты3.Период = &ПарамДатаОкончания)
	|			И Начисления.Валюта3 = Валюты3.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК Валюты4
	|		ПО (Валюты4.Период = &ПарамДатаОкончания)
	|			И Начисления.Валюта4 = Валюты4.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК Валюты5
	|		ПО (Валюты5.Период = &ПарамДатаОкончания)
	|			И Начисления.Валюта5 = Валюты5.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалютДляРасчетовСПерсоналом КАК Валюты6
	|		ПО (Валюты6.Период = &ПарамДатаОкончания)
	|			И Начисления.Валюта6 = Валюты6.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмерТарифныхСтавок.СрезПоследних(&ПарамДатаОкончания, ) КАК ТарифныеРазряды1
	|		ПО Начисления.ТарифныйРазряд1 = ТарифныеРазряды1.ТарифныйРазряд
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмерТарифныхСтавок.СрезПоследних(&ПарамДатаОкончания, ) КАК ТарифныеРазряды2
	|		ПО Начисления.ТарифныйРазряд2 = ТарифныеРазряды2.ТарифныйРазряд
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмерТарифныхСтавок.СрезПоследних(&ПарамДатаОкончания, ) КАК ТарифныеРазряды3
	|		ПО Начисления.ТарифныйРазряд3 = ТарифныеРазряды3.ТарифныйРазряд
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмерТарифныхСтавок.СрезПоследних(&ПарамДатаОкончания, ) КАК ТарифныеРазряды4
	|		ПО Начисления.ТарифныйРазряд4 = ТарифныеРазряды4.ТарифныйРазряд
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмерТарифныхСтавок.СрезПоследних(&ПарамДатаОкончания, ) КАК ТарифныеРазряды5
	|		ПО Начисления.ТарифныйРазряд5 = ТарифныеРазряды5.ТарифныйРазряд
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РазмерТарифныхСтавок.СрезПоследних(&ПарамДатаОкончания, ) КАК ТарифныеРазряды6
	|		ПО Начисления.ТарифныйРазряд6 = ТарифныеРазряды6.ТарифныйРазряд
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоказателиИзПВР КАК Показатели1
	|			ПО Показатели1.Ссылка = Начисления.ВидРасчета
	|			И Показатели1.НомерСтроки = 1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоказателиИзПВР КАК Показатели2
	|			ПО Показатели2.Ссылка = Начисления.ВидРасчета
	|			И Показатели2.НомерСтроки = 2
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоказателиИзПВР КАК Показатели3
	|			ПО Показатели3.Ссылка = Начисления.ВидРасчета
	|			И Показатели3.НомерСтроки = 3
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоказателиИзПВР КАК Показатели4
	|			ПО Показатели4.Ссылка = Начисления.ВидРасчета
	|			И Показатели4.НомерСтроки = 4
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоказателиИзПВР КАК Показатели5
	|			ПО Показатели5.Ссылка = Начисления.ВидРасчета
	|			И Показатели5.НомерСтроки = 5
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоказателиИзПВР КАК Показатели6
	|			ПО Показатели6.Ссылка = Начисления.ВидРасчета
	|			И Показатели6.НомерСтроки = 6
	|ГДЕ
	|	Начисления.Показатель1 <> 0 ИЛИ Начисления.Показатель2 <> 0 ИЛИ Начисления.Показатель3 <> 0 ИЛИ Начисления.Показатель4 <> 0 ИЛИ Начисления.Показатель5 <> 0 ИЛИ Начисления.Показатель6 <> 0 ";
	
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	&ПарамВидРасчета КАК ВидРасчета,
	|	&ПарамДатаНачала КАК ДатаНачала,
	|	&ПарамДатаОкончания КАК ДатаОкончания,
	|	&ПарамДатаНачала КАК ДатаНачалаСобытия,
	|	ИСТИНА КАК Авторасчет,";
	Если Не ОсновноеНачисление Тогда
		Запрос.Текст = Запрос.Текст + "
		|	&ПарамКодВычета КАК КодВычета,";
	КонецЕсли;
	Запрос.Текст = Запрос.Текст + "
	|	Показатели.Сотрудник,
	|	Показатели.Подразделение КАК ПодразделениеОрганизации,
	|	СУММА(ВЫБОР
	|			КОГДА Показатели.НомерСтроки = 1
	|				ТОГДА ЗначенияПлановыхИФактическихПоказателей.ЗначениеПоказателя
	|		КОНЕЦ) КАК Показатель1,
	|	СУММА(ВЫБОР
	|			КОГДА Показатели.НомерСтроки = 2
	|				ТОГДА ЗначенияПлановыхИФактическихПоказателей.ЗначениеПоказателя
	|		КОНЕЦ) КАК Показатель2,
	|	СУММА(ВЫБОР
	|			КОГДА Показатели.НомерСтроки = 3
	|				ТОГДА ЗначенияПлановыхИФактическихПоказателей.ЗначениеПоказателя
	|		КОНЕЦ) КАК Показатель3,
	|	СУММА(ВЫБОР
	|			КОГДА Показатели.НомерСтроки = 4
	|				ТОГДА ЗначенияПлановыхИФактическихПоказателей.ЗначениеПоказателя
	|		КОНЕЦ) КАК Показатель4,
	|	СУММА(ВЫБОР
	|			КОГДА Показатели.НомерСтроки = 5
	|				ТОГДА ЗначенияПлановыхИФактическихПоказателей.ЗначениеПоказателя
	|		КОНЕЦ) КАК Показатель5,
	|	СУММА(ВЫБОР
	|			КОГДА Показатели.НомерСтроки = 6
	|				ТОГДА ЗначенияПлановыхИФактическихПоказателей.ЗначениеПоказателя
	|		КОНЕЦ) КАК Показатель6
	|ИЗ
	|	ВТПоказатели КАК Показатели
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ПлановыеНачисления.Сотрудник КАК Сотрудник,
	|			ПлановыеНачисления.Подразделение КАК Подразделение,
	|			ПлановыеНачисления.ЗачетОтработанногоВремени КАК ЗачетОтработанногоВремени,
	|			ПлановыеНачисления.СпособРасчета КАК СпособРасчета,
	|			ПлановыеНачисления.ППоказатель1 КАК Показатель,
	|			ПлановыеНачисления.Показатель1 КАК ЗначениеПоказателя
	|		ИЗ
	|			ВТПлановыеНачисления КАК ПлановыеНачисления
	|		
	|		ОБЪЕДИНИТЬ
	|		
	|		ВЫБРАТЬ
	|			ПлановыеНачисления.Сотрудник,
	|			ПлановыеНачисления.Подразделение КАК Подразделение,
	|			ЛОЖЬ,
	|			NULL,
	|			ПлановыеНачисления.ППоказатель2,
	|			ПлановыеНачисления.Показатель2
	|		ИЗ
	|			ВТПлановыеНачисления КАК ПлановыеНачисления
	|		
	|		ОБЪЕДИНИТЬ
	|		
	|		ВЫБРАТЬ
	|			ПлановыеНачисления.Сотрудник,
	|			ПлановыеНачисления.Подразделение КАК Подразделение,
	|			ЛОЖЬ,
	|			NULL,
	|			ПлановыеНачисления.ППоказатель3,
	|			ПлановыеНачисления.Показатель3
	|		ИЗ
	|			ВТПлановыеНачисления КАК ПлановыеНачисления
	|		
	|		ОБЪЕДИНИТЬ
	|		
	|		ВЫБРАТЬ
	|			ПлановыеНачисления.Сотрудник,
	|			ПлановыеНачисления.Подразделение КАК Подразделение,
	|			ЛОЖЬ,
	|			NULL,
	|			ПлановыеНачисления.ППоказатель4,
	|			ПлановыеНачисления.Показатель4
	|		ИЗ
	|			ВТПлановыеНачисления КАК ПлановыеНачисления
	|		
	|		ОБЪЕДИНИТЬ
	|		
	|		ВЫБРАТЬ
	|			ПлановыеНачисления.Сотрудник,
	|			ПлановыеНачисления.Подразделение КАК Подразделение,
	|			ЛОЖЬ,
	|			NULL,
	|			ПлановыеНачисления.ППоказатель5,
	|			ПлановыеНачисления.Показатель5
	|		ИЗ
	|			ВТПлановыеНачисления КАК ПлановыеНачисления
	|		
	|		ОБЪЕДИНИТЬ
	|		
	|		ВЫБРАТЬ
	|			ПлановыеНачисления.Сотрудник,
	|			ПлановыеНачисления.Подразделение КАК Подразделение,
	|			ЛОЖЬ,
	|			NULL,
	|			ПлановыеНачисления.ППоказатель6,
	|			ПлановыеНачисления.Показатель6
	|		ИЗ
	|			ВТПлановыеНачисления КАК ПлановыеНачисления
	|		
	|		ОБЪЕДИНИТЬ
	|		
	|		ВЫБРАТЬ
	|			ЗначенияПоказателей.Сотрудник,
	|			ЗначенияПоказателей.Подразделение КАК Подразделение,
	|			ЛОЖЬ,
	|			NULL,
	|			ЗначенияПоказателей.Показатель,
	|			ЗначенияПоказателей.Значение
	|		ИЗ
	|			ВТЗначенияПоказателей КАК ЗначенияПоказателей) КАК ЗначенияПлановыхИФактическихПоказателей
	|		ПО (ЗначенияПлановыхИФактическихПоказателей.Показатель = Показатели.Показатель И ЗначенияПлановыхИФактическихПоказателей.ЗначениеПоказателя <> 0
	|				ИЛИ Показатели.Показатель.ТарифнаяСтавка
	|					И ЗначенияПлановыхИФактическихПоказателей.ЗачетОтработанногоВремени
	|				ИЛИ Показатели.Показатель = ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ТарифнаяСтавкаМесячная)
	|					И ЗначенияПлановыхИФактическихПоказателей.СпособРасчета В (ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоМесячнойТарифнойСтавкеПоДням), ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоМесячнойТарифнойСтавкеПоЧасам))
	|				ИЛИ Показатели.Показатель = ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ТарифнаяСтавкаДневная)
	|					И ЗначенияПлановыхИФактическихПоказателей.СпособРасчета = ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоДневнойТарифнойСтавке)
	|				ИЛИ Показатели.Показатель = ЗНАЧЕНИЕ(Справочник.ПоказателиСхемМотивации.ТарифнаяСтавкаЧасовая)
	|					И ЗначенияПлановыхИФактическихПоказателей.СпособРасчета В (ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.ПоЧасовойТарифнойСтавке), ЗНАЧЕНИЕ(Перечисление.СпособыРасчетаОплатыТруда.СдельныйЗаработок)))	
	|			И (Показатели.Показатель.ВидПоказателя = Значение(Перечисление.ВидыПоказателейСхемМотивации.Индивидуальный) И Показатели.Сотрудник = ЗначенияПлановыхИФактическихПоказателей.Сотрудник
	|			ИЛИ Показатели.Показатель.ВидПоказателя = Значение(Перечисление.ВидыПоказателейСхемМотивации.ПоПодразделению) И Показатели.Подразделение = ЗначенияПлановыхИФактическихПоказателей.Подразделение И Показатели.Сотрудник = ЗначенияПлановыхИФактическихПоказателей.Сотрудник
	|			ИЛИ Показатели.Показатель.ВидПоказателя = Значение(Перечисление.ВидыПоказателейСхемМотивации.Общий) И Показатели.Сотрудник = ЗначенияПлановыхИФактическихПоказателей.Сотрудник)
	|ГДЕ Показатели.Сотрудник ЕСТЬ НЕ NULL
	|СГРУППИРОВАТЬ ПО
	|	Показатели.Сотрудник,
	|	Показатели.Подразделение";
	
	Если ОсновноеНачисление Тогда
		ОсновныеНачисления.Загрузить(Запрос.Выполнить().Выгрузить());
	Иначе
		ДополнительныеНачисления.Загрузить(Запрос.Выполнить().Выгрузить());
	КонецЕсли;
	
КонецПроцедуры //Автозаполнение

// Выполняет расчет основных и дополнительных записей начислений
//
Процедура РассчитатьНачисления() Экспорт
	
	Отказ = Ложь;
	
	// Перечитаем объект и соберем данные для заполнения наборов записей регистров
	НачатьТранзакцию();
	Прочитать();
	ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке().Выбрать();
	// позиционируем выборку
	ВыборкаПоШапкеДокумента.Следующий();
	ВыборкаПоНачислениям = СформироватьЗапросПоНачисления(ВыборкаПоШапкеДокумента).Выбрать();
	ВыборкаПоДопНачислениям = СформироватьЗапросПоДопНачисления().Выбрать();
	ЗафиксироватьТранзакцию();

	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);
	
	// создадим наборы записей для выполнения движений
	НаборОсновныеНачисления = РегистрыРасчета.ОсновныеНачисленияРаботниковОрганизаций.СоздатьНаборЗаписей();
	НаборОсновныеНачисления.Отбор.Регистратор.Значение = Ссылка;
	 	
	НаборДопНачисления = РегистрыРасчета.ДополнительныеНачисленияРаботниковОрганизаций.СоздатьНаборЗаписей();
	НаборДопНачисления.Отбор.Регистратор.Значение = Ссылка;
	
	// Если почасовое отклонение, то записываем движения в регистр ВнутрисменноеВремяРаботниковОрганизаций
	НаборЗаписейРабочееВремя = РегистрыНакопления.ВнутрисменноеВремяРаботниковОрганизаций.СоздатьНаборЗаписей();
	НаборЗаписейРабочееВремя.Отбор.Регистратор.Значение = Ссылка;
	
	// получим реквизиты табличной части
	Пока ВыборкаПоНачислениям.Следующий() Цикл
		ПроверитьЗаполнениеСтрокиНачисления(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям, Отказ, Заголовок);
		Если НЕ Отказ Тогда
			// Заполним записи в наборах записей регистров
			ДобавитьСтрокуОсновныхНачислений(ВыборкаПоНачислениям, ВыборкаПоШапкеДокумента, НаборОсновныеНачисления);
			Если ВыборкаПоНачислениям.СторнируемыйДокумент <> ВыборкаПоШапкеДокумента.ПерерассчитываемыйДокумент Тогда
				ДобавитьСтрокуРабочегоВремени(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям, НаборЗаписейРабочееВремя);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ВыборкаПоДопНачислениям.Количество() > 0 Тогда
		//мСоответствиеДоходовИВычетов = ПроведениеРасчетов.ПолучитьСоответствиеДоходовИВычетов();
		Пока ВыборкаПоДопНачислениям.Следующий() Цикл
			ПроверитьЗаполнениеСтрокиДополнительныеНачисления(ВыборкаПоШапкеДокумента, ВыборкаПоДопНачислениям, Отказ, Заголовок);
			Если НЕ Отказ Тогда
				// Заполним записи в наборах записей регистров
				ДобавитьСтрокуДопНачислений(ВыборкаПоДопНачислениям, ВыборкаПоШапкеДокумента, НаборДопНачисления);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	НаборЗаписейРабочееВремя.Записать();

	// Получим дополнительные записи, имеющие признак сторно, которые необходимо добавить 
	// в текущий набор для того, чтобы в результате сохранения получился максимальный 
	// фактический период действия
	ТаблицаСторноЗаписей = ПроведениеРасчетов.ПолучитьТаблицуСторноЗаписей(НаборОсновныеНачисления.ПолучитьДополнение());
	Для каждого СтрокаСторно Из ТаблицаСторноЗаписей Цикл
		// Заполним записи в наборе записей регистра
		// и в табличной части
		ДобавитьСтрокуСторно(НаборОсновныеНачисления, ОсновныеНачисления, СтрокаСторно);
	КонецЦикла;
	
	// перед тем как начать расчет, сформированные движения надо записать - 
	// по ним чуть позже будем получать дополнительные данные для расчетов

	// рассчитываем записи
	// при этом передаем не только набор записей регистра расчета, но и набор записей регисра 
	// накопления со сведениями об отработанном времени
	ПроведениеРасчетов.РассчитатьЗаписиРегистраРасчета("ОсновныеНачисленияРаботниковОрганизаций", НаборОсновныеНачисления, Неопределено, , ВыборкаПоШапкеДокумента.ГоловнаяОрганизация, ВыборкаПоШапкеДокумента.ОбособленноеПодразделение, ОсновныеНачисления);
	// запишем результат расчета для расчета доп. начислений
	НаборОсновныеНачисления.Записать(Истина, Истина);
	
	ПроведениеРасчетов.РассчитатьЗаписиРегистраРасчета("ДополнительныеНачисленияРаботниковОрганизаций", НаборДопНачисления, Неопределено, , ВыборкаПоШапкеДокумента.ГоловнаяОрганизация, ВыборкаПоШапкеДокумента.ОбособленноеПодразделение, ДополнительныеНачисления);

	//ПроведениеРасчетов.СкорректироватьНормируемыеВычетыПоНДФЛ(ЭтотОбъект);	
	
	// Удаляем движения
	НаборОсновныеНачисления.Очистить();
	НаборОсновныеНачисления.Записать();
	
	НаборДопНачисления.Очистить();
	НаборДопНачисления.Записать();
	
	НаборЗаписейРабочееВремя.Очистить();
	НаборЗаписейРабочееВремя.Записать();

КонецПроцедуры // РассчитатьНачисления()

// Выполняет расчет вычетов по НДФЛ согласно ст. 217 и 221 НК РФ
//
Процедура РассчитатьВычеты() Экспорт

	// проверим можно ли что-то делать с документом
	ОписаниеПричиныОтказа = "";
	Если ПроведениеРасчетов.ДокументНельзяИзменятьЗаднимЧислом(Ссылка, ОписаниеПричиныОтказа) Тогда
		ВызватьИсключение(ОписаниеПричиныОтказа);
	КонецЕсли;
	
	Отказ = Ложь;
	
	// Получим реквизиты шапки из запроса
	ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке().Выбрать();
	// позиционируем выборку
	ВыборкаПоШапкеДокумента.Следующий();
	ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	РезультатЗапроса = СформироватьЗапросПоДопНачисления();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	ВыборкаПоДопНачислениям = РезультатЗапроса.Выбрать();
	//мСоответствиеДоходовИВычетов = ПроведениеРасчетов.ПолучитьСоответствиеДоходовИВычетов();
	Пока ВыборкаПоДопНачислениям.Следующий() Цикл
		ПроверитьЗаполнениеСтрокиДополнительныеНачисления(ВыборкаПоШапкеДокумента, ВыборкаПоДопНачислениям, Отказ);
	КонецЦикла;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("парамОрганизация",					ВыборкаПоШапкеДокумента.ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("парамРегистратор",					Ссылка);
	Запрос.УстановитьПараметр("парамПериодРегистрации",				НачалоМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамКонецМесяцаНалоговогоПериода",	КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамНачалоГода",					НачалоГода(ПериодРегистрации));
	Запрос.УстановитьПараметр("парамКонецГода",						КонецГода(ПериодРегистрации));
	
	// прочитаем т.ч. документа и добавим в каждую строку Физлицо сотрудника и КодДоходаНДФЛ начисления
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Работники.НомерСтроки,
	|	Работники.Сотрудник,
	|	Работники.Сотрудник.Физлицо КАК Физлицо,
	|	Работники.ВидРасчета,
	|	Работники.ВидРасчета.КодДоходаНДФЛ КАК КодДохода,
	|	Работники.ДатаНачала,
	|	Работники.ДатаОкончания,
	|	Работники.Показатель1,
	|	Работники.Показатель2,
	|	Работники.Показатель3,
	|	Работники.Показатель4,
	|	Работники.Показатель5,
	|	Работники.Показатель6,
	|	Работники.Результат,
	|	Работники.ПодразделениеОрганизации,
	|	Работники.Сторно,
	|	Работники.КодВычета,
	|	Работники.СуммаВычета,
	|	Работники.Авторасчет,
	|	Работники.ОплаченоДнейЧасов,
	|	Работники.СторнируемыйДокумент
	|ПОМЕСТИТЬ ВТРаботники
	|ИЗ
	|	Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ДополнительныеНачисления КАК Работники
	|ГДЕ
	|	Работники.Ссылка = &парамРегистратор
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Физлицо,
	|	КодДохода";
	Запрос.Выполнить();
	
	// рассчитаем нормативный вычет по авторским вознаграждениям по ст. 221 НК РФ
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НачисленияРаботников.Сотрудник,
	|	НачисленияРаботников.Физлицо,
	|	НачисленияРаботников.Авторасчет,
	|	НачисленияРаботников.ВидРасчета,
	|	НачисленияРаботников.ДатаНачала,
	|	НачисленияРаботников.ДатаОкончания,
	|	НачисленияРаботников.Показатель1,
	|	НачисленияРаботников.Показатель2,
	|	НачисленияРаботников.Показатель3,
	|	НачисленияРаботников.Показатель4,
	|	НачисленияРаботников.Показатель5,
	|	НачисленияРаботников.Показатель6,
	|	НачисленияРаботников.Результат,
	|	НачисленияРаботников.ОплаченоДнейЧасов,
	|	НачисленияРаботников.ПодразделениеОрганизации,
	|	НачисленияРаботников.Сторно,
	|	НачисленияРаботников.СторнируемыйДокумент,
	|	НачисленияРаботников.КодВычета,
	|	ВЫБОР
	|		КОГДА НачисленияРаботников.Авторасчет
	|				И НачисленияРаботников.КодВычета = ЗНАЧЕНИЕ(Справочник.ВычетыНДФЛ.Код405)
	|				И НеРезиденты.ФизЛицо ЕСТЬ NULL 
	|			ТОГДА НачисленияРаботников.Результат * СведенияОСкидках.НормативЗатрат / 100
	|		ИНАЧЕ НачисленияРаботников.СуммаВычета
	|	КОНЕЦ КАК СуммаВычета
	|ИЗ
	|	ВТРаботники КАК НачисленияРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛВычетыПоДоходам.СрезПоследних(&парамНачалоГода, ) КАК СведенияОСкидках
	|		ПО НачисленияРаботников.КодДохода = СведенияОСкидках.КодДохода
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ГражданствоФизЛицСрезПоследних.ФизЛицо КАК ФизЛицо
	|		ИЗ
	|			РегистрСведений.ГражданствоФизЛиц.СрезПоследних(
	|					&парамКонецГода,
	|					Физлицо В
	|						(ВЫБРАТЬ
	|							Работники.Физлицо
	|						ИЗ
	|							ВТРаботники КАК Работники)) КАК ГражданствоФизЛицСрезПоследних
	|		ГДЕ
	|			ГражданствоФизЛицСрезПоследних.НеЯвляетсяНалоговымРезидентомРФ) КАК НеРезиденты
	|		ПО НачисленияРаботников.Физлицо = НеРезиденты.ФизЛицо
	|
	|УПОРЯДОЧИТЬ ПО
	|	НачисленияРаботников.НомерСтроки";
	
	ДополнительныеНачисления.Загрузить(Запрос.Выполнить().Выгрузить());
	
	//ДоходыП28Ст217 = Новый Массив;
	//ДоходыП28Ст217.Добавить(Справочники.ДоходыНДФЛ.Код2761);
	//ДоходыП28Ст217.Добавить(Справочники.ДоходыНДФЛ.Код2720);
	//ДоходыП28Ст217.Добавить(Справочники.ДоходыНДФЛ.Код2730);
	//ДоходыП28Ст217.Добавить(Справочники.ДоходыНДФЛ.Код2740);
	//ДоходыП28Ст217.Добавить(Справочники.ДоходыНДФЛ.Код2760);
	//ДоходыП28Ст217.Добавить(Справочники.ДоходыНДФЛ.Код2770);
	//ДоходыП28Ст217.Добавить(Справочники.ДоходыНДФЛ.Код2790);
	//Запрос.УстановитьПараметр("ДоходыП28Ст217", ДоходыП28Ст217);
	
	// получим "еще неизрасходованные" размеры вычета, предусмотренного общей суммой на налоговый период
	// п. 28 ст. 217 НК РФ
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	НачисленияРаботников.Физлицо КАК Физлицо,
	//vvv
	|	НАЧАЛОПЕРИОДА(НачисленияРаботников.ДатаНачала, ГОД) КАК ГодНачисления,
	//
	|	НачисленияРаботников.КодВычета КАК КодВычета,
	|	ВЫБОР
	|		КОГДА СведенияОСкидках.ГодовойВычет ЕСТЬ NULL 
	|			ТОГДА 0
	|		КОГДА СведенияОСкидках.ГодовойВычет > ЕСТЬNULL(НДФЛОбороты.СуммаВычетаОборот, 0)
	|			ТОГДА СведенияОСкидках.ГодовойВычет - ЕСТЬNULL(НДФЛОбороты.СуммаВычетаОборот, 0)
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВычета
	|ИЗ
	|	ВТРаботники КАК НачисленияРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛСведенияОДоходах.Обороты(
	|				&парамНачалоГода,
	|				&парамКонецМесяцаНалоговогоПериода,
	|				,
	|				ПериодРегистрации <= &парамПериодРегистрации
	|					И Организация = &парамОрганизация
	//|					И КодДохода В (&ДоходыП28Ст217)
	|					И Физлицо В
	|						(ВЫБРАТЬ
	|							Работники.Физлицо
	|						ИЗ
	|							ВТРаботники КАК Работники)) КАК НДФЛОбороты
	|		ПО НачисленияРаботников.КодДохода = НДФЛОбороты.КодДохода
	|			И НачисленияРаботников.Физлицо = НДФЛОбороты.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛВычетыПоДоходам.СрезПоследних(&парамНачалоГода, ) КАК СведенияОСкидках
	|		ПО НачисленияРаботников.КодДохода = СведенияОСкидках.КодДохода
	|ГДЕ
	|	НачисленияРаботников.Авторасчет
	|	И (НЕ НачисленияРаботников.Сторно)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Физлицо,
	|	КодВычета";
	//vvv перепишем запрос, т.к. в противном случае, когда есть переходящая на след. год мат. помощь (начислена в декабре за январь) - мат. помощь
	//буд. периода учитывалась в составе льготы текущего года.
	Если РегистрыСведений.УчетнаяПолитикаПоРасчетуЗарплатыОрганизаций.Получить(Новый Структура("Организация", ОбщегоНазначения.ГоловнаяОрганизация(Организация))).РассчитыватьЛьготуПоПНПоПериодуДействия ТОгда
		Запрос.УстановитьПараметр("ДатаДеноминации",ОбщегоНазначения.ПолучитьДатуДеноминации());
		Запрос.Текст =
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НачисленияРаботников.Физлицо КАК Физлицо,
		|	НачисленияРаботников.ГодНачисления КАК ГодНачисления,
		|	НачисленияРаботников.КодВычета КАК КодВычета,
		|	НачисленияРаботников.СуммаВычетаОборот,
		|	СведенияОСкидках.ГодовойВычет,
		|	ВЫБОР
		|		КОГДА СведенияОСкидках.ГодовойВычет ЕСТЬ NULL 
		|			ТОГДА 0
		|		КОГДА СведенияОСкидках.ГодовойВычет > ЕСТЬNULL(НачисленияРаботников.СуммаВычетаОборот, 0)
		|			ТОГДА СведенияОСкидках.ГодовойВычет - ЕСТЬNULL(НачисленияРаботников.СуммаВычетаОборот, 0)
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаВычета
		|ИЗ
		|	(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|		НачисленияРаботников.Физлицо КАК Физлицо,
		|		НАЧАЛОПЕРИОДА(НачисленияРаботников.ДатаНачала, ГОД) КАК ГодНачисления,
		|		НачисленияРаботников.КодВычета КАК КодВычета,
		|		НачисленияРаботников.КодДохода КАК КодДохода,
		|		СУММА(НДФЛСведенияОДоходах.СуммаВычета/ВЫБОР КОГДА НДФЛСведенияОДоходах.ПериодРегистрации < &ДатаДеноминации ТОГДА 10000 ИНАЧЕ 1 КОНЕЦ) КАК СуммаВычетаОборот
		|	ИЗ
		|		ВТРаботники КАК НачисленияРаботников
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НДФЛСведенияОДоходах КАК НДФЛСведенияОДоходах
		|			ПО НачисленияРаботников.КодДохода = НДФЛСведенияОДоходах.КодДохода
		|				И НачисленияРаботников.Физлицо = НДФЛСведенияОДоходах.ФизЛицо
		|				И (НДФЛСведенияОДоходах.Период МЕЖДУ НАЧАЛОПЕРИОДА(НачисленияРаботников.ДатаНачала, ГОД) И КОНЕЦПЕРИОДА(НачисленияРаботников.ДатаНачала, ГОД))
		//vvv надо быть осторожным с этой строкой, т.к. в случе пересчета, необходимо пересчитывать все льготы, которые были начислены после этого документа
		|				И (НДФЛСведенияОДоходах.Период <= КОНЕЦПЕРИОДА(НачисленияРаботников.ДатаОкончания, МЕСЯЦ))
		|	ГДЕ
		|		НачисленияРаботников.Авторасчет
		|		И НЕ НачисленияРаботников.Сторно
		|	
		|	СГРУППИРОВАТЬ ПО
		|		НачисленияРаботников.Физлицо,
		|		НАЧАЛОПЕРИОДА(НачисленияРаботников.ДатаНачала, ГОД),
		|		НачисленияРаботников.КодВычета,
		|		НачисленияРаботников.КодДохода) КАК НачисленияРаботников
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НДФЛВычетыПоДоходам КАК СведенияОСкидках
		|		ПО НачисленияРаботников.КодДохода = СведенияОСкидках.КодДохода
		|		И НачисленияРаботников.ГодНачисления = СведенияОСкидках.Период
		|
		|УПОРЯДОЧИТЬ ПО
		|	Физлицо,
		|	КодВычета";
	КонецЕсли;
	//
	ТаблицаВычетов = Запрос.Выполнить().Выгрузить();
	//vvv добавим год
	//ТаблицаВычетов.Индексы.Добавить("Физлицо,КодВычета");
	ТаблицаВычетов.Индексы.Добавить("Физлицо,КодВычета,ГодНачисления");
	//
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НачисленияРаботников.Физлицо КАК Физлицо,
	//vvv
	|	НАЧАЛОПЕРИОДА(НачисленияРаботников.ДатаНачала, ГОД) КАК ГодНачисления,
	//
	|	НачисленияРаботников.КодВычета КАК КодВычета,
	|	НачисленияРаботников.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	ВТРаботники КАК НачисленияРаботников
	|ГДЕ
	|	НачисленияРаботников.Авторасчет
	|	И (НЕ НачисленияРаботников.Сторно)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Физлицо,
	//vvv
	|	ГодНачисления,
	//
	|	КодВычета,
	|	НомерСтроки";
	Выборка = Запрос.Выполнить().Выбрать();
	//vvv
	//СтруктураПоиска = Новый Структура("Физлицо,КодВычета");
	СтруктураПоиска = Новый Структура("Физлицо,КодВычета,ГодНачисления");
	//
	Пока Выборка.Следующий() Цикл
		СтрокаТЧ = ДополнительныеНачисления.Найти(Выборка.НомерСтроки,"НомерСтроки");
		Если СтрокаТЧ <> Неопределено Тогда
			ЗаполнитьЗначенияСвойств(СтруктураПоиска,Выборка);
			СтрокаВычета = ТаблицаВычетов.НайтиСтроки(СтруктураПоиска)[0];
			Если СтрокаВычета.СуммаВычета = 0 Тогда
				СтрокаТЧ.СуммаВычета = 0
			ИначеЕсли СтрокаТЧ.Результат > 0 Тогда
				СтрокаТЧ.СуммаВычета = Мин(СтрокаТЧ.Результат, СтрокаВычета.СуммаВычета);
				СтрокаВычета.СуммаВычета = Макс(0, СтрокаВычета.СуммаВычета - СтрокаТЧ.СуммаВычета);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // РассчитатьВычеты()

// Выполняет перерасчет по заданному списку физлиц
// Если передан пустой список физлиц - перерасчет не проводится
// Если в качестве списка физлиц передан не массив, а Неопределено - перерасчет по всем физлицам 
// регистратора по которым требуется перерасчет
//
// Параметры
// 	Физлица - список значений - ссылки на физлиц
// Возвращаемое значение
//  Нет
//
Процедура Перерассчитать(Физлица = Неопределено) Экспорт
	
	// проверим можно ли что-то делать с документом
	ОписаниеПричиныОтказа = "";
	Если ПроведениеРасчетов.ДокументНельзяИзменятьЗаднимЧислом(Ссылка, ОписаниеПричиныОтказа) Тогда
		ВызватьИсключение(ОписаниеПричиныОтказа);
	КонецЕсли;
	
	Если Не ПроведениеРасчетов.НеобходимостьПерерасчета(Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	// получим список перерассчитываемых физлиц
	ФизлицаСписок = Новый СписокЗначений;
	Если Физлица = Неопределено Тогда
		// таблица ФизлицаДляПерерасчета
		// Список физлиц по которым необходимо выполнить перерасчет
		// Поля:
		//		ФизЛицо
		// Описание:
		// Получает список неповторяющихся физлиц по которым есть записи
		// перерасчета в одной из таблиц перерасчета - по основным или дополнительным начислениям
		//
		ФизлицаДляПерерасчетаТекст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Перерасчет.ФизЛицо
		|ИЗ
		|	(ВЫБРАТЬ
		|		Перерасчет.ФизЛицо
		|	ИЗ
		|		РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций.ПерерасчетОсновныхНачислений КАК Перерасчет
		|	
		|	ГДЕ
		|		Перерасчет.ОбъектПерерасчета = &парамРегистратор
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Перерасчет.ФизЛицо
		|	ИЗ
		|		РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций.ПерерасчетДополнительныхНачислений КАК Перерасчет
		|	
		|	ГДЕ
		|		Перерасчет.ОбъектПерерасчета = &парамРегистратор) КАК Перерасчет";
		
		Запрос = Новый Запрос(ФизлицаДляПерерасчетаТекст);
		Запрос.УстановитьПараметр("парамРегистратор", Ссылка);
		ФизлицаСписок.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ФизЛицо"));
		
	Иначе
		ФизлицаСписок = Физлица;
		
	КонецЕсли;

	// если не перерассчитываем никого - возврат
	Если ФизлицаСписок.Количество()=0 Тогда
		Возврат;
	КонецЕсли;

	// читаем все данные в одной транзакции
	НачатьТранзакцию();
	Прочитать();
	ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке().Выбрать();
	// прочитать движения
	Движения.ОсновныеНачисленияРаботниковОрганизаций.Прочитать();
	Движения.ДополнительныеНачисленияРаботниковОрганизаций.Прочитать();
	Движения.НДФЛСведенияОДоходах.Прочитать();
	Движения.ВзаиморасчетыСРаботникамиОрганизаций.Прочитать();
	ЗафиксироватьТранзакцию();
	
	// позиционируем выборку
	ВыборкаПоШапкеДокумента.Следующий();
	
	////////////////////////////////////////////////////////////////////////
	// Основные, дополнительные начисления и сведения о доходах НДФЛ
	
	// сначала удалим сведения о доходах НДФЛ по перерассчитываемым физлицам
	НДФЛСведенияОДоходах = Движения.НДФЛСведенияОДоходах;
	ПоследнееДвижение = НДФЛСведенияОДоходах.Количество()-1;
	Для Сч = 0 По ПоследнееДвижение Цикл
		// удалим записи по всем физлицам полученного списка
		// обходим в обратном порядке
		Если ФизлицаСписок.НайтиПоЗначению(НДФЛСведенияОДоходах[ПоследнееДвижение - Сч].Физлицо) <> Неопределено Тогда
			НДФЛСведенияОДоходах.Удалить(ПоследнееДвижение - Сч);
		КонецЕсли;
	КонецЦикла;
	
	// перерассчитываем записи начислений
	// при этом передаем не только набор записей регистра расчета, но и набор записей регистра 
	// накопления со сведениями об отработанном времени
	// а также таблицу значений в которую будет заполнять движения для НДФЛСведенияОДоходах
	ДвиженияНДФЛСведенияОДоходах = Движения.НДФЛСведенияОДоходах.Выгрузить();// фактически - создаем структуру таблицы значений
	
	ПроведениеРасчетов.ПереРассчитатьЗаписиРегистраРасчета("ОсновныеНачисленияРаботниковОрганизаций", Движения.ОсновныеНачисленияРаботниковОрганизаций, Физлица, ДвиженияНДФЛСведенияОДоходах, , ВыборкаПоШапкеДокумента.ГоловнаяОрганизация, ВыборкаПоШапкеДокумента.ОбособленноеПодразделение, ОсновныеНачисления);
	// запишем без выполнения пересчета фактического периода действия
	Движения.ОсновныеНачисленияРаботниковОрганизаций.Записать(Истина, Истина);
	
	ПроведениеРасчетов.ПереРассчитатьЗаписиРегистраРасчета("ДополнительныеНачисленияРаботниковОрганизаций", Движения.ДополнительныеНачисленияРаботниковОрганизаций, Физлица, ДвиженияНДФЛСведенияОДоходах,, ВыборкаПоШапкеДокумента.ГоловнаяОрганизация, ВыборкаПоШапкеДокумента.ОбособленноеПодразделение, ДополнительныеНачисления);
	// запишем набор
	Движения.ДополнительныеНачисленияРаботниковОрганизаций.Записать(Истина, Истина);
	
	// запишем движения по доходам НДФЛ, которые формируются при ПереРассчитатьЗаписиРегистраРасчета
	ПроведениеРасчетов.СвернутьДвиженияНДФЛСведенияОДоходах(ДвиженияНДФЛСведенияОДоходах);
	Движения.НДФЛСведенияОДоходах.Загрузить(ДвиженияНДФЛСведенияОДоходах);
	Движения.НДФЛСведенияОДоходах.Записать();
	
	// записываем документ перед вызовом СформироватьВзаиморасчетыСРаботниками
	Записать();
	
	////////////////////////////////////////////////////////////////////////
	// взаиморасчеты с работниками
	
	// сначала удалим сведения о взаиморасчетах с работниками
	ВзаиморасчетыСРаботникамиОрганизаций = Движения.ВзаиморасчетыСРаботникамиОрганизаций;
	ПоследнееДвижение = ВзаиморасчетыСРаботникамиОрганизаций.Количество()-1;
	Для Сч = 0 По ПоследнееДвижение Цикл
		// удалим записи по всем физлицам полученного списка
		// обходим в обратном порядке
		Если ФизлицаСписок.НайтиПоЗначению(ВзаиморасчетыСРаботникамиОрганизаций[ПоследнееДвижение - Сч].Физлицо) <> Неопределено Тогда
			ВзаиморасчетыСРаботникамиОрганизаций.Удалить(ПоследнееДвижение - Сч);
		КонецЕсли;
	КонецЦикла;
	
	// ведется ли учет задолженности в разрезе периодов возникновения задолженности
	УчетЗадолженностиПоМесяцам	= ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "УчетЗадолженностиПоМесяцам");
	
	// теперь сформируем начисления к выплате по начислениям документа для перерассчитываемых работников
	СформироватьВзаиморасчетыСРаботниками(ВыборкаПоШапкеДокумента, Движения.ВзаиморасчетыСРаботникамиОрганизаций, УчетЗадолженностиПоМесяцам, Истина, Физлица);
	Движения.ВзаиморасчетыСРаботникамиОрганизаций.Записать();
	
	////////////////////////////////////////////////////////////////////////
	// Удалим записи перерасчета по которым выполнен перерасчет
	ПроведениеРасчетов.УдалитьСведенияОПерерасчетеДокумента(Ссылка);
	
КонецПроцедуры // Перерассчитать()

// Заполняет документ по перерассчитываемому документу
//
Процедура ЗаполнитьПоПерерассчитываемомуДокументу(ИсходныйДокумент, Знач Сотрудники = Неопределено) Экспорт
	
	ПерерассчитываемыйДокумент = ИсходныйДокумент.Ссылка;
 	ЗаполнитьЗначенияСвойств(ЭтотОбъект,ИсходныйДокумент,,
		"Проведен, Номер, Дата, ПометкаУдаления, ПерерассчитываемыйДокумент, ПериодРегистрации, Комментарий, Ответственный"); // кроме указанных
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ПерерассчитываемыйДокумент", ПерерассчитываемыйДокумент);
	//vvv
	Запрос.УстановитьПараметр("ДатаДеноминации",ОбщегоНазначения.ПолучитьДатуДеноминации());
	//
	
	Если Сотрудники = "ПоДаннымПерерасчета" Тогда
		
		Исправления = ПроведениеРасчетов.ПолучитьДокументИсправление(ПерерассчитываемыйДокумент, "КритерийОтбора"); 
		Запрос.УстановитьПараметр("Исправления", Исправления);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Начисления.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ ВТСотрудникиИсправлений
		|ИЗ
		|	Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ОсновныеНачисления КАК Начисления
		|ГДЕ
		|	Начисления.Ссылка В(&Исправления)
		|	И Начисления.СторнируемыйДокумент = &ПерерассчитываемыйДокумент
		|	И Начисления.Сторно
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	Начисления.Сотрудник
		|ИЗ
		|	Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ДополнительныеНачисления КАК Начисления
		|ГДЕ
		|	Начисления.Ссылка В(&Исправления)
		|	И Начисления.СторнируемыйДокумент = &ПерерассчитываемыйДокумент
		|	И Начисления.Сторно
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник";
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	СотрудникиОрганизаций.Сотрудник
		|ИЗ
		|	Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ОсновныеНачисления КАК СотрудникиОрганизаций
		|ГДЕ
		|	СотрудникиОрганизаций.Ссылка = &ПерерассчитываемыйДокумент
		|	И (НЕ СотрудникиОрганизаций.Сотрудник В
		|				(ВЫБРАТЬ
		|					Начисления.Сотрудник
		|				ИЗ
		|					ВТСотрудникиИсправлений КАК Начисления))
		|	И СотрудникиОрганизаций.Физлицо В
		|			(ВЫБРАТЬ
		|				Перерасчет.ФизЛицо
		|			ИЗ
		|				РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций.ПерерасчетОсновныхНачислений КАК Перерасчет
		|			ГДЕ
		|				Перерасчет.ОбъектПерерасчета = &ПерерассчитываемыйДокумент)
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СотрудникиОрганизаций.Сотрудник
		|ИЗ
		|	Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ДополнительныеНачисления КАК СотрудникиОрганизаций
		|ГДЕ
		|	СотрудникиОрганизаций.Ссылка = &ПерерассчитываемыйДокумент
		|	И (НЕ СотрудникиОрганизаций.Сотрудник В
		|				(ВЫБРАТЬ
		|					Начисления.Сотрудник
		|				ИЗ
		|					ВТСотрудникиИсправлений КАК Начисления))
		|	И СотрудникиОрганизаций.Физлицо В
		|			(ВЫБРАТЬ
		|				Перерасчет.ФизЛицо
		|			ИЗ
		|				РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций.ПерерасчетДополнительныхНачислений КАК Перерасчет
		|			ГДЕ
		|				Перерасчет.ОбъектПерерасчета = &ПерерассчитываемыйДокумент)";
		Сотрудники = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Сотрудник");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Сотрудники", Сотрудники);
	Запрос.УстановитьПараметр("ПоВсемСотрудникам", Сотрудники = Неопределено);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.Сотрудник,
	|	Начисления.Сотрудник.Физлицо КАК Физлицо,
	|	Начисления.ВидРасчета,
	|	Начисления.ДатаНачала,
	|	Начисления.ДатаОкончания,
	|	Начисления.Показатель1,
	|	Начисления.Показатель2,
	|	Начисления.Показатель3,
	|	Начисления.Показатель4,
	|	Начисления.Показатель5,
	|	Начисления.Показатель6,
	|	Начисления.ПодразделениеОрганизации,
	//vvv
	|	Начисления.Должность,
	//|	-Начисления.Результат - ЕСТЬNULL(СУММА(СторноНачисления.Результат), 0) КАК Результат,
	|	-Начисления.Результат / ВЫБОР КОГДА Начисления.Ссылка.ПериодРегистрации<&ДатаДеноминации ТОГДА 10000 ИНАЧЕ 1 КОНЕЦ - ЕСТЬNULL(СУММА(СторноНачисления.Результат), 0) КАК Результат,
	//
	|	-Начисления.ОтработаноДней - ЕСТЬNULL(СУММА(СторноНачисления.ОтработаноДней), 0) КАК ОтработаноДней,
	|	-Начисления.ОтработаноЧасов - ЕСТЬNULL(СУММА(СторноНачисления.ОтработаноЧасов), 0) КАК ОтработаноЧасов,
	|	-Начисления.ОтработаноДнейПоПятидневке - ЕСТЬNULL(СУММА(СторноНачисления.ОтработаноДнейПоПятидневке), 0) КАК ОтработаноДнейПоПятидневке,
	|	-Начисления.НормаДней - ЕСТЬNULL(СУММА(СторноНачисления.НормаДней), 0) КАК НормаДней,
	|	-Начисления.НормаЧасов - ЕСТЬNULL(СУММА(СторноНачисления.НормаЧасов), 0) КАК НормаЧасов,
	|	-Начисления.НормаДнейПоПятидневке - ЕСТЬNULL(СУММА(СторноНачисления.НормаДнейПоПятидневке), 0) КАК НормаДнейПоПятидневке,
	|	-Начисления.ОплаченоДнейЧасов - ЕСТЬNULL(СУММА(СторноНачисления.ОплаченоДнейЧасов), 0) КАК ОплаченоДнейЧасов,
	|	-Начисления.ОтработаноЧасовПоПятидневке - ЕСТЬNULL(СУММА(СторноНачисления.ОтработаноЧасовПоПятидневке), 0) КАК ОтработаноЧасовПоПятидневке,
	|	-Начисления.НормаЧасовПоПятидневке - ЕСТЬNULL(СУММА(СторноНачисления.НормаЧасовПоПятидневке), 0) КАК НормаЧасовПоПятидневке,
	|	Начисления.ДатаНачалаСобытия,
	|	Начисления.ОплачиватьЧасов,
	|	ИСТИНА КАК Сторно,
	|	ЛОЖЬ КАК Авторасчет,
	|	Начисления.Ссылка КАК СторнируемыйДокумент,
	|	Начисления.НомерСтроки КАК НомерСтроки
	|ИЗ
	|	Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ОсновныеНачисления КАК Начисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций КАК СторноНачисления
	|		ПО Начисления.Сотрудник = СторноНачисления.Сотрудник
	|			И Начисления.ВидРасчета = СторноНачисления.ВидРасчета
	|			И (СторноНачисления.ПериодДействияНачало МЕЖДУ Начисления.ДатаНачала И Начисления.ДатаОкончания)
	|			И (СторноНачисления.ПериодДействияКонец МЕЖДУ Начисления.ДатаНачала И Начисления.ДатаОкончания)
	|			И (СторноНачисления.ПериодРегистрации > Начисления.Ссылка.ПериодРегистрации)
	|			И (СторноНачисления.ПериодРегистрации < &ПериодРегистрации)
	|			И (СторноНачисления.СторнируемыйДокумент = Начисления.Ссылка)
	|			И (СторноНачисления.Сторно)
	|ГДЕ
	|	Начисления.Ссылка = &ПерерассчитываемыйДокумент
	|	И (НЕ Начисления.Сторно)
	|	И (&ПоВсемСотрудникам
	|			ИЛИ Начисления.Сотрудник В (&Сотрудники))
	|	И Начисления.Ссылка.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	Начисления.Сотрудник,
	|	Начисления.Сотрудник.Физлицо,
	|	Начисления.ВидРасчета,
	|	Начисления.ДатаНачала,
	|	Начисления.ДатаОкончания,
	|	Начисления.Показатель1,
	|	Начисления.Показатель2,
	|	Начисления.Показатель3,
	|	Начисления.Показатель4,
	|	Начисления.Показатель5,
	|	Начисления.Показатель6,
	|	Начисления.ПодразделениеОрганизации,
	//vvv
	|	Начисления.Должность,
	//
	|	Начисления.ДатаНачалаСобытия,
	|	Начисления.ОплачиватьЧасов,
	|	Начисления.НомерСтроки,
	|	Начисления.Результат,
	|	Начисления.ОтработаноДней,
	|	Начисления.ОтработаноЧасов,
	|	Начисления.ОтработаноДнейПоПятидневке,
	|	Начисления.НормаДней,
	|	Начисления.НормаЧасов,
	|	Начисления.НормаДнейПоПятидневке,
	|	Начисления.ОплаченоДнейЧасов,
	|	Начисления.ОтработаноЧасовПоПятидневке,
	|	Начисления.НормаЧасовПоПятидневке,
	|	Начисления.Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Начисления.Сотрудник,
	|	Начисления.Сотрудник.Физлицо,
	|	Начисления.ВидРасчета,
	|	Начисления.ДатаНачала,
	|	Начисления.ДатаОкончания,
	|	Начисления.Показатель1,
	|	Начисления.Показатель2,
	|	Начисления.Показатель3,
	|	Начисления.Показатель4,
	|	Начисления.Показатель5,
	|	Начисления.Показатель6,
	|	Начисления.ПодразделениеОрганизации,
	//vvv
	|	Начисления.Должность,
	//
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	0,
	|	Начисления.ДатаНачалаСобытия,
	|	Начисления.ОплачиватьЧасов,
	|	ЛОЖЬ,
	|	ИСТИНА,
	|	NULL,
	|	Начисления.НомерСтроки
	|ИЗ
	|	Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ОсновныеНачисления КАК Начисления
	|ГДЕ
	|	Начисления.Ссылка = &ПерерассчитываемыйДокумент
	|	И (НЕ Начисления.Сторно)
	|	И (&ПоВсемСотрудникам
	|			ИЛИ Начисления.Сотрудник В (&Сотрудники))
	|	И Начисления.Ссылка.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сторно УБЫВ,
	|	НомерСтроки";
	
	ОсновныеНачисления.Загрузить(Запрос.Выполнить().Выгрузить());
	
	// выполним запрос к ДополнительныеНачисления
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Начисления.Сотрудник,
	|	Начисления.Сотрудник.Физлицо КАК Физлицо,
	|	Начисления.ВидРасчета,
	|	Начисления.ДатаНачала,
	|	Начисления.ДатаОкончания,
	|	Начисления.Показатель1,
	|	Начисления.Показатель2,
	|	Начисления.Показатель3,
	|	Начисления.Показатель4,
	|	Начисления.Показатель5,
	|	Начисления.Показатель6,
	|	Начисления.ПодразделениеОрганизации,
	//vvv
	|	Начисления.Должность,
	//|	-Начисления.Результат КАК Результат,
	|	-Начисления.Результат / ВЫБОР КОГДА Начисления.Ссылка.ПериодРегистрации<&ДатаДеноминации ТОГДА 10000 ИНАЧЕ 1 КОНЕЦ КАК Результат,
	//
	|	-Начисления.ОплаченоДнейЧасов КАК ОплаченоДнейЧасов,
	|	Начисления.КодВычета,
	//vvv
	//|	 -Начисления.СуммаВычета КАК СуммаВычета,
	|	 -Начисления.СуммаВычета / ВЫБОР КОГДА Начисления.Ссылка.ПериодРегистрации<&ДатаДеноминации ТОГДА 10000 ИНАЧЕ 1 КОНЕЦ КАК СуммаВычета,
	//
	|	ИСТИНА КАК Сторно,
	|	ЛОЖЬ КАК Авторасчет,
	|	Начисления.НомерСтроки КАК НомерСтроки,
	|	Начисления.ДокументОснование,
	|	Начисления.Ссылка КАК СторнируемыйДокумент
	|ИЗ
	|	Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ДополнительныеНачисления КАК Начисления
	|ГДЕ
	|	Начисления.Ссылка = &ПерерассчитываемыйДокумент
	|	И (НЕ Начисления.Сторно)
	|	И (&ПоВсемСотрудникам
	|			ИЛИ Начисления.Сотрудник В (&Сотрудники))
	|	И Начисления.Ссылка.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Начисления.Сотрудник,
	|	Начисления.Сотрудник.Физлицо,
	|	Начисления.ВидРасчета,
	|	Начисления.ДатаНачала,
	|	Начисления.ДатаОкончания,
	|	Начисления.Показатель1,
	|	Начисления.Показатель2,
	|	Начисления.Показатель3,
	|	Начисления.Показатель4,
	|	Начисления.Показатель5,
	|	Начисления.Показатель6,
	|	Начисления.ПодразделениеОрганизации,
	//vvv
	|	Начисления.Должность,
	//
	|	0,
	|	0,
	|	Начисления.КодВычета,
	|	0,
	|	ЛОЖЬ,
	|	ИСТИНА,
	|	Начисления.НомерСтроки,
	|	Начисления.ДокументОснование,
	|	NULL
	|ИЗ
	|	Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ДополнительныеНачисления КАК Начисления
	|ГДЕ
	|	Начисления.Ссылка = &ПерерассчитываемыйДокумент
	|	И (НЕ Начисления.Сторно)
	|	И (&ПоВсемСотрудникам
	|			ИЛИ Начисления.Сотрудник В (&Сотрудники))
	|	И Начисления.Ссылка.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	Сторно УБЫВ,
	|	НомерСтроки";
	
	ДополнительныеНачисления.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры // ЗаполнитьПоПерерассчитываемомуДокументу()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// Добавляет строку сторно начислений в движения и табличную часть документа
//
Процедура ДобавитьСтрокуСторно(Движения, ТабличнаяЧасть, Строка)

	// новая запись движений
	Движение = Движения.Добавить();

    ЗаполнитьЗначенияСвойств(Движение,Строка,"БазовыйПериодНачало,БазовыйПериодКонец,ВидРасчета,Сотрудник,Физлицо,Организация," + 
									"ГрафикРаботы,ГрафикРаботыНорма,ВидУчетаВремени,Показатель1,Показатель2,Показатель3,Показатель4,Показатель5,Показатель6,ПодразделениеОрганизации,ОбособленноеПодразделение," + 
									"ПериодРасчетаСреднегоЗаработкаНачало,ПериодРасчетаСреднегоЗаработкаОкончание,СторнируемыйДокумент,ДатаНачалаСобытия");
	// Свойства
	Движение.ПериодРегистрации				= Строка.ПериодРегистрацииСторно;
	Движение.ПериодДействияНачало			= Строка.ПериодДействияНачалоСторно;
	Движение.ПериодДействияКонец			= Строка.ПериодДействияКонецСторно;
	Движение.Сторно							= Истина;
	Движение.Авторасчет						= Истина;

	// новая строка табличной части сторно начисления
	СтрокаТабличнойЧасти = ТабличнаяЧасть.Добавить();
    ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти,Строка,"ВидРасчета,Сотрудник,Физлицо,ПодразделениеОрганизации,ДатаНачалаСобытия," + 
									"Показатель1,Показатель2,Показатель3,Показатель4,Показатель5,Показатель6,СторнируемыйДокумент,Авторасчет");
	// Свойства
	
	СтрокаТабличнойЧасти.ДатаНачала						= Строка.ПериодДействияНачалоСторно;
	СтрокаТабличнойЧасти.ДатаОкончания					= Строка.ПериодДействияКонецСторно;
	СтрокаТабличнойЧасти.Сторно							= Истина;
	
КонецПроцедуры // ДобавитьСтрокуСторно()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует запрос по шапке документа
//
// Параметры:
//	Режим - режим проведения
//
// Возвращаемое значение:
//	Результат запроса
//
Функция СформироватьЗапросПоШапке()

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",			Ссылка);
	Запрос.УстановитьПараметр("парамПустаяОрганизация",	Справочники.Организации.ПустаяСсылка());
	Запрос.УстановитьПараметр("парамРегистрацияДляЧастиСмены", Перечисления.СпособыРегистрацииВремени.РегистрацияДляЧастиСмены);

	Запрос.Текст =
	"ВЫБРАТЬ
	|	РегистрацияРазовыхНачисленийРаботниковОрганизации.Дата,
	|	РегистрацияРазовыхНачисленийРаботниковОрганизации.ПериодРегистрации,
	|	РегистрацияРазовыхНачисленийРаботниковОрганизации.Организация,
	//vvv
	|	РегистрацияРазовыхНачисленийРаботниковОрганизации.СпособОтраженияВБухучете,
	//
	|	РегистрацияРазовыхНачисленийРаботниковОрганизации.ПерерассчитываемыйДокумент КАК ПерерассчитываемыйДокумент,
	|	РегистрацияРазовыхНачисленийРаботниковОрганизации.ПерерассчитываемыйДокумент.ПериодРегистрации КАК ПериодПерерасчета,
	|	РегистрацияРазовыхНачисленийРаботниковОрганизации.ПерерассчитываемыйДокумент.Организация КАК ОрганизацияПерерасчета,
	|	ВЫБОР
	|		КОГДА РегистрацияРазовыхНачисленийРаботниковОрганизации.Организация.ГоловнаяОрганизация = &парамПустаяОрганизация
	|			ТОГДА РегистрацияРазовыхНачисленийРаботниковОрганизации.Организация
	|		ИНАЧЕ РегистрацияРазовыхНачисленийРаботниковОрганизации.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	РегистрацияРазовыхНачисленийРаботниковОрганизации.Организация КАК ОбособленноеПодразделение,
	|	РегистрацияРазовыхНачисленийРаботниковОрганизации.Ссылка,
	|	ВЫБОР
	|		КОГДА РегистрацияРазовыхНачисленийРаботниковОрганизации.СпособРегистрацииВремени = &парамРегистрацияДляЧастиСмены
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК РегистрируютсяПочасовыеОтклонения
	|ИЗ
	|	Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций КАК РегистрацияРазовыхНачисленийРаботниковОрганизации
	|ГДЕ
	|	РегистрацияРазовыхНачисленийРаботниковОрганизации.Ссылка = &ДокументСсылка";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоШапке()

// Формирует запрос по таблице "Начисления" документа
//
// Параметры:
//	Режим - режим проведения
//
// Возвращаемое значение:
//	Результат запроса.
//
Функция СформироватьЗапросПоНачисления(ВыборкаПоШапкеДокумента)

	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;

	ГоловнаяОрганизация = ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
	СписокСтруктурныхПодразделений = ПроцедурыУправленияПерсоналом.ПолучитьСписокОбособленныхПодразделенийОрганизации(ГоловнаяОрганизация);
	СписокСтруктурныхПодразделений.Добавить(ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("СписокСтруктурныхПодразделений",	СписокСтруктурныхПодразделений);

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",		Ссылка);
	
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",ГоловнаяОрганизация);
	МассивВидовВремени = Новый СписокЗначений;
	МассивВидовВремени.Добавить(Перечисления.ВидыВремени.ЧасовоеНеотработанное);
	МассивВидовВремени.Добавить(Перечисления.ВидыВремени.ЧасовоеОтработанноеВПределахНормы);
	Запрос.УстановитьПараметр("ПочасовоеНачисление",МассивВидовВремени);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтрокиДокумента.Ссылка,
	|	СтрокиДокумента.НомерСтроки,
	|	СтрокиДокумента.Сотрудник КАК Сотрудник,
	|	СтрокиДокумента.Физлицо,
	|	СтрокиДокумента.ВидРасчета,
	|	НАЧАЛОПЕРИОДА(СтрокиДокумента.ДатаНачала, МЕСЯЦ) КАК ПериодДействия,
	|	СтрокиДокумента.ДатаНачала,
	|	СтрокиДокумента.ДатаОкончания,
	|	СтрокиДокумента.Показатель1,
	|	СтрокиДокумента.Показатель2,
	|	СтрокиДокумента.Показатель3,
	|	СтрокиДокумента.Показатель4,
	|	СтрокиДокумента.Показатель5,
	|	СтрокиДокумента.Показатель6,
	|	СтрокиДокумента.Результат,
	|	СтрокиДокумента.ПодразделениеОрганизации,
	|	СтрокиДокумента.ОтработаноДней,
	|	СтрокиДокумента.ОтработаноЧасов,
	|	СтрокиДокумента.ОтработаноДнейПоПятидневке,
	|	СтрокиДокумента.НормаДней,
	|	СтрокиДокумента.НормаЧасов,
	|	СтрокиДокумента.НормаДнейПоПятидневке,
	|	СтрокиДокумента.ДатаНачалаСобытия,
	|	СтрокиДокумента.Сторно,
	//vvv
	|	СтрокиДокумента.ППС,
	|	СтрокиДокумента.СпособОтраженияВБухучете,
	|	СтрокиДокумента.Резерв,
	|	СтрокиДокумента.ПроцентРезерва,
	|	СтрокиДокумента.ИзРезерва,
	|	СтрокиДокумента.СуммаРезерва,
	|	СтрокиДокумента.Должность,
	//
	|	СтрокиДокумента.Авторасчет,
	|	СтрокиДокумента.ОплачиватьЧасов,
	|	СтрокиДокумента.ОплаченоДнейЧасов,
	|	СтрокиДокумента.ОтработаноЧасовПоПятидневке,
	|	СтрокиДокумента.НормаЧасовПоПятидневке,
	|	СтрокиДокумента.СторнируемыйДокумент,
	|	ВЫБОР
	|		КОГДА СтрокиДокумента.Сторно
	|				И СтрокиДокумента.СторнируемыйДокумент = СтрокиДокумента.Ссылка.ПерерассчитываемыйДокумент
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СтрокаИсправления
	|ПОМЕСТИТЬ ВТСтрокиНачислений
	|ИЗ
	|	Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ОсновныеНачисления КАК СтрокиДокумента
	|ГДЕ
	|	СтрокиДокумента.Ссылка = &ДокументСсылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Сотрудник,
	|	ПериодДействия";
	Запрос.Выполнить();
	
	// ВТГрафикиСотрудников
	//		таблица содержит номера строк документа с данными о графике указанного
	//		в этой строке сотрудника из рег-ра сведений РаботникиОрганизации
	// 
	//	Поля:
	//		НомерСтроки
	//      ГрафикРаботы
	//      ГрафикПолногоРабочегоВремени
	//      СуммированныйУчетРабочегоВремени
	// 
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПоследниеДаты.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизации.ПериодЗавершения <= ПоследниеДаты.ДатаНачалаСобытия
	|				И РаботникиОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизации.ГрафикРаботыЗавершения
	|		ИНАЧЕ РаботникиОрганизации.ГрафикРаботы
	|	КОНЕЦ КАК ГрафикРаботы,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизации.ПериодЗавершения <= ПоследниеДаты.ДатаНачалаСобытия
	|				И РаботникиОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизации.ГрафикРаботыЗавершения.ГрафикПолногоРабочегоВремени
	|		ИНАЧЕ РаботникиОрганизации.ГрафикРаботы.ГрафикПолногоРабочегоВремени
	|	КОНЕЦ КАК ГрафикПолногоРабочегоВремени,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизации.ПериодЗавершения <= ПоследниеДаты.ДатаНачалаСобытия
	|				И РаботникиОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА РаботникиОрганизации.ГрафикРаботыЗавершения.СуммированныйУчетРабочегоВремени
	|		ИНАЧЕ РаботникиОрганизации.ГрафикРаботы.СуммированныйУчетРабочегоВремени
	|	КОНЕЦ КАК СуммированныйУчетРабочегоВремени
	|ПОМЕСТИТЬ ВТГрафикиСотрудников
	|ИЗ
	|	(ВЫБРАТЬ
	|		МАКСИМУМ(РаботникиОрганизации.Период) КАК МаксПериод,
	|		СтрокиНачисления.НомерСтроки КАК НомерСтроки,
	|		СтрокиНачисления.Сотрудник КАК Сотрудник,
	|		СтрокиНачисления.ДатаНачалаСобытия КАК ДатаНачалаСобытия
	|	ИЗ
	|		ВТСтрокиНачислений КАК СтрокиНачисления
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|			ПО СтрокиНачисления.ДатаНачалаСобытия >= РаботникиОрганизации.Период
	|				И СтрокиНачисления.Сотрудник = РаботникиОрганизации.Сотрудник
	|	
	|	СГРУППИРОВАТЬ ПО
	|		СтрокиНачисления.НомерСтроки,
	|		СтрокиНачисления.Сотрудник,
	|		СтрокиНачисления.ДатаНачалаСобытия) КАК ПоследниеДаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|		ПО ПоследниеДаты.МаксПериод = РаботникиОрганизации.Период
	|			И ПоследниеДаты.Сотрудник = РаботникиОрганизации.Сотрудник
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки";
	Запрос.Выполнить();
	
	// ВТДанныеИндивидуальныхГрафиковИТабелей
	//		таблица содержит номера строк документа с сотрудниками, для которых введены 
	//		подробные документы использования времени: инд.графики и табели
	// 
	//	Поля:
	//		НомерСтроки
	//      План
	// 
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтрокиНачисления.НомерСтроки КАК НомерСтроки,
	|	ГрафикиРаботыПоВидамВремени.План КАК План
	|ПОМЕСТИТЬ ВТДанныеИндивидуальныхГрафиковИТабелей
	|ИЗ
	|	ВТСтрокиНачислений КАК СтрокиНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГрафикиРаботыПоВидамВремени КАК ГрафикиРаботыПоВидамВремени
	|		ПО СтрокиНачисления.Сотрудник = ГрафикиРаботыПоВидамВремени.ГрафикРаботы
	|			И СтрокиНачисления.ПериодДействия = ГрафикиРаботыПоВидамВремени.Месяц
	|ГДЕ
	|	ГрафикиРаботыПоВидамВремени.ГрафикРаботы ЕСТЬ НЕ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	План";
	Запрос.Выполнить();
	
	// СводныеИндивидуальныеГрафикиРаботы
	//		таблица содержит номера строк документа с сотрудниками, для которых введены сводные 
	//		данные об их графике
	// 
	//	Поля:
	//		НомерСтроки
	// 
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СписокРаботников.НомерСтроки КАК НомерСтроки
	|ПОМЕСТИТЬ ВТСводныеИндивидуальныеГрафикиРаботы
	|ИЗ
	|	ВТСтрокиНачислений КАК СписокРаботников
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СводныеИндивидуальныеГрафики КАК СводныеИндивидуальныеГрафики
	|		ПО СписокРаботников.Сотрудник = СводныеИндивидуальныеГрафики.Сотрудник
	|			И СписокРаботников.ПериодДействия = СводныеИндивидуальныеГрафики.Месяц
	|ГДЕ
	|	СводныеИндивидуальныеГрафики.Сотрудник ЕСТЬ НЕ NULL 
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки";
	Запрос.Выполнить();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТЧНачисления.НомерСтроки КАК НомерСтроки,
	|	ТЧНачисления.Сотрудник,
	|	ТЧНачисления.Сотрудник.Наименование,
	|	ТЧНачисления.Сотрудник.Физлицо КАК Физлицо,
	|	ТЧНачисления.ДатаНачала КАК ПериодДействияНачало,
	|	ВЫБОР
	|		КОГДА ТЧНачисления.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА КОНЕЦПЕРИОДА(ТЧНачисления.ДатаОкончания, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	КОНЕЦ КАК ПериодДействияКонец,
	|	ВЫБОР
	|		КОГДА ТЧНачисления.ВидРасчета.ПериодДействияБазовый
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(ДОБАВИТЬКДАТЕ(ТЧНачисления.ДатаНачала, МЕСЯЦ, -1), МЕСЯЦ)
	|	КОНЕЦ КАК БазовыйПериодНачало,
	|	ВЫБОР
	|		КОГДА ТЧНачисления.ВидРасчета.ПериодДействияБазовый
	|			ТОГДА ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТЧНачисления.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА КОНЕЦПЕРИОДА(ДОБАВИТЬКДАТЕ(ТЧНачисления.ДатаОкончания, МЕСЯЦ, -1), МЕСЯЦ)
	|				ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			КОНЕЦ
	|	КОНЕЦ КАК БазовыйПериодКонец,
	|	ТЧНачисления.ВидРасчета КАК ВидРасчета,
	|	ТЧНачисления.ВидРасчета.СпособРасчета КАК СпособРасчета,
	|	ТЧНачисления.ВидРасчета.ВидВремени КАК ВидВремени,
	|	ТЧНачисления.ВидРасчета.ЗачетНормыВремени КАК ЗачетНормыВремени,
	|	ТЧНачисления.ВидРасчета.ЗачетОтработанногоВремени КАК ЗачетОтработанногоВремени,
	|	ТЧНачисления.ВидРасчета.КодДоходаНДФЛ КАК КодДоходаНДФЛ,
	|	ТЧНачисления.ВидРасчета.ОбозначениеВТабелеУчетаРабочегоВремени КАК ВидИспользованияРабочегоВремени,
	|	ЗНАЧЕНИЕ(Перечисление.ВариантыОбработкиЗаписиПриОтраженииВРеглУчете.ПустаяСсылка)КАК ВариантОбработкиЗаписиПриОтраженииВРеглУчете,
	|	ТЧНачисления.Показатель1,
	|	ТЧНачисления.Показатель2,
	|	ТЧНачисления.Показатель3,
	|	ТЧНачисления.Показатель4,
	|	ТЧНачисления.Показатель5,
	|	ТЧНачисления.Показатель6,
	|	ТЧНачисления.Результат,
	|	ТЧНачисления.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА ГрафикРаботыПоСотруднику.НомерСтроки ЕСТЬ NULL 
	|			ТОГДА ДанныеПоРаботникуНаДатуНачисления.ГрафикРаботы
	|		ИНАЧЕ ТЧНачисления.Сотрудник
	|	КОНЕЦ КАК ГрафикРаботы,
	|	ВЫБОР
			// если введен табель, но нет индивидуального графика, то норму времени надо считать по данным ""общего графика""
	|		КОГДА ТабелиУчетаВремениПоСотруднику.НомерСтроки ЕСТЬ НЕ NULL
	|				И ИндивидуальныеГрафикиРаботы.НомерСтроки ЕСТЬ NULL
	|				И СводныеИндивидуальныеГрафикиРаботы.НомерСтроки ЕСТЬ NULL 
	|			ТОГДА ДанныеПоРаботникуНаДатуНачисления.ГрафикРаботы
			// если у работника сокращенное рабочее время, а норма времени определяется по полному графику
			// и введен индивидуальный график, то норму времени надо считать по-прежнему по данным ""общего графика""
	|		КОГДА ДанныеПоРаботникуНаДатуНачисления.ГрафикПолногоРабочегоВремени <> ЗНАЧЕНИЕ(Справочник.ГрафикиРаботы.ПустаяСсылка)
	|				И (ИндивидуальныеГрафикиРаботы.НомерСтроки ЕСТЬ НЕ NULL
	|					ИЛИ СводныеИндивидуальныеГрафикиРаботы.НомерСтроки ЕСТЬ НЕ NULL )
	|			ТОГДА ДанныеПоРаботникуНаДатуНачисления.ГрафикПолногоРабочегоВремени
	|	КОНЕЦ КАК ГрафикРаботыНорма,
	|	ЕСТЬNULL(ДанныеПоРаботникуНаДатуНачисления.СуммированныйУчетРабочегоВремени, ЛОЖЬ) КАК СуммированныйУчетРабочегоВремени,
	|	ТЧНачисления.НормаДней,
	|	ТЧНачисления.НормаЧасов,
	|	ТЧНачисления.НормаДнейПоПятидневке,
	|	ТЧНачисления.ОплаченоДнейЧасов,
	|	ТЧНачисления.ОтработаноДней,
	|	ТЧНачисления.ОтработаноЧасов,
	|	ТЧНачисления.ОтработаноДнейПоПятидневке,
	|	ТЧНачисления.ОтработаноЧасовПоПятидневке,
	|	ТЧНачисления.НормаЧасовПоПятидневке,
	//vvv
	|	ТЧНачисления.ППС,
	|	ТЧНачисления.СпособОтраженияВБухучете,
	|	ТЧНачисления.Резерв,
	|	ТЧНачисления.ПроцентРезерва,
	|	ТЧНачисления.ИзРезерва,
	|	ТЧНачисления.СуммаРезерва,
	|	ТЧНачисления.Должность,
	//
	|	ТЧНачисления.Сторно,
	|	ТЧНачисления.ДатаНачалаСобытия КАК ДатаНачалаСобытия,
	|	ТЧНачисления.ОплачиватьЧасов КАК ОплачиватьЧасов,
	|	ТЧНачисления.СтрокаИсправления,
	|	ВЫБОР
	|		КОГДА ТЧНачисления.Сотрудник.Организация = &ГоловнаяОрганизация
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОшибкаНеСоответствиеСотрудникаИОрганизации,
	|	ВЫБОР
	|		КОГДА (НЕ ТЧНачисления.ПодразделениеОрганизации.Владелец В (&СписокСтруктурныхПодразделений))
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОшибкаПодразделениеНеПринадлежитОрганизации,
	|	ТЧНачисления.Авторасчет,
	|	ВЫБОР
	|		КОГДА ТЧНачисления.ВидРасчета.ВидВремени В (&ПочасовоеНачисление)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЯвляетсяПочасовымОтклонением,
	|	ВЫБОР
	|		КОГДА ТЧНачисления.Сторно
	|			ТОГДА ТЧНачисления.СторнируемыйДокумент
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СторнируемыйДокумент
	|ИЗ
	|	ВТСтрокиНачислений КАК ТЧНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТГрафикиСотрудников КАК ДанныеПоРаботникуНаДатуНачисления
	|		ПО ТЧНачисления.НомерСтроки = ДанныеПоРаботникуНаДатуНачисления.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ТЧНачисления.НомерСтроки КАК НомерСтроки
	|		ИЗ
	|			ВТДанныеИндивидуальныхГрафиковИТабелей КАК ТЧНачисления) КАК ГрафикРаботыПоСотруднику
	|		ПО ТЧНачисления.НомерСтроки = ГрафикРаботыПоСотруднику.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ТЧНачисления.НомерСтроки КАК НомерСтроки
	|		ИЗ
	|			ВТДанныеИндивидуальныхГрафиковИТабелей КАК ТЧНачисления
	|		ГДЕ
	|			(НЕ ТЧНачисления.План)) КАК ТабелиУчетаВремениПоСотруднику
	|		ПО ТЧНачисления.НомерСтроки = ТабелиУчетаВремениПоСотруднику.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	|			ТЧНачисления.НомерСтроки КАК НомерСтроки
	|		ИЗ
	|			ВТДанныеИндивидуальныхГрафиковИТабелей КАК ТЧНачисления
	|		ГДЕ
	|			ТЧНачисления.План) КАК ИндивидуальныеГрафикиРаботы
	|		ПО ТЧНачисления.НомерСтроки = ИндивидуальныеГрафикиРаботы.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТСводныеИндивидуальныеГрафикиРаботы КАК СводныеИндивидуальныеГрафикиРаботы
	|		ПО ТЧНачисления.НомерСтроки = СводныеИндивидуальныеГрафикиРаботы.НомерСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоНачисления()

// Формирует запрос по таблице "ДополнительныеНачисления" документа
//
// Параметры:
//	Нет.
//
// Возвращаемое значение:
//	Результат запроса.
//
Функция СформироватьЗапросПоДопНачисления()

	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",				Ссылка);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",		ОбщегоНазначения.ГоловнаяОрганизация(Организация));
	Запрос.УстановитьПараметр("НачалоПериодаРегистрации",	ПериодРегистрации);
	Запрос.УстановитьПараметр("КонецПериодаРегистрации",	КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("ДатаРегистрации",			?(Дата > КонецМесяца(ПериодРегистрации),КонецМесяца(ПериодРегистрации),?(Дата < ПериодРегистрации,ПериодРегистрации,Дата)));

	Запрос.Текст =
	"ВЫБРАТЬ
	|	СтрокиНачисления.ВидРасчета,
	|	СтрокиНачисления.ВидРасчета.КодДоходаНДФЛ КАК КодДоходаНДФЛ,
	|	СтрокиНачисления.КодВычета,
	|	СтрокиНачисления.ВидРасчета.СпособРасчета КАК СпособРасчета,
	|	СтрокиНачисления.НомерСтроки КАК НомерСтроки,
	|	СтрокиНачисления.Показатель1,
	|	СтрокиНачисления.Показатель2,
	|	СтрокиНачисления.Показатель3,
	|	СтрокиНачисления.Показатель4,
	|	СтрокиНачисления.Показатель5,
	|	СтрокиНачисления.Показатель6,
	|	СтрокиНачисления.Результат,
	|	СтрокиНачисления.ПодразделениеОрганизации,
	|	СтрокиНачисления.Сторно,
	//vvv
	|	СтрокиНачисления.ППС,
	|	СтрокиНачисления.СпособОтраженияВБухучете,
	|	СтрокиНачисления.Резерв,
	|	СтрокиНачисления.ПроцентРезерва,
	|	СтрокиНачисления.ИзРезерва,
	|	СтрокиНачисления.СуммаРезерва,
	|	СтрокиНачисления.Должность,
	//
	|	СтрокиНачисления.Сотрудник,
	|	СтрокиНачисления.Сотрудник.Физлицо КАК Физлицо,
	|	СтрокиНачисления.ДатаНачала КАК БазовыйПериодНачало,
	|	ВЫБОР
	|		КОГДА СтрокиНачисления.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА КОНЕЦПЕРИОДА(СтрокиНачисления.ДатаОкончания, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	КОНЕЦ КАК БазовыйПериодКонец,
	|	СтрокиНачисления.ВидРасчета.ЧислоМесяцев КАК ЧислоМесяцев,
	|	СтрокиНачисления.Авторасчет,
	| 	0 КАК СкидкаПриНалогообложении,
	|	СтрокиНачисления.СуммаВычета,
	|	ВЫБОР
	|		КОГДА ДанныеПоРаботникуДоРегистрации.ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|			ТОГДА ВЫБОР
	|					КОГДА ДОБАВИТЬКДАТЕ(ДанныеПоРаботникуДоРегистрации.Период, ДЕНЬ, -1) < &НачалоПериодаРегистрации
	|						ТОГДА &ДатаРегистрации
	|					ИНАЧЕ ДОБАВИТЬКДАТЕ(ДанныеПоРаботникуДоРегистрации.Период, ДЕНЬ, -1)
	|				КОНЕЦ
	|		КОГДА ДанныеПоРаботникуДоРегистрации.ПериодЗавершения <= &ДатаРегистрации
	|				И ДанныеПоРаботникуДоРегистрации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				И ДанныеПоРаботникуДоРегистрации.ПричинаИзмененияСостоянияЗавершения = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)
	|			ТОГДА ВЫБОР
	|					КОГДА ДОБАВИТЬКДАТЕ(ДанныеПоРаботникуДоРегистрации.ПериодЗавершения, ДЕНЬ, -1) < &НачалоПериодаРегистрации
	|						ТОГДА &ДатаРегистрации
	|					ИНАЧЕ ДОБАВИТЬКДАТЕ(ДанныеПоРаботникуДоРегистрации.ПериодЗавершения, ДЕНЬ, -1)
	|				КОНЕЦ
	|		КОГДА ДанныеПоРаботникуПослеРегистрации.ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.ПриемНаРаботу)
	|			ТОГДА ВЫБОР
	|					КОГДА ДанныеПоРаботникуПослеРегистрации.Период > &КонецПериодаРегистрации
	|						ТОГДА &ДатаРегистрации
	|					ИНАЧЕ ДанныеПоРаботникуПослеРегистрации.Период
	|				КОНЕЦ
	|		ИНАЧЕ &ДатаРегистрации
	|	КОНЕЦ КАК ДатаРегистрации,
	|	СтрокиНачисления.ОплаченоДнейЧасов,
	|	ВЫБОР
	|		КОГДА СтрокиНачисления.Сторно
	|			ТОГДА СтрокиНачисления.СторнируемыйДокумент
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СторнируемыйДокумент,
	|	СтрокиНачисления.ДокументОснование
	|ИЗ
	|	Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ДополнительныеНачисления КАК СтрокиНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|				&ДатаРегистрации,
	|				Сотрудник В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						Начисления.Сотрудник
	|					ИЗ
	|						Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ДополнительныеНачисления КАК Начисления
	|					ГДЕ
	|						Начисления.Ссылка = &ДокументСсылка)) КАК ДанныеПоРаботникуДоРегистрации
	|		ПО СтрокиНачисления.Сотрудник = ДанныеПоРаботникуДоРегистрации.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПервых(
	|				&ДатаРегистрации,
	|				Сотрудник В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						Начисления.Сотрудник
	|					ИЗ
	|						Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ДополнительныеНачисления КАК Начисления
	|					ГДЕ
	|						Начисления.Ссылка = &ДокументСсылка)) КАК ДанныеПоРаботникуПослеРегистрации
	|		ПО СтрокиНачисления.Сотрудник = ДанныеПоРаботникуПослеРегистрации.Сотрудник
	|ГДЕ
	|	СтрокиНачисления.Ссылка = &ДокументСсылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоНачисления()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение, не заполнен или
// заполнен некорректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//	Отказ					- флаг отказа в проведении,
//	Заголовок				- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок = "")

	//  Организация
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана организация, по которой выполняются начисления!", Отказ, Заголовок);
	КонецЕсли;
	
	//  ПериодРегистрации
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ПериодРегистрации) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указан месяц, в котором выполняются начисления!", Отказ, Заголовок);
	КонецЕсли;
	
	// соответствие периодов документа и перерассчитываемого документа
	Если ВыборкаПоШапкеДокумента.ПериодПерерасчета <> null 
		и ВыборкаПоШапкеДокумента.ПериодРегистрации <= ВыборкаПоШапкеДокумента.ПериодПерерасчета Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Период документа должен быть больше периода перерассчитываемого документа!", Отказ, Заголовок);
	КонецЕсли;
	// соответствие организаций документа и перерассчитываемого документа
	Если ВыборкаПоШапкеДокумента.ОрганизацияПерерасчета <> null 
		и ВыборкаПоШапкеДокумента.ОбособленноеПодразделение <> ВыборкаПоШапкеДокумента.ОрганизацияПерерасчета Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Организация, заданная для документа, должна совпадать с организацией перерассчитываемого документа!", Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения реквизитов в строке ТЧ "ОсновныеНачисления" документа.
// Если какой-то из реквизтов, влияющий на проведение, не заполнен или
// заполнен некорректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса к ТЧ документа,
// все проверяемые реквизиты должны быть включены в выборку.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//	ВыборкаПоСтрокамДокумента	- спозиционированная на определеной строке выборка 
//								  из результата запроса к ТЧ документа, 
//	Отказ						- флаг отказа в проведении,
//	Заголовок					- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеСтрокиНачисления(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, Отказ, Заголовок)

	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) +
									""" табл. части ""Начисления"": ";
	
	// Сотрудник
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Сотрудник) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не задан Сотрудник!", Отказ, Заголовок);
	КонецЕсли;

	// ВидРасчета
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ВидРасчета) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указан вид расчета!", Отказ, Заголовок);
	ИначеЕсли Не ВыборкаПоСтрокамДокумента.СтрокаИсправления Тогда
		Если ВыборкаПоШапкеДокумента.РегистрируютсяПочасовыеОтклонения <> ВыборкаПоСтрокамДокумента.ЯвляетсяПочасовымОтклонением Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "указанный вид расчета не соответствует способу регистрации времени в документе!", Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;

	// Дата начала
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодДействияНачало) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указана дата начала начисления!", Отказ, Заголовок);
		
	ИначеЕсли Не ВыборкаПоСтрокамДокумента.ЯвляетсяПочасовымОтклонением Тогда
		// Дата окончания
		Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодДействияКонец) Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указана дата окончания начисления!", Отказ, Заголовок);
		Иначе
			Если ВыборкаПоСтрокамДокумента.ПериодДействияКонец < ВыборкаПоСтрокамДокумента.ПериодДействияНачало Тогда
				ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "дата окончания начисления не должна быть меньше даты начала!", Отказ, Заголовок);
				
			Иначе
				Если НачалоМесяца(ВыборкаПоСтрокамДокумента.ПериодДействияКонец) <> НачалоМесяца(ВыборкаПоСтрокамДокумента.ПериодДействияНачало) Тогда
					ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "даты начала и окончания должны принадлежать одному месяцу!", Отказ, Заголовок);
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;

	// Дата начала события
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДатаНачалаСобытия) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указана дата начала события!", Отказ, Заголовок);
	КонецЕсли;
	
	Если ВыборкаПоШапкеДокумента.РегистрируютсяПочасовыеОтклонения И ВыборкаПоСтрокамДокумента.ЯвляетсяПочасовымОтклонением Тогда
		
		// ВидИспользованияРабочегоВремени
		Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ВидИспользованияРабочегоВремени) Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указан учет времени для вида расчета!", Отказ, Заголовок);
		КонецЕсли;
		
		// ОплачиватьЧасов
		Если ВыборкаПоСтрокамДокумента.ОплачиватьЧасов <= 0 Или ВыборкаПоСтрокамДокумента.ОплачиватьЧасов > 24 Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "количество оплачиваемых часов должно быть положительным и не превышать 24!", Отказ, Заголовок);
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПериодДействияНачало) Тогда
		// Подразделение
		Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ПодразделениеОрганизации) Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указано подразделение организации!", Отказ, Заголовок);
		ИначеЕсли ВыборкаПоСтрокамДокумента.ОшибкаПодразделениеНеПринадлежитОрганизации Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "указано подразделение, принадлежащее другой организации!", Отказ, Заголовок);
		КонецЕсли;
		
		// График работы
		Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ГрафикРаботы) Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "по состоянию на " + Формат(ВыборкаПоСтрокамДокумента.ПериодДействияНачало,"ДЛФ=DD") + " не задан рабочий график работника, либо он еще не принят на работу!", Отказ, Заголовок);
		КонецЕсли;
	КонецЕсли;
	
	// Организация сотрудника должна совпадать с организацией документа
	Если ВыборкаПоСтрокамДокумента.ОшибкаНеСоответствиеСотрудникаИОрганизации Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + " указанный сотрудник оформлен на другую организацию!", Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиНачисления()

// Проверяет правильность заполнения реквизитов в строке ТЧ "ДополнительныеНачисления" документа.
// Если какой-то из реквизтов, влияющий на проведение, не заполнен или
// заполнен некорректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса к ТЧ документа,
// все проверяемые реквизиты должны быть включены в выборку.
//
// Параметры:
//	ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//	ВыборкаПоСтрокамДокумента	- спозиционированная на определеной строке выборка 
//								  из результата запроса к ТЧ документа, 
//	Отказ						- флаг отказа в проведении,
//	Заголовок					- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеСтрокиДополнительныеНачисления(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, Отказ, Заголовок = "")

	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) +
									""" табл. части ""Доп. начисления"": ";
	
	// Сотрудник
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Сотрудник) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не задан сотрудник!", Отказ, Заголовок);
	КонецЕсли;

	// ВидРасчета
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ВидРасчета) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указан вид расчета!", Отказ, Заголовок);
		
	ИначеЕсли ВыборкаПоСтрокамДокумента.СпособРасчета = Перечисления.СпособыРасчетаОплатыТруда.Процентом
		Или ВыборкаПоСтрокамДокумента.СпособРасчета = Перечисления.СпособыРасчетаОплатыТруда.СевернаяНадбавка
		Или ВыборкаПоСтрокамДокумента.СпособРасчета = Перечисления.СпособыРасчетаОплатыТруда.ОтСтажаПроцентом Тогда
		
		// Дата начала
		Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.БазовыйПериодНачало) Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указана дата начала базового периода начисления!", Отказ, Заголовок);
		КонецЕсли;
		// Дата окончания
		Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.БазовыйПериодКонец) Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указана дата окончания базового периода начисления!", Отказ, Заголовок);
		КонецЕсли;
		
	КонецЕсли;
	
	// Проверка на сумму вычета
	Если ВыборкаПоСтрокамДокумента.СуммаВычета <> 0 И Не ПроведениеРасчетов.ВычетСоответствуетДоходу(ВыборкаПоСтрокамДокумента.КодДоходаНДФЛ, ВыборкаПоСтрокамДокумента.КодВычета, мСоответствиеДоходовИВычетов) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "указанный код вычета не предусмотрен для кода дохода (" + ?(ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.КодДоходаНДФЛ), СокрЛП(ВыборкаПоСтрокамДокумента.КодДоходаНДФЛ), "не указан") + "), с которым будет зарегистрировано в учете по НДФЛ начисление """ + ?(ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ВидРасчета), ВыборкаПоСтрокамДокумента.ВидРасчета, "<не указано>") + """!", Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеСтрокиНачисления()

// По строке выборок из результатов запроса по документу формируем движения по регистру
//
// Параметры: 
//	ВыборкаПоСтрокамДокумента	- спозиционированная на определеной строке выборка 
//								  из результата запроса к ТЧ документа, 
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуОсновныхНачислений(ВыборкаПоСтрокамДокумента, ВыборкаПоШапкеДокумента, НаборЗаписей)
	
	Движение = НаборЗаписей.Добавить();

	// Свойства
	Движение.ПериодРегистрации				= ПериодРегистрации;
	Движение.ПериодДействияНачало			= ВыборкаПоСтрокамДокумента.ПериодДействияНачало;
	Движение.ПериодДействияКонец			= ВыборкаПоСтрокамДокумента.ПериодДействияКонец;
	Движение.БазовыйПериодНачало			= ВыборкаПоСтрокамДокумента.БазовыйПериодНачало;
	Движение.БазовыйПериодКонец				= ВыборкаПоСтрокамДокумента.БазовыйПериодКонец;
	Движение.ВидРасчета						= ВыборкаПоСтрокамДокумента.ВидРасчета;
	Движение.Сторно							= ВыборкаПоСтрокамДокумента.Сторно;

	// Измерения
	Движение.Сотрудник						= ВыборкаПоСтрокамДокумента.Сотрудник;
	Движение.ФизЛицо						= ВыборкаПоСтрокамДокумента.ФизЛицо;
	Движение.Организация					= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;

	// Ресурсы
	Движение.Результат						= ВыборкаПоСтрокамДокумента.Результат;
	// ресурсы по отработанному времени
	Если ВыборкаПоСтрокамДокумента.ЗачетОтработанногоВремени Тогда
		Движение.ОтработаноДней				= ВыборкаПоСтрокамДокумента.ОтработаноДней;
		Движение.ОтработаноЧасов			= ВыборкаПоСтрокамДокумента.ОтработаноЧасов;
		Движение.ОтработаноДнейПоПятидневке	= ВыборкаПоСтрокамДокумента.ОтработаноДнейПоПятидневке;
		Движение.ОтработаноЧасовПоПятидневке= ВыборкаПоСтрокамДокумента.ОтработаноЧасовПоПятидневке;
	КонецЕсли;
	// ресурсы по норме времени
	Если ВыборкаПоСтрокамДокумента.ЗачетНормыВремени Тогда
		Движение.НормаДней					= ВыборкаПоСтрокамДокумента.НормаДней;
		Движение.НормаЧасов					= ВыборкаПоСтрокамДокумента.НормаЧасов;
		Движение.НормаДнейПоПятидневке		= ВыборкаПоСтрокамДокумента.НормаДнейПоПятидневке;
		Движение.НормаЧасовПоПятидневке		= ВыборкаПоСтрокамДокумента.НормаЧасовПоПятидневке;
	КонецЕсли;
	Движение.ОплаченоДнейЧасов				= ВыборкаПоСтрокамДокумента.ОплаченоДнейЧасов;

	// Реквизиты
	Движение.ГрафикРаботы					= ВыборкаПоСтрокамДокумента.ГрафикРаботы;
	Движение.ГрафикРаботыНорма				= ВыборкаПоСтрокамДокумента.ГрафикРаботыНорма;
	Движение.Показатель1					= ВыборкаПоСтрокамДокумента.Показатель1;
	Движение.Показатель2					= ВыборкаПоСтрокамДокумента.Показатель2;
	Движение.Показатель3					= ВыборкаПоСтрокамДокумента.Показатель3;
	Движение.Показатель4					= ВыборкаПоСтрокамДокумента.Показатель4;	
	Движение.Показатель5					= ВыборкаПоСтрокамДокумента.Показатель5;
	Движение.Показатель6					= ВыборкаПоСтрокамДокумента.Показатель6;	
	Движение.ВидУчетаВремени				= ПроведениеРасчетов.ПолучитьВидУчетаВремени(ВыборкаПоСтрокамДокумента.СпособРасчета, ВыборкаПоСтрокамДокумента.ВидВремени, ВыборкаПоСтрокамДокумента.СуммированныйУчетРабочегоВремени,ВыборкаПоСтрокамДокумента.ВидИспользованияРабочегоВремени,ВыборкаПоСтрокамДокумента.ВидРасчета.Показатели);
	Движение.ПодразделениеОрганизации		= ВыборкаПоСтрокамДокумента.ПодразделениеОрганизации;
	Движение.ОбособленноеПодразделение		= ВыборкаПоШапкеДокумента.ОбособленноеПодразделение;
	Движение.ДатаНачалаСобытия				= ВыборкаПоСтрокамДокумента.ДатаНачалаСобытия;
	Движение.ВариантОбработкиЗаписиПриОтраженииВРеглУчете = ВыборкаПоСтрокамДокумента.ВариантОбработкиЗаписиПриОтраженииВРеглУчете;
	Движение.СторнируемыйДокумент			= ВыборкаПоСтрокамДокумента.СторнируемыйДокумент;
	Движение.Авторасчет						= ВыборкаПоСтрокамДокумента.Авторасчет;
	//vvv
	Движение.ОтчисленияНаППС = ?(ВыборкаПоСтрокамДокумента.ППС,Перечисления.ДоходыФСС_НС.Облагается,Перечисления.ДоходыФСС_НС.НеОблагается);
	Движение.СпособОтраженияВБухучете = ВыборкаПоШапкеДокумента.СпособОтраженияВБухучете;
	Движение.ПроцентРезерва= ВыборкаПоСтрокамДокумента.ПроцентРезерва;
	Движение.ИзРезерва= ВыборкаПоСтрокамДокумента.ИзРезерва;
	Движение.СуммаРезерва= ВыборкаПоСтрокамДокумента.СуммаРезерва;
	Движение.Резерв = ВыборкаПоСтрокамДокумента.Резерв;
	Движение.Должность = ВыборкаПоСтрокамДокумента.Должность;
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.СпособОтраженияВБухучете) ТОгда
		Движение.СпособОтраженияВБухучете = ВыборкаПоСтрокамДокумента.СпособОтраженияВБухучете;
	КонецЕсли;	
	//
	
КонецПроцедуры // ДобавитьСтрокуОсновныхНачислений()

// По строке выборок из результатов запроса по документу формируем движения по регистру
//
// Параметры:
//	ВыборкаПоСтрокамДокумента	- спозиционированная на определеной строке выборка
//								  из результата запроса к ТЧ документа,
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуДопНачислений(ВыборкаПоСтрокамДокумента, ВыборкаПоШапкеДокумента, НаборЗаписей)
	
	Движение = НаборЗаписей.Добавить();

	// Свойства
	Движение.ПериодРегистрации			= НачалоМесяца(ВыборкаПоСтрокамДокумента.ДатаРегистрации);
	Движение.БазовыйПериодНачало		= ВыборкаПоСтрокамДокумента.БазовыйПериодНачало;
	Движение.БазовыйПериодКонец			= ВыборкаПоСтрокамДокумента.БазовыйПериодКонец;
	Движение.ВидРасчета					= ВыборкаПоСтрокамДокумента.ВидРасчета;
	Движение.Сторно						= ВыборкаПоСтрокамДокумента.Сторно;

	// Измерения
	Движение.Сотрудник					= ВыборкаПоСтрокамДокумента.Сотрудник;
	Движение.ФизЛицо					= ВыборкаПоСтрокамДокумента.ФизЛицо;
	Движение.Организация				= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;

	// Ресурсы
	Движение.Результат					= ВыборкаПоСтрокамДокумента.Результат;
	Движение.ОплаченоДнейЧасов			= ВыборкаПоСтрокамДокумента.ОплаченоДнейЧасов;

	// Реквизиты
	Движение.Показатель1				= ВыборкаПоСтрокамДокумента.Показатель1;
	Движение.Показатель2				= ВыборкаПоСтрокамДокумента.Показатель2;
	Движение.Показатель3				= ВыборкаПоСтрокамДокумента.Показатель3;
	Движение.Показатель4				= ВыборкаПоСтрокамДокумента.Показатель4;
	Движение.Показатель5				= ВыборкаПоСтрокамДокумента.Показатель5;
	Движение.Показатель6				= ВыборкаПоСтрокамДокумента.Показатель6;
	Движение.ЧислоМесяцев				= ВыборкаПоСтрокамДокумента.ЧислоМесяцев;
	Движение.ПодразделениеОрганизации	= ВыборкаПоСтрокамДокумента.ПодразделениеОрганизации;
	Движение.ДокументОснование			= ВыборкаПоСтрокамДокумента.ДокументОснование;
	Движение.ОбособленноеПодразделение	= ВыборкаПоШапкеДокумента.ОбособленноеПодразделение;
	Движение.СторнируемыйДокумент		= ВыборкаПоСтрокамДокумента.СторнируемыйДокумент;
	Движение.СкидкаПриНалогообложении	= ВыборкаПоСтрокамДокумента.СкидкаПриНалогообложении;
	Движение.Авторасчет					= ВыборкаПоСтрокамДокумента.Авторасчет;
	//vvv
	Движение.ОтчисленияНаППС = ?(ВыборкаПоСтрокамДокумента.ППС,Перечисления.ДоходыФСС_НС.Облагается,Перечисления.ДоходыФСС_НС.НеОблагается);
	Движение.СпособОтраженияВБухучете = ВыборкаПоШапкеДокумента.СпособОтраженияВБухучете;
	Движение.ПроцентРезерва= ВыборкаПоСтрокамДокумента.ПроцентРезерва;
	Движение.ИзРезерва= ВыборкаПоСтрокамДокумента.ИзРезерва;
	Движение.СуммаРезерва= ВыборкаПоСтрокамДокумента.СуммаРезерва;
	Движение.Резерв = ВыборкаПоСтрокамДокумента.Резерв;
	Движение.Должность = ВыборкаПоСтрокамДокумента.Должность;
	Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.СпособОтраженияВБухучете) ТОгда
		Движение.СпособОтраженияВБухучете = ВыборкаПоСтрокамДокумента.СпособОтраженияВБухучете;
	КонецЕсли;
	//
	
КонецПроцедуры // ДобавитьСтрокуДопНачислений

// По выборке из результатов запроса по документу формируем движения по регистру
//
// Параметры: 
//	ВыборкаПоШапкеДокумента				- спозиционированная на определеной строке выборка 
//										  из результата запроса к документу 
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуРабочегоВремени(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям, НаборЗаписей)
	
	Если ВыборкаПоНачислениям.ЯвляетсяПочасовымОтклонением Тогда
		Движение = НаборЗаписей.Добавить();
		
		// Свойства
		Движение.Период = ВыборкаПоНачислениям.ПериодДействияНачало;
		
		// Измерения
		Движение.Сотрудник							= ВыборкаПоНачислениям.Сотрудник;
		Движение.Организация						= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
		Движение.ВидИспользованияРабочегоВремени	= ВыборкаПоНачислениям.ВидИспользованияРабочегоВремени;
		
		// Ресурсы
		Движение.Часов								= ВыборкаПоНачислениям.ОплачиватьЧасов;
	КонецЕсли;
	
КонецПроцедуры // ДобавитьСтрокуРабочегоВремени()

// получает доходы НДФЛ по табличным частям с доходами
// Параметры:
//		ВыборкаПоШапкеДокумента - спозиционированная выборка по шапке документа
//		
Процедура СформироватьДоходыПоКодамНДФЛ(ВыборкаПоШапкеДокумента, НаборЗаписей)
	
	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("парамСсылка",			Ссылка);
	Запрос.УстановитьПараметр("ДатаНалоговогоПериода", КонецГода(ПериодРегистрации));
	КодОплатыТрудаПоНДФЛ = Новый Массив;
	КодОплатыТрудаПоНДФЛ.Добавить(Справочники.ДоходыНДФЛ.КодДоходаПоУмолчанию);
	//КодОплатыТрудаПоНДФЛ.Добавить(Справочники.ДоходыНДФЛ.Код2012);
	Запрос.УстановитьПараметр("КодОплатыТрудаПоНДФЛ", КодОплатыТрудаПоНДФЛ);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Доходы.Физлицо,
	|	Доходы.КодДохода,
	|	Доходы.Период,
	|	СУММА(Доходы.СуммаДохода) КАК СуммаДохода,
	|	Доходы.КодВычета,
	|	СУММА(Доходы.СуммаВычета) КАК СуммаВычета,
	|	Доходы.ПодразделениеОрганизации,
	|	Доходы.ВидРасчета
	|ИЗ
	|	(ВЫБРАТЬ
	|		Основные.Сотрудник.Физлицо КАК Физлицо,
	|		Основные.ВидРасчета.КодДоходаНДФЛ КАК КодДохода,
	|		ВЫБОР
	|			КОГДА Основные.ВидРасчета.КодДоходаНДФЛ В (&КодОплатыТрудаПоНДФЛ)
	|				ТОГДА Основные.ДатаНачала
	|			ИНАЧЕ Основные.Ссылка.ПериодРегистрации
	|		КОНЕЦ КАК Период,
	|		Основные.Результат КАК СуммаДохода,
	|		NULL КАК КодВычета,
	|		0 КАК СуммаВычета,
	|		Основные.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|		Основные.ВидРасчета
	|	ИЗ
	|		Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ОсновныеНачисления КАК Основные
	|	ГДЕ
	|		Основные.Ссылка = &парамСсылка
	|		И Основные.ВидРасчета.КодДоходаНДФЛ <> ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.ПустаяСсылка)
	|		И Основные.Результат <> 0
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		Дополнительные.Сотрудник.Физлицо,
	|		Дополнительные.ВидРасчета.КодДоходаНДФЛ,
	//vvv комментируем, иначе при внесении доп. начислений за прошлые месяцы в текущем месяце, подоходный включается в тек. месяц, а не в месяц за который начисление
	//|		Дополнительные.Ссылка.ПериодРегистрации,
	|		Дополнительные.ДатаНачала,
	//
	|		Дополнительные.Результат,
	|		Дополнительные.КодВычета,
	|		Дополнительные.СуммаВычета,
	|		Дополнительные.ПодразделениеОрганизации,
	|		Дополнительные.ВидРасчета
	|	ИЗ
	|		Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ДополнительныеНачисления КАК Дополнительные
	|	ГДЕ
	|		Дополнительные.Ссылка = &парамСсылка
	|		И Дополнительные.ВидРасчета.КодДоходаНДФЛ <> ЗНАЧЕНИЕ(Справочник.ДоходыНДФЛ.ПустаяСсылка)
	//vvv комментируем, добавим условие для СуммыВычета, чтобы можно было редактировать сумму предоставленых льгот по МП,
	//для людей, переведеных из других районов (не актуально при работе в одной базе). В последствии сделать отдельный документ
	//|		И Дополнительные.Результат <> 0) КАК Доходы
	|		И (Дополнительные.Результат <> 0 ИЛИ Дополнительные.СуммаВычета <> 0)) КАК Доходы
	//
	|
	|СГРУППИРОВАТЬ ПО
	|	Доходы.Физлицо,
	|	Доходы.КодДохода,
	|	Доходы.Период,
	|	Доходы.КодВычета,
	|	Доходы.ПодразделениеОрганизации,
	|	Доходы.ВидРасчета";
	
	ДоходыПоКодам = Запрос.Выполнить().Выбрать();
	// сформируем движения НДФЛСведенияОДоходах
	Пока ДоходыПоКодам.Следующий() Цикл
		Движение = НаборЗаписей.Добавить();
		
		// свойства
		Движение.Период						= ДоходыПоКодам.Период;
		
		// измерения 
		Движение.Организация				= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
		Движение.Физлицо					= ДоходыПоКодам.Физлицо;
		Движение.КодДохода					= ДоходыПоКодам.КодДохода;
		Движение.ПериодРегистрации			= НачалоМесяца(ПериодРегистрации);
		
		// ресурсы
		Движение.СуммаДохода				= ДоходыПоКодам.СуммаДохода;
		Движение.СуммаВычета				= ДоходыПоКодам.СуммаВычета;
		
		// реквизиты
		Движение.ОбособленноеПодразделение	= ВыборкаПоШапкеДокумента.ОбособленноеПодразделение;
		Движение.КодВычета					= ДоходыПоКодам.КодВычета;
		Движение.ИсчисленоИзЗарплаты		= Истина;
		Движение.ПодразделениеОрганизации	= ДоходыПоКодам.ПодразделениеОрганизации;
		Движение.ВидРасчета                 = ДоходыПоКодам.ВидРасчета;
	КонецЦикла;
КонецПроцедуры // СформироватьДоходыПоКодамНДФЛ()

// Вычисляет разницу между начислениями и удержаниями работника и формирует
// движения по взаиморасчетам с работниками
//
// Параметры:
//	ВыборкаПоШапкеДокумента	- спозиционированная выборка по шапке документа
//	НаборЗаписей			- набор записей 
//	Перерасчет				- признак проведения перерасчетов, по умолчанию - Ложь
//	Физлица					- список физлиц, по которым производится расчет, по умолчанию - отсутствует
//
// Возвращаемое значение:
//	Нет.
//	
Процедура СформироватьВзаиморасчетыСРаботниками(ВыборкаПоШапкеДокумента, НаборЗаписей, УчетЗадолженностиПоМесяцам, Перерасчет = Ложь, Физлица = Неопределено)
	
	Если Перерасчет Тогда
		Если Физлица = Неопределено Тогда
			// таблица ФизлицаДляПерерасчета
			// Список физлиц по которым необходимо выполнить перерасчет
			// Поля:
			//		ФизЛицо
			// Описание:
			// Получает список неповторяющихся физлиц по которым есть записи
			// перерасчета в одной из таблиц перерасчета - по основным или дополнительным начислениям
			//
			ФизлицаДляПерерасчетаТекст = 
			"ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	Перерасчет.ФизЛицо
			|ИЗ
			|	(ВЫБРАТЬ
			|		Перерасчет.ФизЛицо
			|	ИЗ
			|		РегистрРасчета.ОсновныеНачисленияРаботниковОрганизаций.ПерерасчетОсновныхНачислений КАК Перерасчет
			|	
			|	ГДЕ
			|		Перерасчет.ОбъектПерерасчета = &парамСсылка
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Перерасчет.ФизЛицо
			|	ИЗ
			|		РегистрРасчета.ДополнительныеНачисленияРаботниковОрганизаций.ПерерасчетДополнительныхНачислений КАК Перерасчет
			|	
			|	ГДЕ
			|		Перерасчет.ОбъектПерерасчета = &парамСсылка) КАК Перерасчет";
			
			// НачисленияРаботников
			//	Поля:
			//		Физлицо
			//		СуммаДохода - сумма "к выплате" - увличение состояния взаиморасчета с работником
			//
			
			НачисленияРаботниковТекст = 
			"ВЫБРАТЬ
			|	Доходы.Физлицо,
			|	СУММА(Доходы.СуммаДохода) КАК СуммаДохода
			|ИЗ
			|	(ВЫБРАТЬ
			|		Основные.Сотрудник.Физлицо КАК Физлицо,
			|		Основные.Результат КАК СуммаДохода
			|	ИЗ
			|		Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ОсновныеНачисления КАК Основные
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (" + ФизлицаДляПерерасчетаТекст + ") КАК ФизлицаДляПерерасчета
			|			ПО ФизлицаДляПерерасчета.ФизЛицо = Основные.Сотрудник.Физлицо
			|	
			|	ГДЕ
			|		Основные.Ссылка = &парамСсылка И
			|		(Основные.Результат <> 0)
			|		И (НЕ Основные.ВидРасчета.ЯвляетсяДоходомВНатуральнойФорме)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Дополнительные.Сотрудник.Физлицо,
			|		Дополнительные.Результат
			|	ИЗ
			|		Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ДополнительныеНачисления КАК Дополнительные
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ (" + ФизлицаДляПерерасчетаТекст + ") КАК ФизлицаДляПерерасчета
			|			ПО ФизлицаДляПерерасчета.ФизЛицо = Дополнительные.Сотрудник.Физлицо
			|	
			|	ГДЕ
			|		Дополнительные.Ссылка = &парамСсылка И
			|		(Дополнительные.Результат <> 0)
			|		И (НЕ Дополнительные.ВидРасчета.ЯвляетсяДоходомВНатуральнойФорме)) КАК Доходы
			|	
			|СГРУППИРОВАТЬ ПО
			|	Доходы.Физлицо";
			
			Запрос = Новый Запрос(НачисленияРаботниковТекст);
		Иначе
			
			НачисленияРаботниковТекст = 
			"ВЫБРАТЬ
			|	Доходы.Физлицо,
			|	СУММА(Доходы.СуммаДохода) КАК СуммаДохода
			|ИЗ
			|	(ВЫБРАТЬ
			|		Основные.Сотрудник.Физлицо КАК Физлицо,
			|		Основные.Результат КАК СуммаДохода
			|	ИЗ
			|		Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ОсновныеНачисления КАК Основные
			|	ГДЕ
			|		Основные.Физлицо В(&парамФизлица)
			|		И Основные.Ссылка = &парамСсылка
			|		И Основные.Результат <> 0
			|		И (НЕ Основные.ВидРасчета.ЯвляетсяДоходомВНатуральнойФорме)
			|	
			|	ОБЪЕДИНИТЬ ВСЕ
			|	
			|	ВЫБРАТЬ
			|		Дополнительные.Сотрудник.Физлицо,
			|		Дополнительные.Результат
			|	ИЗ
			|		Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ДополнительныеНачисления КАК Дополнительные
			|	ГДЕ
			|		Дополнительные.Физлицо В(&парамФизлица)
			|		И Дополнительные.Ссылка = &парамСсылка
			|		И Дополнительные.Результат <> 0
			|		И (НЕ Дополнительные.ВидРасчета.ЯвляетсяДоходомВНатуральнойФорме)) КАК Доходы
			|
			|СГРУППИРОВАТЬ ПО
			|	Доходы.Физлицо";
			
			Запрос = Новый Запрос(НачисленияРаботниковТекст);
			Запрос.УстановитьПараметр("парамФизлица", Физлица);
			
		КонецЕсли;
	Иначе
		НачисленияРаботниковТекст = 
		"ВЫБРАТЬ
		|	Доходы.Физлицо,
		|	СУММА(Доходы.СуммаДохода) КАК СуммаДохода
		|ИЗ
		|	(ВЫБРАТЬ
		|		Основные.Сотрудник.Физлицо КАК Физлицо,
		|		Основные.Результат КАК СуммаДохода
		|	ИЗ
		|		Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ОсновныеНачисления КАК Основные
		|	ГДЕ
		|		Основные.Ссылка = &парамСсылка
		|		И Основные.Результат <> 0
		|		И (НЕ Основные.ВидРасчета.ЯвляетсяДоходомВНатуральнойФорме)
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		Дополнительные.Сотрудник.Физлицо,
		|		Дополнительные.Результат
		|	ИЗ
		|		Документ.РегистрацияРазовыхНачисленийРаботниковОрганизаций.ДополнительныеНачисления КАК Дополнительные
		|	ГДЕ
		|		Дополнительные.Ссылка = &парамСсылка
		|		И Дополнительные.Результат <> 0
		|		И (НЕ Дополнительные.ВидРасчета.ЯвляетсяДоходомВНатуральнойФорме)) КАК Доходы
		|
		|СГРУППИРОВАТЬ ПО
		|	Доходы.Физлицо";
		
		Запрос = Новый Запрос(НачисленияРаботниковТекст);
		
	КонецЕсли;
		
	// Установим параметры запроса
	Запрос.УстановитьПараметр("парамСсылка" , Ссылка);
	
	Доходы = Запрос.Выполнить().Выбрать();
	// сформируем движения ВзаиморасчетыСРаботникамиОрганизаций
	Пока Доходы.Следующий() Цикл
		Движение = НаборЗаписей.Добавить();
		
		// свойства
		Движение.Период					= КонецМесяца(ПериодРегистрации);
		Движение.ВидДвижения			= ВидДвиженияНакопления.Приход;
		
		// измерения 
		Движение.Физлицо				= Доходы.Физлицо;
		Движение.Организация			= Организация;
		//vvv комментируем
		//Если УчетЗадолженностиПоМесяцам Тогда
			Движение.ПериодВзаиморасчетов	= ПериодРегистрации;
		//КонецЕсли;
		
		// ресурсы
		Движение.СуммаВзаиморасчетов	= Доходы.СуммаДохода;
		
	КонецЦикла;
	
КонецПроцедуры // СформироватьВзаиморасчетыСРаботниками()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПриКопировании(ОбъектКопирования)
	ПерерассчитываемыйДокумент = Неопределено
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);
	
	// Получим реквизиты шапки из запроса
	ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке().Выбрать();

	Если ВыборкаПоШапкеДокумента.Следующий() Тогда

		//Надо позвать проверку заполнения реквизитов шапки
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);

		// Движения стоит добавлять, если в проведении еще не отказано (отказ =ложь)
		Если НЕ Отказ Тогда

			// Получение учетной политики по персоналу организации
			// ведется ли учет задолженности в разрезе периодов возникновения задолженности
			УчетЗадолженностиПоМесяцам = ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "УчетЗадолженностиПоМесяцам");
		
			////////////////////////////////////////////////////////////////////////
			// основные Начисления

			Движения.ОсновныеНачисленияРаботниковОрганизаций.мВыполнятьВспомогательныеРасчеты = Истина;
			
			// получим реквизиты табличной части
			ВыборкаПоНачислениям = СформироватьЗапросПоНачисления(ВыборкаПоШапкеДокумента).Выбрать();

			Пока ВыборкаПоНачислениям.Следующий() Цикл 

				// проверим очередную строку табличной части
				ПроверитьЗаполнениеСтрокиНачисления(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям, Отказ, Заголовок);
				Если НЕ Отказ Тогда
					// Заполним записи в наборах записей регистров
					ДобавитьСтрокуОсновныхНачислений(ВыборкаПоНачислениям, ВыборкаПоШапкеДокумента, Движения.ОсновныеНачисленияРаботниковОрганизаций);
					ДобавитьСтрокуРабочегоВремени(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям, Движения.ВнутрисменноеВремяРаботниковОрганизаций);
					//vvv
					СервисныеПроцедурыИФункции.ДобавитьСтрокуРезерва(ВыборкаПоНачислениям, ВыборкаПоШапкеДокумента, Движения.РезервНачислений);
					//
				КонецЕсли;
			КонецЦикла;

			////////////////////////////////////////////////////////////////////////
			// дополнительные Начисления

			РезультатЗапроса = СформироватьЗапросПоДопНачисления();
			Если Не РезультатЗапроса.Пустой() Тогда
	
				// получим реквизиты табличной части
				ВыборкаПоДопНачислениям = РезультатЗапроса.Выбрать();
				
				// Устанавливаем соответствие доходов и положенных по ним вычетов для целей исчисления НДФЛ
				//мСоответствиеДоходовИВычетов = ПроведениеРасчетов.ПолучитьСоответствиеДоходовИВычетов();

				Пока ВыборкаПоДопНачислениям.Следующий() Цикл 

					// проверим очередную строку табличной части
					ПроверитьЗаполнениеСтрокиДополнительныеНачисления(ВыборкаПоШапкеДокумента, ВыборкаПоДопНачислениям, Отказ, Заголовок);
					Если НЕ Отказ Тогда
						// Заполним записи в наборах записей регистров
						ДобавитьСтрокуДопНачислений(ВыборкаПоДопНачислениям, ВыборкаПоШапкеДокумента, Движения.ДополнительныеНачисленияРаботниковОрганизаций);
						//vvv
						СервисныеПроцедурыИФункции.ДобавитьСтрокуРезерва(ВыборкаПоДопНачислениям, ВыборкаПоШапкеДокумента, Движения.РезервНачислений);
						//
					КонецЕсли;
				КонецЦикла;

			КонецЕсли;
			
			//vvv
			Если НЕ Отказ Тогда
				ПроведениеРасчетов.ДобавитьДвиженияПериодическихВыплат(Движения, Ссылка);
			КонецЕсли;
			//
			
			////////////////////////////////////////////////////////////////////////
			// доходы НДФЛ
			
			// сформируем доходы НДФЛ по начислениям документа
			СформироватьДоходыПоКодамНДФЛ(ВыборкаПоШапкеДокумента, Движения.НДФЛСведенияОДоходах);
			
			////////////////////////////////////////////////////////////////////////
			// взаиморасчеты с работниками
			
			// сформируем начисления к выплате по начислениям документа
			СформироватьВзаиморасчетыСРаботниками(ВыборкаПоШапкеДокумента, Движения.ВзаиморасчетыСРаботникамиОрганизаций, УчетЗадолженностиПоМесяцам);

			// выполним удаление перерасчетов исправленного документа
			Если Не Отказ И ЗначениеЗаполнено(ПерерассчитываемыйДокумент) Тогда
				ПроведениеРасчетов.УдалитьСведенияОПерерасчетеДокумента(Ссылка, Истина); // Только по исправленным документам
			КонецЕсли;
			
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры // ОбработкаПроведения()

Процедура ОбработкаУдаленияПроведения(Отказ)
    Движения.ОсновныеНачисленияРаботниковОрганизаций.мВыполнятьВспомогательныеРасчеты = Истина;
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	КраткийСоставДокумента = ПроцедурыУправленияПерсоналом.ЗаполнитьКраткийСоставДокумента(ОсновныеНачисления, ДополнительныеНачисления);
	
	Если СпособРегистрацииВремени = Перечисления.СпособыРегистрацииВремени.РегистрацияДляЧастиСмены Тогда
		ОсновныеНачисления.ЗагрузитьКолонку(ОсновныеНачисления.ВыгрузитьКолонку("ДатаНачала"),"ДатаОкончания")
	Иначе 
		Для каждого СтрокаТЧ Из ОсновныеНачисления Цикл
			СтрокаТЧ.ОплачиватьЧасов = 0
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

