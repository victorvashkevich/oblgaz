////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

Перем СоответствиеВалютныеСпособыРасчета;
Перем мДлинаСуток;

////////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
//

// Формирует записи регистра "ОсновныеНачисленияРаботниковОрганизаций" по данным шапки документа
Процедура СформироватьДвиженияПоНачислениям(ВыборкаПоШапкеДокумента, НаборОсновныеНачисления)
	
	// строка движений
	Движение = НаборОсновныеНачисления.Добавить();
	
	// Свойства
	Движение.ПериодРегистрации          = НачалоМесяца(ДатаНачала);
	Движение.ПериодДействияНачало       = НачалоДня(ДатаНачала);
	Движение.ПериодДействияКонец 	    = КонецДня(ДатаНачала);
	Движение.ВидРасчета				    = ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеПоУходуЗаРебенкомДо1_5Лет;
	
	// Измерения
	Движение.Сотрудник                    = ВыборкаПоШапкеДокумента.Сотрудник;
	Движение.ФизЛицо                    = ВыборкаПоШапкеДокумента.ФизЛицо;
	Движение.Организация				= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
	
	// Реквизиты
	
	Движение.ОбособленноеПодразделение	= ВыборкаПоШапкеДокумента.Организация;

	Движение.ВидУчетаВремени  	        = Перечисления.ВидыУчетаВремени.ПоДням;
	Движение.ПериодРасчетаСреднегоЗаработкаНачало	= ВыборкаПоШапкеДокумента.ПериодРасчетаСреднегоЗаработкаНачало;
	Движение.ПериодРасчетаСреднегоЗаработкаОкончание= ВыборкаПоШапкеДокумента.ПериодРасчетаСреднегоЗаработкаОкончание;
	
	Движение.ДатаНачалаСобытия			= ДатаНачала;
	Движение.Авторасчет					= Истина;
			
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

#Если Клиент Тогда

// Формирует запрос по документу и связанным регистрам сведений
//
// Параметры:
//	Режим - строка, может принимать значения:
//			"ПоРеквизитамДокумента"
//			"ПоТабличнойЧастиДокумента"
//
// Возвращаемое значение:
//	Результат запроса с данными об организации или о работниках из табличной части
//
Функция СформироватьЗапросДляПечати2004()

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ТекущийДокумент",		Ссылка);
	Запрос.УстановитьПараметр("Руководитель",			Перечисления.ОтветственныеЛицаОрганизаций.Руководитель);
	Запрос.УстановитьПараметр("ДатаДокумента",			Дата);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация",	ОбщегоНазначения.ГоловнаяОрганизация(Организация));
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОтпускПоУходуЗаРебенком.Дата КАК ДатаДок,
	|	ОтпускПоУходуЗаРебенком.Номер КАК НомерДок,
	|	ЕСТЬNULL(ФИОФизЛицСрезПоследнихРаботника.Фамилия + "" "" + ФИОФизЛицСрезПоследнихРаботника.Имя + "" "" + ФИОФизЛицСрезПоследнихРаботника.Отчество, ОтпускПоУходуЗаРебенком.Сотрудник.Наименование) КАК Работник,
	|	ВЫРАЗИТЬ(ОтпускПоУходуЗаРебенком.Организация.НаименованиеПолное КАК СТРОКА(300)) КАК НазваниеОрганизации,
	|	ОтпускПоУходуЗаРебенком.Организация.КодПоОКПО КАК КодПоОКПО,
	|	ОтветственныеЛицаОрганизацийСрезПоследних.Должность.Представление КАК ДолжностьРуководителя,
	|	ЕСТЬNULL(ФИОФизЛицСрезПоследних.Фамилия + "" "" + ФИОФизЛицСрезПоследних.Имя + "" "" + ФИОФизЛицСрезПоследних.Отчество, ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо.Наименование) КАК ФИОРуководителя,
	|	ОтветственныеЛицаОрганизацийСрезПоследних.ОтветственноеЛицо,
	|	ОтпускПоУходуЗаРебенком.ДатаНачала КАК ДатаНачала,
	|	ОтпускПоУходуЗаРебенком.ДатаОкончания КАК ДатаОкончания,
	|	ВЫБОР
	|		КОГДА ОтпускПоУходуЗаРебенком.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА 0
	|		ИНАЧЕ РАЗНОСТЬДАТ(ОтпускПоУходуЗаРебенком.ДатаНачала, ОтпускПоУходуЗаРебенком.ДатаОкончания, ДЕНЬ) + 1
	|	КОНЕЦ КАК ПродолжительностьВсего,
	|	ВЫБОР
	|		КОГДА  ДанныеОРаботникахОрганизации.ПериодЗавершения <= ОтпускПоУходуЗаРебенком.ДатаНачала
	|				И ДанныеОРаботникахОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|		ТОГДА ДанныеОРаботникахОрганизации.ПодразделениеОрганизацииЗавершенияНаименование
	|		ИНАЧЕ ДанныеОРаботникахОрганизации.ПодразделениеОрганизацииНаименование
	|	КОНЕЦ КАК ПодразделениеРаботника,
	|	ВЫБОР
	|		КОГДА ДанныеОРаботникахОрганизации.ПериодЗавершения <= ОтпускПоУходуЗаРебенком.ДатаНачала
	|				И ДанныеОРаботникахОрганизации.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|		ТОГДА ДанныеОРаботникахОрганизации.ДолжностьЗавершенияНаименование
	|		ИНАЧЕ ДанныеОРаботникахОрганизации.ДолжностьНаименование  
	|	КОНЕЦ КАК Должность,
	|	""Отпуск по уходу за ребенком"" КАК ВидДопОтпуска,
	|	ОтпускПоУходуЗаРебенком.Организация.Префикс,
	|	ОтпускПоУходуЗаРебенком.Сотрудник.Код КАК ТабельныйНомер
	|ИЗ
	|	Документ.ОтпускПоУходуЗаРебенком КАК ОтпускПоУходуЗаРебенком
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(&ДатаДокумента, ОтветственноеЛицо = &Руководитель) КАК ОтветственныеЛицаОрганизацийСрезПоследних
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаДокумента, ) КАК ФИОФизЛицСрезПоследних
	|			ПО ОтветственныеЛицаОрганизацийСрезПоследних.ФизическоеЛицо = ФИОФизЛицСрезПоследних.ФизЛицо
	|		ПО ОтпускПоУходуЗаРебенком.Организация = ОтветственныеЛицаОрганизацийСрезПоследних.СтруктурнаяЕдиница
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаДокумента, ) КАК ФИОФизЛицСрезПоследнихРаботника
	|		ПО ОтпускПоУходуЗаРебенком.Сотрудник.Физлицо = ФИОФизЛицСрезПоследнихРаботника.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			СписокДат.Сотрудник КАК Сотрудник,
	|           РаботникиОрганизации.ПериодЗавершения КАК ПериодЗавершения,
	|			РаботникиОрганизации.ПодразделениеОрганизации.Наименование КАК ПодразделениеОрганизацииНаименование,
	|			РаботникиОрганизации.Должность.Наименование КАК ДолжностьНаименование,
	|			РаботникиОрганизации.ПодразделениеОрганизацииЗавершения.Наименование КАК ПодразделениеОрганизацииЗавершенияНаименование,
	|			РаботникиОрганизации.ДолжностьЗавершения.Наименование КАК ДолжностьЗавершенияНаименование
	|		ИЗ
	|			(ВЫБРАТЬ
	|				ОтпускПоУходуЗаРебенком.Сотрудник КАК Сотрудник,
	|				МАКСИМУМ(РаботникиВнутри.Период) КАК ДатаПоследнегоИзменения
	|			ИЗ
	|				РегистрСведений.РаботникиОрганизаций КАК РаботникиВнутри
	|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтпускПоУходуЗаРебенком КАК ОтпускПоУходуЗаРебенком
	|					ПО РаботникиВнутри.Период < ОтпускПоУходуЗаРебенком.ДатаНачала
	|						И РаботникиВнутри.Сотрудник = ОтпускПоУходуЗаРебенком.Сотрудник
	|			ГДЕ
	|				ОтпускПоУходуЗаРебенком.Ссылка = &ТекущийДокумент
	|			
	|			СГРУППИРОВАТЬ ПО
	|				ОтпускПоУходуЗаРебенком.Сотрудник) КАК СписокДат
	|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|				ПО СписокДат.ДатаПоследнегоИзменения = РаботникиОрганизации.Период
	|					И СписокДат.Сотрудник = РаботникиОрганизации.Сотрудник) КАК ДанныеОРаботникахОрганизации
	|		ПО ОтпускПоУходуЗаРебенком.Сотрудник = ДанныеОРаботникахОрганизации.Сотрудник
	|ГДЕ
	|	ОтпускПоУходуЗаРебенком.Ссылка = &ТекущийДокумент";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросДляПечати()

// Функция формирует табличный документ с печатной формой "Т-6",
//
// Возвращаемое значение:
//	Табличный документ - печатная форма
//
Функция ПечатьТ6_2004()
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ОтпускПоУходуЗаРебенком_Т6";
	ТабДокумент.ОриентацияСтраницы = ОриентацияСтраницы.Портрет;
	
	// получаем данные для печати
	Выборка = СформироватьЗапросДляПечати2004().Выбрать();
	//ВыборкаРаботники = СформироватьЗапросДляПечати2004("ПоТабличнойЧастиДокумента").Выбрать();

	// запоминаем области макета
	Макет = ПолучитьМакет("Т6_от_5_1_2004");
	ОбластьМакетаОтчета = Макет.ПолучитьОбласть("Т6"); // Область документа

	Если Выборка.Следующий() Тогда
		ОбластьМакетаОтчета.Параметры.Заполнить(Выборка); // Шапка документа.
 		ОбластьМакетаОтчета.Параметры.НазваниеОрганизации	= СокрЛП(ОбластьМакетаОтчета.Параметры.НазваниеОрганизации);
		
		ТабДокумент.Вывести(ОбластьМакетаОтчета);
	КонецЕсли;

	// если не было ни одного работника - выводим пустой бланк
	Если ТабДокумент.ВысотаТаблицы = 0 Тогда
		ТабДокумент.Вывести(ОбластьМакетаОтчета);
	КонецЕсли;

	Возврат ТабДокумент;

КонецФункции // ПечатьТ6()

////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура осуществляет печать документа. Можно направить печать на 
// экран или принтер, а также распечатать необходмое количество копий.
//
//	Название макета печати передается в качестве параметра,
// по переданному названию находим имя макета в соответствии.
//
// Параметры:
//	НазваниеМакета - строка, название макета.
//
Функция Печать(ИмяМакета, КоличествоЭкземпляров = 1, НаПринтер = Ложь) Экспорт

	// Получить экземпляр документа на печать
	ИмяФормы = "";
	Если ИмяМакета = "Т6_от_5_1_2004" Тогда
		ИмяФормы	= " (форма Т-6)";
		ТабДокумент	= ПечатьТ6_2004()
	ИначеЕсли ИмяМакета = "РасчетСреднегоЗаработка" Тогда
		ИмяФормы = " (расчет среднего заработка)";
		Если Не Проведен Тогда
			РаботаСДиалогами.ВывестиПредупреждение("Расчет среднего заработка можно распечатать только после проведения документа!");
			Возврат Неопределено;
		КонецЕсли;
		ТабДокумент = ФормированиеПечатныхФорм.ПечатьРасчетаСреднегоЗаработка(Ссылка, ДатаНачала, 12, "с " + Формат(ДатаНачала,"ДФ=dd.MM.yyyy") + " по " + Формат(ДатаОкончания,"ДФ=dd.MM.yyyy"), Перечисления.СпособыРасчетаОплатыТруда.ПособиеПоУходуЗаРебенкомДо1_5Лет, Ложь);
	КонецЕсли;

	Возврат РаботаСДиалогами.НапечататьДокумент(ТабДокумент, КоличествоЭкземпляров, НаПринтер, РаботаСДиалогами.СформироватьЗаголовокДокумента(ЭтотОбъект,Метаданные().Синоним + ИмяФормы));

КонецФункции // Печать()

#КонецЕсли

Процедура Рассчитать(РассчитываемыеТаблицы, РежимПерерасчета = Ложь) Экспорт
	
	//Перед вызовом процедуры документ должнен быть записан, движения должны быть удалены 
	
	Отказ = Ложь;
	
	Заголовок = СтрЗаменить(ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка),"Проведение документа: ","Расчет документа: ");
	// ДатаНачала
	Если НЕ ЗначениеЗаполнено(ДатаНачала) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана дата начала отпуска!", Отказ, Заголовок);
	КонецЕсли;
	// ДатаОкончания
	Если НЕ ЗначениеЗаполнено(ДатаОкончания) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана дата окончания отпуска!", Отказ, Заголовок);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат
	КонецЕсли;
	
	ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке().Выбрать();
	// позиционируем выборку
	ВыборкаПоШапкеДокумента.Следующий();
	
	ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок, Ложь);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
    УчетнаяПолитикаПоПерсоналуОрганизации = глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации");
	//ПоддержкаВнутреннегоСовместительства = ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(УчетнаяПолитикаПоПерсоналуОрганизации, Организация, "ПоддержкаВнутреннегоСовместительства");
	ПоддержкаВнутреннегоСовместительства = Истина;
	// Создадим наборы записей используемых регистров 
	НаборОсновныеНачисления = РегистрыРасчета.ОсновныеНачисленияРаботниковОрганизаций.СоздатьНаборЗаписей();
	НаборОсновныеНачисления.Отбор.Регистратор.Значение = Ссылка;
	
	НаборРасчетСреднего = РегистрыРасчета.РасчетСреднегоЗаработка.СоздатьНаборЗаписей();
	НаборРасчетСреднего.Отбор.Регистратор.Значение = Ссылка;
	
	Если Не РассчитываемыеТаблицы.РасчетСреднего Тогда

		// получим реквизиты табличной части РасчетСреднего 
		
		ВыборкаПоРасчетСреднего = СформироватьЗапросПоРасчетСреднего().Выбрать();
		Пока ВыборкаПоРасчетСреднего.Следующий() Цикл
			
			// проверим очередную строку табличной части
			ПроверитьЗаполнениеСтрокиРасчетСреднего(ВыборкаПоШапкеДокумента, ВыборкаПоРасчетСреднего, Отказ, Заголовок, ПоддержкаВнутреннегоСовместительства);
			
			Если НЕ Отказ Тогда
				ДобавитьСтрокуРасчетаСреднего(ВыборкаПоШапкеДокумента, ВыборкаПоРасчетСреднего, НаборРасчетСреднего);
			КонецЕсли;
			
		КонецЦикла;
		
		Если НЕ Отказ Тогда
			НаборРасчетСреднего.Записать();
		КонецЕсли;
		
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат
	КонецЕсли; 
	
	СформироватьДвиженияПоНачислениям(ВыборкаПоШапкеДокумента, НаборОсновныеНачисления);
	
	// рассчитываем записи начислений и если надо, записи расчета среднего заработка
	Если РассчитываемыеТаблицы.РасчетСреднего Тогда
		НаборОсновныеНачисления.Записать();
		ПроведениеРасчетов.СформироватьИЗаписатьДанныеРасчетаСреднего("ОсновныеНачисленияРаботниковОрганизаций", Ссылка, НаборРасчетСреднего);
		РасчетСреднего.Загрузить(НаборРасчетСреднего.Выгрузить());
	КонецЕсли;
	Если РассчитываемыеТаблицы.Начисления Тогда
		
		Если НаборОсновныеНачисления.Модифицированность() Тогда
			НаборОсновныеНачисления.Записать();
		КонецЕсли;
		
		МассивСпособовРасчета = Новый Массив;
		МассивСпособовРасчета.Добавить(Перечисления.СпособыРасчетаОплатыТруда.ПособиеПоУходуЗаРебенкомДо1_5Лет);
		НеобходимыеДанные = ПроведениеРасчетов.ПолучитьСтруктуруНеобходимыхДанных(МассивСпособовРасчета,Новый Массив);
		НеобходимыеДанные.СреднийЗаработок = Истина;
		НеобходимыеДанные.СреднийЗаработокПособий = Истина;
		
		ИсходныеДанные = ПроведениеРасчетов.ПолучитьДанныеДляРасчета("ОсновныеНачисленияРаботниковОрганизаций", 
									ВыборкаПоШапкеДокумента.КатегорияНачисления, 
									НеобходимыеДанные, 
									Ссылка, 
									ВыборкаПоШапкеДокумента.ГоловнаяОрганизация, 
									НачалоМесяца(ДатаНачала));
									
		Если Не ИсходныеДанные.Следующий() ИЛИ НЕ ЗначениеЗаполнено(ИсходныеДанные.СреднийЗаработокПособий) ИЛИ НЕ ЗначениеЗаполнено(ИсходныеДанные.ОтработаноДнейПособий) Тогда
			ОбщегоНазначения.СообщитьОбОшибке("Нет данных для расчета среднедневного заработка!",,Заголовок);
			СреднедневнойЗаработок = 0;
		Иначе 	
			СреднедневнойЗаработок = Окр(ИсходныеДанные.СреднийЗаработокПособий / ИсходныеДанные.ОтработаноДнейПособий,2);
		КонецЕсли;	
		
	КонецЕсли;

	Если Не РежимПерерасчета Тогда
		НаборРасчетСреднего.Очистить();
		НаборРасчетСреднего.Записать();
	КонецЕсли;
	
	// Удаляем вспомогательные движения  
	НаборОсновныеНачисления.Очистить();
	НаборОсновныеНачисления.Записать();
	
КонецПроцедуры  // РассчитатьНачисления

Процедура Перерассчитать(Физлица = Неопределено) Экспорт
	
	// Выполним полный перерасчет документа
	Рассчитать(Новый Структура("Начисления,РасчетСреднего",Истина,Истина), Истина);
	
	// Зафиксируем данные табличных частей после расчета
	Записать();
	
	Если ВыплачиватьПособиеДоПолутораЛет Тогда
		Для каждого Движение Из Движения.ПлановыеНачисленияРаботниковОрганизаций Цикл
			Если Движение.ВидРасчетаИзмерение <> ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеПоУходуЗаРебенкомДо1_5Лет Тогда
				Продолжить;
			КонецЕсли;
			Движение.Показатель1 = СреднедневнойЗаработок;
		КонецЦикла;
		Движения.ПлановыеНачисленияРаботниковОрганизаций.Записать();
	КонецЕсли;
	
	НаборЗаписейПерерасчета = РегистрыРасчета.РасчетСреднегоЗаработка.Перерасчеты.ПерерасчетСреднегоЗаработка.СоздатьНаборЗаписей();
	НаборЗаписейПерерасчета.Отбор.ОбъектПерерасчета.Значение = Ссылка;
	НаборЗаписейПерерасчета.Записать();
	
КонецПроцедуры // Перерассчитать()

// Выполняет заполнение таблицы плановых начислений и удержаний 
Процедура ЗаполнитьПлановыеНачисленияУдержания() Экспорт
	
	// Прекратим действующие до даты документа начисления работника
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ФизЛицо", Физлицо);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
    Запрос.УстановитьПараметр("ГоловнаяОрганизация", ОбщегоНазначения.ГоловнаяОрганизация(Организация));
	Запрос.УстановитьПараметр("ДатаАктуальности", ?(ЗначениеЗаполнено(ДатаНачала),ДатаНачала,Дата));
	ОтпускаПоУходу = Новый Массив;
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ОтпускПоУходуЗаРебенкомБезОплаты);
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеМатериОдиночкедо1_5лет);
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеМатериОдиночкеДо3Лет);
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеНаРебенкаДо3Лет);
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеНаРебенкаДо3ЛетНаВторого);
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеНаРебенкаДо3ЛетНаРебенкаИнвалида);
    ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеНаРебенкаСНадбавкой40До3Лет);
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеНеРабМатериНадб40До3Лет);
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеНеРабМатериОдиночкеДо1_5Лет);
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеНеРабМатериОдиночкеДо3Лет);
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеНеРаботающейМатериДо3Лет);
	Запрос.УстановитьПараметр("ОтпускаПоУходу",ОтпускаПоУходу);

		
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА СрезПлановыхНачислений.ПериодЗавершения <= &ДатаАктуальности
	|				И СрезПлановыхНачислений.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА СрезПлановыхНачислений.ВидРасчетаЗавершения
	|		ИНАЧЕ СрезПлановыхНачислений.ВидРасчета
	|	КОНЕЦ КАК ВидРасчета,
	|	СрезПлановыхНачислений.Сотрудник,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить) КАК Действие
	|ИЗ
	|	РегистрСведений.ПлановыеНачисленияРаботниковОрганизаций.СрезПоследних(
	|			&ДатаАктуальности,
	|			Сотрудник.ФизЛицо = &ФизЛицо
	|				И (НЕ ВидРасчетаИзмерение ССЫЛКА ПланВидовРасчета.ДополнительныеНачисленияОрганизаций)
	|				И Организация = &ГоловнаяОрганизация
	|				И Регистратор <> &Регистратор) КАК СрезПлановыхНачислений
	|ГДЕ
	|	ВЫБОР
	|			КОГДА СрезПлановыхНачислений.ПериодЗавершения <= &ДатаАктуальности
	|					И СрезПлановыхНачислений.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА СрезПлановыхНачислений.ДействиеЗавершения
	|			ИНАЧЕ СрезПлановыхНачислений.Действие
	|		КОНЕЦ <> ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
	|	И (НЕ ВЫБОР
	|				КОГДА СрезПлановыхНачислений.ПериодЗавершения <= &ДатаАктуальности
	|						И СрезПлановыхНачислений.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|					ТОГДА СрезПлановыхНачислений.ВидРасчетаЗавершения
	|				ИНАЧЕ СрезПлановыхНачислений.ВидРасчета
	|			КОНЕЦ В (&ОтпускаПоУходу))";
	
	ОсновныеНачисления.Загрузить(Запрос.Выполнить().Выгрузить());
	
	// Прекратим действующие до даты документа удержания работника
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ФизЛицо", ФизЛицо);
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("ДатаАктуальности", ?(ЗначениеЗаполнено(ДатаНачала),ДатаНачала,Дата));
	
	Запрос.Текст = 
    "ВЫБРАТЬ РАЗРЕШЕННЫЕ
    |	СрезПлановыхУдержаний.ВидРасчета КАК ВидРасчета,
    |	СрезПлановыхУдержаний.ДокументОснование КАК ДокументОснование,
    |	ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить) КАК Действие
    |ИЗ
    |	РегистрСведений.ПлановыеУдержанияРаботниковОрганизаций.СрезПоследних(
    |		&ДатаАктуальности,
    |		ФизЛицо = &ФизЛицо
    |			И Организация = &Организация
    |			И Регистратор <> &Регистратор) КАК СрезПлановыхУдержаний
    |ГДЕ
    |	ВЫБОР
    |			КОГДА СрезПлановыхУдержаний.ПериодЗавершения <= &ДатаАктуальности
    |					И СрезПлановыхУдержаний.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
    |				ТОГДА СрезПлановыхУдержаний.ДействиеЗавершения
    |			ИНАЧЕ СрезПлановыхУдержаний.Действие
    |		КОНЕЦ <> ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)";
	
	ПлановыеУдержания.Загрузить(Запрос.Выполнить().Выгрузить());
		
КонецПроцедуры

// Возвращает доступные варианты печати документа
//
// Возвращаемое значение:
//  Струткура, каждая строка которой соответствует одному из вариантов печати
//  
Функция ПолучитьСтруктуруПечатныхФорм() Экспорт
	
	Если НастройкаПравДоступа.ДоступнаРольРасчетчикаРегл() Тогда
		Возврат Новый Структура("Т6_от_5_1_2004", "Форма Т-6");
	Иначе
		Возврат Новый Структура("Т6_от_5_1_2004", "Форма Т-6");
	КонецЕсли;

КонецФункции // ПолучитьСтруктуруПечатныхФорм()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ДЛЯ ОБЕСПЕЧЕНИЯ ПРОВЕДЕНИЯ ДОКУМЕНТА

// Формирует запрос по шапке документа
//
// Параметры:
//	Режим - режим проведения
//
// Возвращаемое значение:
//	Результат запроса
//
Функция СформироватьЗапросПоШапке()

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка",	Ссылка);
	ОтпускаПоУходу = Новый Массив;
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ОтпускПоУходуЗаРебенкомБезОплаты);
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеМатериОдиночкедо1_5лет);
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеМатериОдиночкеДо3Лет);
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеНаРебенкаДо3Лет);
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеНаРебенкаДо3ЛетНаВторого);
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеНаРебенкаДо3ЛетНаРебенкаИнвалида);
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеНаРебенкаСНадбавкой40До3Лет);
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеНеРабМатериНадб40До3Лет);
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеНеРабМатериОдиночкеДо1_5Лет);
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеНеРабМатериОдиночкеДо3Лет);
	ОтпускаПоУходу.Добавить(ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ПособиеНеРаботающейМатериДо3Лет);
	Запрос.УстановитьПараметр("ОтпускаПоУходу",ОтпускаПоУходу);
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ОтпускПоУходуЗаРебенком.Дата,
	|	ОтпускПоУходуЗаРебенком.Организация,
	|	ВЫБОР
	|		КОГДА ОтпускПоУходуЗаРебенком.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА ОтпускПоУходуЗаРебенком.Организация
	|		ИНАЧЕ ОтпускПоУходуЗаРебенком.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	ОтпускПоУходуЗаРебенком.Ответственный,
	|	ОтпускПоУходуЗаРебенком.Ссылка,
	|	ОтпускПоУходуЗаРебенком.Сотрудник,
	|	ОтпускПоУходуЗаРебенком.Сотрудник.Физлицо КАК Физлицо,
	|	ОтпускПоУходуЗаРебенком.ДатаНачала,
	|	ОтпускПоУходуЗаРебенком.ДатаОкончания,
	|	ВЫБОР
	|		КОГДА ОтпускПоУходуЗаРебенком.Сотрудник.ВидЗанятости = ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВнутреннийСовместитель,
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ОтпускПоУходуЗаРебенком.ДатаНачала, МЕСЯЦ), МЕСЯЦ, -12) КАК ПериодРасчетаСреднегоЗаработкаНачало,
	|	ДОБАВИТЬКДАТЕ(НАЧАЛОПЕРИОДА(ОтпускПоУходуЗаРебенком.ДатаНачала, МЕСЯЦ), СЕКУНДА, -1) КАК ПериодРасчетаСреднегоЗаработкаОкончание,
	|	ОсновныеНачисленияОрганизаций.КатегорияНачисления,
	|	ОсновныеНачисленияОрганизаций.СпособРасчета,
	|	ОтпускПоУходуЗаРебенком.ДатаОкончанияПособияДоПолутораЛет,
	|	ОтпускПоУходуЗаРебенком.ДатаОкончанияПособияДоТрехЛет,
	|	ОтпускПоУходуЗаРебенком.СреднедневнойЗаработок,
	|	ОтпускПоУходуЗаРебенком.КоличествоДетей,
	|	ОтпускПоУходуЗаРебенком.КоличествоПервыхДетей,
	|	ОтпускПоУходуЗаРебенком.ВыплачиватьПособиеДоПолутораЛет,
	|	ОтпускПоУходуЗаРебенком.ВыплачиватьПособиеДоТрехЛет,
	|	ОтпускПоУходуЗаРебенком.ВидРасчетаДоПолутораЛет,
	|	ОтпускПоУходуЗаРебенком.ВидРасчетаДоТрехЛет,
	|	ОтпускПоУходуЗаРебенком.ПроцентПособияДоПолутораЛет,
	|	ОтпускПоУходуЗаРебенком.ПроцентПособияДотрехЛет,
	|	ВЫБОР
	|		КОГДА ОтпускПоУходуЗаРебенком.Сотрудник.Организация = ВЫБОР
	|				КОГДА ОтпускПоУходуЗаРебенком.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|					ТОГДА ОтпускПоУходуЗаРебенком.Организация
	|				ИНАЧЕ ОтпускПоУходуЗаРебенком.Организация.ГоловнаяОрганизация
	|			КОНЕЦ
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК ОшибкаНеСоответствиеСотрудникаИОрганизации,
	|	ОтпускПоУходуЗаРебенком.ОсвобождатьСтавку,
	|	ОтпускПоУходуЗаРебенком.ВыплачиватьПособиеДоТрехЛетНаВторогоРебенка,
	|	ОтпускПоУходуЗаРебенком.ВидРасчетаДоТрехЛетНаВторогоРебенка,
	|	ОтпускПоУходуЗаРебенком.ПроцентПособияДотрехЛетНаВторогоРебенка,
	|	ОтпускПоУходуЗаРебенком.ДатаОкончанияПособияДоТрехЛетНаВторогоРебенка
	|ИЗ
	|	Документ.ОтпускПоУходуЗаРебенком КАК ОтпускПоУходуЗаРебенком
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.ОсновныеНачисленияОрганизаций КАК ОсновныеНачисленияОрганизаций
	|		ПО (ОсновныеНачисленияОрганизаций.Ссылка В (&ОтпускаПоУходу))
	|ГДЕ
	|	ОтпускПоУходуЗаРебенком.Ссылка = &ДокументСсылка";
	
	Запрос.Текст = ТекстЗапроса;

	Возврат Запрос.Выполнить();


КонецФункции // СформироватьЗапросПоШапке()

Функция СформироватьЗапросПоРабочимМестам(ВыборкаПоШапкеДокумента)

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("Дата" , ДатаНачала);
	Запрос.УстановитьПараметр("Физлицо" , ВыборкаПоШапкеДокумента.Физлицо);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация" , ВыборкаПоШапкеДокумента.ГоловнаяОрганизация);

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &Дата
	|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА РаботникиОрганизацииСрезПоследних.ЗанимаемыхСтавокЗавершения
	|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.ЗанимаемыхСтавок
	|	КОНЕЦ КАК ЗанимаемыхСтавок,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &Дата
	|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизацииЗавершения
	|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации
	|	КОНЕЦ КАК ПодразделениеОрганизации,
	|	ВЫБОР
	|		КОГДА &Дата >= РаботникиОрганизацииСрезПоследних.ПериодЗавершения
	|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА РаботникиОрганизацииСрезПоследних.ДолжностьЗавершения
	|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.Должность
	|	КОНЕЦ КАК Должность,
	|	РаботникиОрганизацииСрезПоследних.Сотрудник,
	|	РаботникиОрганизацииСрезПоследних.Организация,
	|	РаботникиОрганизацииСрезПоследних.УсловияТрудаИзмерение,
	|	РаботникиОрганизацииСрезПоследних.ВидДеятельностиИзмерение
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|			&Дата,
	|			Сотрудник В
	|				(ВЫБРАТЬ
	|					СотрудникиОрганизаций.Ссылка
	|				ИЗ
	|					Справочник.СотрудникиОрганизаций КАК СотрудникиОрганизаций
	|				ГДЕ
	|					СотрудникиОрганизаций.Физлицо = &Физлицо
	|					И СотрудникиОрганизаций.Организация = &ГоловнаяОрганизация)) КАК РаботникиОрганизацииСрезПоследних
	|ГДЕ
	|	ВЫБОР
	|			КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &Дата
	|					И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|				ТОГДА РаботникиОрганизацииСрезПоследних.ПричинаИзмененияСостоянияЗавершения
	|			ИНАЧЕ РаботникиОрганизацииСрезПоследних.ПричинаИзмененияСостояния
	|		КОНЕЦ <> ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.Увольнение)";
				   
	Запрос.Текст = ТекстЗапроса;

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоРабочимМестам()

// Формирует запрос по таблице "ПлановыеНачисления" документа
//
// Параметры:
//
// Возвращаемое значение:
//	Результат запроса.
//
Функция СформироватьЗапросПоОсновнымНачислениям(ВыборкаПоШапкеДокумента, ПроверкаШтатногоРасписания)
	
	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
	
	Если ПроверкаШтатногоРасписания Тогда
		
		Запрос.УстановитьПараметр("Дата", ДатаНачала);
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЧНачисления.Ссылка КАК Ссылка,
		|	ТЧНачисления.НомерСтроки КАК НомерСтроки,
		|	ТЧНачисления.Сотрудник КАК Сотрудник,
		|	ТЧНачисления.Ссылка.ДатаНачала КАК Период,
		|	ТЧНачисления.Действие КАК Действие,
		|	ВЫБОР
		|		КОГДА ТЧНачисления.Ссылка.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА ДОБАВИТЬКДАТЕ(ТЧНачисления.Ссылка.ДатаОкончания, ДЕНЬ, 1)
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|	КОНЕЦ КАК ПериодЗавершения,
		|	ВЫБОР
		|		КОГДА ТЧНачисления.Ссылка.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА ВЫБОР
		|					КОГДА ТЧНачисления.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Начать)
		|					КОГДА ТЧНачисления.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Начать)
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
		|					ИНАЧЕ ТЧНачисления.Действие
		|				КОНЕЦ
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.ПустаяСсылка)
		|	КОНЕЦ КАК ДействиеЗавершения,
		|	ТЧНачисления.ВидРасчета КАК ВидРасчета,
		|	ВЫБОР
		|		КОГДА ТЧНачисления.ВидРасчета.ЗачетОтработанногоВремени
		|			ТОГДА """"
		|		ИНАЧЕ ТЧНачисления.ВидРасчета
		|	КОНЕЦ КАК ВидРасчетаИзмерение,
		|	ТЧНачисления.ВидРасчета.СпособРасчета КАК СпособРасчета,
		|	ТЧНачисления.ВидРасчета.ЗачетОтработанногоВремени КАК ЗачетОтработанногоВремени,
		|	ТЧНачисления.ВидРасчета.ТребуетВводаТарифногоРазряда КАК ТребуетВводаТарифногоРазряда,
		|	ТЧНачисления.ВидРасчета.ПроизвольнаяФормулаРасчета КАК ПроизвольнаяФормулаРасчета,
		|	ТЧНачисления.Показатель1 КАК Показатель1,
		|	ТЧНачисления.Валюта1 КАК Валюта1,
		|	ТЧНачисления.ТарифныйРазряд1 КАК ТарифныйРазряд1,
		|	ТЧНачисления.Показатель2 КАК Показатель2,
		|	ТЧНачисления.Валюта2 КАК Валюта2,
		|	ТЧНачисления.ТарифныйРазряд2 КАК ТарифныйРазряд2,
		|	ТЧНачисления.Показатель3 КАК Показатель3,
		|	ТЧНачисления.Валюта3 КАК Валюта3,
		|	ТЧНачисления.ТарифныйРазряд3 КАК ТарифныйРазряд3,
		|	ТЧНачисления.Показатель4 КАК Показатель4,
		|	ТЧНачисления.Валюта4 КАК Валюта4,
		|	ТЧНачисления.ТарифныйРазряд4 КАК ТарифныйРазряд4,
		|	ТЧНачисления.Показатель5 КАК Показатель5,
		|	ТЧНачисления.Валюта5 КАК Валюта5,
		|	ТЧНачисления.ТарифныйРазряд5 КАК ТарифныйРазряд5,
		|	ТЧНачисления.Показатель6 КАК Показатель6,
		|	ТЧНачисления.Валюта6 КАК Валюта6,
		|	ТЧНачисления.ТарифныйРазряд6 КАК ТарифныйРазряд6
		|ПОМЕСТИТЬ ВТНачисления
		|ИЗ
		|	Документ.ОтпускПоУходуЗаРебенком.ОсновныеНачисления КАК ТЧНачисления
		|ГДЕ
		|	ТЧНачисления.Ссылка = &ДокументСсылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник";
		Запрос.Выполнить();
		
		// здесь добавим данные для штатного расписания
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЧНачисления.НомерСтроки КАК НомерСтроки,
		|	ТЧНачисления.Ссылка,
		|	ТЧНачисления.Сотрудник,
		|	ВЫБОР
		|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &Дата
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизацииЗавершения
		|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации
		|	КОНЕЦ КАК ПодразделениеОрганизации,
		|	ВЫБОР
		|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &Дата
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА РаботникиОрганизацииСрезПоследних.ДолжностьЗавершения
		|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.Должность
		|	КОНЕЦ КАК Должность,
		|	ВЫБОР
		|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &Дата
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА РаботникиОрганизацииСрезПоследних.ЗанимаемыхСтавокЗавершения
		|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.ЗанимаемыхСтавок
		|	КОНЕЦ КАК ЗанимаемыхСтавок,
		|	ВЫБОР
		|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &Дата
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА РаботникиОрганизацииСрезПоследних.ГрафикРаботыЗавершения
		|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.ГрафикРаботы
		|	КОНЕЦ КАК ГрафикРаботы,
		|	ТЧНачисления.Период,
		|	ТЧНачисления.Действие,
		|	ТЧНачисления.ПериодЗавершения,
		|	ТЧНачисления.ДействиеЗавершения,
		|	ТЧНачисления.ВидРасчета,
		|	ТЧНачисления.ВидРасчетаИзмерение,
		|	ТЧНачисления.СпособРасчета,
		|	ТЧНачисления.ЗачетОтработанногоВремени,
		|	ТЧНачисления.ТребуетВводаТарифногоРазряда,
		|	ТЧНачисления.ПроизвольнаяФормулаРасчета,
		|	ТЧНачисления.Показатель1,
		|	ТЧНачисления.Валюта1,
		|	ТЧНачисления.ТарифныйРазряд1,
		|	ТЧНачисления.Показатель2,
		|	ТЧНачисления.Валюта2,
		|	ТЧНачисления.ТарифныйРазряд2,
		|	ТЧНачисления.Показатель3,
		|	ТЧНачисления.Валюта3,
		|	ТЧНачисления.ТарифныйРазряд3,
		|	ТЧНачисления.Показатель4,
		|	ТЧНачисления.Валюта4,
		|	ТЧНачисления.ТарифныйРазряд4,
		|	ТЧНачисления.Показатель5,
		|	ТЧНачисления.Валюта5,
		|	ТЧНачисления.ТарифныйРазряд5,
		|	ТЧНачисления.Показатель6,
		|	ТЧНачисления.Валюта6,
		|	ТЧНачисления.ТарифныйРазряд6
		|ИЗ
		|	ВТНачисления КАК ТЧНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(
		|				&Дата,
		|				Сотрудник В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						Сотрудники.Сотрудник
		|					ИЗ
		|						ВТНачисления КАК Сотрудники)) КАК РаботникиОрганизацииСрезПоследних
		|		ПО ТЧНачисления.Сотрудник = РаботникиОрганизацииСрезПоследних.Сотрудник
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЧНачисления.Ссылка КАК Ссылка,
		|	ТЧНачисления.НомерСтроки КАК НомерСтроки,
		|	ТЧНачисления.Сотрудник КАК Сотрудник,
		|	"""" КАК ПодразделениеОрганизации,
		|	"""" КАК Должность,
		|	"""" КАК ЗанимаемыхСтавок,
		|	"""" КАК ГрафикРаботы,
		|	ТЧНачисления.Ссылка.ДатаНачала КАК Период,
		|	ТЧНачисления.Действие КАК Действие,
		|	ВЫБОР
		|		КОГДА ТЧНачисления.Ссылка.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА ДОБАВИТЬКДАТЕ(ТЧНачисления.Ссылка.ДатаОкончания, ДЕНЬ, 1)
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|	КОНЕЦ КАК ПериодЗавершения,
		|	ВЫБОР
		|		КОГДА ТЧНачисления.Ссылка.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА ВЫБОР
		|					КОГДА ТЧНачисления.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Начать)
		|					КОГДА ТЧНачисления.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Начать)
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
		|					ИНАЧЕ ТЧНачисления.Действие
		|				КОНЕЦ
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.ПустаяСсылка)
		|	КОНЕЦ КАК ДействиеЗавершения,
		|	ТЧНачисления.ВидРасчета КАК ВидРасчета,
		|	ВЫБОР
		|		КОГДА ТЧНачисления.ВидРасчета.ЗачетОтработанногоВремени
		|			ТОГДА """"
		|		ИНАЧЕ ТЧНачисления.ВидРасчета
		|	КОНЕЦ КАК ВидРасчетаИзмерение,
		|	ТЧНачисления.ВидРасчета.СпособРасчета КАК СпособРасчета,
		|	ТЧНачисления.ВидРасчета.ЗачетОтработанногоВремени КАК ЗачетОтработанногоВремени,
		|	ТЧНачисления.ВидРасчета.ТребуетВводаТарифногоРазряда КАК ТребуетВводаТарифногоРазряда,
		|	ТЧНачисления.ВидРасчета.ПроизвольнаяФормулаРасчета КАК ПроизвольнаяФормулаРасчета,
		|	ТЧНачисления.Показатель1 КАК Показатель1,
		|	ТЧНачисления.Валюта1 КАК Валюта1,
		|	ТЧНачисления.ТарифныйРазряд1 КАК ТарифныйРазряд1,
		|	ТЧНачисления.Показатель2 КАК Показатель2,
		|	ТЧНачисления.Валюта2 КАК Валюта2,
		|	ТЧНачисления.ТарифныйРазряд2 КАК ТарифныйРазряд2,
		|	ТЧНачисления.Показатель3 КАК Показатель3,
		|	ТЧНачисления.Валюта3 КАК Валюта3,
		|	ТЧНачисления.ТарифныйРазряд3 КАК ТарифныйРазряд3,
		|	ТЧНачисления.Показатель4 КАК Показатель4,
		|	ТЧНачисления.Валюта4 КАК Валюта4,
		|	ТЧНачисления.ТарифныйРазряд4 КАК ТарифныйРазряд4,
		|	ТЧНачисления.Показатель5 КАК Показатель5,
		|	ТЧНачисления.Валюта5 КАК Валюта5,
		|	ТЧНачисления.ТарифныйРазряд5 КАК ТарифныйРазряд5,
		|	ТЧНачисления.Показатель6 КАК Показатель6,
		|	ТЧНачисления.Валюта6 КАК Валюта6,
		|	ТЧНачисления.ТарифныйРазряд6 КАК ТарифныйРазряд6
		|ИЗ
		|	Документ.ОтпускПоУходуЗаРебенком.ОсновныеНачисления КАК ТЧНачисления
		|ГДЕ
		|	ТЧНачисления.Ссылка = &ДокументСсылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	КонецЕсли;
	
	Возврат ПроцедурыУправленияПерсоналом.СформироватьЗапросПоПлановымНачислениям(Запрос.Выполнить().Выгрузить(), ПроверкаШтатногоРасписания);

КонецФункции // СформироватьЗапросПоПлановымНачислениям()

// Формирует запрос по таблице "ПлановыеНачисления" документа
//
// Параметры: 
//  Режим        - режим проведения.
//
// Возвращаемое значение:
//  Результат запроса.
//
Функция СформироватьЗапросПоЕжемесячнымПособиям(ВыборкаПоШапкеДокумента, ПроверкаШтатногоРасписания)

		Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
	
	Если ПроверкаШтатногоРасписания Тогда
		
		Запрос.УстановитьПараметр("Дата", ДатаНачала);
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЧНачисления.Ссылка КАК Ссылка,
		|	ТЧНачисления.НомерСтроки КАК НомерСтроки,
		|	ТЧНачисления.Ссылка.Сотрудник КАК Сотрудник,
		|	ТЧНачисления.Ссылка.ДатаНачала КАК Период,
		|	ТЧНачисления.Действие КАК Действие,
		|	ВЫБОР
		|		КОГДА ТЧНачисления.Ссылка.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА ДОБАВИТЬКДАТЕ(ТЧНачисления.Ссылка.ДатаОкончания, ДЕНЬ, 1)
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|	КОНЕЦ КАК ПериодЗавершения,
		|	ВЫБОР
		|		КОГДА ТЧНачисления.Ссылка.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА ВЫБОР
		|					КОГДА ТЧНачисления.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Начать)
		|					КОГДА ТЧНачисления.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Начать)
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
		|					ИНАЧЕ ТЧНачисления.Действие
		|				КОНЕЦ
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.ПустаяСсылка)
		|	КОНЕЦ КАК ДействиеЗавершения,
		|	ТЧНачисления.ВидРасчета КАК ВидРасчета,
		|	ВЫБОР
		|		КОГДА ТЧНачисления.ВидРасчета.ЗачетОтработанногоВремени
		|			ТОГДА """"
		|		ИНАЧЕ ТЧНачисления.ВидРасчета
		|	КОНЕЦ КАК ВидРасчетаИзмерение,
		|	ТЧНачисления.ВидРасчета.СпособРасчета КАК СпособРасчета,
		|	ТЧНачисления.ВидРасчета.ЗачетОтработанногоВремени КАК ЗачетОтработанногоВремени,
		|	ТЧНачисления.ВидРасчета.ТребуетВводаТарифногоРазряда КАК ТребуетВводаТарифногоРазряда,
		|	ТЧНачисления.ВидРасчета.ПроизвольнаяФормулаРасчета КАК ПроизвольнаяФормулаРасчета,
		|	ТЧНачисления.Показатель1 КАК Показатель1,
		|	ТЧНачисления.Валюта1 КАК Валюта1,
		|	ТЧНачисления.ТарифныйРазряд1 КАК ТарифныйРазряд1,
		|	ТЧНачисления.Показатель2 КАК Показатель2,
		|	ТЧНачисления.Валюта2 КАК Валюта2,
		|	ТЧНачисления.ТарифныйРазряд2 КАК ТарифныйРазряд2,
		|	ТЧНачисления.Показатель3 КАК Показатель3,
		|	ТЧНачисления.Валюта3 КАК Валюта3,
		|	ТЧНачисления.ТарифныйРазряд3 КАК ТарифныйРазряд3,
		|	ТЧНачисления.Показатель4 КАК Показатель4,
		|	ТЧНачисления.Валюта4 КАК Валюта4,
		|	ТЧНачисления.ТарифныйРазряд4 КАК ТарифныйРазряд4,
		|	ТЧНачисления.Показатель5 КАК Показатель5,
		|	ТЧНачисления.Валюта5 КАК Валюта5,
		|	ТЧНачисления.ТарифныйРазряд5 КАК ТарифныйРазряд5,
		|	ТЧНачисления.Показатель6 КАК Показатель6,
		|	ТЧНачисления.Валюта6 КАК Валюта6,
		|	ТЧНачисления.ТарифныйРазряд6 КАК ТарифныйРазряд6
		|ПОМЕСТИТЬ ВТНачисления
		|ИЗ
		|	Документ.ОтпускПоУходуЗаРебенком.ЕжемесячныеПособия КАК ТЧНачисления
		|ГДЕ
		|	ТЧНачисления.Ссылка = &ДокументСсылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник";
		Запрос.Выполнить();
		
		// здесь добавим данные для штатного расписания
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЧНачисления.НомерСтроки КАК НомерСтроки,
		|	ТЧНачисления.Ссылка,
		|	ТЧНачисления.Сотрудник,
		|	ВЫБОР
		|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &Дата
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизацииЗавершения
		|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.ПодразделениеОрганизации
		|	КОНЕЦ КАК ПодразделениеОрганизации,
		|	ВЫБОР
		|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &Дата
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА РаботникиОрганизацииСрезПоследних.ДолжностьЗавершения
		|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.Должность
		|	КОНЕЦ КАК Должность,
		|	ВЫБОР
		|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &Дата
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА РаботникиОрганизацииСрезПоследних.ЗанимаемыхСтавокЗавершения
		|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.ЗанимаемыхСтавок
		|	КОНЕЦ КАК ЗанимаемыхСтавок,
		|	ВЫБОР
		|		КОГДА РаботникиОрганизацииСрезПоследних.ПериодЗавершения <= &Дата
		|				И РаботникиОрганизацииСрезПоследних.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА РаботникиОрганизацииСрезПоследних.ГрафикРаботыЗавершения
		|		ИНАЧЕ РаботникиОрганизацииСрезПоследних.ГрафикРаботы
		|	КОНЕЦ КАК ГрафикРаботы,
		|	ТЧНачисления.Период,
		|	ТЧНачисления.Действие,
		|	ТЧНачисления.ПериодЗавершения,
		|	ТЧНачисления.ДействиеЗавершения,
		|	ТЧНачисления.ВидРасчета,
		|	ТЧНачисления.ВидРасчетаИзмерение,
		|	ТЧНачисления.СпособРасчета,
		|	ТЧНачисления.ЗачетОтработанногоВремени,
		|	ТЧНачисления.ТребуетВводаТарифногоРазряда,
		|	ТЧНачисления.ПроизвольнаяФормулаРасчета,
		|	ТЧНачисления.Показатель1,
		|	ТЧНачисления.Валюта1,
		|	ТЧНачисления.ТарифныйРазряд1,
		|	ТЧНачисления.Показатель2,
		|	ТЧНачисления.Валюта2,
		|	ТЧНачисления.ТарифныйРазряд2,
		|	ТЧНачисления.Показатель3,
		|	ТЧНачисления.Валюта3,
		|	ТЧНачисления.ТарифныйРазряд3,
		|	ТЧНачисления.Показатель4,
		|	ТЧНачисления.Валюта4,
		|	ТЧНачисления.ТарифныйРазряд4,
		|	ТЧНачисления.Показатель5,
		|	ТЧНачисления.Валюта5,
		|	ТЧНачисления.ТарифныйРазряд5,
		|	ТЧНачисления.Показатель6,
		|	ТЧНачисления.Валюта6,
		|	ТЧНачисления.ТарифныйРазряд6
		|ИЗ
		|	ВТНачисления КАК ТЧНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(
		|				&Дата,
		|				Сотрудник В
		|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
		|						Сотрудники.Сотрудник
		|					ИЗ
		|						ВТНачисления КАК Сотрудники)) КАК РаботникиОрганизацииСрезПоследних
		|		ПО ТЧНачисления.Сотрудник = РаботникиОрганизацииСрезПоследних.Сотрудник
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
		
	Иначе
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТЧНачисления.Ссылка КАК Ссылка,
		|	ТЧНачисления.НомерСтроки КАК НомерСтроки,
		|	ТЧНачисления.Ссылка.Сотрудник КАК Сотрудник,
		|	"""" КАК ПодразделениеОрганизации,
		|	"""" КАК Должность,
		|	"""" КАК ЗанимаемыхСтавок,
		|	"""" КАК ГрафикРаботы,
		|	ТЧНачисления.Ссылка.ДатаНачала КАК Период,
		|	ТЧНачисления.Действие КАК Действие,
		|	ВЫБОР
		|		КОГДА ТЧНачисления.Ссылка.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА ДОБАВИТЬКДАТЕ(ТЧНачисления.Ссылка.ДатаОкончания, ДЕНЬ, 1)
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|	КОНЕЦ КАК ПериодЗавершения,
		|	ВЫБОР
		|		КОГДА ТЧНачисления.Ссылка.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
		|			ТОГДА ВЫБОР
		|					КОГДА ТЧНачисления.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Начать)
		|					КОГДА ТЧНачисления.Действие = ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Начать)
		|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
		|					ИНАЧЕ ТЧНачисления.Действие
		|				КОНЕЦ
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.ПустаяСсылка)
		|	КОНЕЦ КАК ДействиеЗавершения,
		|	ТЧНачисления.ВидРасчета КАК ВидРасчета,
		|	ВЫБОР
		|		КОГДА ТЧНачисления.ВидРасчета.ЗачетОтработанногоВремени
		|			ТОГДА """"
		|		ИНАЧЕ ТЧНачисления.ВидРасчета
		|	КОНЕЦ КАК ВидРасчетаИзмерение,
		|	ТЧНачисления.ВидРасчета.СпособРасчета КАК СпособРасчета,
		|	ТЧНачисления.ВидРасчета.ЗачетОтработанногоВремени КАК ЗачетОтработанногоВремени,
		|	ТЧНачисления.ВидРасчета.ТребуетВводаТарифногоРазряда КАК ТребуетВводаТарифногоРазряда,
		|	ТЧНачисления.ВидРасчета.ПроизвольнаяФормулаРасчета КАК ПроизвольнаяФормулаРасчета,
		|	ТЧНачисления.Показатель1 КАК Показатель1,
		|	ТЧНачисления.Валюта1 КАК Валюта1,
		|	ТЧНачисления.ТарифныйРазряд1 КАК ТарифныйРазряд1,
		|	ТЧНачисления.Показатель2 КАК Показатель2,
		|	ТЧНачисления.Валюта2 КАК Валюта2,
		|	ТЧНачисления.ТарифныйРазряд2 КАК ТарифныйРазряд2,
		|	ТЧНачисления.Показатель3 КАК Показатель3,
		|	ТЧНачисления.Валюта3 КАК Валюта3,
		|	ТЧНачисления.ТарифныйРазряд3 КАК ТарифныйРазряд3,
		|	ТЧНачисления.Показатель4 КАК Показатель4,
		|	ТЧНачисления.Валюта4 КАК Валюта4,
		|	ТЧНачисления.ТарифныйРазряд4 КАК ТарифныйРазряд4,
		|	ТЧНачисления.Показатель5 КАК Показатель5,
		|	ТЧНачисления.Валюта5 КАК Валюта5,
		|	ТЧНачисления.ТарифныйРазряд5 КАК ТарифныйРазряд5,
		|	ТЧНачисления.Показатель6 КАК Показатель6,
		|	ТЧНачисления.Валюта6 КАК Валюта6,
		|	ТЧНачисления.ТарифныйРазряд6 КАК ТарифныйРазряд6
		|ИЗ
		|	Документ.ОтпускПоУходуЗаРебенком.ЕжемесячныеПособия КАК ТЧНачисления
		|ГДЕ
		|	ТЧНачисления.Ссылка = &ДокументСсылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки";
	КонецЕсли;
	
	Возврат ПроцедурыУправленияПерсоналом.СформироватьЗапросПоПлановымНачислениям(Запрос.Выполнить().Выгрузить(), ПроверкаШтатногоРасписания);

КонецФункции // СформироватьЗапросПоЕжемесячнымПособиям()


// Формирует запрос по таблице "ПлановыеУдержания" документа
//
// Параметры:
//
// Возвращаемое значение:
//	Результат запроса.
//
Функция СформироватьЗапросПоПлановымУдержаниям(ВыборкаПоШапкеДокумента)

	Запрос = Новый Запрос;

	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);
	Запрос.УстановитьПараметр("Дата" , ДатаНачала);
	Запрос.УстановитьПараметр("Физлицо" , ВыборкаПоШапкеДокумента.Физлицо);
	Запрос.УстановитьПараметр("ГоловнаяОрганизация" , ВыборкаПоШапкеДокумента.ГоловнаяОрганизация);
	
	
	МенеджерВТ = Новый МенеджерВременныхТаблиц;
	Запрос.МенеджерВременныхТаблиц = МенеджерВТ;	
	
	// получим временную таблицу с показателями начислений
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Удержания.ВидРасчета				КАК ВидРасчета,
	|	МАКСИМУМ(Показатели.НомерСтроки)			КАК КоличествоПоказателей,
	|	Показатели1.Показатель.Предопределенный		КАК Показатель1Предопределенный,
	|	Показатели1.Показатель.Наименование			КАК Показатель1Наименование,
	|	Показатели1.Показатель.ТипПоказателя		КАК Показатель1ТипПоказателя,
	|	Показатели1.Показатель.ВозможностьИзменения КАК Показатель1ВозможностьИзменения,
	|	Показатели2.Показатель.Предопределенный		КАК Показатель2Предопределенный,
	|	Показатели2.Показатель.Наименование			КАК Показатель2Наименование,
	|	Показатели2.Показатель.ТипПоказателя		КАК Показатель2ТипПоказателя,
	|	Показатели2.Показатель.ВозможностьИзменения КАК Показатель2ВозможностьИзменения,
	|	Показатели3.Показатель.Предопределенный		КАК Показатель3Предопределенный,
	|	Показатели3.Показатель.Наименование			КАК Показатель3Наименование,
	|	Показатели3.Показатель.ТипПоказателя 		КАК Показатель3ТипПоказателя,
	|	Показатели3.Показатель.ВозможностьИзменения КАК Показатель3ВозможностьИзменения,
	|	Показатели4.Показатель.Предопределенный		КАК Показатель4Предопределенный,
	|	Показатели4.Показатель.Наименование 		КАК Показатель4Наименование,
	|	Показатели4.Показатель.ТипПоказателя 		КАК Показатель4ТипПоказателя,
	|	Показатели4.Показатель.ВозможностьИзменения КАК Показатель4ВозможностьИзменения,
	|	Показатели5.Показатель.Предопределенный		КАК Показатель5Предопределенный,
	|	Показатели5.Показатель.Наименование 		КАК Показатель5Наименование,
	|	Показатели5.Показатель.ТипПоказателя 		КАК Показатель5ТипПоказателя,
	|	Показатели5.Показатель.ВозможностьИзменения КАК Показатель5ВозможностьИзменения,
	|	Показатели6.Показатель.Предопределенный		КАК Показатель6Предопределенный,
	|	Показатели6.Показатель.Наименование 		КАК Показатель6Наименование,
	|	Показатели6.Показатель.ТипПоказателя 		КАК Показатель6ТипПоказателя,
	|	Показатели6.Показатель.ВозможностьИзменения КАК Показатель6ВозможностьИзменения,
	|	Показатели1.ЗапрашиватьПриКадровыхПеремещениях КАК Показатель1ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели2.ЗапрашиватьПриКадровыхПеремещениях КАК Показатель2ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели3.ЗапрашиватьПриКадровыхПеремещениях КАК Показатель3ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели4.ЗапрашиватьПриКадровыхПеремещениях КАК Показатель4ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели5.ЗапрашиватьПриКадровыхПеремещениях КАК Показатель5ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели6.ЗапрашиватьПриКадровыхПеремещениях КАК Показатель6ЗапрашиватьПриКадровыхПеремещениях
	|ПОМЕСТИТЬ ВТПоказателиУдержаний
	|ИЗ
	|	Документ.ВводСведенийОПлановыхУдержанияхРаботниковОрганизаций.Удержания КАК Удержания
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УдержанияОрганизаций.Показатели КАК Показатели
	|		ПО Удержания.ВидРасчета = Показатели.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УдержанияОрганизаций.Показатели КАК Показатели1
	|		ПО Удержания.ВидРасчета = Показатели1.Ссылка
	|			И (Показатели1.НомерСтроки = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УдержанияОрганизаций.Показатели КАК Показатели2
	|		ПО Удержания.ВидРасчета = Показатели2.Ссылка
	|			И (Показатели2.НомерСтроки = 2)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УдержанияОрганизаций.Показатели КАК Показатели3
	|		ПО Удержания.ВидРасчета = Показатели3.Ссылка
	|			И (Показатели3.НомерСтроки = 3)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УдержанияОрганизаций.Показатели КАК Показатели4
	|		ПО Удержания.ВидРасчета = Показатели4.Ссылка
	|			И (Показатели4.НомерСтроки = 4)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УдержанияОрганизаций.Показатели КАК Показатели5
	|		ПО Удержания.ВидРасчета = Показатели5.Ссылка
	|			И (Показатели5.НомерСтроки = 5)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовРасчета.УдержанияОрганизаций.Показатели КАК Показатели6
	|		ПО Удержания.ВидРасчета = Показатели6.Ссылка
	|			И (Показатели6.НомерСтроки = 6)
	|ГДЕ
	|	Удержания.Ссылка = &ДокументСсылка
	|
	|СГРУППИРОВАТЬ ПО
	|	Удержания.ВидРасчета,
	|	Показатели1.Показатель,
	|	Показатели2.Показатель,
	|	Показатели3.Показатель,
	|	Показатели4.Показатель,
	|	Показатели5.Показатель,
	|	Показатели6.Показатель,
	|	Показатели1.ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели2.ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели3.ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели4.ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели5.ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели6.ЗапрашиватьПриКадровыхПеремещениях
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ВидРасчета";		

	Запрос.Выполнить();	
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Удержания.НомерСтроки,
	|	Удержания.Действие,
	|	Удержания.ВидРасчета,
	|	Удержания.ВидРасчета.СпособРасчета КАК СпособРасчета,
	|	Удержания.ВидРасчета.ПроизвольнаяФормулаРасчета КАК ПроизвольнаяФормулаРасчета,
	|	Удержания.Показатель1,
	|	Удержания.Валюта1,
	|	Удержания.Показатель2,
	|	Удержания.Валюта2,
	|	Удержания.Показатель3,
	|	Удержания.Валюта3,
	|	Удержания.Показатель4,
	|	Удержания.Валюта4,
	|	Удержания.Показатель5,
	|	Удержания.Валюта5,
	|	Удержания.Показатель6,
	|	Удержания.Валюта6,
	|	ЕСТЬNULL(Показатели.КоличествоПоказателей, 0) КАК КоличествоПоказателей,
	|	Показатели.Показатель1Предопределенный,
	|	Показатели.Показатель1Наименование,
	|	Показатели.Показатель1ТипПоказателя,
	|	Показатели.Показатель1ВозможностьИзменения,
	|	Показатели.Показатель2Предопределенный,
	|	Показатели.Показатель2Наименование,
	|	Показатели.Показатель2ТипПоказателя,
	|	Показатели.Показатель2ВозможностьИзменения,
	|	Показатели.Показатель3Предопределенный,
	|	Показатели.Показатель3Наименование,
	|	Показатели.Показатель3ТипПоказателя,
	|	Показатели.Показатель3ВозможностьИзменения,
	|	Показатели.Показатель4Предопределенный,
	|	Показатели.Показатель4Наименование,
	|	Показатели.Показатель4ТипПоказателя,
	|	Показатели.Показатель4ВозможностьИзменения,
	|	Показатели.Показатель5Предопределенный,
	|	Показатели.Показатель5Наименование,
	|	Показатели.Показатель5ТипПоказателя,
	|	Показатели.Показатель5ВозможностьИзменения,
	|	Показатели.Показатель6Предопределенный,
	|	Показатели.Показатель6Наименование,
	|	Показатели.Показатель6ТипПоказателя,
	|	Показатели.Показатель6ВозможностьИзменения,
	|	Удержания.ДокументОснование,
	|	ВЫБОР
	|		КОГДА Удержания.Ссылка.ДатаНачала > ПлановыеУдержания.ПериодЗавершения
	|				И ПлановыеУдержания.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ПлановыеУдержания.Показатель1Завершения
	|		ИНАЧЕ ПлановыеУдержания.Показатель1
	|	КОНЕЦ КАК Показатель1Завершения,
	|	ВЫБОР
	|		КОГДА Удержания.Ссылка.ДатаНачала > ПлановыеУдержания.ПериодЗавершения
	|				И ПлановыеУдержания.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ПлановыеУдержания.Валюта1Завершения
	|		ИНАЧЕ ПлановыеУдержания.Валюта1
	|	КОНЕЦ КАК Валюта1Завершения,
	|	ВЫБОР
	|		КОГДА Удержания.Ссылка.ДатаНачала > ПлановыеУдержания.ПериодЗавершения
	|				И ПлановыеУдержания.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ПлановыеУдержания.Показатель2Завершения
	|		ИНАЧЕ ПлановыеУдержания.Показатель2
	|	КОНЕЦ КАК Показатель2Завершения,
	|	ВЫБОР
	|		КОГДА Удержания.Ссылка.ДатаНачала > ПлановыеУдержания.ПериодЗавершения
	|				И ПлановыеУдержания.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ПлановыеУдержания.Валюта2Завершения
	|		ИНАЧЕ ПлановыеУдержания.Валюта2
	|	КОНЕЦ КАК Валюта2Завершения,
	|	ВЫБОР
	|		КОГДА Удержания.Ссылка.ДатаНачала > ПлановыеУдержания.ПериодЗавершения
	|				И ПлановыеУдержания.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ПлановыеУдержания.Показатель3Завершения
	|		ИНАЧЕ ПлановыеУдержания.Показатель3
	|	КОНЕЦ КАК Показатель3Завершения,
	|	ВЫБОР
	|		КОГДА Удержания.Ссылка.ДатаНачала > ПлановыеУдержания.ПериодЗавершения
	|				И ПлановыеУдержания.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ПлановыеУдержания.Валюта3Завершения
	|		ИНАЧЕ ПлановыеУдержания.Валюта3
	|	КОНЕЦ КАК Валюта3Завершения,
	|	ВЫБОР
	|		КОГДА Удержания.Ссылка.ДатаНачала > ПлановыеУдержания.ПериодЗавершения
	|				И ПлановыеУдержания.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ПлановыеУдержания.Показатель4Завершения
	|		ИНАЧЕ ПлановыеУдержания.Показатель4
	|	КОНЕЦ КАК Показатель4Завершения,
	|	ВЫБОР
	|		КОГДА Удержания.Ссылка.ДатаНачала > ПлановыеУдержания.ПериодЗавершения
	|				И ПлановыеУдержания.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ПлановыеУдержания.Валюта4Завершения
	|		ИНАЧЕ ПлановыеУдержания.Валюта4
	|	КОНЕЦ КАК Валюта4Завершения,
	|	ВЫБОР
	|		КОГДА Удержания.Ссылка.ДатаНачала > ПлановыеУдержания.ПериодЗавершения
	|				И ПлановыеУдержания.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ПлановыеУдержания.Показатель5Завершения
	|		ИНАЧЕ ПлановыеУдержания.Показатель5
	|	КОНЕЦ КАК Показатель5Завершения,
	|	ВЫБОР
	|		КОГДА Удержания.Ссылка.ДатаНачала > ПлановыеУдержания.ПериодЗавершения
	|				И ПлановыеУдержания.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ПлановыеУдержания.Валюта5Завершения
	|		ИНАЧЕ ПлановыеУдержания.Валюта5
	|	КОНЕЦ КАК Валюта5Завершения,
	|	ВЫБОР
	|		КОГДА Удержания.Ссылка.ДатаНачала > ПлановыеУдержания.ПериодЗавершения
	|				И ПлановыеУдержания.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ПлановыеУдержания.Показатель6Завершения
	|		ИНАЧЕ ПлановыеУдержания.Показатель6
	|	КОНЕЦ КАК Показатель6Завершения,
	|	ВЫБОР
	|		КОГДА Удержания.Ссылка.ДатаНачала > ПлановыеУдержания.ПериодЗавершения
	|				И ПлановыеУдержания.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА ПлановыеУдержания.Валюта6Завершения
	|		ИНАЧЕ ПлановыеУдержания.Валюта6
	|	КОНЕЦ КАК Валюта6Завершения,
	|	ДвойныеСтроки.КонфликтнаяСтрокаНомер,
	|	Показатели.Показатель1ЗапрашиватьПриКадровыхПеремещениях КАК Показатель1ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели.Показатель2ЗапрашиватьПриКадровыхПеремещениях КАК Показатель2ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели.Показатель3ЗапрашиватьПриКадровыхПеремещениях КАК Показатель3ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели.Показатель4ЗапрашиватьПриКадровыхПеремещениях КАК Показатель4ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели.Показатель5ЗапрашиватьПриКадровыхПеремещениях КАК Показатель5ЗапрашиватьПриКадровыхПеремещениях,
	|	Показатели.Показатель6ЗапрашиватьПриКадровыхПеремещениях КАК Показатель6ЗапрашиватьПриКадровыхПеремещениях
	|ИЗ
	|	Документ.ОтпускПоУходуЗаРебенком.ПлановыеУдержания КАК Удержания
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			Удержания.НомерСтроки КАК НомерСтроки,
	|			МИНИМУМ(ПовторяющиесяСтроки.НомерСтроки) КАК КонфликтнаяСтрокаНомер
	|		ИЗ
	|			Документ.ОтпускПоУходуЗаРебенком.ПлановыеУдержания КАК Удержания
	|				ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтпускПоУходуЗаРебенком.ПлановыеУдержания КАК ПовторяющиесяСтроки
	|				ПО Удержания.Ссылка = ПовторяющиесяСтроки.Ссылка
	|					И Удержания.НомерСтроки < ПовторяющиесяСтроки.НомерСтроки
	|					И Удержания.ВидРасчета = ПовторяющиесяСтроки.ВидРасчета
	|					И Удержания.ДокументОснование = ПовторяющиесяСтроки.ДокументОснование
	|		ГДЕ
	|			Удержания.Ссылка = &ДокументСсылка
	|		
	|		СГРУППИРОВАТЬ ПО
	|			Удержания.НомерСтроки) КАК ДвойныеСтроки
	|		ПО Удержания.НомерСтроки = ДвойныеСтроки.НомерСтроки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеУдержанияРаботниковОрганизаций.СрезПоследних(
	|				&Дата,
	|				Физлицо = &Физлицо
	|					И Организация = &ГоловнаяОрганизация) КАК ПлановыеУдержания
	|		ПО Удержания.ВидРасчета = ПлановыеУдержания.ВидРасчета
	|			И Удержания.ДокументОснование = ПлановыеУдержания.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТПоказателиУдержаний КАК Показатели
	|		ПО Удержания.ВидРасчета = Показатели.ВидРасчета
	|ГДЕ
	|	Удержания.Ссылка = &ДокументСсылка
	|	И Удержания.Действие <> ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.НеИзменять)";

	Запрос.Текст = ТекстЗапроса;

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоПлановымУдержаниям()

// Формирует запрос по таблице "РасчетСреднего" документа
//
// Параметры: 
//  Режим - режим проведения
//
// Возвращаемое значение:
//  Результат запроса.
//
Функция СформироватьЗапросПоРасчетСреднего()

	Запрос = Новый Запрос;
	
	// Установим параметры запроса
	Запрос.УстановитьПараметр("ДокументСсылка", Ссылка);

	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтрокаРасчетСреднего.НомерСтроки,
	|	ВЫБОР
	|		КОГДА СтрокаРасчетСреднего.Ссылка.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА СтрокаРасчетСреднего.Ссылка.Организация
	|		ИНАЧЕ СтрокаРасчетСреднего.Ссылка.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК Организация,
	|	СтрокаРасчетСреднего.ВидРасчета,
	|	СтрокаРасчетСреднего.БазовыйПериодНачало КАК БазовыйПериодНачало,
	|	ВЫБОР
	|		КОГДА СтрокаРасчетСреднего.БазовыйПериодКонец <> ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|			ТОГДА КОНЕЦПЕРИОДА(СтрокаРасчетСреднего.БазовыйПериодКонец, ДЕНЬ)
	|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1, 0, 0, 0)
	|	КОНЕЦ КАК БазовыйПериодКонец,
	|	1 КАК КоэффициентИндексации,
	|	СтрокаРасчетСреднего.Результат,
	|	СтрокаРасчетСреднего.ОтработаноПоПятидневке,
	|	СтрокаРасчетСреднего.НормаПоПятидневке,
	|	СтрокаРасчетСреднего.ОтработаноДней,
	|	0 КАК ОтработаноЧасов,
	|	СтрокаРасчетСреднего.ЧислоМесяцев,
	|	СтрокаРасчетСреднего.Сотрудник,
	|	СтрокаРасчетСреднего.Сотрудник.Физлицо КАК Физлицо
	|ИЗ
	|	Документ.ОтпускПоУходуЗаРебенком.РасчетСреднего КАК СтрокаРасчетСреднего
	|ГДЕ
	|	СтрокаРасчетСреднего.Ссылка = &ДокументСсылка";

	Возврат Запрос.Выполнить();

КонецФункции // СформироватьЗапросПоРасчетСреднего()

// Проверяет правильность заполнения шапки документа.
// Если какой-то из реквизтов шапки, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по шапке,
// все проверяемые реквизиты должны быть включены в выборку по шапке.
//
// Параметры:
//	ВыборкаПоШапкеДокумента	- выборка из результата запроса по шапке документа,
//	Отказ					- флаг отказа в проведении,
//	Заголовок				- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок, ПриПроведении = Истина)

	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Организация) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана организация!", Отказ, Заголовок);
	КонецЕсли;
	
	// Сотрудник
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.Сотрудник) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не выбран работник!", Отказ, Заголовок);
	КонецЕсли;

	// ВнутреннийСовместитель
	Если ВыборкаПоШапкеДокумента.ВнутреннийСовместитель Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Документ не может быть оформлен на внутреннего совместителя!", Отказ, Заголовок);
	КонецЕсли;
	
	// Организация сотрудника должна совпадать с организацией документа
	Если ВыборкаПоШапкеДокумента.ОшибкаНеСоответствиеСотрудникаИОрганизации Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Указанный сотрудник оформлен на другую организацию!", Отказ, Заголовок);
	КонецЕсли;
	
	// Период отпуска
	Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ДатаНачала) Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана дата начала отпуска!", Отказ, Заголовок);
	ИначеЕсли ВыборкаПоШапкеДокумента.ДатаОкончания < ВыборкаПоШапкеДокумента.ДатаНачала Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Дата окончания отпуска не должна быть меньше даты начала!", Отказ, Заголовок);
	КонецЕсли; 
	
	Если ПриПроведении Тогда
		Если ВыборкаПоШапкеДокумента.ВыплачиватьПособиеДоПолутораЛет Тогда
			Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ДатаОкончанияПособияДоПолутораЛет) Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Не указана дата окончания выплаты пособия до полутора лет!", Отказ, Заголовок);
			ИначеЕсли ВыборкаПоШапкеДокумента.ДатаОкончанияПособияДоПолутораЛет > ВыборкаПоШапкеДокумента.ДатаОкончания Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Дата окончания выплаты пособия до полутора лет превышает дату окончания отпуска, будет использована дата окончания отпуска.");
			ИначеЕсли ВыборкаПоШапкеДокумента.ДатаОкончанияПособияДоПолутораЛет < ВыборкаПоШапкеДокумента.ДатаНачала Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Дата окончания выплаты пособия до полутора лет не должна быть меньше даты начала отпуска!", Отказ, Заголовок);
			КонецЕсли;
			//Если ВыборкаПоШапкеДокумента.КоличествоДетей = 0 Тогда
			//	ОбщегоНазначения.СообщитьОбОшибке("Не указано количество детей, за которыми осуществляется уход!", Отказ, Заголовок);
			//КонецЕсли;
		КонецЕсли;
		Если ВыборкаПоШапкеДокумента.ВыплачиватьПособиеДоТрехЛет Тогда
			Если НЕ ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ДатаОкончанияПособияДоТрехЛет) Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Не указана дата окончания выплаты пособия до трех лет!", Отказ, Заголовок);
			ИначеЕсли ВыборкаПоШапкеДокумента.ДатаОкончанияПособияДоТрехЛет > ВыборкаПоШапкеДокумента.ДатаОкончания Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Дата окончания выплаты пособия до трех лет превышает дату окончания отпуска, будет использована дата окончания отпуска.");
			ИначеЕсли ВыборкаПоШапкеДокумента.ДатаОкончанияПособияДоТрехЛет < ВыборкаПоШапкеДокумента.ДатаНачала Тогда
				ОбщегоНазначения.СообщитьОбОшибке("Дата окончания выплаты пособия до трех лет не должна быть меньше даты начала отпуска!", Отказ, Заголовок);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеШапки()

// Проверяет правильность заполнения реквизитов в строке ТЧ "ПлановыеНачисления" документа
// Если какой-то из реквизтов, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по строке ТЧ документа,
// все проверяемые реквизиты должны быть включены в выборку.
//
// Параметры:
//	ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//	ВыборкаПоСтрокамДокумента	- спозиционированная на определенной строке выборка 
//								  из результата запроса по товарам документа, 
//	Отказ						- флаг отказа в проведении,
//	Заголовок					- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеСтрокиОсновныеНачисления(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, Отказ, Заголовок, ПроверкаШтатногоРасписания, ПроверятьМестоРаботы, ПроверятьВалюту)
	
	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) +
	""" табл. части ""Основные начисления"": ";

	НетВР = НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ВидРасчета);
	НетСотрудника = ПроверятьМестоРаботы И НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Сотрудник);
	НетДействия = НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Действие);
	
	Если НетВР И Не НетДействия Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указано начисление!", Отказ, Заголовок);
	КонецЕсли;
	
	// Действие
	Если НетДействия Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указано действие!", Отказ, Заголовок);
	КонецЕсли;
	
	// ВидРасчета
	Если Не НетВР И Не НетДействия Тогда
		
		// Соответствие размера "основного" начисления штатному расписанию
		Если ПроверкаШтатногоРасписания И НЕ ВыборкаПоСтрокамДокумента.РазмерСоответствуетШТР и Не ВыборкаПоСтрокамДокумента.ТребуетВводаТарифногоРазряда Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "размер основного начисления не соответствует штатному расписанию!", Отказ, Заголовок);
		КонецЕсли;
		
		Если ВыборкаПоСтрокамДокумента.Действие = Перечисления.ВидыДействияСНачислением.Начать или ВыборкаПоСтрокамДокумента.Действие = Перечисления.ВидыДействияСНачислением.Изменить Тогда
			ИспользуютсяНачисленияВВалюте = ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "ИспользуютсяНачисленияВВалюте");
			ПроведениеРасчетов.ПроверкаПоказателейВПлановыхНачислениях(ВыборкаПоСтрокамДокумента, СтрокаНачалаСообщенияОбОшибке, Истина, Отказ, Заголовок, ИспользуютсяНачисленияВВалюте, СоответствиеВалютныеСпособыРасчета);
		КонецЕсли;
		
		Если Не НетСотрудника и ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДвойнаяСтрока) Тогда
			Если ВыборкаПоСтрокамДокумента.ОсновноеНачисление Тогда
				СтрокаСообщениеОбОшибке = "основное начисление работника следует редактировать в одной строке (см. строку " + ВыборкаПоСтрокамДокумента.ДвойнаяСтрока + ")!"; 
			Иначе
				СтрокаСообщениеОбОшибке = "работнику не может быть назначено одно и тоже начисление дважды (см. строку " + ВыборкаПоСтрокамДокумента.ДвойнаяСтрока + ")!"; 
			КонецЕсли;
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + СтрокаСообщениеОбОшибке, Отказ, Заголовок);
		КонецЕсли;
		
	КонецЕсли;
	
	// Сотрудник
	Если НетСотрудника Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указано рабочее место работника!", Отказ, Заголовок);
	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеСтрокиОсновныеНачисления()

// Проверяет правильность заполнения реквизитов в строке ТЧ "ПлановыеУдержания" документа
// Если какой-то из реквизтов, влияющий на проведение не заполнен или
// заполнен не корректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса по строке ТЧ документа,
// все проверяемые реквизиты должны быть включены в выборку.
//
// Параметры: 
//	ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//	ВыборкаПоСтрокамДокумента	- спозиционированная на определенной строке выборка 
//								  из результата запроса по товарам документа, 
//	Отказ						- флаг отказа в проведении,
//	Заголовок					- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеСтрокиПлановыхУдержаний(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, Отказ, Заголовок)
	
	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) +
	""" табл. части ""Удержания"": ";

	// ВидРасчета
	НетВР = НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ВидРасчета);
	НетДействия = НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Действие);
	
	Если НетВР Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указан вид удержания!", Отказ, Заголовок);
	КонецЕсли;
	
	// Действие
	Если НетДействия Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указано действие с удержанием!", Отказ, Заголовок);
	КонецЕсли;
	
	Если Не НетВР И Не НетДействия Тогда	
	
		// Одинаковые строки
		Если ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.КонфликтнаяСтрокаНомер) Тогда
			ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + " обнаружено повторное назначение того же удержания в строке №" + ВыборкаПоСтрокамДокумента.КонфликтнаяСтрокаНомер + "!", Отказ, Заголовок);
		КонецЕсли;
		
		ИспользуютсяНачисленияВВалюте = ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "ИспользуютсяНачисленияВВалюте");
		
		ПроведениеРасчетов.ПроверкаПоказателейВПлановыхНачислениях(ВыборкаПоСтрокамДокумента, СтрокаНачалаСообщенияОбОшибке, Ложь, Отказ, Заголовок, ИспользуютсяНачисленияВВалюте, СоответствиеВалютныеСпособыРасчета);

		// Основание
		//Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ДокументОснование) Тогда
		//	ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указано основание удержания!", Отказ, Заголовок);
		//КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // ПроверитьЗаполнениеСтрокиПлановыхУдержаний()

// Проверяет правильность заполнения реквизитов в строке ТЧ "Начисления" документа.
// Если какой-то из реквизтов, влияющий на проведение, не заполнен или
// заполнен некорректно, то выставляется флаг отказа в проведении.
// Проверка выполняется по выборке из результата запроса к ТЧ документа,
// все проверяемые реквизиты должны быть включены в выборку.
//
// Параметры: 
//  ВыборкаПоШапкеДокумента		- выборка из результата запроса по шапке документа,
//  ВыборкаПоСтрокамДокумента	- спозиционированная на определенной строке выборка 
//  							  из результата запроса к ТЧ документа, 
//  Отказ 						- флаг отказа в проведении,
//	Заголовок				- Заголовок для сообщений об ошибках проведения.
//
Процедура ПроверитьЗаполнениеСтрокиРасчетСреднего(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, Отказ, Заголовок, ПроверятьМестоРаботы)

	СтрокаНачалаСообщенияОбОшибке = "В строке номер """+ СокрЛП(ВыборкаПоСтрокамДокумента.НомерСтроки) +
									""" табл. части ""Расчет среднего"": ";
	
	// Вид расчета
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.ВидРасчета) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указан вид расчета среднего заработка!", Отказ, Заголовок);
	КонецЕсли;
	
	// Дата начала базового периода
	Если НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.БазовыйПериодНачало) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указана дата начала базового периода!", Отказ, Заголовок);
		// Дата окончания базового периода
	ИначеЕсли НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.БазовыйПериодКонец) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указана дата окончания базового периода!", Отказ, Заголовок);
	ИначеЕсли ВыборкаПоСтрокамДокумента.БазовыйПериодКонец < ВыборкаПоСтрокамДокумента.БазовыйПериодНачало Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "дата начала базового периода не может быть больше даты окончания базового периода!", Отказ, Заголовок);
	КонецЕсли; 
	
	// Сотрудник
	Если ПроверятьМестоРаботы И НЕ ЗначениеЗаполнено(ВыборкаПоСтрокамДокумента.Сотрудник) Тогда
		ОбщегоНазначения.СообщитьОбОшибке(СтрокаНачалаСообщенияОбОшибке + "не указано рабочее место работника!", Отказ, Заголовок);
	КонецЕсли;
	
КонецПроцедуры // ПроверитьЗаполнениеСтрокиРасчетСреднего()

// По строке выборки результата запроса по документу формируем движения по регистру ПлановыеНачисленияРаботниковОрганизаций
//
// Параметры: 
//  ВыборкаПоШапкеДокумента 	- выборка из результата запроса по шапке документа,
//  ВыборкаПоРаботникиОрганизации 	- выборка из результата запроса по табличной чавсти документа,
//
// Возвращаемое значение:
//  Нет.
//
Процедура ДобавитьСтрокуОтпуска(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента)
	
	Движение = Движения.ПлановыеНачисленияРаботниковОрганизаций.Добавить();
	
	// Свойства
	Движение.Период					= ВыборкаПоШапкеДокумента.ДатаНачала;
	
	// Измерения
	Движение.Сотрудник				= ВыборкаПоСтрокамДокумента.Сотрудник;
	Движение.Организация			= ВыборкаПоСтрокамДокумента.Организация;
	Движение.ВидРасчетаИзмерение	= ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ОтпускПоУходуЗаРебенкомБезОплаты;
	
	// Ресурсы
	Движение.Действие				= Перечисления.ВидыДействияСНачислением.Начать;
	Движение.ВидРасчета				= ПланыВидовРасчета.ОсновныеНачисленияОрганизаций.ОтпускПоУходуЗаРебенкомБезОплаты;
	
	Движение.ПериодЗавершения		= ВыборкаПоШапкеДокумента.ДатаОкончания + мДлинаСуток;
	Движение.ДействиеЗавершения		= Перечисления.ВидыДействияСНачислением.Прекратить;
	Движение.ВидРасчетаЗавершения	= Движение.ВидРасчета;
	
	Движение = Движения.СостояниеРаботниковОрганизаций.Добавить();
	
	// Свойства
	Движение.Период					= ВыборкаПоШапкеДокумента.ДатаНачала;
	
	// Измерения
	Движение.Сотрудник				= ВыборкаПоСтрокамДокумента.Сотрудник;
	Движение.Организация			= ВыборкаПоСтрокамДокумента.Организация;
	
	// Ресурсы
	Движение.Состояние				= Перечисления.СостоянияРаботникаОрганизации.ОтпускПоУходуЗаРебенком;
	
	Движение.ПериодЗавершения		= ВыборкаПоШапкеДокумента.ДатаОкончания + мДлинаСуток;
	Движение.СостояниеЗавершения	= Перечисления.СостоянияРаботникаОрганизации.Работает;
	
	Если ВыборкаПоШапкеДокумента.ОсвобождатьСтавку Тогда
		
		Движение = Движения.ЗанятыеШтатныеЕдиницыОрганизаций.Добавить();
		
		// Свойства
		Движение.Период						= ВыборкаПоШапкеДокумента.ДатаНачала;
		Движение.ВидДвижения				= ВидДвиженияНакопления.Расход;
		
		// Измерения
		Движение.ПодразделениеОрганизации	= ВыборкаПоСтрокамДокумента.ПодразделениеОрганизации;
		Движение.Должность					= ВыборкаПоСтрокамДокумента.Должность;
		Движение.УсловияТрудаИзмерение		= ВыборкаПоСтрокамДокумента.УсловияТрудаИзмерение;
		Движение.ВидДеятельностиИзмерение	= ВыборкаПоСтрокамДокумента.ВидДеятельностиИзмерение;
		
		// Ресурсы
		Движение.КоличествоСтавок			= ВыборкаПоСтрокамДокумента.ЗанимаемыхСтавок; 
		
		Если ЗначениеЗаполнено(ВыборкаПоШапкеДокумента.ДатаОкончания) Тогда
			
			Движение = Движения.ЗанятыеШтатныеЕдиницыОрганизаций.Добавить();
			
			// Свойства
			Движение.Период					= ВыборкаПоШапкеДокумента.ДатаОкончания + мДлинаСуток;
			Движение.ВидДвижения			= ВидДвиженияНакопления.Приход;
			
			// Измерения
			Движение.ПодразделениеОрганизации	= ВыборкаПоСтрокамДокумента.ПодразделениеОрганизации;
			Движение.Должность					= ВыборкаПоСтрокамДокумента.Должность;
			
			// Ресурсы
			Движение.КоличествоСтавок		= ВыборкаПоСтрокамДокумента.ЗанимаемыхСтавок;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ДобавитьСтрокуОтпуска

// По строке выборки результата запроса по документу формируем движения по регистру ПлановыеНачисленияРаботниковОрганизаций
//
// Параметры:
//	ВыборкаПоШапкеДокумента			- выборка из результата запроса по шапке документа,
//	ВыборкаПоРаботникиОрганизации	- выборка из результата запроса по табличной чавсти документа,
//
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуОсновныеНачисления(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента)

	Если ВыборкаПоСтрокамДокумента.Действие = Перечисления.ВидыДействияСНачислением.НеИзменять Тогда
		Возврат;
	КонецЕсли;
	
	// Движения по регистру "ПлановыеНачисленияРаботниковОрганизаций"
	ПроцедурыУправленияПерсоналом.СформироватьДвижениеПоПлановымНачислениям(Движения.ПлановыеНачисленияРаботниковОрганизаций.Добавить(), ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента);
	
КонецПроцедуры // ДобавитьСтрокуОсновныеНачисления()

// По строке выборки результата запроса по документу формируем движения по регистру ПлановыеУдержанияРаботниковОрганизаций
//
// Параметры: 
//	ВыборкаПоШапкеДокумента			- выборка из результата запроса по шапке документа,
//	ВыборкаПоРаботникиОрганизации	- выборка из результата запроса по табличной чавсти документа,
//
// Возвращаемое значение:
//	Нет.
//
Процедура ДобавитьСтрокуПлановыхУдержаний(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента)

	Если ВыборкаПоСтрокамДокумента.Действие = Перечисления.ВидыДействияСНачислением.НеИзменять Тогда
		Возврат;
	КонецЕсли;
	
	Движение = Движения.ПлановыеУдержанияРаботниковОрганизаций.Добавить();
	
	// Свойства
	Движение.Период							= ВыборкаПоШапкеДокумента.ДатаНачала;

	// Измерения
	Движение.ФизЛицо						= ВыборкаПоШапкеДокумента.ФизЛицо;
	Движение.Организация					= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
	Движение.ВидРасчета						= ВыборкаПоСтрокамДокумента.ВидРасчета;
	Движение.ДокументОснование				= ВыборкаПоСтрокамДокумента.ДокументОснование;
	
	// Ресурсы
	СтрокаРесурсов = "Действие,ВидРасчета";
	Движение.Действие						= ВыборкаПоСтрокамДокумента.Действие;
	Если ВыборкаПоСтрокамДокумента.Действие <> Перечисления.ВидыДействияСНачислением.Прекратить Тогда
		СтрокаРесурсов = СтрокаРесурсов 
		+ ",Показатель1,Валюта1,Показатель2,Валюта2,Показатель3,Валюта3,Показатель4,Валюта4,Показатель5,Валюта5,Показатель6,Валюта6";
	КонецЕсли;
	
	Движение.ПериодЗавершения			= ВыборкаПоШапкеДокумента.ДатаОкончания + мДлинаСуток;
	Если ВыборкаПоСтрокамДокумента.Действие = Перечисления.ВидыДействияСНачислением.Прекратить Тогда	
		Движение.ДействиеЗавершения	= Перечисления.ВидыДействияСНачислением.Начать
	ИначеЕсли ВыборкаПоСтрокамДокумента.Действие = Перечисления.ВидыДействияСНачислением.Начать Тогда	
		Движение.ДействиеЗавершения	= Перечисления.ВидыДействияСНачислением.Прекратить
	Иначе	
		Движение.ДействиеЗавершения	= ВыборкаПоСтрокамДокумента.Действие;
	КонецЕсли;
	
	Если ВыборкаПоСтрокамДокумента.Действие <> Перечисления.ВидыДействияСНачислением.Начать Тогда
		СтрокаРесурсов = СтрокаРесурсов 
		+ ",Показатель1Завершения,Валюта1Завершения,Показатель2Завершения,Валюта2Завершения,Показатель3Завершения,Валюта3Завершения,Показатель4Завершения,Валюта4Завершения,Показатель5Завершения,Валюта5Завершения,Показатель6Завершения,Валюта6Завершения";
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(Движение,ВыборкаПоСтрокамДокумента,СтрокаРесурсов);
	
КонецПроцедуры // ДобавитьСтрокуПлановыхУдержаний()

// По строке выборок из результатов запроса по документу формируем движения по регистру
//
// Параметры: 
//  ВыборкаПоСтрокамДокумента				- спозиционированная на определенной строке выборка 
//				  							  из результата запроса к ТЧ документа, 
// Возвращаемое значение:
//  Нет.
//
Процедура ДобавитьСтрокуРасчетаСреднего(ВыборкаПоШапкеДокумента, ВыборкаПоСтрокамДокумента, НаборОсновныеНачисления)
	
	СуммаРесурсов = ВыборкаПоСтрокамДокумента.Результат + ВыборкаПоСтрокамДокумента.ОтработаноПоПятидневке +
		ВыборкаПоСтрокамДокумента.НормаПоПятидневке + ВыборкаПоСтрокамДокумента.ОтработаноДней + ВыборкаПоСтрокамДокумента.ОтработаноЧасов;
		
	Если СуммаРесурсов <> 0 Тогда
		
		Движение = НаборОсновныеНачисления.Добавить();
		
		// Свойства
		Движение.ПериодРегистрации				= ВыборкаПоШапкеДокумента.ДатаНачала;
		Движение.БазовыйПериодНачало			= ВыборкаПоСтрокамДокумента.БазовыйПериодНачало;
		Движение.БазовыйПериодКонец				= ВыборкаПоСтрокамДокумента.БазовыйПериодКонец;
		Движение.ВидРасчета						= ВыборкаПоСтрокамДокумента.ВидРасчета;
		
		// Измерения
		Движение.Сотрудник						= ВыборкаПоСтрокамДокумента.Сотрудник;
		Движение.ФизЛицо						= ВыборкаПоСтрокамДокумента.ФизЛицо;
		Движение.Организация					= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
		
		// Ресурсы
		Движение.Результат						= ВыборкаПоСтрокамДокумента.Результат;
		Движение.ОтработаноПоПятидневке			= ВыборкаПоСтрокамДокумента.ОтработаноПоПятидневке;
		Движение.НормаПоПятидневке				= ВыборкаПоСтрокамДокумента.НормаПоПятидневке;
		Движение.ОтработаноДней					= ВыборкаПоСтрокамДокумента.ОтработаноДней;
		Движение.ОтработаноЧасов				= ВыборкаПоСтрокамДокумента.ОтработаноЧасов;
		
		// Реквизиты
		Движение.КоэффициентИндексации 			= ВыборкаПоСтрокамДокумента.КоэффициентИндексации;
		Движение.ДатаНачалаСобытия				= ВыборкаПоШапкеДокумента.ДатаНачала;
		Движение.ДатаНачалаРасчетногоПериода	= ВыборкаПоШапкеДокумента.ПериодРасчетаСреднегоЗаработкаНачало;
		Движение.ДатаОкончанияРасчетногоПериода	= ВыборкаПоШапкеДокумента.ПериодРасчетаСреднегоЗаработкаОкончание;
		Движение.СпособРасчета					= ВыборкаПоШапкеДокумента.СпособРасчета;
		Движение.ПериодРасчетаСреднегоЗаработка	= 12;
		Движение.ЧислоМесяцев					= ВыборкаПоСтрокамДокумента.ЧислоМесяцев;
		
	КонецЕсли; 
	
КонецПроцедуры 

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаПроведения(Отказ, Режим)

	// Заголовок для сообщений об ошибках проведения.
	Заголовок = ОбщегоНазначения.ПредставлениеДокументаПриПроведении(Ссылка);

	СоответствиеВалютныеСпособыРасчета = ПроведениеРасчетов.ПолучитьСоответствиеСпособовРасчетаТребующихВалюту();
	
	Если НЕ ЗначениеЗаполнено(ДатаНачала) и ВидОперации = Перечисления.ВидыОперацийОтпускаПоУходу.ОтпускПоУходуЗаРебенком Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана дата начала отпуска!", Отказ, Заголовок);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаОкончания) и ВидОперации = Перечисления.ВидыОперацийОтпускаПоУходу.ОтпускПоУходуЗаРебенком Тогда
		ОбщегоНазначения.СообщитьОбОшибке("Не указана дата окончания отпуска!", Отказ, Заголовок);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат
	КонецЕсли; 
	
	// Получим реквизиты шапки из запроса
	ВыборкаПоШапкеДокумента = СформироватьЗапросПоШапке().Выбрать();

	Если ВыборкаПоШапкеДокумента.Следующий() Тогда

		Движения.ОсновныеНачисленияРаботниковОрганизаций.мВыполнятьСписаниеФактическогоОтпуска = Истина;
		//Движения.ОсновныеНачисленияРаботниковОрганизаций.мВыполнятьРасчетПериодаНачисленияСотрудника = 
		//	ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации"), Организация, "ПроверкаПериодовНачисления");
		//
		//Надо позвать проверку заполнения реквизитов шапки
		ПроверитьЗаполнениеШапки(ВыборкаПоШапкеДокумента, Отказ, Заголовок);
		
		// Движения стоит добавлять, если в проведении еще не отказано (отказ =ложь)
		Если НЕ Отказ Тогда
			
			УчетнаяПолитикаПоПерсоналуОрганизации = глЗначениеПеременной("глУчетнаяПолитикаПоПерсоналуОрганизации");
			
			ПроверкаШтатногоРасписания = ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(УчетнаяПолитикаПоПерсоналуОрганизации, Организация, "ПроверкаШтатногоРасписания");
			//ПроверятьМестоРаботы = ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(УчетнаяПолитикаПоПерсоналуОрганизации, Организация, "ПоддержкаВнутреннегоСовместительства");
			ПроверятьМестоРаботы = Истина;
			ПроверятьВалюту = ПроцедурыУправленияПерсоналом.ЗначениеУчетнойПолитикиПоПерсоналуОрганизации(УчетнаяПолитикаПоПерсоналуОрганизации, Организация, "ИспользуютсяНачисленияВВалюте");
			
			Если ВидОперации = Перечисления.ВидыОперацийОтпускаПоУходу.ОтпускПоУходуЗаРебенком тогда
				
					
			Если ВыборкаПоШапкеДокумента.ВыплачиватьПособиеДоТрехЛет Тогда
				
				Движение = Движения.ПлановыеНачисленияРаботниковОрганизаций.Добавить();
				
				// Свойства
				Движение.Период						= ВыборкаПоШапкеДокумента.ДатаНачала;
				
				// Измерения
				Движение.Сотрудник					= ВыборкаПоШапкеДокумента.Сотрудник;
				Движение.Организация				= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
				Движение.ВидРасчетаИзмерение		= ВыборкаПоШапкеДокумента.ВидРасчетаДоТрехЛет;
				
				// Ресурсы
				Движение.Действие					= Перечисления.ВидыДействияСНачислением.Начать;
				Движение.ВидРасчета					= ВыборкаПоШапкеДокумента.ВидРасчетаДоТрехЛет;
				Движение.Показатель1				= ВыборкаПоШапкеДокумента.ПроцентПособияДоТрехЛет;
				
				Движение.ПериодЗавершения			= Мин(ВыборкаПоШапкеДокумента.ДатаОкончания,ВыборкаПоШапкеДокумента.ДатаОкончанияПособияДоТрехЛет) + 86400;
				Движение.ДействиеЗавершения			= Перечисления.ВидыДействияСНачислением.Прекратить;
				Движение.ВидРасчетаЗавершения		= Движение.ВидРасчета;

			КонецЕсли;
			
			Если ВыборкаПоШапкеДокумента.ВыплачиватьПособиеДоТрехЛетНаВторогоРебенка Тогда
				
				Движение = Движения.ПлановыеНачисленияРаботниковОрганизаций.Добавить();
				
				// Свойства
				Движение.Период						= ВыборкаПоШапкеДокумента.ДатаНачала;
				
				// Измерения
				Движение.Сотрудник					= ВыборкаПоШапкеДокумента.Сотрудник;
				Движение.Организация				= ВыборкаПоШапкеДокумента.ГоловнаяОрганизация;
				Движение.ВидРасчетаИзмерение		= ВыборкаПоШапкеДокумента.ВидРасчетаДоТрехЛетНаВторогоРебенка;
				
				// Ресурсы
				Движение.Действие					= Перечисления.ВидыДействияСНачислением.Начать;
				Движение.ВидРасчета					= ВыборкаПоШапкеДокумента.ВидРасчетаДоТрехЛетНаВторогоРебенка;
				Движение.Показатель1				= ВыборкаПоШапкеДокумента.ПроцентПособияДоТрехЛетНаВторогоРебенка;
					
				Движение.ПериодЗавершения			= Мин(ВыборкаПоШапкеДокумента.ДатаОкончания,ВыборкаПоШапкеДокумента.ДатаОкончанияПособияДоТрехЛетНаВторогоРебенка) + 86400;
				Движение.ДействиеЗавершения			= Перечисления.ВидыДействияСНачислением.Прекратить;
				Движение.ВидРасчетаЗавершения		= Движение.ВидРасчета;

			КонецЕсли;

			
			// провести в регистр "состояние работников организаций"
			
			ВыборкаПоМестамРаботы = СформироватьЗапросПоРабочимМестам(ВыборкаПоШапкеДокумента).Выбрать();
			Пока ВыборкаПоМестамРаботы.Следующий() Цикл
				ДобавитьСтрокуОтпуска(ВыборкаПоШапкеДокумента, ВыборкаПоМестамРаботы);
			КонецЦикла;
			
			// получим реквизиты табличной части
			ВыборкаПоНачислениям = СформироватьЗапросПоОсновнымНачислениям(ВыборкаПоШапкеДокумента, ПроверкаШтатногоРасписания).Выбрать();
			Пока ВыборкаПоНачислениям.Следующий() Цикл

				// проверим очередную строку табличной части
				ПроверитьЗаполнениеСтрокиОсновныеНачисления(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям, Отказ, Заголовок, ПроверкаШтатногоРасписания, ПроверятьМестоРаботы, ПроверятьВалюту);

				Если НЕ Отказ Тогда
					ДобавитьСтрокуОсновныеНачисления(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям);
				КонецЕсли;

			КонецЦикла;
			
			// получим реквизиты табличной части
			ВыборкаПоУдержаниям = СформироватьЗапросПоПлановымУдержаниям(ВыборкаПоШапкеДокумента).Выбрать();
			Пока ВыборкаПоУдержаниям.Следующий() Цикл

				// проверим очередную строку табличной части
				ПроверитьЗаполнениеСтрокиПлановыхУдержаний(ВыборкаПоШапкеДокумента, ВыборкаПоУдержаниям, Отказ, Заголовок);

				Если НЕ Отказ Тогда
					ДобавитьСтрокуПлановыхУдержаний(ВыборкаПоШапкеДокумента, ВыборкаПоУдержаниям);
				КонецЕсли;

			КонецЦикла;
			
			Иначе
				
				// получим реквизиты табличной части
			ВыборкаПоНачислениям = СформироватьЗапросПоЕжемесячнымПособиям(ВыборкаПоШапкеДокумента, ПроверкаШтатногоРасписания).Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаПоНачислениям.Следующий() Цикл 

				// проверим очередную строку табличной части
				//ПроверитьЗаполнениеСтрокиОсновныеНачисления(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям, Отказ, Заголовок);

				Если НЕ Отказ Тогда
					ДобавитьСтрокуОсновныеНачисления(ВыборкаПоШапкеДокумента, ВыборкаПоНачислениям);
				КонецЕсли;

			КонецЦикла;
			Конецесли;
			
			// получим реквизиты табличной части
			//ВыборкаПоРасчетСреднего = СформироватьЗапросПоРасчетСреднего().Выбрать();
			//Пока ВыборкаПоРасчетСреднего.Следующий() Цикл
			//	
			//	// проверим очередную строку табличной части
			//	ПроверитьЗаполнениеСтрокиРасчетСреднего(ВыборкаПоШапкеДокумента, ВыборкаПоРасчетСреднего, Отказ, Заголовок, ПроверятьМестоРаботы);
			//	
			//	Если НЕ Отказ Тогда
			//		ДобавитьСтрокуРасчетаСреднего(ВыборкаПоШапкеДокумента, ВыборкаПоРасчетСреднего, Движения.РасчетСреднегоЗаработка);
			//	КонецЕсли;
			//	
			//КонецЦикла;
		
		КонецЕсли;

	КонецЕсли;


КонецПроцедуры // ОбработкаПроведения()

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОПЕРАТОРЫ ОСНОВНОЙ ПРОГРАММЫ

мДлинаСуток = 86400;

