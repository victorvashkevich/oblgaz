////////////////////////////////////////////////////////////////////////////////
// Процедуры печати документа.

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт	
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ДопущениеКРаботе") Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ДопущениеКРаботе", "Приказ о допущении к работе", ПриказОДопущении(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ОтстранениеОтРаботы") Тогда
			УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ОтстранениеОтРаботы", "Приказ об отстранении от работы", ПриказОбОтстранении(МассивОбъектов, ОбъектыПечати));
	КонецЕсли;
	
КонецПроцедуры	

Функция ПриказОДопущении(ДокументСсылка, ОбъектыПечати)
	
	ДокументРезультат = Новый ТабличныйДокумент;

	Макет = Документы.МедицинскийОсмотр.ПолучитьМакет("ДопущениеКРаботе");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);
	Запрос.УстановитьПараметр("ДатаСреза", ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница",ДокументСсылка.Организация);
		
	Запрос.Текст = ФормированиеПечатныхФормЗК.ПолучитьТекстЗапросаПоОтветственнымЛицам(
		"ДатаСреза",
		"ОтветственноеЛицо = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.Руководитель)
		|И СтруктурнаяЕдиница = &СтруктурнаяЕдиница");
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МедицинскийОсмотр.Организация.НаименованиеПолное КАК НаименованиеОрганизации,
	|	МедицинскийОсмотр.Номер КАК КодДокумента,
	|	МедицинскийОсмотр.Дата,
	|	МедицинскийОсмотр.Номер,
	|	ЕСТЬNULL(ФИОФизЛиц.Фамилия, """") КАК Фамилия,
	|	ЕСТЬNULL(ФИОФизЛиц.Имя, """") КАК Имя,
	|	ЕСТЬNULL(ФИОФизЛиц.Отчество, """") КАК Отчество,
	|	ВЫБОР
	|		КОГДА РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	|				И РаботникиОрганизаций.ПериодЗавершения < &ДатаСреза
	|			ТОГДА ЕСТЬNULL(РаботникиОрганизаций.ДолжностьЗавершения.Наименование, """")
	|		ИНАЧЕ ЕСТЬNULL(РаботникиОрганизаций.Должность.Наименование, """")
	|	КОНЕЦ КАК ДолжностьСотрудника,
	|	ЕСТЬNULL(КонтактнаяИнформация.Поле4, """") КАК Поле3,
	|	ЕСТЬNULL(КонтактнаяИнформация.Поле2, """") КАК Поле1,
	|	ОтветственныеЛицаОрганизаций.Должность КАК ДолжностьРуководителя,
	|	ОтветственныеЛицаОрганизаций.НаименованиеОтветственногоЛица КАК ФИОРуководителя,
	|	МедицинскийОсмотр.Ссылка КАК Медосмотр,
	|	РаботникиДокумента.ФизЛицо КАК РаботникФизЛицо,
	|	РаботникиДокумента.ФизЛицо.Пол КАК Пол,
	|	РаботникиДокумента.Сотрудник
	|ИЗ
	|	Документ.МедицинскийОсмотр КАК МедицинскийОсмотр
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	|		ПО МедицинскийОсмотр.Организация = КонтактнаяИнформация.Объект
	|			И (КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	|			И (КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ФактАдресОрганизации))
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МедицинскийОсмотр.Работники КАК РаботникиДокумента
	|		ПО МедицинскийОсмотр.Ссылка = РаботникиДокумента.Ссылка
	|			И (РаботникиДокумента.РезультатОсмотра = ЗНАЧЕНИЕ(Перечисление.ВидыРезультатовМедицинскогоОсмотра.Годен))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаСреза, ) КАК ФИОФизЛиц
	|		ПО (ФИОФизЛиц.ФизЛицо = РаботникиДокумента.Сотрудник.Физлицо)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(&ДатаСреза, ) КАК РаботникиОрганизаций
	|		ПО (РаботникиОрганизаций.Организация = МедицинскийОсмотр.Организация)
	|			И (РаботникиОрганизаций.Сотрудник = РаботникиДокумента.Сотрудник)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОбОтветственномЛице КАК ОтветственныеЛицаОрганизаций
	|		ПО МедицинскийОсмотр.Организация = ОтветственныеЛицаОрганизаций.СтруктурнаяЕдиница
	|ГДЕ
	|	МедицинскийОсмотр.Ссылка = &Документ
	|
	|УПОРЯДОЧИТЬ ПО
	|	Медосмотр,
	|	РаботникиДокумента.НомерСтроки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Медосмотр") Цикл
		ОбластьШапка.Параметры.НаименованиеОрганизации = Выборка.НаименованиеОрганизации;
		ОбластьШапка.Параметры.ДатаДокумента = Формат(Выборка.Дата, "ДЛФ=ДД");
		Город = ?(ЗначениеЗаполнено(Выборка.Поле3), Выборка.Поле3, Выборка.Поле1);
		Если Прав(Город, 2) = " г" Тогда
			Город = "г. " + Лев(Город, СтрДлина(Город)-2);
		КонецЕсли;
		ОбластьШапка.Параметры.Город = Город;
		ОбластьШапка.Параметры.НомерПриказа = Выборка.Номер;
		ДокументРезультат.Вывести(ОбластьШапка);
		НомерСтроки = 0;
		
		Пока Выборка.Следующий() Цикл
			
			Если ЗначениеЗаполнено(Выборка.Сотрудник) Тогда
				НомерСтроки = НомерСтроки + 1;
				
				Фамилия = Выборка.Фамилия;
				УниверсальныеМеханизмы.Просклонять(глЗначениеПеременной("глКомпонентаСклоненияФИО"), Фамилия, 4, Выборка.Пол, Фамилия);
				Имя = Выборка.Имя;
				Отчество = Выборка.Отчество;
				ФИОСокращенно = ?(НЕ ПустаяСтрока(Фамилия), 
				Фамилия + ?(НЕ ПустаяСтрока(Имя)," " + Лев(Имя,1) + "." + ?(НЕ ПустаяСтрока(Отчество),Лев(Отчество,1)+".", ""), ""),
				"");
				ОбластьСтрока.Параметры.ФИОДолжностьСотрудника = ФИОСокращенно + ?(НЕ ПустаяСтрока(Выборка.ДолжностьСотрудника),  " (" + Выборка.ДолжностьСотрудника + ")", "");
				ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
				ДокументРезультат.Вывести(ОбластьСтрока);
			КонецЕсли;
		КонецЦикла;
		
		ОбластьПодвал.Параметры.Основания = "Медицинский осмотр " + Выборка.КодДокумента + " от " + Формат(Выборка.Дата, "ДЛФ=ДД");
		ОбластьПодвал.Параметры.ФИОДиректора = Выборка.ФИОРуководителя;
		ОбластьПодвал.Параметры.ДолжностьРук = Выборка.ДолжностьРуководителя;
		
		ДокументРезультат.Вывести(ОбластьПодвал);
		
	КонецЦикла;
	
	Возврат ДокументРезультат;	
	
КонецФункции //ПечатьПриказаОДопущении()

Функция ПриказОбОтстранении(ДокументСсылка, ОбъектыПечати)
	
	ДокументРезультат = Новый ТабличныйДокумент;

	Макет = Документы.МедицинскийОсмотр.ПолучитьМакет("ОтстранениеОтРаботы");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьСтрока = Макет.ПолучитьОбласть("Строка");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Документ", ДокументСсылка);
	Запрос.УстановитьПараметр("ДатаСреза", ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница",ДокументСсылка.Организация);
		
	Запрос.Текст = ФормированиеПечатныхФормЗК.ПолучитьТекстЗапросаПоОтветственнымЛицам(
		"ДатаСреза",
		"ОтветственноеЛицо = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.Руководитель)
		|И СтруктурнаяЕдиница = &СтруктурнаяЕдиница");
	Запрос.Выполнить();
	
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	МедицинскийОсмотр.Организация.НаименованиеПолное КАК НаименованиеОрганизации,
	               |	МедицинскийОсмотр.Номер КАК КодДокумента,
	               |	МедицинскийОсмотр.Дата,
	               |	МедицинскийОсмотр.Номер,
	               |	ЕСТЬNULL(ФИОФизЛиц.Фамилия, """") КАК Фамилия,
	               |	ЕСТЬNULL(ФИОФизЛиц.Имя, """") КАК Имя,
	               |	ЕСТЬNULL(ФИОФизЛиц.Отчество, """") КАК Отчество,
	               |	ВЫБОР
	               |		КОГДА РаботникиОрганизаций.ПериодЗавершения <> ДАТАВРЕМЯ(1, 1, 1)
	               |				И РаботникиОрганизаций.ПериодЗавершения < &ДатаСреза
	               |			ТОГДА ЕСТЬNULL(РаботникиОрганизаций.ДолжностьЗавершения.Наименование, """")
	               |		ИНАЧЕ ЕСТЬNULL(РаботникиОрганизаций.Должность.Наименование, """")
	               |	КОНЕЦ КАК ДолжностьСотрудника,
	               |	ЕСТЬNULL(КонтактнаяИнформация.Поле4, """") КАК Поле3,
	               |	ЕСТЬNULL(КонтактнаяИнформация.Поле2, """") КАК Поле1,
	               |	ОтветственныеЛицаОрганизаций.Должность КАК ДолжностьРуководителя,
	               |	ОтветственныеЛицаОрганизаций.НаименованиеОтветственногоЛица КАК ФИОРуководителя,
	               |	ЕСТЬNULL(ФИОБухгалтера.Фамилия, """") КАК ФамилияБухгалтера,
	               |	ЕСТЬNULL(ФИОБухгалтера.Имя, """") КАК ИмяБухгалтера,
	               |	ЕСТЬNULL(ФИОБухгалтера.Отчество, """") КАК ОтчествоБухгалтера,
	               |	ЕСТЬNULL(ФИОКадровика.Фамилия, """") КАК ФамилияКадровика,
	               |	ЕСТЬNULL(ФИОКадровика.Имя, """") КАК ИмяКадровика,
	               |	ЕСТЬNULL(ФИОКадровика.Отчество, """") КАК ОтчествоКадровика,
	               |	ЕСТЬNULL(МедицинскийОсмотр.ВидМедосмотра, """") КАК ВидДопуска,
	               |	МедицинскийОсмотр.Ссылка КАК Медосмотр,
	               |	РаботникиДокумента.Сотрудник,
	               |	РаботникиДокумента.ФизЛицо.Пол КАК Пол
	               |ИЗ
	               |	Документ.МедицинскийОсмотр КАК МедицинскийОсмотр
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
	               |		ПО МедицинскийОсмотр.Организация = КонтактнаяИнформация.Объект
	               |			И (КонтактнаяИнформация.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыКонтактнойИнформации.Адрес))
	               |			И (КонтактнаяИнформация.Вид = ЗНАЧЕНИЕ(Справочник.ВидыКонтактнойИнформации.ФактАдресОрганизации))
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Документ.МедицинскийОсмотр.Работники КАК РаботникиДокумента
	               |		ПО МедицинскийОсмотр.Ссылка = РаботникиДокумента.Ссылка
	               |			И (РаботникиДокумента.РезультатОсмотра = ЗНАЧЕНИЕ(Перечисление.ВидыРезультатовМедицинскогоОсмотра.НЕГоден))
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаСреза, ) КАК ФИОФизЛиц
	               |		ПО (ФИОФизЛиц.ФизЛицо = РаботникиДокумента.Сотрудник.Физлицо)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций.СрезПоследних(&ДатаСреза, ) КАК РаботникиОрганизаций
	               |		ПО (РаботникиОрганизаций.Организация = МедицинскийОсмотр.Организация)
	               |			И (РаботникиОрганизаций.Сотрудник = РаботникиДокумента.Сотрудник)
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТДанныеОбОтветственномЛице КАК ОтветственныеЛицаОрганизаций
	               |		ПО МедицинскийОсмотр.Организация = ОтветственныеЛицаОрганизаций.СтруктурнаяЕдиница
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(&ДатаСреза, ) КАК ОтветственныеЛицаБухгалтер
	               |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаСреза, ) КАК ФИОБухгалтера
	               |			ПО ОтветственныеЛицаБухгалтер.ФизическоеЛицо = ФИОБухгалтера.ФизЛицо
	               |		ПО МедицинскийОсмотр.Организация = ОтветственныеЛицаБухгалтер.СтруктурнаяЕдиница
	               |			И (ОтветственныеЛицаБухгалтер.ОтветственноеЛицо = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.ГлавныйБухгалтер))
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОтветственныеЛицаОрганизаций.СрезПоследних(&ДатаСреза, ) КАК ОтветственныеЛицаКадровик
	               |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ДатаСреза, ) КАК ФИОКадровика
	               |			ПО ОтветственныеЛицаКадровик.ФизическоеЛицо = ФИОКадровика.ФизЛицо
	               |		ПО МедицинскийОсмотр.Организация = ОтветственныеЛицаКадровик.СтруктурнаяЕдиница
	               |			И (ОтветственныеЛицаКадровик.ОтветственноеЛицо = ЗНАЧЕНИЕ(Перечисление.ОтветственныеЛицаОрганизаций.РуководительКадровойСлужбы))
	               |ГДЕ
	               |	МедицинскийОсмотр.Ссылка = &Документ
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Медосмотр,
	               |	РаботникиДокумента.НомерСтроки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.СледующийПоЗначениюПоля("Медосмотр") Цикл
		ОбластьШапка.Параметры.НаименованиеОрганизации = Выборка.НаименованиеОрганизации;
		ОбластьШапка.Параметры.ДатаДокумента = Формат(Выборка.Дата, "ДЛФ=ДД");
		Город = ?(ЗначениеЗаполнено(Выборка.Поле3), Выборка.Поле3, Выборка.Поле1);
		Если Прав(Город, 2) = " г" Тогда
			Город = "г. " + Лев(Город, СтрДлина(Город)-2);
		КонецЕсли;
		ОбластьШапка.Параметры.Город = Город;
		ОбластьШапка.Параметры.НомерПриказа = Выборка.Номер;
		
		ДокументРезультат.Вывести(ОбластьШапка);
		
		НомерСтроки = 0;
		
		Пока Выборка.Следующий() Цикл
			Если ЗначениеЗаполнено(Выборка.Сотрудник) Тогда
				НомерСтроки = НомерСтроки + 1;
				
				Фамилия = Выборка.Фамилия;
				УниверсальныеМеханизмы.Просклонять(глЗначениеПеременной("глКомпонентаСклоненияФИО"), Фамилия, 4, Выборка.Пол, Фамилия);
				Имя = Выборка.Имя;
				Отчество = Выборка.Отчество;
				ФИОСокращенно = ?(НЕ ПустаяСтрока(Фамилия), 
				Фамилия + ?(НЕ ПустаяСтрока(Имя)," " + Лев(Имя,1) + "." + ?(НЕ ПустаяСтрока(Отчество),Лев(Отчество,1)+".", ""), ""),
				"");
				ОбластьСтрока.Параметры.ФИОДолжностьСотрудника = ФИОСокращенно + ?(НЕ ПустаяСтрока(Выборка.ДолжностьСотрудника),  " (" + Выборка.ДолжностьСотрудника + ")", "");
				ОбластьСтрока.Параметры.НомерСтроки = НомерСтроки;
				ДокументРезультат.Вывести(ОбластьСтрока);
			КонецЕсли;
		КонецЦикла;
		
		ОбластьПодвал.Параметры.Основания = "Медицинский осмотр " + Выборка.КодДокумента + " от " + Формат(Выборка.Дата, "ДЛФ=ДД");
		ОбластьПодвал.Параметры.ФИОДиректора = Выборка.ФИОРуководителя;
		ОбластьПодвал.Параметры.ДолжностьРук = Выборка.ДолжностьРуководителя;
		ОбластьПодвал.Параметры.ФИОНачальникаОтделаКадров = ?(ЗначениеЗаполнено( Выборка.ФамилияКадровика), Лев(Выборка.ИмяКадровика, 1) + ". " + Лев(Выборка.ОтчествоКадровика, 1) + ". " + Выборка.ФамилияКадровика, "");
		ОбластьПодвал.Параметры.ФИОГлавногоБухгалтера = ?(ЗначениеЗаполнено(Выборка.ФамилияБухгалтера), Лев(Выборка.ИмяБухгалтера, 1) + ". " + Лев(Выборка.ОтчествоБухгалтера, 1) + ". " + Выборка.ФамилияБухгалтера, "");
		
		ДокументРезультат.Вывести(ОбластьПодвал);
	КонецЦикла;	
	
	Возврат ДокументРезультат;
	
КонецФункции //ПечатьПриказаОбОтстранении()

